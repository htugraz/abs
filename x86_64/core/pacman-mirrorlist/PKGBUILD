# $Id: PKGBUILD 239554 2015-05-19 09:36:52Z bluewind $
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pacman-mirrorlist
pkgver=20150519
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman"
arch=('any')
url="https://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=(mirrorlist)

# NOTE on building this package:
# * Go to the trunk/ directory
# * Run bash -c ". PKGBUILD; updatelist"
# * Update the checksums, update pkgver
# * Build the package

updatelist() {
  rm -f mirrorlist
  curl -o mirrorlist https://www.archlinux.org/mirrorlist/all/
}

package() {
  mkdir -p $pkgdir/etc/pacman.d
  install -m644 $srcdir/mirrorlist $pkgdir/etc/pacman.d/
}

md5sums=('a0a8b03f5ab856932a89a94f26ed32c0')
sha256sums=('38154110b69fb6b3bac4bdecdef097ee72720b17879367522a07b4d8e103cc74')
