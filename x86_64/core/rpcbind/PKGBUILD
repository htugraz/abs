# $Id: PKGBUILD 193786 2013-08-31 10:51:12Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=rpcbind
pkgver=0.2.1
pkgrel=2
pkgdesc="portmap replacement which supports RPC over various protocols"
arch=(i686 x86_64)
depends=('bash' 'glibc' 'libtirpc')
url="http://rpcbind.sourceforge.net"
license=('custom')
replaces=('portmap')
source=(http://downloads.sourceforge.net/sourceforge/rpcbind/rpcbind-0.2.1.tar.bz2
        rpcbind-sunrpc.patch
        rpcbind.service)
install=rpcbind.install
md5sums=('0a5f9c2142af814c55d957aaab3bcc68'
         'c02ac36a98baac70b8a26190524b7b73'
         'a7b23a32be2eb52d7dec52da36d4eba1'
)
prepare() {
  cd $srcdir/$pkgname-$pkgver
  # patch for iana services file
  patch -Np1 -i ../rpcbind-sunrpc.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --with-rpcuser=rpc --enable-warmstarts --with-statedir=/run
  make
}

check() {
  cd $srcdir/$pkgname-$pkgver
  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  # install missing man page - https://bugs.archlinux.org/task/21271
  install -m644 man/rpcinfo.8 $pkgdir/usr/share/man/man8/
  # install systemd service file
  install -D -m644 $srcdir/rpcbind.service $pkgdir/usr/lib/systemd/system/rpcbind.service
  # install license
  install -D -m644 COPYING $pkgdir/usr/share/licenses/rpcbind/COPYING
}
