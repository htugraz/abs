# $Id: PKGBUILD 188782 2013-06-20 06:35:10Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=gzip
pkgver=1.6
pkgrel=1
pkgdesc="GNU compression utility"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gzip/"
license=('GPL3')
groups=('base' 'base-devel')
depends=('glibc' 'bash' 'less')
install=gzip.install
source=(ftp://ftp.gnu.org/pub/gnu/gzip/gzip-$pkgver.tar.xz{,.sig})
md5sums=('da981f86677d58a106496e68de6f8995'
         'SKIP')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make prefix=${pkgdir}/usr install
}
