# $Id: PKGBUILD 203545 2014-01-13 14:20:07Z dan $

pkgname=pth
pkgver=2.0.7
pkgrel=5
pkgdesc="The GNU Portable Threads."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/pth/"
license=('LGPL')
depends=('glibc' 'awk')
options=('!makeflags')
source=(ftp://ftp.gnu.org/gnu/pth/$pkgname-$pkgver.tar.gz{,.sig})
sha1sums=('9a71915c89ff2414de69fe104ae1016d513afeee'
          '8cb16dffbf15ba92bda8d08918eaf5995642fc38')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --mandir=/usr/share/man \
              --disable-static \
              --enable-shared \
              --with-pic

  sed -i "s|awk=''|awk=/bin/awk|g" shtool # "Cannot find a reasonable Awk"

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR=${pkgdir} install
}
