# $Id: PKGBUILD 228359 2015-01-03 06:22:26Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=automake
pkgver=1.14.1
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="http://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
checkdepends=('dejagnu' 'python' 'gcc-fortran' 'java-environment' 'vala' 'emacs' 'cscope')
install=automake.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('7fc29854c520f56b07aa232a0f880292'
         'SKIP')
validpgpkeys=('A0460631BECA5C1CCD1C8362FC818E17429F96EA')   # Stefano Lattarini

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # skip failing test...
  sed -i "/instmany-python.sh/d" Makefile.in
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
