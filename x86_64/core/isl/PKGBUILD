# $Id: PKGBUILD 197163 2013-10-23 13:05:00Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=isl
pkgver=0.12.1
pkgrel=2
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('i686' 'x86_64')
url="http://freecode.com/projects/isl"
depends=('gmp')
license=('MIT')
source=(ftp://ftp.linux.student.kuleuven.be/pub/people/skimo/isl/$pkgname-$pkgver.tar.bz2)
md5sums=('27d381085ff1b5733c3e8a9b6ad24a4d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 DESTDIR="$pkgdir/" install

  install -dm755 $pkgdir/usr/share/gdb/auto-load/usr/lib/
  mv $pkgdir/{,/usr/share/gdb/auto-load/}usr/lib/libisl.so.10.2.1-gdb.py
  
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}
