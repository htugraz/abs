# $Id: PKGBUILD 226107 2014-11-12 07:44:32Z tpowa $
# Contributor: Dennis "Gyroplast" Herbrich <dennis@archlinux.org>,  dorphell <dorphell@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=isdn4k-utils
pkgver=3.27
pkgrel=1
pkgdesc="User space administration programs and tools for ISDN"
arch=('i686' 'x86_64')
url="http://www.isdn4linux.de/"
license=('GPL')
makedepends=('git')
depends=('ncurses' 'sh') 
backup=(etc/isdn/{callerid.conf,isdn.conf,rate.conf,isdnlog.isdnctrl0.options})
options=('!makeflags')
source=(isdn4k-utils::git+git://git.misdn.eu/isdn4k-utils.git#tag=v${pkgver}
        config
        va_list.patch)
replaces=('capi4k-utils')
md5sums=('SKIP'
         'ea3c71054234f9ef6bcf9325e36c4a38'
         '81709ee10336a2df684a5398f42ef514')

prepare() {
  CPPFLAGS="$CPPFLAGS -O2"
  cd $srcdir/$pkgname
  patch -Np1 -i $srcdir/va_list.patch
  cp $srcdir/config ./.config
  cd capiinfo
  aclocal
  automake --add-missing
  autoreconf
  cd $srcdir/$pkgname
  cd capifax
  aclocal 
  automake --add-missing
  autoreconf
  cd $srcdir/$pkgname
  make subconfig
}

build() {
  cd $srcdir/$pkgname
  make
}

package() {
  cd $srcdir/$pkgname
  install -dm755 $pkgdir/usr/bin
  make DESTDIR=$pkgdir install
  chmod 755 $pkgdir/usr/bin/*
}
