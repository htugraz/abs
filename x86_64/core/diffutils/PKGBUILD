# $Id: PKGBUILD 216930 2014-07-16 17:04:50Z seblu $
# Mainainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=diffutils
pkgver=3.3
pkgrel=2
pkgdesc='Utility programs used for creating patch files'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/diffutils'
license=('GPL3')
groups=('base')
depends=('glibc' 'bash')
install=diffutils.install
source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('99180208ec2a82ce71f55b0d7389f1b3'
         '203a2f2101dfcc09cdac655be353fd78')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
