# $Id: PKGBUILD 241774 2015-07-03 21:22:17Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=eog-plugins
pkgver=3.16.0
pkgrel=2
pkgdesc="Plugins for Eye of Gnome"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/EyeOfGnome/Plugins"
license=('GPL2')
depends=('eog' 'libchamplain' 'libgdata' 'python')
makedepends=('intltool' 'gobject-introspection')
install=eog-plugins.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('6fab1c1e0705fc2b0e4a63caf3ba1c5b64b4eaa1301e4cbc04bb934428b68450')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc --localstatedir=/var

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
