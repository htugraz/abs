# $Id: PKGBUILD 208005 2014-03-17 04:25:28Z fyan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: Giulio Bai <giulio@hewle.com>

pkgname=accerciser
pkgver=3.8.2
pkgrel=2
pkgdesc="Interactive Python accessibility explorer for the GNOME desktop"
arch=('any')
url="http://live.gnome.org/Accerciser"
license=('BSD')
depends=('python' 'gtk3' 'python-atspi' 'ipython' 'hicolor-icon-theme')
makedepends=('itstool' 'docbook-xsl' 'intltool')
install=accerciser.install
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/accerciser/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('f4fef2bfa23a9344b613c39dfa128e4d07d98f87cd5011d7d649b2a9c0a6aadd')

build () {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
                 --disable-schemas-compile
  make
}

package () {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR=${pkgdir} install
  install -m 755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m 644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}
}
