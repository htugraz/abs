# $Id: PKGBUILD 228156 2014-12-29 17:18:14Z foutrelis $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-qtruby
pkgver=4.14.3
pkgrel=2
pkgdesc="Ruby bindings for the Qt libraries"
url="https://projects.kde.org/projects/kde/kdebindings/ruby/qtruby"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt' 'ruby')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen')
conflicts=('kdebindings-ruby')
source=("http://download.kde.org/stable/${pkgver}/src/qtruby-${pkgver}.tar.xz")
sha1sums=('8a211c62dc5a343894010c1ebcac7d593966f07e')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../qtruby-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCUSTOM_RUBY_SITE_LIB_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]') \
    -DCUSTOM_RUBY_SITE_ARCH_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorarchdir"]')
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
