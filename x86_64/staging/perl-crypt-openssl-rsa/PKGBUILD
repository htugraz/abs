# $Id: PKGBUILD 56402 2011-10-07 09:48:12Z spupykin $
# Maintainer: Florian Pritz <bluewind@xinu.at> 
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-crypt-openssl-rsa
pkgver=0.28
pkgrel=5
pkgdesc="Interface to OpenSSL RSA methods"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Crypt-OpenSSL-RSA"
depends=('perl-crypt-openssl-random' 'perl-crypt-openssl-bignum')
license=('GPL')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/P/PE/PERLER/Crypt-OpenSSL-RSA-$pkgver.tar.gz")
md5sums=('86217a5036fc63779c30420b5fd84129')

build() {
  cd  "$srcdir/Crypt-OpenSSL-RSA-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd  "$srcdir/Crypt-OpenSSL-RSA-$pkgver"

  make test
}

package() {
  cd  "$srcdir/Crypt-OpenSSL-RSA-$pkgver"
  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
