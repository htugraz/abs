# $Id: PKGBUILD 200264 2013-11-24 06:25:00Z eric $
# Maintainer:
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libftdi
pkgver=1.0
pkgrel=1
pkgdesc="A library to talk to FTDI chips"
arch=('i686' 'x86_64')
url="http://www.intra2net.com/en/developer/libftdi/download.php"
license=('GPL2' 'LGPL2.1')
depends=('libusbx')
makedepends=('boost' 'cmake')
source=(http://www.intra2net.com/en/developer/libftdi/download/${pkgname}1-$pkgver.tar.bz2{,.sig})
sha1sums=('5be76cfd7cd36c5291054638f7caf4137303386f'
          'SKIP')

prepare() {
  cd ${pkgname}1-$pkgver
  sed -i 's|LIB_SUFFIX 64|LIB_SUFFIX ""|' CMakeLists.txt
}

build() {
  cd ${pkgname}1-$pkgver
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_SKIP_BUILD_RPATH=ON -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd ${pkgname}1-$pkgver/build
  make DESTDIR="$pkgdir" install
  find examples -type f -perm -755 -exec install {} "$pkgdir/usr/bin" \;
}
