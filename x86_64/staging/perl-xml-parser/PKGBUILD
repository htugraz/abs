# $Id: PKGBUILD 213738 2014-05-28 21:29:01Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-xml-parser
pkgver=2.41
pkgrel=5
pkgdesc="Expat-based XML parser module for perl"
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/XML-Parser/"
depends=('perl' 'expat')
replaces=('perlxml')
provides=("perlxml=${pkgver}")
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TODDR/XML-Parser-${pkgver}.tar.gz")
md5sums=('c320d2ffa459e6cdc6f9f59c1185855e')

build() {
  cd XML-Parser-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-Parser-${pkgver}
  make test
}

package() {
  cd XML-Parser-${pkgver}
  make DESTDIR="${pkgdir}" install
}
