# $Id: PKGBUILD 236150 2015-04-10 19:24:03Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-knavalbattle
pkgver=15.04.0
pkgrel=1
pkgdesc="A ship sinking game"
url="http://kde.org/applications/games/knavalbattle/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/knavalbattle-${pkgver}.tar.xz")
sha1sums=('a052697d6636b55940e93f0d22f15e0caabba2b7')

build() {
  mkdir build
  cd build
  cmake ../knavalbattle-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
