# $Id: PKGBUILD 251389 2015-11-19 21:14:06Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kget
pkgver=15.11.80
pkgrel=1
pkgdesc='Download Manager'
url='http://kde.org/applications/internet/kget/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'kdebase-lib' 'libktorrent' 'libmms' 'kdepimlibs4')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kget-${pkgver}.tar.xz")
sha1sums=('5c28c222bdecd2d026839e37d833d2b43de395c6')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kget-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF \
    -DWITH_NepomukWidgets=OFF \
    -DWITH_KDE4Workspace=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
