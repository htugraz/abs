# $Id: PKGBUILD 246599 2015-09-19 10:33:28Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marcin "avalan" Falkiewicz <avalatron@gmail.com>
# Contributor: C Anthony Risinger <anthony@xtfx.me>

pkgbase=python-ply
pkgname=(python-ply python2-ply)
pkgver=3.7
pkgrel=2
pkgdesc='Implementation of lex and yacc parsing tools'
arch=('any')
url='http://www.dabeaz.com/ply/'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${url}ply-$pkgver.tar.gz"
        LICENSE
        python-3.5.patch)
sha256sums=('f85fb7b44c1c9a04873e6d76fa2c2eef43f2cfd81468aa714a9c63af7ae0af80'
            '87c20dd0a774f4d4ff837b4f1555f7eb1ed1b8dc1e3223cd105e5c1e282d62bf'
            'b7969d290e659d9756a2b0ad3b19481a4b32e1ec79590db1c7ab64bcc6519029')

prepare() {
  # https://github.com/dabeaz/ply/pull/78
  (cd ply-$pkgver; patch -p1 -i ../python-3.5.patch)

  cp -a ply-$pkgver{,-py2}
}

check() {
  cd "ply-$pkgver/test"
  python testlex.py
  python testyacc.py

  cd "$srcdir/ply-$pkgver-py2/test"
  python2 testlex.py
  python2 testyacc.py
}

package_python-ply() {
  depends=('python')

  cd "ply-$pkgver"
  python setup.py install --root="$pkgdir"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-ply() {
  depends=('python2')

  cd "ply-$pkgver-py2"
  python2 setup.py install --root="$pkgdir"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
