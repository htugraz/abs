# $Id: PKGBUILD 251378 2015-11-19 21:10:53Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-svgpart
pkgver=15.11.80
pkgrel=1
pkgdesc="A KPart for viewing SVGs"
url="https://projects.kde.org/projects/kde/kdegraphics/svgpart"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/svgpart-${pkgver}.tar.xz")
sha1sums=('9e7e8f9e1debcab36bf0d89111636cd91cb710ec')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../svgpart-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
