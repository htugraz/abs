# $Id: PKGBUILD 236111 2015-04-10 19:11:07Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbruch
pkgver=15.04.0
pkgrel=1
pkgdesc="Exercise Fractions"
url="http://kde.org/applications/education/kbruch/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kxmlgui')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
install=${pkgname}.install
conflicts=('kdeedu-kbruch')
replaces=('kdeedu-kbruch')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kbruch-${pkgver}.tar.xz")
sha1sums=('bb6e80a3d8230c589d04c8e66d7cf8ce88253c94')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kbruch-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
