# $Id: PKGBUILD 251366 2015-11-19 21:07:21Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-lskat
pkgver=15.11.80
pkgrel=1
pkgdesc="Lieutenant Skat is a fun and engaging card game for two players"
url="http://kde.org/applications/games/lskat/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/lskat-${pkgver}.tar.xz")
sha1sums=('bc12bc5a53ba0c9969ff17ee7e3170354ab82817')

build() {
  mkdir -p build
  cd build
  cmake ../lskat-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
