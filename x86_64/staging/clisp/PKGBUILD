# $Id: PKGBUILD 245428 2015-09-07 08:26:49Z foutrelis $
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
pkgname=clisp
pkgver=2.49
pkgrel=5
pkgdesc="ANSI Common Lisp interpreter, compiler and debugger"
arch=('i686' 'x86_64')
license=('GPL')
url="http://clisp.cons.org/"
depends=('readline' 'libsigsegv')
provides=('common-lisp')
makedepends=('ffcall')
options=('!makeflags' '!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/clisp/clisp-${pkgver}.tar.bz2"
        "clisp-gcc5.patch")
md5sums=('1962b99d5e530390ec3829236d168649'
         'f576778ab7fc879d9509decfd559d32e')

prepare() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -Np1 -i ../clisp-gcc5.patch
}

build() {
  cd $srcdir/${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-readline --with-ffcall src
  cd src
  ./makemake --prefix=/usr --with-readline --with-ffcall --with-dynamic-ffi > Makefile
  make
  sed -i 's,http://www.lisp.org/HyperSpec/,http://www.lispworks.com/reference/HyperSpec/,g' config.lisp
  make
}

check() {
  cd $srcdir/${pkgname}-${pkgver}/src
  make check
}

package() {
  cd $srcdir/${pkgname}-${pkgver}/src
  make DESTDIR=$pkgdir install
}
