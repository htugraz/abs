# $Id: PKGBUILD 247061 2015-09-22 05:16:37Z fyan $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=system-config-printer
pkgver=1.5.7
pkgrel=2
pkgdesc="A CUPS printer configuration tool and status applet"
url="http://cyberelk.net/tim/software/system-config-printer/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('python-pycups' 'python-dbus' 'python-pycurl' 'libnotify'
         'python-requests' 'python-gobject' 'gtk3' 'python-cairo')
makedepends=('intltool' 'xmlto' 'docbook-xsl' 'desktop-file-utils')
optdepends=('libgnome-keyring: password management'
            'python-pysmbc: SMB browser support'
            'python-packagekit: to install drivers with PackageKit'
            'cups-pk-helper: PolicyKit helper to configure cups with fine-grained privileges')
source=(http://cyberelk.net/tim/data/${pkgname}/${pkgver%*.*}/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('1a0c6a4566a5479733945f6db32d6f00'
         'SKIP')
validpgpkeys=('02EA6349CE58E1BCAA8514F57955128DA3A1ABD3' #Tim Waugh <twaugh@redhat.com> <tim@cyberelk.net>
              '4629AFE960EC20BEC12E3104B7C20D079491EA63') #Tim Waugh <tim@cyberelk.net>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udevdir=/usr/lib/udev --with-udev-rules 
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # Compile *.pyc
  find "${pkgdir}" -name '*.py' -exec python -mpy_compile {} +
  # Compile *.pyo
  find "${pkgdir}" -name '*.py' -exec python -O -mpy_compile {} +
  
  # new cups 2.0.0 service naming
  sed -i "s|cups.socket|org.cups.cupsd.socket|g" ${pkgdir}/usr/lib/systemd/system/configure-printer@.service
}
