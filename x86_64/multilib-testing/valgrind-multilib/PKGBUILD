# $Id: PKGBUILD 118733 2014-09-10 00:23:56Z allan $
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=valgrind-multilib
pkgver=3.9.0
pkgrel=6
pkgdesc="A tool to help find memory-management problems in programs for multilib"
arch=(x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('lib32-glibc>=2.20' 'lib32-glibc<2.21' 'perl')
makedepends=('gdb' 'openmpi' 'gcc-multilib')
optdepends=('openmpi: MPI support')
options=('staticlibs' '!emptydirs')
source=(http://valgrind.org/downloads/valgrind-$pkgver.tar.bz2
        valgrind-3.9.0-glibc-2.20.patch
        add_xabort_op.patch)
sha1sums=('9415e28933de9d6687f993c4bb797e6bd49583f1'
          '2080c3d44de20857f18b03a93404a8f3d3721f7e'
          '126e90494e0134a70649456cd1a1f1369024c345')

provides=('valgrind')
conflicts=('valgrind')

prepare() {
  cd valgrind-$pkgver
  patch -p1 -i "$srcdir"/valgrind-3.9.0-glibc-2.20.patch
  patch -p0 -i "$srcdir"/add_xabort_op.patch
}

build() {
  cd valgrind-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd valgrind-$pkgver
  make DESTDIR="$pkgdir" install
}
