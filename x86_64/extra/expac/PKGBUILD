# $Id: PKGBUILD 87501 2013-04-02 00:06:04Z dreisner $
# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=expac
pkgver=4
pkgrel=1
pkgdesc="pacman database extraction utility"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/expac"
license=('GPL')
depends=('pacman')
makedepends=('perl')
source=("http://code.falconindy.com/archive/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
md5sums=('5104ad808155e8d2d381d9282caf7bae'
         'SKIP')

prepare() {
  cd "$pkgname-$pkgver"

  sed '/\*\//q' expac.c >LICENSE
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ft=sh syn=sh
