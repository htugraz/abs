# $Id: PKGBUILD 240396 2015-06-07 10:17:58Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz at interia pl>

pkgname=x265
pkgver=1.7
pkgrel=1
pkgdesc='Open Source H265/HEVC video encoder'
arch=('i686' 'x86_64')
url='https://bitbucket.org/multicoreware/x265'
license=('GPL')
depends=('gcc-libs')
makedepends=('yasm' 'cmake')
provides=('libx265.so')
source=($pkgname-$pkgver.tar.bz2::$url/get/$pkgver.tar.bz2)
md5sums=('d6020c277b05ab44f8222342aaa9c99f')

build() {
  cd multicoreware-x265-*/build/linux
  [[ $CARCH == x86_64 ]] && LDFLAGS+=',-z,noexecstack'
  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr ../../source
  make
}

package() {
  cd multicoreware-x265-*/build/linux
  make DESTDIR="$pkgdir" install
}
