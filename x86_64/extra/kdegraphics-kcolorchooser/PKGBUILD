# $Id: PKGBUILD 201088 2013-12-06 12:32:19Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kcolorchooser
pkgver=4.11.4
pkgrel=1
pkgdesc="Color Chooser"
url="http://kde.org/applications/graphics/kcolorchooser/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kcolorchooser-${pkgver}.tar.xz")
sha1sums=('b50e9c80ca1e5dbc3b976144513958430eaa6078')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kcolorchooser-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
