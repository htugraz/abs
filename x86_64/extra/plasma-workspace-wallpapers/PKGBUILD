# $Id: PKGBUILD 245022 2015-08-29 08:50:04Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-workspace-wallpapers
pkgver=5.4.0
pkgrel=1
pkgdesc='Additional wallpapers for the Plasma Workspace'
arch=('any')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
makedepends=('extra-cmake-modules')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('8ad2e9423bd01f7302652fe3a79d2484')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
