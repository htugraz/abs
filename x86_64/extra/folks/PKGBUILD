# $Id: PKGBUILD 224591 2014-10-16 12:00:36Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.10.0
pkgrel=1
pkgdesc="Library to aggregates people into metacontacts"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Projects/Folks"
license=(LGPL2.1)
depends=(telepathy-glib libgee libxml2 evolution-data-server zeitgeist)
makedepends=(gobject-introspection intltool vala python2)
install=folks.install
options=('!emptydirs')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-${pkgver}.tar.xz)
sha256sums=('66e76bb9d095b6c0ba41e37e791e11762c67fd36498511a65085d7a39b771cc2')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --disable-schemas-compile \
    --disable-libsocialweb-backend --disable-fatal-warnings

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
