# $Id: PKGBUILD 219433 2014-08-07 12:09:19Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=threadweaver
pkgver=5.1.0
pkgrel=1
pkgdesc='ThreadWeaver'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/threadweaver'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('3a79f7b25845a2bf22617e00fe388410')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
