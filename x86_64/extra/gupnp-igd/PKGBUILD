# $Id: PKGBUILD 226773 2014-11-21 04:58:30Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gupnp-igd
pkgver=0.2.4
pkgrel=2
pkgdesc="A library to handle UPnP IGD port mapping"
arch=('i686' 'x86_64')
url="http://www.gupnp.org"
license=('LGPL')
depends=('gupnp')
makedepends=('gobject-introspection' 'python2-gobject2')
checkdepends=('python2-gobject')
options=('!makeflags')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver::3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('38c4a6d7718d17eac17df95a3a8c337677eda77e58978129ad3182d769c38e44')

build() {
  cd ${pkgname}-${pkgver}
  PYTHON=python2 ./configure --prefix=/usr --enable-python
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

