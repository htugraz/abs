# $Id: PKGBUILD 200351 2013-11-26 23:08:09Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=gstreamer-vaapi
pkgname=(gst-vaapi gstreamer0.10-vaapi)
pkgver=0.5.7
pkgrel=1
pkgdesc="GStreamer Multimedia Framework VA Plugins"
arch=(i686 x86_64)
license=(LGPL)
url="http://www.freedesktop.org/software/vaapi/releases/gstreamer-vaapi/"
makedepends=(gst-plugins-base gst-plugins-bad gstreamer0.10-base-plugins gstreamer0.10-bad-plugins
             libva libxrandr)
options=(!emptydirs)

#source=(git://gitorious.org/vaapi/${pkgbase}.git#tag=$pkgver)
source=($url/$pkgbase-${pkgver}.tar.bz2)
sha1sums=('6baa794e5f95a22fbb5e0788ec29c8a78f40b5b3')

prepare() {
  mkdir build build-0.10
  #cd $pkgbase
  #NO_CONFIGURE=1 ./autogen.sh
}

_build() (
  cd "$1"; shift
  ../$pkgbase-$pkgver/configure --prefix=/usr --disable-static "$@"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
)

build() {
  _build build
  _build build-0.10 --with-gstreamer-api=0.10 --disable-wayland
}

check() {
  make -C build -k check
  make -C build-0.10 -k check
}

package_gst-vaapi() {
  depends=(gst-plugins-base gst-plugins-bad libva libxrandr)
  make -C build DESTDIR="$pkgdir" install
}

package_gstreamer0.10-vaapi() {
  depends=(gstreamer0.10-base-plugins gstreamer0.10-bad-plugins libva libxrandr)
  make -C build-0.10 DESTDIR="$pkgdir" install
}
