# $Id: PKGBUILD 216719 2014-07-08 18:41:03Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xcb-util-image
pkgver=0.3.9
pkgrel=2
pkgdesc="Utility libraries for XC Binding - Port of Xlib's XImage and XShmImage functions"
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org"
license=('custom')
depends=('libxcb>=1.7' 'xcb-util>=0.3.9')
makedepends=('xorg-util-macros')
source=("http://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.bz2"
        'LICENSE')
sha256sums=('d35a3c0eaae171f5081ec127fb9cec174b716ce5d78196a87f2785376da021df'
            'ded299aa179dcf0d885bf89274a4db77a530e03f9f5e7cf1c3c4ef1d60e914b9')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() { 
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
