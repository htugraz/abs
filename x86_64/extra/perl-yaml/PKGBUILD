# $Id: PKGBUILD 218420 2014-07-31 03:47:03Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-yaml
pkgver=0.98
pkgrel=1
pkgdesc="Perl/CPAN Module YAML : YAML Aint Markup Language"
arch=('any')
url="http://search.cpan.org/dist/YAML/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/I/IN/INGY/YAML-${pkgver}.tar.gz)
sha1sums=('3af7814056b1541ccaf06a95a1baf268216358e8')

build() {
  cd YAML-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-${pkgver}
  make test
}

package() {
  cd YAML-${pkgver}
  make DESTDIR="${pkgdir}" install
}
