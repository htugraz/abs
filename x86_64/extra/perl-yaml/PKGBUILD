# $Id: PKGBUILD 215238 2014-06-17 05:02:26Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-yaml
pkgver=0.94
pkgrel=1
pkgdesc="Perl/CPAN Module YAML : YAML Aint Markup Language"
arch=('any')
url="http://search.cpan.org/dist/YAML/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/I/IN/INGY/YAML-${pkgver}.tar.gz)
sha1sums=('9cb873c1776843963ec42d90fbec9f511ef0a720')

build() {
  cd YAML-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-${pkgver}
  make test
}

package() {
  cd YAML-${pkgver}
  make DESTDIR="${pkgdir}" install
}
