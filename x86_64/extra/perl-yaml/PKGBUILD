# $Id: PKGBUILD 216977 2014-07-18 02:54:36Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-yaml
pkgver=0.97
pkgrel=1
pkgdesc="Perl/CPAN Module YAML : YAML Aint Markup Language"
arch=('any')
url="http://search.cpan.org/dist/YAML/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/I/IN/INGY/YAML-${pkgver}.tar.gz)
sha1sums=('66d83b28bffee3d385abb932cc42b4a09e8140c3')

build() {
  cd YAML-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-${pkgver}
  make test
}

package() {
  cd YAML-${pkgver}
  make DESTDIR="${pkgdir}" install
}
