# $Id: PKGBUILD 246923 2015-09-20 08:22:08Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgeomap
pkgver=15.08.1
pkgrel=1
pkgdesc="A wrapper around world map components for browsing and arranging photos on a map"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkgeomap"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'libmarble4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('605b2230690af4e506ce3fde56c6b618c7c03183')

prepare() {
  mkdir -p build

# Fix build with libmarble4
  sed -e 's|#include <marble|#include <marble4|' -i $pkgname-$pkgver/libkgeomap/*.cpp -i $pkgname-$pkgver/libkgeomap/*.h
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
