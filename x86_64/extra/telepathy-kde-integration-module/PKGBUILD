# $Id: PKGBUILD 207574 2014-03-12 12:14:58Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-integration-module
_pkgname=ktp-kded-integration-module
pkgver=0.8.0
pkgrel=1
pkgdesc="Sits in KDED and takes care of various bits of system integration like setting user to auto-away or handling connection errors"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-common-internals')
makedepends=('cmake' 'automoc4')
groups=('kde-telepathy')
source=("http://download.kde.org/stable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('cbd25fb6050f335cf8c2dc75b626f5c46d746ddb')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
