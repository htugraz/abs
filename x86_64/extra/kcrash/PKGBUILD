# $Id: PKGBUILD 225829 2014-11-10 08:23:03Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcrash
pkgver=5.4.0
pkgrel=1
pkgdesc='Support for application crash analysis and bug report from apps'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kcrash'
license=('LGPL')
depends=('kcoreaddons' 'kwindowsystem')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('971649d12a57cf1c05c77fb3c4370aea')

prepare() {
  mkdir -p build
}

build() {
  # LIBEXEC is needed for drkonqi
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
