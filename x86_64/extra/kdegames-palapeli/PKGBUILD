# $Id: PKGBUILD 224996 2014-10-20 08:17:10Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-palapeli
pkgver=4.14.2
pkgrel=1
pkgdesc="A single-player jigsaw puzzle game"
url="http://kde.org/applications/games/palapeli/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames' 'qhull')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/palapeli-${pkgver}.tar.xz")
sha1sums=('5a3ccd58c945c9bc259430d0676bad5c2e5a2795')

build() {
  mkdir build
  cd build
  cmake ../palapeli-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
