# $Id: PKGBUILD 225323 2014-10-25 12:39:39Z fyan $
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=neon
pkgver=0.30.1
pkgrel=1
pkgdesc="HTTP and WebDAV client library with a C interface"
arch=('i686' 'x86_64')
url="http://www.webdav.org/neon/"
license=('GPL' 'LGPL')
depends=('krb5' 'expat' 'ca-certificates')
options=('libtool') # FS#16067
source=(http://www.webdav.org/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha1sums=('efec2a6f17d9bd2323345320e3067349ddc9cf79'
          'SKIP')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-expat \
    --enable-shared \
    --disable-static \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
