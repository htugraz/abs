# $Id: PKGBUILD 255595 2015-12-10 05:03:27Z foutrelis $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=portsmf
pkgver=228
pkgrel=2
pkgdesc="Portable library for reading/writing Standard MIDI Files"
arch=('i686' 'x86_64')
url="http://sourceforge.net/p/portmedia/wiki/portsmf/"
license=('MIT')
depends=('gcc-libs')
makedepends=('subversion')
options=('!staticlibs')
source=(portsmf::svn+https://svn.code.sf.net/p/portmedia/code/portsmf/trunk#revision=$pkgver
        portsmf-shared.patch)
sha1sums=('SKIP'
          'd07104f2b62e1458b8ff40b27673d992431d801d')

pkgver() {
  cd $pkgname
  printf "%s" "$(svnversion | tr -d 'A-z')"
}

prepare() {
  cd $pkgname
  chmod 755 configure
  patch -p1 -i "${srcdir}/portsmf-shared.patch"
  autoreconf -f -i
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libdir=/usr/lib --includedir=/usr/include/portsmf
  make 
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" includedir=/usr/include/portsmf install
  install -D -m644 portSMF.pc "${pkgdir}/usr/lib/pkgconfig/portSMF.pc"
  install -D -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
