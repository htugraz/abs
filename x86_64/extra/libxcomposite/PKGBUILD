# $Id: PKGBUILD 198272 2013-10-30 13:51:36Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxcomposite
pkgver=0.4.4
pkgrel=1
pkgdesc="X11 Composite extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'compositeproto')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libXcomposite-${pkgver}.tar.bz2")
sha256sums=('ede250cd207d8bee4a338265c3007d7a68d5aca791b6ac41af18e9a2aeb34178')

build() {
  cd "${srcdir}/libXcomposite-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXcomposite-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
