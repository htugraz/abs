# $Id: PKGBUILD 205721 2014-02-10 10:59:00Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-knavalbattle
pkgver=4.12.2
pkgrel=1
pkgdesc="A ship sinking game"
url="http://kde.org/applications/games/knavalbattle/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/knavalbattle-${pkgver}.tar.xz")
sha1sums=('7c27a477dafe835d8d783e3e6b96547f1d2aaaae')

build() {
  mkdir build
  cd build
  cmake ../knavalbattle-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
