# $Id: PKGBUILD 235433 2015-04-04 08:29:09Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=3.6.1
pkgrel=1
pkgdesc="Lightweight, advanced audio player focused on audio quality"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('BSD')
depends=('gtk2' 'glib2' 'libguess' 'libsm' 'audacious-plugins'
         'gnome-icon-theme' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('python2') # for gdbus-codegen
optdepends=('unzip: zipped skins support')
provides=('audacious-player')
conflicts=('audacious-player')
replaces=('audacious-player')
install=$pkgname.install
source=(http://distfiles.audacious-media-player.org/$pkgname-$pkgver.tar.bz2)
sha256sums=('85d1d5a80240f45c858bb25af6565c13d53e4b92882eb15fb2b18511fabf3de6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --with-buildstamp='Arch Linux'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
