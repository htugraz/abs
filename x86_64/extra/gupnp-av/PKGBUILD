# $Id: PKGBUILD 199920 2013-11-19 00:13:08Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>
# Contributor: Denis Zawada <deno@rootnode.net>

pkgname=gupnp-av
pkgver=0.12.4
pkgrel=1
pkgdesc="Library to ease handling and implementation of UPnP A/V profiles"
arch=(i686 x86_64)
url="http://gupnp.org/"
license=(LGPL)
depends=(gupnp)
makedepends=(gobject-introspection gtk-doc vala)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('548a9cef8ab3007734e20a4ce284c422ae299b7e024a4824299f6ae7e3dd7a5b')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
