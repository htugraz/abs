# $Id: PKGBUILD 205684 2014-02-10 10:58:27Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kanagram
pkgver=4.12.2
pkgrel=1
pkgdesc="Letter Order Game"
url="http://kde.org/applications/education/kanagram/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libkdeedu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kanagram-${pkgver}.tar.xz")
sha1sums=('8b293b3c7c4489ca0cdd1a81c86eee7ac59a903b')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kanagram-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
