# $Id: PKGBUILD 195276 2013-09-29 19:06:21Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-http-daemon
pkgver=6.01
pkgrel=2
pkgdesc="A simple http server class"
arch=('any')
url="http://search.cpan.org/dist/HTTP-Daemon"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-http-date' 'perl-http-message' 'perl-lwp-mediatypes')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/HTTP-Daemon-${pkgver}.tar.gz)
sha1sums=('d3b7614d4b3be4b61d26011efe90026c955102a4')

build() {
  cd "${srcdir}/HTTP-Daemon-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/HTTP-Daemon-${pkgver}"
  make test
}

package() {
    cd "${srcdir}/HTTP-Daemon-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
