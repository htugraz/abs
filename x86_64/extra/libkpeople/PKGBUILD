# $Id: PKGBUILD 207577 2014-03-12 12:15:54Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=libkpeople
pkgver=0.2.1
pkgrel=1
pkgdesc="A contact aggregation library for KDE"
arch=('i686' 'x86_64')
url='http://community.kde.org/KTp/libkpeople'
license=('LGPL')
depends=('kdepimlibs')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/unstable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('41d054aca63b667683e3d8eea789347c39a4894e')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}/" install
}
