# $Id: PKGBUILD 252347 2015-12-04 15:58:48Z schiv $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: see .contrib

pkgname=ardour
pkgver=4.4
pkgrel=1
pkgdesc="Professional-grade digital audio workstation"
arch=('i686' 'x86_64')
url="http://ardour.org/"
license=('GPL')
depends=('liblrdf' 'liblo' 'lilv' 'suil' 'taglib'
         'aubio' 'rubberband' 'gtkmm')
makedepends=('git' 'python2' 'boost' 'cppunit' 'doxygen' 'graphviz')
optdepends=('xjadeo: video monitoring'
            'harvid: video timeline')
source=("$pkgname-$pkgver::git+https://github.com/${pkgname^}/$pkgname.git#tag=$pkgver"
        "$pkgname.desktop")
md5sums=('SKIP'
         'c376b07a961137386d6b47594acb38ca')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  sed -i 's/python ${SRC}/python2 ${SRC}/' libs/pbd/wscript
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --with-backends="jack,alsa" \
                        --libjack=weak \
                        --optimize \
                        --docs \
                        --cxx11

  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  # install some freedesktop.org compatibility
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
