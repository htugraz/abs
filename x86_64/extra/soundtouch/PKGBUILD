# $Id: PKGBUILD 208129 2014-03-18 04:35:54Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=soundtouch
pkgver=1.8.0
pkgrel=1
pkgdesc="An audio processing library"
arch=('i686' 'x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs')
source=(http://www.surina.net/soundtouch/${pkgname}-${pkgver}.tar.gz)
sha1sums=('2cee2c63194227b4a58da01a7cd92b8632f139cd')

prepare() {
  cd ${pkgname}
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
}

build() {
  cd ${pkgname}
  [ "${CARCH}" = "x86_64" ] && CONFIGFLAG="--with-pic"
  ./bootstrap
  ./configure --prefix=/usr --enable-shared ${CONFIGFLAG}
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/soundtouch install
}
