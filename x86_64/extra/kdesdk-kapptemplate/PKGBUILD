# $Id: PKGBUILD 201148 2013-12-06 12:36:36Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-kapptemplate
pkgver=4.11.4
pkgrel=1
pkgdesc='KDE Template Generator'
url='http://kde.org/applications/development/kapptemplate/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kapptemplate-${pkgver}.tar.xz")
sha1sums=('5c015ec6fe1fa4155c1d9208d721bdbd0224a904')

build() {
  mkdir build
  cd build
  cmake ../kapptemplate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
