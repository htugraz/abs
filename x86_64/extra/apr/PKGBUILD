# $Id: PKGBUILD 200759 2013-12-04 19:27:48Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=apr
pkgver=1.5.0
pkgrel=1
pkgdesc="The Apache Portable Runtime"
arch=('i686' 'x86_64')
url="http://apr.apache.org/"
depends=('util-linux')
license=('APACHE')
source=(http://www.apache.org/dist/apr/apr-${pkgver}.tar.bz2{,.asc})
md5sums=('cc93bd2c12d0d037f68e21cc6385dc31'
         'SKIP')

build() {
  cd "${srcdir}/apr-${pkgver}"
  ./configure --prefix=/usr --includedir=/usr/include/apr-1 \
    --with-installbuilddir=/usr/share/apr-1/build \
    --enable-nonportable-atomics \
    --with-devrandom=/dev/urandom --disable-static
  make
}

check() {
  cd "${srcdir}/apr-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/apr-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
