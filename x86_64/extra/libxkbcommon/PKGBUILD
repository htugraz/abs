# $Id: PKGBUILD 220587 2014-08-23 09:31:43Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>

pkgbase=libxkbcommon
pkgname=(libxkbcommon libxkbcommon-x11)
pkgver=0.4.3
pkgrel=1
pkgdesc="Keyboard handling library using XKB data"
arch=(i686 x86_64)
url="http://xkbcommon.org/"
license=(custom)
depends=(xkeyboard-config glibc)
makedepends=(libxcb doxygen xorg-util-macros)
source=("http://xkbcommon.org/download/$pkgname-$pkgver.tar.xz")
md5sums=('26c57ff21438ed45de2a4ca609177db9')

prepare() {
  mkdir -p x11/usr/{include/xkbcommon,lib/pkgconfig}
}

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

package_libxkbcommon() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/libxkbcommon/LICENSE"

### Split libxkbcommon-x11

  mv "$pkgdir"/usr/lib/*x11* "$srcdir/x11/usr/lib"
  mv "$pkgdir"/usr/lib/pkgconfig/*x11* "$srcdir/x11/usr/lib/pkgconfig"
  mv "$pkgdir"/usr/include/xkbcommon/*x11* "$srcdir/x11/usr/include/xkbcommon"
}

package_libxkbcommon-x11() {
  pkgdesc="Keyboard handling library using XKB data for X11 XCB clients"
  depends=(libxkbcommon libxcb)

  mv x11/* "$pkgdir"

  install -Dm644 $pkgbase-$pkgver/COPYING "$pkgdir/usr/share/licenses/libxkbcommon-x11/LICENSE"
}
