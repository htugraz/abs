# $Id: PKGBUILD 249082 2015-10-13 07:37:15Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksudoku
pkgver=15.08.2
pkgrel=1
pkgdesc="A logic-based symbol placement puzzle"
url="http://kde.org/applications/games/ksudoku/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4' 'glu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/ksudoku-${pkgver}.tar.xz")
sha1sums=('6330106b1009d69d54fc7f99bf8bb744e5f02f58')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ksudoku-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
