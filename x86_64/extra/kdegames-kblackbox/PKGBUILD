# $Id: PKGBUILD 202524 2013-12-22 12:48:02Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kblackbox
pkgver=4.12.0
pkgrel=1
pkgdesc="A game of hide and seek played on a grid of boxes"
url="http://kde.org/applications/games/kblackbox/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kblackbox-${pkgver}.tar.xz")
sha1sums=('c70a8460147042abe3dd75109e15e67e960e4e61')

build() {
  mkdir build
  cd build
  cmake ../kblackbox-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
