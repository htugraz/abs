# $Id: PKGBUILD 244596 2015-08-23 08:54:49Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbreakout
pkgver=15.08.0
pkgrel=1
pkgdesc="A Breakout-like game"
url="http://kde.org/applications/games/kbreakout/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kbreakout')
replaces=('kdegames-kbreakout')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kbreakout-${pkgver}.tar.xz")
sha1sums=('56705e3cd4d76f6d3064d39bb256f02b577fb453')

build() {
  mkdir build
  cd build
  cmake ../kbreakout-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
