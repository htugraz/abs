# $Id: PKGBUILD 206544 2014-03-01 08:20:12Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=qimageblitz
pkgver=0.0.6
pkgrel=3
pkgdesc="A graphical effect and filter library for KDE"
arch=('i686' 'x86_64')
url="http://www.kde.org/"
license=('GPL2')
depends=('qt4')
makedepends=('cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('0ae2f7d4e0876764a97ca73799f61df4')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
