# $Id: PKGBUILD 208112 2014-03-18 02:28:52Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=glsof
pkgver=2.4.1
_fmver=2.4.1
_qver=1.0.0
pkgrel=1
pkgdesc="Two separate GUI utilities (Queries and Filemonitor) to the lsof command line utility"
arch=('i686' 'x86_64')
url="http://glsof.sourceforge.net/"
license=('GPL3')
depends=('sh' 'java-runtime' 'lsof')
source=(http://downloads.sourceforge.net/sourceforge/glsof/filemonitor-${_fmver}.tar.gz \
        http://downloads.sourceforge.net/sourceforge/glsof/queries-${_qver}/queries_linux32.tar.gz \
        http://downloads.sourceforge.net/sourceforge/glsof/queries-${_qver}/queries_linux64.tar.gz \
        glsof-filemonitor glsof-queries)
sha1sums=('adc9f668788e331990ca3420320751ceb9ef336a'
          'd5a17d66dd02cd78bad5ffdda0b936c7848e4890'
          'c5dd12e9b2860f1c427ed7cd482e898978051d2c'
          '9c3097119aa98723c5c59196e6089226829cf09d'
          '2797c326904af16a254c3e39b8c529ea14ef37e5')

package() {
  cd "${srcdir}"
  install -D -m755 glsof-filemonitor "${pkgdir}/usr/bin/glsof-filemonitor"
  install -D -m755 glsof-queries "${pkgdir}/usr/bin/glsof-queries"
  install -d "${pkgdir}/usr/share/java/glsof"
  install -D -m644 filemonitor/*.jar "${pkgdir}/usr/share/java/glsof"
  install -D -m644 filemonitor/*.txt "${pkgdir}/usr/share/java/glsof"
  if [ "$CARCH" = "i686" ]; then
    install -D -m644 queries_linux32/queries.jar "${pkgdir}/usr/share/java/glsof/queries.jar"
    install -D -m644 queries_linux32/queries.pdf "${pkgdir}/usr/share/doc/glsof/queries.pdf"
  elif [ "$CARCH" = "x86_64" ]; then
    install -D -m644 queries_linux64/queries.jar "${pkgdir}/usr/share/java/glsof/queries.jar"
    install -D -m644 queries_linux64/queries.pdf "${pkgdir}/usr/share/doc/glsof/queries.pdf"
  fi
}
