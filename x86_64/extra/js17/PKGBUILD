# $Id: PKGBUILD 183635 2013-04-25 18:13:54Z ioni $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=js17
pkgver=17.0.0
pkgrel=3
pkgdesc="JavaScript interpreter and libraries (legacy)"
arch=(i686 x86_64)
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/SpiderMonkey/Releases/17"
license=(MPL)
depends=(nspr gcc-libs readline)
makedepends=(python2 zip libffi)
conflicts=('js<24.2.0')
options=(!staticlibs)
source=(http://ftp.mozilla.org/pub/mozilla.org/js/mozjs$pkgver.tar.gz)
md5sums=('20b6f8f1140ef6e47daa3b16965c9202')

prepare() {
  cd mozjs$pkgver/js/src

  # Fix incompatibility with Perl 5.22
  sed -i 's/(defined\((@TEMPLATE_FILE)\))/\1/' config/milestone.pl
}

build() {
  unset CPPFLAGS
  cd mozjs$pkgver/js/src
  ./configure --prefix=/usr --with-system-nspr --with-system-ffi \
     --enable-readline --enable-threadsafe
  make
}

check() {
  cd mozjs$pkgver/js/src
  make check
}

package() {
  cd mozjs$pkgver/js/src
  make DESTDIR="$pkgdir" install
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -x {} +
}

# vim:set ts=2 sw=2 et:
