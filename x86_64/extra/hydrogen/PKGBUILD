# $Id: PKGBUILD 229601 2015-01-19 21:35:13Z schiv $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: see .contrib

pkgname=hydrogen
pkgver=0.9.6.1
pkgrel=1
pkgdesc="An advanced drum machine"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.hydrogen-music.org/"
depends=('libarchive' 'liblrdf' 'qt4' 'jack' 'libpulse')
optdepends=('rubberband')
makedepends=('cmake')
source=("https://github.com/hydrogen-music/hydrogen/archive/$pkgver.tar.gz")
install=$pkgname.install
md5sums=('527a44eec22fd31f55e6b3cf1089613d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # lash will have to wait until I bring it into [extra] --schiv
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DWANT_CPPUNIT=OFF \
          -DWANT_LRDF=ON
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
