# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua

pkgname=tomcat-native
pkgver=1.1.32
pkgrel=1
pkgdesc='Optional component for Tomcat to use certain native resources for performance, compatibility'
arch=('i686' 'x86_64')
url='http://tomcat.apache.org/native-doc/'
license=('APACHE')
depends=('apr')
makedepends=('java-environment')
options=('!emptydirs')
source=(http://www.apache.org/dist/tomcat/tomcat-connectors/native/${pkgver}/source/${pkgname}-${pkgver}-src.tar.gz)
sha256sums=('26936848f727e162f513bd2b5f7b2300d7d716a5e92e5f4ebcd0e1eff8e09584')

build() {
  cd "${srcdir}/tomcat-native-${pkgver}-src/jni/native"

  ./configure \
    --prefix=/usr \
    --with-java-home=/usr/lib/jvm/default \
    --with-apr=/usr \
    --with-ssl=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-src/jni/native"

  make DESTDIR="${pkgdir}" install
}
