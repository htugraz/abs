# $Id: PKGBUILD 216259 2014-07-02 15:52:08Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-screenshooter
pkgver=1.8.1
pkgrel=2
pkgdesc="Plugin that makes screenshots for the Xfce panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-screenshooter"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libsoup' 'hicolor-icon-theme')
makedepends=('intltool')
install=$pkgname.install
source=(http://archive.xfce.org/src/apps/$pkgname/1.8/$pkgname-$pkgver.tar.bz2)
conflicts=('xfce4-screenshooter-plugin')
replaces=('xfce4-screenshooter-plugin')
sha256sums=('40419892bd28989315eed053c159bba0f4264ed8c6c6738806024e481eab9492')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
