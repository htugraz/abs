# $Id: PKGBUILD 137896 2011-09-12 10:03:30Z pierre $
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=bc
pkgver=1.06
pkgrel=8
pkgdesc="An arbitrary precision calculator language"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/bc/"
license=('GPL')
depends=('readline')
replaces=('bc-readline')
conflicts=('bc-readline')
install=bc.install
source=("ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'build-fix.patch' 'bc-1.06-info-dir-entry.patch')
md5sums=('d44b5dddebd8a7a7309aea6c36fda117'
         'fc7ecbd9e55ef04c6d3a495692626116'
         'b6fe9869830aca0d34badf810fe0cfb3')

build() {
  export CFLAGS="${CFLAGS/-O?/-O3}"

  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/build-fix.patch"
  patch -p1 -i "${srcdir}/bc-1.06-info-dir-entry.patch"

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --infodir=/usr/share/info --with-readline
  make LEX="flex -I"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
