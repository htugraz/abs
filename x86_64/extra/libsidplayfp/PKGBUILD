# $Id: PKGBUILD 238572 2015-05-07 09:33:14Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Jonas Nyr√©n <jonas.nyren*mindkiller.com>

pkgname=libsidplayfp
pkgver=1.7.1
pkgrel=1
pkgdesc='Library to play Commodore 64 music derived from libsidplay2'
url='http://sourceforge.net/projects/sidplay-residfp/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gcc-libs')
makedepends=('vice' 'xa')
optdepends=('vice: better SID support')
source=("http://downloads.sourceforge.net/project/sidplay-residfp/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('0ff83093dbce8afec69ab0850f0ebe0a202e6436')

build() {
	cd "${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/sidplayfp"
	ln -t "${pkgdir}/usr/share/sidplayfp" -s /usr/lib/vice/C64/{kernal,basic,chargen}
}
