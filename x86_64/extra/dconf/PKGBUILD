# $Id: PKGBUILD 210422 2014-04-14 20:05:58Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=0.20.0
pkgrel=1
pkgdesc="A low-level configuration system."
arch=(i686 x86_64)
url="http://live.gnome.org/dconf"
license=('LGPL2.1')
depends=('glib2')
makedepends=('vala' 'gtk3' 'intltool' 'docbook-xsl' 'python' 'python2')
optdepends=('gtk3: for dconf-editor')
install=dconf.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('22c046a247d05ea65ad181e3aef4009c898a5531f76c0181f8ec0dfef83447d9')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/dconf
  make
}

package() {
  cd "$pkgname-$pkgver"
  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
