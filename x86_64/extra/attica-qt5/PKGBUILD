# $Id: PKGBUILD 216668 2014-07-08 18:31:49Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=attica-qt5
pkgver=5.0.0
pkgrel=1
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/attica'
pkgdesc='A Qt5 library that implements the Open Collaboration Services API'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/attica-${pkgver}.tar.xz")
md5sums=('73d1d1953b12eda42f6f4010df889b73')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../attica-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
