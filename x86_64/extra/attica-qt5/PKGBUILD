# $Id: PKGBUILD 225831 2014-11-10 08:23:24Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=attica-qt5
pkgver=5.4.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Qt5 library that implements the Open Collaboration Services API'
url='https://projects.kde.org/projects/frameworks/attica'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/attica-${pkgver}.tar.xz")
md5sums=('493c08ce82d5ab34bf2efeb1a01349ab')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../attica-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
