# $Id: PKGBUILD 219308 2014-08-05 08:44:16Z jgc $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgname=gnumeric
pkgver=1.12.17
pkgrel=1
pkgdesc="A GNOME Spreadsheet Program"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'psiconv' 'pygobject-devel' 'gnome-doc-utils'
             'gobject-introspection')
optdepends=('python2-gobject: for python plugin support'
            'perl: for perl plugin support'
            'psiconv: for Psion 5 file support'
            'yelp: for viewing the help manual')
install=gnumeric.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz
        revert-warnings.patch)
sha256sums=('ec5cb6cc02b3de7f86619e258deb68a3dcfd4c85e83a4048b924c307ef607ba1'
            'bcafca016b809000c2a5bf911e2e3dfa4de28f9e541d9964574cac5c7ce09e53')

prepare() {
  cd $pkgname-$pkgver

  patch -Np0 -i ../revert-warnings.patch

  # Remove invalid mime type. (FS#26338)
  sed -i -e 's/zz-application\/zz-winassoc-xls;//' gnumeric.desktop.in

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile --enable-introspection
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
