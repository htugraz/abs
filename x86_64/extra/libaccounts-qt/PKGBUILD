# $Id: PKGBUILD 206451 2014-02-26 19:59:34Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=libaccounts-qt
pkgname=('libaccounts-qt' 'libaccounts-qt-doc')
pkgver=1.11
pkgrel=1
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
makedepends=('qt4' 'libaccounts-glib' 'doxygen')
source=("http://accounts-sso.googlecode.com/files/accounts-qt-$pkgver.tar.bz2")
sha1sums=('a09eb0487db215d6b8809fc33790cdbb2f785393')

prepare() {
  cd accounts-qt-$pkgver
  sed -i 's|SUBDIRS  += Accounts tests|SUBDIRS += Accounts|' accounts-qt.pro

  sed -i 's|QHG_LOCATION           = qhelpgenerator|QHG_LOCATION = qhelpgenerator-qt4|' doc/doxy.conf
}

build() {
  cd accounts-qt-$pkgver
  qmake-qt4 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package_libaccounts-qt() {
  pkgdesc="Qt-based client library for accessing the online accounts database"
  depends=('qt4' 'libaccounts-glib')

  cd accounts-qt-$pkgver
  make INSTALL_ROOT="$pkgdir" install_subtargets
}

package_libaccounts-qt-doc() {
  cd accounts-qt-$pkgver
  make INSTALL_ROOT="$pkgdir" install_documentation
}
