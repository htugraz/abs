# $Id: PKGBUILD 47486 2011-05-23 15:55:23Z tdziedzic $
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-crypt-openssl-random
pkgver=0.06
pkgrel=2
pkgdesc="Interface to OpenSSL PRNG methods"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Crypt-OpenSSL-Random"
depends=('perl' 'openssl')
license=('GPL')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/R/RU/RURBAN/Crypt-OpenSSL-Random-$pkgver.tar.gz)
md5sums=('3771d3417f658d28f8682adc1e087136')

build() {
  cd  "$srcdir/Crypt-OpenSSL-Random-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd  "$srcdir/Crypt-OpenSSL-Random-$pkgver"

  make test
}

package() {
  cd  "$srcdir/Crypt-OpenSSL-Random-$pkgver"

  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
