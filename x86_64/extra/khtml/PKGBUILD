# $Id: PKGBUILD 225870 2014-11-10 08:30:39Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=khtml
pkgver=5.4.0
pkgrel=1
pkgdesc='KHTML APIs'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/khtml'
license=('LGPL')
depends=('giflib' 'kparts' 'kjs')
makedepends=('extra-cmake-modules')
groups=('kf5-aids')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=('65f90421ff7c061b025251ad07373d18')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
