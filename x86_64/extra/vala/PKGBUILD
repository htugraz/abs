# $Id: PKGBUILD 229443 2015-01-16 14:16:51Z anatolik $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>

pkgname=vala
pkgver=0.26.2
pkgrel=1
pkgdesc="Compiler for the GObject type system"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Vala"
license=('LGPL')
depends=('glib2')
makedepends=('libxslt')
checkdepends=('dbus' 'libx11' 'gobject-introspection')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('37f13f430c56a93b6dac85239084681fd8f31c407d386809c43bc2f2836e03c4')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --enable-vapigen
    make
}

check() {
    cd $pkgname-$pkgver
    make check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
