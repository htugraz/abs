# $Id: PKGBUILD 215928 2014-07-01 01:38:25Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=beep
pkgver=1.3
pkgrel=3
pkgdesc="Advanced PC speaker beeping program"
arch=('i686' 'x86_64')
url="http://www.johnath.com/beep"
license=('GPL2')
depends=('glibc')
source=(http://www.johnath.com/beep/${pkgname}-${pkgver}.tar.gz)
sha1sums=('8cba283e507dc9e401da011a9c92a2e24ea2bf70')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i "s:-Wall:${CFLAGS}:" Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -D -m755 beep "${pkgdir}/usr/bin/beep"
  install -D -m644 beep.1.gz "${pkgdir}/usr/share/man/man1/beep.1.gz"
}
