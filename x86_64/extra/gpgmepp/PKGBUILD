# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gpgmepp
pkgver=15.08.3
pkgrel=1
pkgdesc="C++ bindings/wrapper for gpgme"
arch=(i686 x86_64)
url="https://projects.kde.org/gpgmepp"
license=(LGPL)
depends=(gpgme qt5-base)
makedepends=(extra-cmake-modules python boost)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('1556129f2114bfddc1dd2fb3b75ecc03')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
