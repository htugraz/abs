# $Id: PKGBUILD 209251 2014-03-30 17:32:01Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=0.9.8
pkgrel=1
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="http://www.indilib.org/index.php?title=Main_Page"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libnova' 'cfitsio' 'boost-libs' 'libusb-compat' 'libjpeg' 'gsl')
makedepends=('cmake' 'boost')
options=('staticlibs') # libindiclient builds as static only
source=("http://www.indilib.org/jdownloads/Source/${pkgname}_${pkgver}.tar.gz")
md5sums=('2d31a15911d018d0cdf5f9da92a87f0f')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
