# $Id: PKGBUILD 223295 2014-09-26 17:22:57Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-picmi
pkgver=4.14.1
pkgrel=1
pkgdesc="A nonogram logic game for KDE"
url="http://kde.org/applications/games/picmi/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
replaces=('picmi')
conflicts=('picmi')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/picmi-${pkgver}.tar.xz")
sha1sums=('bbefedf62178ada10af8101d16d593e8450f5318')

build() {
  mkdir build
  cd build
  cmake ../picmi-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
