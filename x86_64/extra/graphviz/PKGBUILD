# $Id: PKGBUILD 198050 2013-10-30 12:29:51Z allan $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=graphviz
pkgver=2.34.0
pkgrel=2
pkgdesc='Graph visualization software'
url='http://www.graphviz.org/'
license=('CPL')
arch=('i686' 'x86_64')
depends=('libltdl' 'gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'gts')
makedepends=('swig' 'mono' 'guile' 'lua51' 'ocaml' 'perl' 'php' 'python2' 'r' 'tk' 'qt4')
optdepends=('mono: sharp bindings'
            'guile: guile bindings'
            'lua51: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'php: php bindings'
            'python2: python bindings'
            'r: r bindings'
            'tcl: tcl bindings'
            'qt4: gvedit')
source=("${url}/pub/${pkgname}/stable/SOURCES/${pkgname}-${pkgver}.tar.gz"
        'dotty.patch')
sha1sums=('5a0c00bebe7f4c7a04523db21f40966dc9f0d441'
          '31bc9f505c8b6470289a0d6ec31c237765cba239')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../dotty.patch
	sed \
		-e '/LIBPOSTFIX="64"/d' \
		-i configure
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export PYTHON=python2
	export LUA=lua5.1

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
