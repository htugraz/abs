# $Id: PKGBUILD 234660 2015-03-24 20:11:07Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=bluedevil-frameworks
pkgver=5.2.2
pkgrel=1
pkgdesc='Integrate the Bluetooth technology within KDE workspace and applications'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/bluedevil'
license=('GPL2')
depends=('libbluedevil-frameworks' 'kio')
makedepends=('extra-cmake-modules')
conflicts=('bluedevil')
install="$pkgname.install"
groups=('plasma')
source=("http://download.kde.org/stable/plasma/$pkgver/bluedevil-$pkgver.tar.xz")
md5sums=('007f7755d4b2f827323d1d379aa55f13')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../bluedevil-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
