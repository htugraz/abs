# $Id: PKGBUILD 241144 2015-06-20 15:35:19Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gdm
pkgname=(gdm libgdm)
pkgver=3.16.1.1
pkgrel=3
pkgdesc="Gnome Display Manager"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
depends=(gnome-shell gnome-session upower xorg-xrdb xorg-server xorg-server-xwayland xorg-xhost)
makedepends=(itstool intltool yelp-tools gobject-introspection)
checkdepends=('check')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz
        0001-Add-Arch-Linux-PAM-config-files.patch
        0002-Xsession-Don-t-start-ssh-agent-by-default.patch
        0003-manager-fix-monitor-hotplug-segfault.patch
        0004-gdm-x-wayland-session-don-t-start-dbus-daemon-if-unn.patch)
sha256sums=('2d2409fdc0a53b080377730a7775bc3239f8584829ff327d3465def0b20433db'
            'c1e5aec336ff14a74224b4aa76173e2b40e281431dc299ac991ad550d6cd7f0e'
            'f16effe4d9f015ebf3582e1bdedeaa9b85939d6d1a2aafae250c2a0dcbd6ae3e'
            'e22311e3c690b7127f01cad221cd1626848dbaae283aedc4f9897e2a3c8052e2'
            '553dc302b1d8b695a77fcff31ff28867a6eddf1e0775995a922a109225b07711')

prepare() {
  cd $pkgbase-$pkgver

  patch -Np1 -i ../0001-Add-Arch-Linux-PAM-config-files.patch
  patch -Np1 -i ../0002-Xsession-Don-t-start-ssh-agent-by-default.patch
  patch -Np1 -i ../0003-manager-fix-monitor-hotplug-segfault.patch
  patch -Np1 -i ../0004-gdm-x-wayland-session-don-t-start-dbus-daemon-if-unn.patch

  AUTOPOINT='intltoolize --automake -c' autoreconf -fi
}

build() {
  cd $pkgbase-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/gdm \
    --localstatedir=/var \
    --disable-static \
    --disable-schemas-compile \
    --with-at-spi-registryd-directory=/usr/lib/at-spi2-core \
    --with-check-accelerated-directory=/usr/lib/gnome-session \
    --with-authentication-agent-directory=/usr/lib/polkit-gnome \
    --with-gnome-settings-daemon-directory=/usr/lib/gnome-settings-daemon \
    --without-tcp-wrappers \
    --without-console-kit \
    --without-plymouth \
    --with-systemd \
    --with-default-pam-config=arch

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

check() {
  cd $pkgbase-$pkgver
  make check
}

package_gdm() {
  depends+=(libgdm)
  optdepends=('fprintd: fingerprint authentication')
  backup=(etc/pam.d/gdm-autologin etc/pam.d/gdm-fingerprint etc/pam.d/gdm-launch-environment
          etc/pam.d/gdm-password etc/pam.d/gdm-smartcard etc/gdm/custom.conf
          etc/gdm/Xsession etc/gdm/PostSession/Default etc/gdm/PreSession/Default)
  groups=(gnome)
  install=gdm.install

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  chmod 711 "$pkgdir/var/log/gdm"
  rm -r "$pkgdir/var/run"

### Split libgdm
  make -C libgdm DESTDIR="$pkgdir" uninstall
  mv "$pkgdir/usr/share/glib-2.0/schemas/org.gnome.login-screen.gschema.xml" "$srcdir"
}

package_libgdm() {
  pkgdesc="GDM support library"
  depends=(systemd glib2)
  install=libgdm.install

  cd $pkgbase-$pkgver
  make -C libgdm DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/org.gnome.login-screen.gschema.xml" \
    "$pkgdir/usr/share/glib-2.0/schemas/org.gnome.login-screen.gschema.xml"
}
