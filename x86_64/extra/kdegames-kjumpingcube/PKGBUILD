# $Id: PKGBUILD 202534 2013-12-22 12:48:18Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kjumpingcube
pkgver=4.12.0
pkgrel=1
pkgdesc="A simple tactical game"
url="http://kde.org/applications/games/kjumpingcube/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kjumpingcube-${pkgver}.tar.xz")
sha1sums=('c54a04bb1bc1769018ca31731190038ba3624ce7')

build() {
  mkdir build
  cd build
  cmake ../kjumpingcube-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
