# $Id: PKGBUILD 214874 2014-06-10 17:53:59Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kjumpingcube
pkgver=4.13.2
pkgrel=1
pkgdesc="A simple tactical game"
url="http://kde.org/applications/games/kjumpingcube/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kjumpingcube-${pkgver}.tar.xz")
sha1sums=('cb0d4131d6ad13cccb54cb2a4ae3a453ae7e15b1')

build() {
  mkdir build
  cd build
  cmake ../kjumpingcube-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
