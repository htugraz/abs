# $Id: PKGBUILD 220562 2014-08-22 16:21:39Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xine-ui
pkgver=0.99.9
pkgrel=1
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.xine-project.org"
depends=('xine-lib' 'curl' 'libxft' 'libxxf86vm' 'lirc-utils' 'xdg-utils'
         'shared-mime-info' 'hicolor-icon-theme' 'desktop-file-utils')
options=('!strip')
install=xine-ui.install
source=(http://downloads.sourceforge.net/xine/${pkgname}-${pkgver}.tar.xz)
sha1sums=('d4f4c17e93d984faa14e363db1320bf9cea7ebaf')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-x --enable-lirc --without-aalib --enable-debug
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" desktopdir=/usr/share/applications install
}
