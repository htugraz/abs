# $Id: PKGBUILD 204566 2014-01-22 23:15:09Z heftig $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=1.8.4
pkgrel=1
pkgdesc='Visual diff and merge tool'
url='http://meldmerge.org/'
license=('GPL')
arch=('any')
makedepends=('intltool' 'gnome-doc-utils')
depends=('pygtk' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'python2-dbus: open a new tab in an already running instance'
            'python2-gconf: gnome integration')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('4d0b87a367c0a0ee8bb0403cfb366ebe92b10418')

install=install
options=('!emptydirs')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's/env python/env python2/' bin/meld
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PYTHON=/usr/bin/python2 prefix=/usr
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PYTHON=/usr/bin/python2 prefix=/usr DESTDIR="${pkgdir}" install \
		update_desktop_database= update_mime_database=
}
