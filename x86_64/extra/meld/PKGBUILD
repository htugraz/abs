# $Id: PKGBUILD 202386 2013-12-21 15:03:15Z heftig $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=1.8.3
pkgrel=2
pkgdesc='Visual diff and merge tool'
url='http://meldmerge.org/'
license=('GPL')
arch=('any')
makedepends=('intltool' 'gnome-doc-utils')
depends=('pygtk' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'python2-dbus: open a new tab in an already running instance'
            'python2-gconf: gnome integration')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        fs38148.patch)
sha1sums=('b2321e45466a963cb7bd85e6b648c9648b35af52'
          'fd32ca8db546e7d63f99433de5724d642fba88f8')

install=install
options=('!emptydirs')

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix a crash when using Italian
  patch -Np1 -i ../fs38148.patch

  sed -i 's/update-/#update-/' Makefile
  sed -i 's/env python/env python2/' bin/meld
}

build() {
  cd ${pkgname}-${pkgver}
  make PYTHON=/usr/bin/python2 prefix=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make PYTHON=/usr/bin/python2 prefix=/usr DESTDIR="${pkgdir}" install
}
