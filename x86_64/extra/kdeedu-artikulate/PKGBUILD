# $Id: PKGBUILD 246855 2015-09-20 08:20:20Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-artikulate
pkgver=15.08.1
pkgrel=1
pkgdesc="Improve your pronunciation by listening to native speakers"
url="https://projects.kde.org/projects/kde/kdeedu/artikulate"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kdebase-runtime' 'qt4-gstreamer' 'kqtquickcharts')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/stable/applications/${pkgver}/src/artikulate-${pkgver}.tar.xz")
sha1sums=('3208de73738b1e3f840d33c058573111fb029a42')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../artikulate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
