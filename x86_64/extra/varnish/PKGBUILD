# $Id: PKGBUILD 200672 2013-12-01 15:48:29Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Douglas Soares de Andrade
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=varnish
pkgver=3.0.4
pkgrel=3
pkgdesc="High-performance HTTP accelerator"
arch=('i686' 'x86_64')
url="http://www.varnish-cache.org/"
license=('BSD')
depends=('gcc' 'libedit' 'pcre')
backup=('etc/varnish/default.vcl')
install=$pkgname.install
source=("http://repo.varnish-cache.org/source/$pkgname-$pkgver.tar.gz"
        '0001-Make-up-our-mind-Any-req.-we-receive-from-the-client.patch'
        varnish-vcl-reload
        varnish.service)
md5sums=('a130ce9c3504b9603a46542635e18701'
         '189d3b91645dec80710ae06e03d4200b'
         '03196dee7fc68e75069393d52c370762'
         '276f1761b3c7c6a83688038659194bbb')

prepare() {
  cd "$pkgname-$pkgver"

  # https://www.varnish-cache.org/trac/ticket/1367
  patch -Np1 <"$srcdir/0001-Make-up-our-mind-Any-req.-we-receive-from-the-client.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --sbindir=/usr/bin

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  install -m755 "$srcdir/varnish-vcl-reload" "$pkgdir/usr/bin"
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  # license
  install -Dm644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
