# $Id: PKGBUILD 242527 2015-07-27 16:29:53Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeclarative
pkgver=5.12.0
pkgrel=2
pkgdesc='Provides integration of QML and KDE Frameworks'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdeclarative'
license=('LGPL')
depends=('kio' 'libepoxy' 'kpackage')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz" 'cpu-plotter.patch')
md5sums=('62cb010adb59c15129743ad57fdc198a'
         '12f60b78bf77a69957349989d33ceeda')

prepare() {
  mkdir -p build

# Fix high CPU usage in plotter component http://bugs.kde.org/show_bug.cgi?id=348385
  cd $pkgname-$pkgver
  patch -p1 -i ../cpu-plotter.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
