# $Id: PKGBUILD 201357 2013-12-09 12:20:22Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xorg-xinit
pkgver=1.3.3
pkgrel=3
pkgdesc="X.Org initialisation program "
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth' 'inetutils')
makedepends=('xorg-util-macros')
optdepends=('xorg-twm' 'xterm')
backup=('etc/skel/.xinitrc'
        'etc/skel/.xsession'
        'etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
source=(${url}/releases/individual/app/xinit-${pkgver}.tar.bz2
        06_move_serverauthfile_into_tmp.diff
        fs25361.patch
        xinitrc
        xsession
        xserverrc
        cpp-4.8-compat.patch
        fix-globals-shadowing.patch)
options=(!emptydirs)
sha256sums=('74b2878de6e3d5b5948f1562a77e7f53b4e10621e505ddb278050c7f3cae00af'
            'a5c6ced5822bbb3145e0f30a4b12995782ca6d80ff2d927851ec91b5e41212cb'
            '431335ceb84f75aaa96fba285a596f61a63629f94756f8063e4f621ae143629b'
            '38a23848b49de8a5c635a128a8b32a6cd0a08a9ee153ad97cb25abd82cfda1bc'
            '2b94a42f2521cb5662fb9a5765a7507456dc0352ac9d0f44c56b359dcdb38898'
            '5e87bff928c143d9db9966048f6530e83e9e9a1cac09ce4c25f8aabc08611b6f'
            '194104efba9bcbf8ec0bd9ac1b145017d459b734afbc8bbfc37e361efd467a96'
            '6491c96dd0e98b6f5a45c4f0183eabcf2fb54728c81e793910c19d4107606bb1')

prepare() {
  cd xinit-${pkgver}
  patch -Np1 -i ../06_move_serverauthfile_into_tmp.diff
  patch -Np1 -i ../fs25361.patch
  patch -Np1 -i ../cpp-4.8-compat.patch
  patch -Np1 -i ../fix-globals-shadowing.patch
  sed -i -e 's/XSLASHGLOB.sh/XSLASHGLOB/' xinitrc.cpp
}

build() {
  cd xinit-${pkgver}
  autoreconf -fi
  ./configure --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd xinit-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/skel"
  install -m644 "${srcdir}/xinitrc" "${pkgdir}/etc/skel/.xinitrc"
  install -m644 "${srcdir}/xsession" "${pkgdir}/etc/skel/.xsession"
  install -m644 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
