# $Id: PKGBUILD 197777 2013-10-30 02:07:55Z eric $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: ezzetabi <ezzetabi at gawab dot com>

pkgname=ppl
pkgver=1.1
pkgrel=1
pkgdesc="A modern library for convex polyhedra and other numerical abstractions"
arch=('i686' 'x86_64')
url="http://bugseng.com/products/ppl"
license=('GPL3')
depends=('gmp')
options=('!docs')
source=(http://bugseng.com/products/ppl/download/ftp/releases/$pkgver/ppl-$pkgver.tar.gz{,.sign})
md5sums=('4f2422c0ef3f409707af32108deb30a7'
         'SKIP')

build() {
  cd ppl-$pkgver
  ./configure --prefix=/usr --enable-interfaces="c,cxx"
  make
}

check() {
  cd ppl-$pkgver
  make check
}

package() {
  cd ppl-$pkgver
  make DESTDIR="$pkgdir" install
}
