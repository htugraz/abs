# $Id: PKGBUILD 224610 2014-10-16 12:01:08Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=baobab
pkgver=3.14.1
pkgrel=1
pkgdesc="A graphical directory tree analyzer"
arch=(i686 x86_64)
url="http://gnome.org"
license=('GPL2')
depends=('dconf' 'gtk3' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('intltool' 'itstool' 'vala' 'gobject-introspection' 'python2')
groups=('gnome')
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('76966521858f3c46fedbf38deb8f9847737ef0bc3c5f330c51d1feb68a711d17')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # https://bugs.archlinux.org/task/31861
  sed -i '/MimeType/d' "$pkgdir/usr/share/applications/org.gnome.baobab.desktop"
}

# vim:set ts=2 sw=2 et:
