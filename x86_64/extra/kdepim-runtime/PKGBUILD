# $Id: PKGBUILD 202459 2013-12-22 12:46:39Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=4.12.0
pkgrel=1
pkgdesc='Extends the functionality of kdepim'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'libkgapi' 'libkolab' 'libkfbapi')
makedepends=('cmake' 'automoc4' 'boost')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('fe988ea74cf83f1dae99f81bc02eefaa834c664c')

prepare() {
  mkdir build
}

build() {
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
