# $Id: PKGBUILD 206479 2014-02-27 14:24:33Z svenstaro $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=nvidia-lts
pkgver=331.49
_extramodules=extramodules-3.10-lts
pkgrel=1
pkgdesc="NVIDIA drivers for linux-lts"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-lts>=3.10.26' "nvidia-utils=$pkgver" 'nvidia-libgl')
makedepends=('linux-lts-headers>=3.10.26')
provides=('nvidia')
license=('custom')
install=nvidia-lts.install
options=(!strip)
source=("ftp://download.nvidia.com/XFree86/Linux-x86/${pkgver}/NVIDIA-Linux-x86-${pkgver}.run"
        "ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run")
md5sums=('176c77f9a10eaa652860dbc24042bff2'
         'd0c944466a36cae8b1e23330b27a721c')

[[ "$CARCH" = "i686" ]] && _pkg="NVIDIA-Linux-x86-${pkgver}"
[[ "$CARCH" = "x86_64" ]] && _pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  sh ${_pkg}.run --extract-only
  cd "${_pkg}/kernel"
  make SYSSRC=/usr/lib/modules/${_kernver}/build module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
    "${pkgdir}/usr/lib/modules/${_extramodules}/kernel/drivers/video/nvidia.ko"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/kernel/drivers/video/nvidia.ko"
  install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-lts.conf"
  echo "blacklist nvidiafb" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-lts.conf"
  install -D -m644 "${srcdir}/${_pkg}/LICENSE" "${pkgdir}/usr/share/licenses/nvidia-lts/LICENSE"
}
