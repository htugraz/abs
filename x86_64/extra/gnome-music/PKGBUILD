# $Id: PKGBUILD 235872 2015-04-09 18:45:53Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=3.16.0
pkgrel=1
pkgdesc="Music player and management application"
arch=(i686 x86_64)
license=('GPL')
url="https://wiki.gnome.org/Apps/Music"
depends=(grilo grilo-plugins tracker python gobject-introspection libdmapsharing python-dbus dbus-glib gnome-themes-standard gtk3 gvfs python-gobject python-cairo gst-plugins-base)
makedepends=(intltool itstool python2)
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
groups=('gnome-extra')
install=gnome-music.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('910d8a60d65b70c8ae79a40afa7a4e372d5246ba14d16befba3c3982152b0a93')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="${pkgdir}" install
}
