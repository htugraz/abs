# $Id: PKGBUILD 236415 2015-04-14 01:19:25Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=3.16.1
pkgrel=1
pkgdesc="Music player and management application"
arch=(i686 x86_64)
license=('GPL')
url="https://wiki.gnome.org/Apps/Music"
depends=(grilo grilo-plugins tracker python gobject-introspection libdmapsharing python-dbus dbus-glib gnome-themes-standard gtk3 gvfs python-gobject python-cairo gst-plugins-base)
makedepends=(intltool itstool python2)
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
groups=('gnome-extra')
install=gnome-music.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('116945d87fbac6aea2aedfa8e6a98bef720675916a0a6e9ae21aa83d450ad2df')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="${pkgdir}" install
}
