# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=3.10.1
pkgrel=2
pkgdesc="Music player and management application"
arch=(i686 x86_64)
license=('GPL')
url="https://wiki.gnome.org/Apps/Music"
depends=(grilo grilo-plugins tracker python gobject-introspection libdmapsharing python-dbus dbus-glib gnome-icon-theme gnome-themes-standard gtk3 gvfs tracker python-gobject)
makedepends=(intltool)
groups=('gnome-extra')
install=gnome-music.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        notification.patch)
sha256sums=('3b75643e8a6e4d9b44b2572f0ddecfa0ec32affd9172c43a4bec909d15ac9d7c'
            '629ff5a277aecd97d3e1040d3eee70b8f011f3cf65c7fa8ec03fc22b829059fd')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../notification.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="${pkgdir}" install
}
