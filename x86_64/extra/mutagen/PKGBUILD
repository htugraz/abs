# $Id: PKGBUILD 226246 2014-11-14 16:08:09Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=mutagen
pkgname=('mutagen' 'python-mutagen')
pkgver=1.26
pkgrel=1
arch=('any')
url="https://bitbucket.org/lazka/mutagen"
license=('GPL2')
makedepends=('python' 'python2')
DLAGENTS=('https::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 --insecure -o %o %u')
source=(https://bitbucket.org/lazka/mutagen/downloads/${pkgbase}-${pkgver}.tar.gz{,.sig})
sha1sums=('776c31d4ddeff7797e4ea279a31d797fc474ae8b'
          'SKIP')

prepare() {
  cp -r ${pkgbase}-${pkgver} python-${pkgbase}-${pkgver}
}

check() {
  cd ${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python2 setup.py test
  cd ../python-${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python setup.py test
}

package_mutagen() {
  pkgdesc="An audio metadata tag reader and writer"
  depends=('python2')
 
  cd ${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}"
}

package_python-mutagen() {
  pkgdesc="An audio metadata tag reader and writer library for Python 3"
  depends=('python')

  cd python-${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}"
  rm -r "${pkgdir}"/usr/{bin,share}
}
