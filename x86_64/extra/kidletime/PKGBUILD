# $Id: PKGBUILD 242057 2015-07-10 13:35:18Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kidletime
pkgver=5.12.0
pkgrel=1
pkgdesc='Monitoring user activity'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kidletime'
license=('LGPL')
depends=('qt5-x11extras' 'libxss')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('f87b44e5acd0c3a0ec891450a11f6c2b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
