# $Id: PKGBUILD 204132 2014-01-15 10:16:51Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kbruch
pkgver=4.12.1
pkgrel=1
pkgdesc="Exercise Fractions"
url="http://kde.org/applications/education/kbruch/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kbruch-${pkgver}.tar.xz")
sha1sums=('12fbc445327ef3e5535b5e302ee417f915615fce')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kbruch-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
