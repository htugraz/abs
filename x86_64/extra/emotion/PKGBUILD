# $Id: PKGBUILD 199733 2013-11-15 12:15:33Z ronald $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=emotion
pkgver=1.7.9
pkgrel=1
pkgdesc="Library to easily integrate media playback into EFL applications"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('gstreamer0.10' 'edje' 'eeze')
options=('!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('c7817d6252ed90d06f9e4ef491f9399fb6213990')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-generic \
    --enable-gstreamer \
    --disable-generic-vlc \
    --disable-xine \
    --disable-install-examples \
    --disable-doc
  make
}

package(){
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # install license files
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING
}

