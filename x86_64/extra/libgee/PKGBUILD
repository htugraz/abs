# $Id: PKGBUILD 230338 2015-01-31 19:11:49Z anatolik $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libgee
pkgver=0.16.1
pkgrel=1
pkgdesc="GObject collection library"
url="http://live.gnome.org/Libgee"
license=(LGPL2.1)
arch=(i686 x86_64)
depends=(glib2)
makedepends=(gobject-introspection vala python2)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('d95f8ea8e78f843c71b1958fa2fb445e4a325e4821ec23d0d5108d8170e564a5')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  # generates a bazillion traps - make sure systemd's core_pattern
  # is deactivated, or you'll DoS the journal and the system
  #make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
