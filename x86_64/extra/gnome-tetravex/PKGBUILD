# $Id: PKGBUILD 248605 2015-10-08 19:52:00Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-tetravex
pkgver=3.18.0
pkgrel=1
pkgdesc="Complete the puzzle by matching numbered tiles"
arch=(i686 x86_64)
license=(GPL)
depends=(hicolor-icon-theme gtk3)
makedepends=(intltool itstool gobject-introspection vala)
conflicts=(gnome-games)
replaces=(gnome-games)
options=(!emptydirs)
install=gnome-tetravex.install
url="https://live.gnome.org/Tetravex"
groups=(gnome-extra)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('ea00ff5ea9a3b583c4d755cefa2725dd7b3b0781972af1c56377c7eda48cb579')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
