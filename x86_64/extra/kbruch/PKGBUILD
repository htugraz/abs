# $Id: PKGBUILD 249029 2015-10-13 07:14:27Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbruch
pkgver=15.08.2
pkgrel=1
pkgdesc="Exercise Fractions"
url="http://kde.org/applications/education/kbruch/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kxmlgui')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
install=${pkgname}.install
conflicts=('kdeedu-kbruch')
replaces=('kdeedu-kbruch')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kbruch-${pkgver}.tar.xz")
sha1sums=('22d0997c646e540f1b958c44899e32410c6116c0')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kbruch-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
