# $Id: PKGBUILD 197910 2013-10-30 11:37:30Z allan $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=dmapi
pkgver=2.2.12
pkgrel=2
pkgdesc="Data migration API"
arch=('i686' 'x86_64')
url="http://oss.sgi.com/projects/xfs/"
license=('GPL')
depends=('glibc')
makedepends=('xfsprogs')
provides=('xfsdmapi')
conflicts=('xfsdmapi')
replaces=('xfsdmapi')
source=("ftp://oss.sgi.com/projects/xfs/cmd_tars/$pkgname-$pkgver.tar.gz")

build() {
  cd $srcdir/dmapi-$pkgver
  [ "$CARCH" = "x86_64" ] && export CFLAGS="$CLFAGS -fPIC"
  export INSTALL_USER=root
  export INSTALL_GROUP=root
  export DEBUG=-DNDEBUG
  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/dmapi-$pkgver
  make DIST_ROOT=$pkgdir install install-dev
  # the static lib
  install -D -m644 libdm/.libs/libdm.a $pkgdir/usr/lib/libdm.a
  # and the header file, please
  install -D -m644 include/dmapi.h $pkgdir/usr/include/xfs/dmapi.h
  # kill libexec dir
  rm -rf $pkgdir/usr/libexec
}
md5sums=('cd825d4e141c16011367e0a0dd98c9c5')
