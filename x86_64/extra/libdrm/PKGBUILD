# $Id: PKGBUILD 234953 2015-03-26 07:48:48Z tpowa $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm
pkgver=2.4.60
pkgrel=2
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('valgrind')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig}
        COPYING
        0001-xf86drm-Fix-ioctl-struct-clearing-in-drmAgpEnable.patch)
sha256sums=('99575fc6c8e31f59193f5320fd4db7a5478e2641b5266147caab9aa875b59889'
            'SKIP'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93'
            'b9a4e77e50a0c11fbdf95b277fe4a6943eba3a63b5b90c548cbc5159db1d32f3')
validpgpkeys=('B97BD6A80CAC4981091AE547FE558C72A67013C3') # Maarten Lankhorst <maarten.lankhorst@canonical.com>
validpgpkeys+=('215DEE688925CCB965BE5DA97C03D7797B6E1AE2') # Damien Lespiau <damien.lespiau@intel.com>

prepare() {
  cd $pkgname-$pkgver
  
  # fix permission issue on r128 cards, pushed by Connor Behan
  # tested by Tobias Powalowski
  patch -Np1 -i ${srcdir}/0001-xf86drm-Fix-ioctl-struct-clearing-in-drmAgpEnable.patch

  # pthread is useless in Linux
  sed -i "/pthread-stubs/d" configure.ac
  autoreconf --force --install

}
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-udev
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
