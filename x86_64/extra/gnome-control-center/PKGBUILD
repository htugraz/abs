# $Id: PKGBUILD 198004 2013-10-30 12:12:43Z allan $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-control-center
pkgver=3.10.1
pkgrel=1
pkgdesc="The Control Center for GNOME"
license=(GPL)
arch=(i686 x86_64)
depends=(accountsservice cups-pk-helper gnome-bluetooth gnome-desktop gnome-menus
         gnome-online-accounts gnome-settings-daemon gsettings-desktop-schemas gtk3
         libgtop network-manager-applet sound-theme-freedesktop upower libpwquality
         gnome-color-manager smbclient modemmanager libgnomekbd)
makedepends=(gnome-doc-utils intltool docbook-xsl gnome-common)
optdepends=('system-config-printer: Printer settings'
            'gnome-user-share: Bluetooth and WebDAV file sharing'
            'rygel: media sharing'
            'vino: screen sharing'
            'openssh: remote login')
groups=(gnome)
url="http://www.gnome.org"
install=gnome-control-center.install
options=('!emptydirs')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        buildfix.patch)
sha256sums=('fbfc1a935c33901c6afc999160696e0cde60ccb9997679cc8d0a2ff952568cbe'
            'f58644e8735d3eb1e911ba2373e6c91cfde67b1ceaf54dca179b3a1c8db7466d')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../buildfix.patch
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static --disable-silent-rules

  #https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  # Fix a warning
  chown 102:0 "$pkgdir/usr/share/polkit-1/rules.d"
  chmod 700 "$pkgdir/usr/share/polkit-1/rules.d"
}
