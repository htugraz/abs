# $Id: PKGBUILD 226917 2014-11-24 07:17:18Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-blinken
pkgver=4.14.3
pkgrel=1
pkgdesc="Memory Enhancement Game"
url="http://kde.org/applications/education/blinken/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/blinken-${pkgver}.tar.xz")
sha1sums=('a8d03b08fd768cccfb74c6d06cd544f575429954')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../blinken-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
