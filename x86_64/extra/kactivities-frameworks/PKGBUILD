# $Id: PKGBUILD 228816 2015-01-09 07:50:29Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-frameworks
pkgver=5.6.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Core components for the KDE's Activities"
url='https://projects.kde.org/projects/frameworks/kactivities'
license=('LGPL')
depends=('kcmutils' 'kdeclarative')
makedepends=('extra-cmake-modules' 'boost' 'python')
provides=('kactivities')
conflicts=('kactivities4' 'libkactivities-frameworks')
replaces=('libkactivities-frameworks')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/kactivities-${pkgver}.tar.xz")
md5sums=('b44b8db3ab72eb5a6ae35a10a51c392a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
