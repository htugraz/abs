# $Id: PKGBUILD 213046 2014-05-17 19:05:34Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kactivities-frameworks
pkgname=('kactivities-frameworks' 'libkactivities-frameworks')
pkgver=4.99.0.c67df41
pkgrel=1
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('LGPL')
makedepends=('qt5-declarative' 'kservice' 'extra-cmake-modules' 'boost' 'git')
groups=('kf5')
#source=("http://download.kde.org/unstable/frameworks/${pkgver}/kactivities-${pkgver}.tar.xz")
source=('git://anongit.kde.org/kactivities.git#commit=c67df4161a9b2ba4b9c34be31ddb95b15fa42c2a')
md5sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package_kactivities-frameworks() {
  pkgdesc="The KActivity Manager Daemon"
  depends=('kwindowsystem' 'libkactivities-frameworks')
  provides=('kactivities')
  conflicts=('kactivities')

  cd build
  install -Dm755 src/service/kactivitymanagerd \
    "${pkgdir}"/usr/bin/kactivitymanagerd
}

package_libkactivities-frameworks() {
  pkgdesc="Core components for the KDE's Activities"
  depends=('kconfig' 'qt5-declarative' 'ki18n' 'kcoreaddons' 'kdbusaddons')

  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm "${pkgdir}"/usr/bin/kactivitymanagerd
  rmdir "${pkgdir}"/usr/bin
}
