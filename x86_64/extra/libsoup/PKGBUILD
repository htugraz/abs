# $Id: PKGBUILD 227995 2014-12-24 11:32:42Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libsoup
pkgver=2.48.1
pkgrel=1
pkgdesc="GNOME HTTP Library"
arch=(i686 x86_64)
license=(LGPL)
url="http://www.gnome.org"
depends=(glib2 libxml2 glib-networking sqlite)
makedepends=(intltool gobject-introspection python python2)
checkdepends=(apache php php-apache)
provides=("libsoup-gnome=$pkgver-$pkgrel")
conflicts=(libsoup-gnome)
replaces=(libsoup-gnome)
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('9b0d14b36e36a3131a06c6e3aa7245716e6904e3e636df81c0b6c8bd3f646f9a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
