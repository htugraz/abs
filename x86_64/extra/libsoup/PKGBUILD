# $Id: PKGBUILD 248642 2015-10-08 19:52:57Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libsoup
pkgver=2.52.0
pkgrel=1
pkgdesc="GNOME HTTP Library"
arch=(i686 x86_64)
license=(LGPL)
url="http://www.gnome.org"
depends=(glib2 libxml2 glib-networking sqlite)
makedepends=(intltool gobject-introspection python vala)
checkdepends=(apache php php-apache)
provides=("libsoup-gnome=$pkgver-$pkgrel")
conflicts=(libsoup-gnome)
replaces=(libsoup-gnome)
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('6c6c366622a1a9d938e0cea9b557fa536f088784251d31381ccd1b115a466785')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
#  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
