# $Id: PKGBUILD 189971 2013-07-12 02:57:43Z eric $
# Maintainer:
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=perl-bit-vector
pkgver=7.3
pkgrel=1
pkgdesc='Efficient bit vector, set of integers and "big int" math library'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'PerlArtistic')
url="http://search.cpan.org/dist/Bit-Vector/"
depends=('perl-carp-clan' 'perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/ST/STBEY/Bit-Vector-${pkgver}.tar.gz)
md5sums=('684e63a93db1e0eda25a103258b39725')

build() {
  cd Bit-Vector-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Bit-Vector-$pkgver
  make test
}

package() {
  cd Bit-Vector-$pkgver
  make DESTDIR="${pkgdir}" install
}
