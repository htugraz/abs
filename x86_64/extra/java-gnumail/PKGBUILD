# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=gnumail
pkgname=java-${_libname}
pkgver=1.1.2
pkgrel=2
pkgdesc="GNU implementation of the JavaMail API specification, version 1.3"
arch=('any')
url="http://www.gnu.org/s/classpathx/javamail/javamail.html"
license=('GPL')
depends=('java-runtime-headless')
makedepends=('apache-ant' 'java-activation-gnu' 'java-inetlib')
source=(http://ftp.gnu.org/gnu/classpathx/mail-${pkgver}.tar.gz)
md5sums=('0a94ff4328ceb6a4131be96946976a33')

build() {
  cd "${srcdir}/mail-${pkgver}"

  ant \
    -lib /usr/share/java/inetlib.jar \
    dist
}

package() {
  cd "${srcdir}/mail-${pkgver}"

  install -dm755 ${pkgdir}/usr/share/java/${_libname}
  for j in gnumail gnumail-providers; do
    install -m644 ${j}.jar ${pkgdir}/usr/share/java/${_libname}/${j}-${pkgver}.jar
    ln -s ${_libname}/${j}-${pkgver}.jar ${pkgdir}/usr/share/java/${j}.jar
  done
}
