# $Id: PKGBUILD 205646 2014-02-10 10:57:51Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-okular
pkgver=4.12.2
pkgrel=1
pkgdesc='Document Viewer'
arch=('i686' 'x86_64')
url="http://kde.org/applications/graphics/okular/"
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime' 'qimageblitz' 'chmlib' 'djvulibre' 'libspectre'
         'libkexiv2')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/okular-${pkgver}.tar.xz")
sha1sums=('02f8276f0f7d5d3425d1204fded7ee3cec8637e6')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../okular-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_ActiveApp=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
