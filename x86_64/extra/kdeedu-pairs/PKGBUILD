# $Id: PKGBUILD 240062 2015-06-02 06:59:18Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-pairs
pkgver=15.04.2
pkgrel=1
pkgdesc="A memory and pairs game for KDE"
url="https://projects.kde.org/projects/kde/kdeedu/pairs"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/pairs-${pkgver}.tar.xz")
sha1sums=('1ed3ee71341d6cafa9864d26f47aca2764a7982e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../pairs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
