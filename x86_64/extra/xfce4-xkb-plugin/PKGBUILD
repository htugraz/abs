# $Id: PKGBUILD 233327 2015-03-10 18:23:29Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-xkb-plugin
pkgver=0.7.1
pkgrel=2
pkgdesc="Plugin to switch keyboard layouts for the Xfce4 panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-xkb-plugin"
license=('custom')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxklavier' 'librsvg')
makedepends=('intltool')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('61fe2e33fe99939d5d06a682e94e40e05ef844c930ad612154090d158b2ce681')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
