# $Id: PKGBUILD 192010 2013-08-04 05:25:31Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xload
pkgver=1.1.2
pkgrel=1
pkgdesc="System load average display for X"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxmu' 'libxt' 'libx11')
makedepends=('xorg-util-macros')
source=(http://xorg.freedesktop.org/archive/individual/app/xload-${pkgver}.tar.bz2)
sha256sums=('83f8e6260435f1df2e2e5036bb3325688b79f0b33069ef445eff5058d127e078')

build() {
  cd "${srcdir}/xload-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xload-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
