# $Id: PKGBUILD 250147 2015-11-06 14:38:20Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
pkgbase=python-systemd
pkgname=(python-systemd python2-systemd)
pkgver=231
pkgrel=1
_commit=b71b8b64ebb28ddbf1130e9127f1e9fc5fb6117f
pkgdesc="Python bindings for systemd"
arch=(i686 x86_64)
url="https://github.com/systemd/python-systemd"
license=(LGPL)
makedepends=(systemd python python2 git)
source=("$pkgbase::git+${url}#tag=v${pkgver}")
sha256sums=('SKIP')

prepare() {
  cp -a $pkgbase python2
  mv $pkgbase python3
}

build() {
  cd python3
  make PYTHON=/usr/bin/python3

  cd ../python2
  make PYTHON=/usr/bin/python2
}

package_python-systemd() {
  depends=(python3 libsystemd)

  cd python3
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-systemd() {
  depends=(python2 libsystemd)

  cd python2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
