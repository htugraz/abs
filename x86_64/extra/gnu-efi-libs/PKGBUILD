# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

[[ "${CARCH}" == "x86_64" ]] && _EFI_ARCH="x86_64"
[[ "${CARCH}" == "i686" ]] && _EFI_ARCH="ia32"

_pkgver="3.0"
pkgname="gnu-efi-libs"
pkgver="${_pkgver}w"
pkgrel="2"
pkgdesc="Library for building UEFI Applications using GNU toolchain"
url="http://sourceforge.net/projects/gnu-efi/"
license=('GPL')
arch=('x86_64' 'i686')
options=('!strip' '!makeflags' '!buildflags' 'staticlibs')
makedepends=('pciutils')
source=("http://download.sourceforge.net/gnu-efi/gnu-efi_${pkgver}.orig.tar.gz"
        "build-fix.patch")

prepare() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	patch -p1 -i "${srcdir}/build-fix.patch"
}

build() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	make -j1
	make -j1 -C apps all

}

package() {
	cd "${srcdir}/gnu-efi-${_pkgver}/"

	make INSTALLROOT="${pkgdir}" PREFIX="/usr" LIBDIR="/usr/lib" install

	install -d "${pkgdir}/usr/share/gnu-efi/apps/${_EFI_ARCH}/"
	install -D -m0644 "${srcdir}/gnu-efi-${_pkgver}/apps"/*.efi "${pkgdir}/usr/share/gnu-efi/apps/${_EFI_ARCH}/"
}

md5sums=('36d1c5e7b6edd4733700aaf749d9b80c'
         'c4f8e361fd3e133da1ef8307a302c424')
