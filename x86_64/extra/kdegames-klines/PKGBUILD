# $Id: PKGBUILD 211417 2014-04-18 18:02:31Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-klines
pkgver=4.13.0
pkgrel=1
pkgdesc="A simple but highly addictive, one player game"
url="http://kde.org/applications/games/klines/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/klines-${pkgver}.tar.xz")
sha1sums=('d1af4dafd0feb9f62394b2f4f410993a72c45d33')

build() {
  mkdir build
  cd build
  cmake ../klines-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
