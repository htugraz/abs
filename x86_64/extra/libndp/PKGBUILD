# $Id: PKGBUILD 216514 2014-07-04 20:47:43Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libndp
pkgver=1.3
pkgrel=1
pkgdesc="Library for Neighbor Discovery Protocol"
arch=(i686 x86_64)
url="http://libndp.org/"
license=(LGPL2.1)
depends=(glibc)
source=($url/files/$pkgname-$pkgver.tar.gz)
sha256sums=('58a013427debfc554f1ff6555bccfd3ba910d50effc8231cfcad79f0f19c3f9e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
