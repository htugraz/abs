# $Id: PKGBUILD 226836 2014-11-22 17:22:32Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xorg-xkbcomp
pkgver=1.3.0
pkgrel=1
pkgdesc="X Keyboard description compiler"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxkbfile')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(${url}/releases/individual/app/xkbcomp-${pkgver}.tar.bz2{,.sig})
sha256sums=('cfac973778fabf5216121ad60b7af8ab74ce7513af0f9260cf8c5309e1622b2a'
            'SKIP')

build() {
  cd xkbcomp-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xkbcomp-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
