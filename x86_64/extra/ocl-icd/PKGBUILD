# $Id: PKGBUILD 241166 2015-06-21 20:02:56Z lcarlier $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=ocl-icd
pkgver=2.2.7
_pkgver=667
pkgrel=1
pkgdesc="OpenCL ICD Bindings"
arch=('i686' 'x86_64')
url="https://forge.imag.fr/projects/ocl-icd/"
license=('BSD')
depends=('glibc')
makedepends=('ruby' 'opencl-headers>=1.2')
checkdepends=()
provides=('libcl' 'opencl-icd-loader')
conflicts=('libcl')
replaces=('libcl')
source=(http://forge.imag.fr/frs/download.php/$_pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('23dd2615d7d73257b326d3dd6c519649')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

# fail in i686
#check() {
#  cd "$srcdir/$pkgname-$pkgver"
#  make -k check
#}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -m755 -d "$pkgdir/usr/share/licenses/ocl-icd"
  install -m644 "$srcdir/$pkgname-$pkgver/COPYING" "${pkgdir}/usr/share/licenses/ocl-icd/"
}

# vim:set ts=2 sw=2 et:
