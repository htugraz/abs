# $Id: PKGBUILD 217676 2014-07-21 20:29:20Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-cervisia
pkgver=4.13.3
pkgrel=1
pkgdesc='CVS Frontend'
url='http://kde.org/applications/development/cervisia/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/cervisia-${pkgver}.tar.xz")
sha1sums=('ee72b49f9a4e0ae9196e6675bd64c32c0ccd6afb')

build() {
  mkdir build
  cd build
  cmake ../cervisia-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
