# $Id: PKGBUILD 198547 2013-10-30 15:31:55Z allan $
# Maintainer: tobias <tobias at archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-clipman-plugin
pkgver=1.2.3
pkgrel=2
pkgdesc="A clipboard plugin for the Xfce4 panel"
arch=('i686' 'x86_64')
license=('GPL')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-clipman-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libunique' 'libxtst')
makedepends=('intltool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('61f3be97efa379cb358980c94e14692a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|$(TARGET_DIR)/C/media|$(docdir)/html/C/media|' doc/Makefile.in
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-unique \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
