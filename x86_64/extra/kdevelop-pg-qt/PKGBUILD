# $Id: PKGBUILD 255335 2015-12-10 04:52:11Z foutrelis $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-pg-qt
pkgver=1.0.0
pkgrel=4
pkgdesc="KDevelop Parser Generator, a LL(1) parser generator used by KDevelop language plugins"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'fix-build.patch')
sha1sums=('468bc91c5be16061dc265e9471f75462b32b73d5'
          '9e085b24a89cf7f1c986dcc1fa16d23968c20882')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/fix-build.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
