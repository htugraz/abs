# $Id: PKGBUILD 206636 2014-03-03 22:02:08Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=yelp
pkgver=3.10.2
pkgrel=1
pkgdesc="A help browser for GNOME"
arch=('i686' 'x86_64')
depends=('webkitgtk' 'yelp-xsl' 'libxslt' 'hicolor-icon-theme' 'xz' 'bzip2'
         'desktop-file-utils' 'dconf')
makedepends=('intltool' 'gtk-doc' 'itstool')
groups=('gnome')
license=('GPL')
options=('!emptydirs')
url="http://www.gnome.org"
install=yelp.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('31cdff0df058fd42752808b6454b774281505f42695ee8b20891eb1332c34338')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
