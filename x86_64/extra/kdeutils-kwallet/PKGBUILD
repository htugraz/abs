# $Id: PKGBUILD 202611 2013-12-22 12:50:29Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kwallet
pkgver=4.12.0
pkgrel=1
pkgdesc='Wallet Management Tool'
url='http://kde.org/applications/system/kwalletmanager/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kwallet-${pkgver}.tar.xz")
sha1sums=('44c7e225989b77fd9be7e6804bf3f4702c62efa3')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kwallet-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
