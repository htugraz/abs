# $Id: PKGBUILD 198979 2013-11-05 20:26:01Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kwallet
pkgver=4.11.3
pkgrel=1
pkgdesc='Wallet Management Tool'
url='http://kde.org/applications/system/kwalletmanager/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kwallet-${pkgver}.tar.xz")
sha1sums=('7d9a7cfdec09d7acf662aba75478bbd41b52e1ad')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kwallet-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
