# $Id: PKGBUILD 234253 2015-03-20 20:09:44Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktimer
pkgver=15.08.3
pkgrel=1
pkgdesc='Countdown Launcher'
url='http://kde.org/applications/utilities/ktimer/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kio' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdeutils-ktimer')
replaces=('kdeutils-ktimer')
groups=('kde-applications' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/ktimer-${pkgver}.tar.xz")
sha1sums=('656ca15a30f1fca1c34ff4efd3e4c34a4ddd82f4')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../ktimer-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
