# $Id: PKGBUILD 209566 2014-03-31 10:21:59Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kget
pkgver=4.12.4
pkgrel=1
pkgdesc='Download Manager'
url='http://kde.org/applications/internet/kget/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-workspace' 'kdebase-lib' 'libktorrent' 'libmms' 'gpgme' 'nepomuk-widgets')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kget-${pkgver}.tar.xz")
sha1sums=('94beea1646a746af92679665d2d1b2c38a35abe9')

build() {
  mkdir build
  cd build
  cmake ../kget-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
