# $Id: PKGBUILD 206883 2014-03-05 14:38:19Z andrea $
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=libpqxx
pkgver=4.0.1
pkgrel=2
pkgdesc="C++ client API for PostgreSQL"
arch=('i686' 'x86_64')
license=('custom')
url="http://pqxx.org/development/libpqxx/"
depends=('postgresql-libs>=8.4.1')
makedepends=('python2')
source=("http://pqxx.org/download/software/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('6ea888b9ba85dd7cef1b182dc5f223a2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|python|python2|' tools/splitconfig
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
