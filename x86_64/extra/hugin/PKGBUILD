# $Id: PKGBUILD 243143 2015-08-10 09:49:15Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2015.0.0
pkgrel=1
pkgdesc='Panorama photo stitcher'
url='http://hugin.sourceforge.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('cmake' 'boost' 'tclap' 'mesa' 'swig')
depends=('wxgtk' 'boost-libs' 'enblend-enfuse' 'exiv2' 'libpano13'
         'python' 'lensfun' 'lapack' 'make' 'sqlite' 'perl-image-exiftool'
         'desktop-file-utils')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver%.*}/${pkgname}-${pkgver/r/_r}.tar.bz2"
        "http://downloads.sourceforge.net/boost/boost_1_55_0.tar.bz2")
sha1sums=('be7153a0791f2147153e93e8711912ef23b60b61'
          'cef9a0cc7084b1d639e06cd3bc34e4251524c840')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver/rc?}"
	cmake . \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_LAPACK=yes \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver/rc?}"
	make DESTDIR="${pkgdir}" install
}
