# $Id: PKGBUILD 246919 2015-09-20 08:22:00Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kruler
pkgver=15.08.1
pkgrel=1
pkgdesc="Screen Ruler"
url="http://kde.org/applications/graphics/kruler/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('knotifications' 'kxmlgui')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegraphics-kruler')
replaces=('kdegraphics-kruler')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kruler-${pkgver}.tar.xz")
sha1sums=('0bb8d04234261628a91ecb4c4870cf83d145f21e')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kruler-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
