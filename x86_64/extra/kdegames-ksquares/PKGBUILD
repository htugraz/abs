# $Id: PKGBUILD 231006 2015-02-07 09:39:32Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksquares
pkgver=14.12.2
pkgrel=1
pkgdesc="A modeled after the well known pen and paper based game of Dots and Boxes"
url="http://kde.org/applications/games/ksquares/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/ksquares-${pkgver}.tar.xz")
sha1sums=('89961f4e6b66aa347bd91c2de46bf7a48c769a45')

build() {
  mkdir build
  cd build
  cmake ../ksquares-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
