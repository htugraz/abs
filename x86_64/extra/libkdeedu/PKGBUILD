# $Id: PKGBUILD 223210 2014-09-26 17:20:44Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdeedu
pkgver=4.14.1
pkgrel=1
pkgdesc="Libraries used by KDE Education applications"
url="https://projects.kde.org/projects/kde/kdeedu/libkdeedu"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
replaces=('kdeedu-libkdeedu' 'kdeedu-data')
options=('staticlibs') # libqtmmlwidget.a needed by KAlgebra
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('97288b7f357681c847a70b245f67711df979ae1e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
