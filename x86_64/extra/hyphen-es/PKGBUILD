# $Id: PKGBUILD 200368 2013-11-27 08:13:27Z angvp $
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=hyphen-es
pkgver=0.7
pkgrel=1
epoch=1
pkgdesc="Spanish hyphenation rules"
arch=('any')
url="https://forja.rediris.es/projects/rla-es/"
license=('LGPL3')
optdepends=('hyphen: offers hyphenation library functions')
noextract=('es_ANY.oxt')
source=('http://forja.rediris.es/frs/download.php/2933/es_ANY.oxt')
md5sums=('e3d4b38f280e7376178529db2ece982b')

package() {
  cd "$srcdir"
  LANG=en_US.utf8 bsdtar -xf es_ANY.oxt
  install -Dm644 hyph_es_ANY.dic "$pkgdir/usr/share/hyphen/hyph_es_ANY.dic"

  pushd "$pkgdir/usr/share/hyphen/"
  es_ES_aliases="es_AR es_BO es_CL es_CO es_CR es_CU es_DO es_EC es_ES es_GT es_HN es_MX es_NI es_PA es_PE es_PR es_SV es_UY es_VE"
  for lang in $es_ES_aliases; do
        ln -s hyph_es_ANY.dic hyph_$lang.dic
  done
  popd

  # the symlinks
  install -dm755 "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename $file) .
    done
  popd
  
  # docs
  install -Dm644 README_hyph_es_ANY.txt "${pkgdir}/usr/share/doc/$pkgname/README_hyph_es_ANY.txt"
}
