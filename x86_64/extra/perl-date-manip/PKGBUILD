# $Id: PKGBUILD 214320 2014-06-06 06:08:17Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=perl-date-manip
pkgver=6.45
pkgrel=1
pkgdesc="Date::Manip - date manipulation routines"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/Date-Manip/"
depends=('perl')
checkdepends=('perl-test-inter')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/SB/SBECK/Date-Manip-${pkgver}.tar.gz)
md5sums=('f5818a9c2f090cd73b0faeb9ae3aad5c')

build() {
  cd Date-Manip-${pkgver}
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Date-Manip-${pkgver}
  perl Build test
}

package() {
  cd Date-Manip-${pkgver}
  perl Build destdir="${pkgdir}" install
}
