# $Id: PKGBUILD 233323 2015-03-10 17:32:06Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-smartbookmark-plugin
pkgver=0.4.6
pkgrel=1
pkgdesc="Plugin for the Xfce4 panel that lets you quicksearch from selected websites"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-smartbookmark-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool' 'libxt')
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/0.4/$pkgname-$pkgver.tar.bz2
        xfce4-smartbookmark-plugin-archlinux.patch)
sha256sums=('3bed2b322cd39ce5845d6da07938c702260f3f1a9700b690ef377fba752ba24f'
            '61f35a8e93f1aaf7f46b2c90af9788155bea67f045185d74c6b384a97deeacab')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np0 -i "$srcdir/xfce4-smartbookmark-plugin-archlinux.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
