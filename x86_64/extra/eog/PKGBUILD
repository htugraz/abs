# $Id: PKGBUILD 199439 2013-11-12 18:34:54Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=3.10.2
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
arch=(i686 x86_64)
license=(GPL)
depends=(gnome-desktop libexif lcms2 desktop-file-utils exempi libpeas librsvg gnome-icon-theme dconf)
makedepends=(gtk-doc intltool itstool)
install=eog.install
groups=(gnome)
options=('!emptydirs')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('5ccc98609ca666420719383cc7d9b44335bbc0aacf048e38036d1f946ce54763')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # remove it from Xfce menu that ships its own thingy
  echo "NotShowIn=XFCE" >> $pkgdir/usr/share/applications/eog.desktop
}
