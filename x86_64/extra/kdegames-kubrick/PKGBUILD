# $Id: PKGBUILD 204401 2014-01-19 11:01:06Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kubrick
pkgver=4.12.1
pkgrel=2
pkgdesc="Based on the famous Rubik's Cube"
url="http://kde.org/applications/games/kubrick/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames' 'glu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kubrick-${pkgver}.tar.xz")
sha1sums=('7cb19977169329a002572733cfde3b0f7e05f42e')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kubrick-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
