# $Id: PKGBUILD 204204 2014-01-15 10:18:20Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-filesharing
pkgver=4.12.1
pkgrel=1
pkgdesc='Properties dialog plugin to share a directory with the local network'
url='https://projects.kde.org/projects/kde/kdenetwork/kdenetwork-filesharing'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-runtime' 'smbclient')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kdenetwork-filesharing-${pkgver}.tar.xz")
sha1sums=('19b787112886dc5940e4c20f45fc0643ff8ff1f8')

build() {
  mkdir build
  cd build
  cmake ../kdenetwork-filesharing-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
