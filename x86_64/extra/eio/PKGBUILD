# $Id: PKGBUILD 199729 2013-11-15 12:14:20Z ronald $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=eio
pkgver=1.7.9
pkgrel=1
pkgdesc="Async IO library using Ecore"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('ecore')
options=('!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('6a849c5da4b1213d018ca46dcea1130a135e99e6')

build() {
  cd $srcdir/$pkgname-$pkgver

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-posix-threads \
    --disable-debug-threads \
    --enable-inotify \
    --disable-install-examples \
    --disable-doc
  make
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # install license files
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/COPYING
}

