# $Id: PKGBUILD 213504 2014-05-24 00:00:04Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=vte3
pkgname=(vte3 vte-common)
pkgver=0.36.2
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!emptydirs')
makedepends=('intltool' 'gobject-introspection' 'gtk3')
url="http://www.gnome.org"
source=(http://download.gnome.org/sources/vte/${pkgver::4}/vte-$pkgver.tar.xz)
sha256sums=('f45eed3aed823068c7563345ea947be0e6ddb3dacd74646e6d7d26a921e04345')

build() {
  cd "vte-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection
  make
}

package_vte3(){
  depends=('gtk3' 'vte-common')
  cd "vte-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/lib/vte/gnome-pty-helper
}

package_vte-common() {
  pkgdesc="Common files used by vte and vte3"
  depends=('glibc')
  cd "vte-$pkgver"

  make -C gnome-pty-helper DESTDIR="$pkgdir" install
}
