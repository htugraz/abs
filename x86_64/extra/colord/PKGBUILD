# $Id: PKGBUILD 235377 2015-04-03 12:01:13Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord
pkgver=1.2.9
pkgrel=2
pkgdesc="System daemon for managing color devices"
arch=(i686 x86_64)
url="http://www.freedesktop.org/software/colord"
license=(GPL2)
depends=(lcms2 libgusb polkit sqlite systemd dconf dbus)
makedepends=(intltool gobject-introspection vala docbook2x sane bash-completion
             argyllcms)
optdepends=('sane: scanner support'
            'argyllcms: color profiling')
replaces=('shared-color-profiles')
install=colord.install
source=($url/releases/$pkgname-$pkgver.tar.xz{,.asc}
        0001-Fix-session-helper-failing-to-find-argyll-tool.patch)
sha1sums=('1b6319812b12ab7f906983959bbaaf2e5b78bdb7'
          'SKIP'
          '6d92d75cda7736e734f7472bed52dbc90ca3ea93')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../0001-Fix-session-helper-failing-to-find-argyll-tool.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-vala --enable-sane --with-daemon-user=colord
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # the build system has no colord user, so the chown fails
  chown -R 124:124 "$pkgdir/var/lib/colord"
}

# vim:set ts=2 sw=2 et:
