# $Id: PKGBUILD 244694 2015-08-23 08:57:02Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=print-manager
pkgver=15.08.0
pkgrel=1
pkgdesc="A tool for managing print jobs and printers"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdeutils/print-manager'
license=('GPL' 'LGPL' 'FDL')
depends=('libcups' 'kcmutils' 'plasma-framework')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'qt5-tools')
optdepends=('system-config-printer: auto-detect the printer driver')
replaces=('kdeutils-print-manager')
conflicts=('kdeutils-print-manager')
groups=('kde-applications' 'kdeutils')
source=("http://download.kde.org/stable/applications/${pkgver}/src/print-manager-${pkgver}.tar.xz")
sha1sums=('616630ded6b612cb4b5137e31e7f0beb5a2cfe9c')

build() {
  mkdir build
  cd build
  cmake ../print-manager-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
