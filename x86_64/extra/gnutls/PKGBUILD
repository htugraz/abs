# $Id: PKGBUILD 204692 2014-01-25 10:02:55Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=gnutls
pkgver=3.2.9
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL2.1')
url="http://www.gnutls.org/"
install=gnutls.install
options=('!zipman')
depends=('gcc-libs' 'libtasn1' 'readline' 'zlib' 'nettle' 'p11-kit')
makedepends=('valgrind' 'strace' 'datefudge')
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.2/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('9ae2bfa55d4c12dd6005cfb7014977f9'
         'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--with-zlib \
	--disable-static \
	--disable-guile \
	--disable-valgrind-tests
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check || /bin/true
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
}
