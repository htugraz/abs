# $Id: PKGBUILD 198009 2013-10-30 12:14:33Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.10.0
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
groups=(gnome)
depends=(udisks2 gtk3 libsecret libpwquality libcanberra libdvdread libnotify desktop-file-utils gnome-icon-theme-symbolic)
makedepends=(intltool docbook-xsl gnome-settings-daemon)
options=('!emptydirs')
install=gnome-disk-utility.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('34e23a62e711ec543670550f9312a5ad6610ce807d57345146b6af989788b8aa')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
