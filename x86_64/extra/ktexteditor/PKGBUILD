# $Id: PKGBUILD 216707 2014-07-08 18:37:19Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.0.0
pkgrel=1
pkgdesc='KTextEditor framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/ktexteditor'
license=('LGPL')
depends=('kparts')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('fb438acf5b10c0e9a620916c2bfe5797')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
