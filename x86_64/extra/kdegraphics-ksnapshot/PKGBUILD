# $Id: PKGBUILD 220390 2014-08-20 15:32:59Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-ksnapshot
pkgver=4.14.0
pkgrel=1
pkgdesc="Screen Capture Program"
url="http://kde.org/applications/graphics/ksnapshot/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime' 'libkipi')
makedepends=('cmake' 'automoc4')
optdepends=('kipi-plugins')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/ksnapshot-${pkgver}.tar.xz")
sha1sums=('f6bff85913aed34f91fc22ca1cf34be9c9476342')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../ksnapshot-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
