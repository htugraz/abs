# $Id: PKGBUILD 198839 2013-11-05 19:47:27Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-marble
pkgver=4.11.3
pkgrel=1
pkgdesc="Desktop Globe"
url="http://kde.org/applications/education/marble/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'gpsd')
optdepends=('gpsd: gps support')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/marble-${pkgver}.tar.xz")
sha1sums=('b0b0cf06b4c60262861e748c6815d62513df13a7')

build() {
  mkdir build
  cd build
  cmake ../marble-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
