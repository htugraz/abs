# $Id: PKGBUILD 205726 2014-02-10 10:59:04Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kpatience
pkgver=4.12.2
pkgrel=1
pkgdesc="Offers a selection of solitaire card games"
url="http://kde.org/applications/games/kpat/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kpat-${pkgver}.tar.xz")
sha1sums=('b64ac2574e2cddcc487958d10858aa420b16916d')

build() {
  mkdir build
  cd build
  cmake ../kpat-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
