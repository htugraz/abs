# $Id: PKGBUILD 208352 2014-03-22 17:47:04Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-python2
pkgver=1.6.0
_pkgver=4.6.0
pkgrel=2
pkgdesc="Python 2 language and documentation plugin for KDevelop"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop' 'python2')
makedepends=('cmake' 'automoc4' 'kdevelop-pg-qt')
conflicts=('kdevelop-python')
source=("http://download.kde.org/stable/kdevelop/${_pkgver}/src/kdev-python-${pkgver}.tar.xz")
sha1sums=('88618dbd3a80d5193f49f6c61a6ad02a5e9c0652')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kdev-python-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
