# $Id: PKGBUILD 199737 2013-11-15 12:16:38Z ronald $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=elementary
pkgver=1.7.9
pkgrel=1
pkgdesc="Enlightenment's widget set"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('edje' 'e_dbus' 'efreet' 'ethumb' 'eio')
options=('!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('4d68e4f77d6b3471a45a4e33ae3af18d0ca42ab1')

build() {
  cd $srcdir/$pkgname-$pkgver

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/elementary \
    --with-internal-maximum-log-level=3 \
    --disable-static \
    --enable-quick-launch \
    --enable-emotion \
    --enable-ecore-x \
    --enable-ecore-fb \
    --disable-ecore-sdl \
    --disable-ecore-wayland \
    --enable-edbus \
    --enable-efreet \
    --disable-eweather \
    --disable-emap \
    --enable-ethumb \
    --disable-debug \
    --disable-install-examples \
    --disable-doc
  make
}

package(){
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # install license files
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING
}

