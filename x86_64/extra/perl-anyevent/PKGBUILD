# $Id: PKGBUILD 238376 2015-05-03 05:31:30Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-anyevent
pkgver=7.09
pkgrel=1
epoch=4
pkgdesc='The DBI of event loop programming'
arch=('any')
url="https://metacpan.org/release/AnyEvent"
license=('PerlArtistic' 'GPL')
depends=('perl-ev' 'perl-guard' 'perl-async-interrupt')
optdepends=('perl-net-ssleay: TLS/SSL connections with AnyEvent::Handle'
            'perl-json-xs: send or receive JSON with AnyEvent::Handle')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/AnyEvent-$pkgver.tar.gz")
md5sums=('b5b2aa93ce7927dd0b5a21992202ab5d')

build() {
  cd AnyEvent-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd AnyEvent-$pkgver
  make test
}

package() {
  cd AnyEvent-$pkgver
  make DESTDIR="$pkgdir" install
}
