# $Id: PKGBUILD 227540 2014-12-11 16:11:19Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kjsembed
pkgver=5.5.0
pkgrel=1
pkgdesc='Embedded JS'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kjsembed'
license=('LGPL')
depends=('qt5-svg' 'ki18n' 'kjs')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools' 'python')
groups=('kf5-aids')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=('4e896f8ce01149feee97b2fdab85e7cf')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
