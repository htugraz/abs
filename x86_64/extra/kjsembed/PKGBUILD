# $Id: PKGBUILD 234018 2015-03-17 09:50:32Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kjsembed
pkgver=5.8.0
pkgrel=1
pkgdesc='Embedded JS'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kjsembed'
license=('LGPL')
depends=('qt5-svg' 'ki18n' 'kjs')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools' 'python')
groups=('kf5-aids')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=('30d32919babdf97c40cfccf1cd1676cb')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
