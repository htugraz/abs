# $Id: PKGBUILD 224956 2014-10-20 08:11:15Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-parley
pkgver=4.14.2
pkgrel=1
pkgdesc="Vocabulary Trainer"
url="http://kde.org/applications/education/parley/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libkdeedu')
makedepends=('cmake' 'automoc4')
optdepends=('kdebindings-kross-python: scripting support')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/parley-${pkgver}.tar.xz")
sha1sums=('081ac6ac2868d707f13a9dd762dd132465307e03')

build() {
  mkdir build
  cd build
  cmake ../parley-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
