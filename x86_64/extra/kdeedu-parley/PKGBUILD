# $Id: PKGBUILD 213203 2014-05-19 15:01:53Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-parley
pkgver=4.13.1
pkgrel=1
pkgdesc="Vocabulary Trainer"
url="http://kde.org/applications/education/parley/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libkdeedu')
makedepends=('cmake' 'automoc4')
optdepends=('kdebindings-kross-python: scripting support')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/parley-${pkgver}.tar.xz")
sha1sums=('db3097ab6392c8de78fd74c170feb46062591fb4')

build() {
  mkdir build
  cd build
  cmake ../parley-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
