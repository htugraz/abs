# $Id: PKGBUILD 229628 2015-01-21 13:38:57Z juergen $
# Maintainer: Jürgen Hötzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=bigloo
_pkgver=4.1a-2
pkgver=${_pkgver/-/_}
pkgrel=3
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
license=('GPL' 'LGPL')
depends=('gmp' 'openssl' 'libunistring')
makedepends=('java-environment' 'emacs' 'zip' 'sqlite' 'alsa-lib' 'flac' 'avahi' 'chrpath')
optdepends=('java-environment' 'emacs' 'zip' 'sqlite' 'alsa-lib' 'flac' 'avahi')
options=('!makeflags')
install=bigloo.install
source=(ftp://ftp-sop.inria.fr/indes/fp/Bigloo/${pkgname}${_pkgver}.tar.gz)
md5sums=('64f17f126691baf1eb2c22a4f26edc1b')

elisp_dir=/usr/share/emacs/site-lisp/bigloo
build() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  ./configure --prefix=/usr \
    --enable-ssl \
    --enable-sqlite \
    --lispdir=${elisp_dir} \
    --enable-avahi \
    --enable-flac \
    --disable-gstreamer \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo \
    --jvm=yes
  make build compile-bee
}

check() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  make DESTDIR="${pkgdir}" install install-bee
  make -C manuals DESTDIR="${pkgdir}" install-bee

  chrpath -d "${pkgdir}"/usr/lib/bigloo/4.1a/*.so \
    "${pkgdir}"/usr/lib/bigloo/4.1a/bmem/bmem.so
  chmod 644 "${pkgdir}"${elisp_dir}/etc/bass-snap.au \
    "${pkgdir}"${elisp_dir}/images/*
}
