# $Id: PKGBUILD 241517 2015-06-30 20:25:10Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=blinken
pkgver=15.04.3
pkgrel=1
pkgdesc="Memory Enhancement Game"
url="http://kde.org/applications/education/blinken/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kxmlgui' 'phonon-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
install=${pkgname}.install
replaces=('kdeedu-blinken')
conflicts=('kdeedu-blinken')
source=("http://download.kde.org/stable/applications/${pkgver}/src/blinken-${pkgver}.tar.xz")
sha1sums=('06b2ad6aff2f702ab1285d6e5c3c39d33492adb7')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../blinken-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
