# $Id: PKGBUILD 235855 2015-04-09 18:45:28Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgver=3.16.0
pkgrel=1
pkgdesc="Clocks applications for GNOME"
arch=(i686 x86_64)
url="https://www.gnome.org/"
license=(GPL)
depends=(gtk3 libcanberra libgweather gnome-desktop
         hicolor-icon-theme gnome-themes-standard geoclue2 geocode-glib gsound)
makedepends=(intltool vala gobject-introspection itstool)
groups=(gnome-extra)
install=gnome-clocks.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('ae58898784046469e4df715c610c50c44e95b168a414af8f8661fdb23c0c71cb')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
