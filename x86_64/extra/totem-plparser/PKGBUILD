# $Id: PKGBUILD 206115 2014-02-18 17:40:26Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem-plparser
pkgver=3.10.1
pkgrel=1
url="http://www.gnome.org"
pkgdesc="Totem playlist parser library"
license=('LGPL')
arch=(i686 x86_64)
depends=('gmime' 'libsoup' 'libarchive')
makedepends=('intltool' 'gobject-introspection' 'libquvi')
optdepends=('libquvi: video site handling (Youtube etc.)')
source=(http://ftp.gnome.org/pub/gnome/sources/totem-pl-parser/${pkgver%.*}/totem-pl-parser-$pkgver.tar.xz)
sha256sums=('13d20222e7230eff1b22acc2b6b54d3c910590782a3ac39b859857407fb5e413')

build() {
  cd totem-pl-parser-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static
  make
}

package(){
  cd totem-pl-parser-$pkgver
  make DESTDIR="$pkgdir" install
}
