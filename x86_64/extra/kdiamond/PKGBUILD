# $Id: PKGBUILD 249054 2015-10-13 07:29:12Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdiamond
pkgver=15.08.2
pkgrel=1
pkgdesc="A single player puzzle game"
url="http://kde.org/applications/games/kdiamond/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kdiamond')
replaces=('kdegames-kdiamond')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kdiamond-${pkgver}.tar.xz")
sha1sums=('dbfe8dae584dd8a5708a3dc9dab2d13d9a46abe8')

build() {
  mkdir -p build
  cd build
  cmake ../kdiamond-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
