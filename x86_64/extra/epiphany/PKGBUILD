# $Id: PKGBUILD 224597 2014-10-16 12:00:46Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.14.0
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME web browser based on the WebKit rendering engine."
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup' 'libnotify' 'gsettings-desktop-schemas' 'webkit2gtk' 'nss' 'iso-codes' 'dconf' 'desktop-file-utils' 'gcr' 'gnome-desktop' 'libwnck3' 'gnome-themes-standard')
makedepends=('intltool' 'itstool' 'docbook-xml' 'startup-notification' 'gobject-introspection' 'python2')
options=('!emptydirs')
groups=('gnome')
url="https://wiki.gnome.org/Apps/Web"
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('694c2e0790741e0c7475544ee35565dd56e46551ff2ca3044e175bcddcb1b713')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/$pkgname

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
