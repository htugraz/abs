# $Id: PKGBUILD 205456 2014-02-05 15:22:56Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libmbim
pkgver=1.6.0
pkgrel=1
pkgdesc="MBIM modem protocol helper library"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/libmbim/"
license=(GPL2)
depends=(glib2 bash systemd)
makedepends=(gtk-doc python)
source=(http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('e0efbdaf2aeddec033b4c7c5d0f7934c91ce535c5c9f0cadf1e67527d10ab082')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --with-tests --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
