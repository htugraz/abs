# $Id: PKGBUILD 220467 2014-08-20 21:05:22Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libmbim
pkgver=1.10.0
pkgrel=1
pkgdesc="MBIM modem protocol helper library"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/libmbim/"
license=(GPL2)
depends=(glib2 bash systemd)
makedepends=(gtk-doc python)
source=(http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('4e5eade46a61d0cd1f8b6ad47bee938868766eca617a64e7acd2d0b72ff38b6e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static --with-tests
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
