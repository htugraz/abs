# $Id: PKGBUILD 248043 2015-10-01 16:05:10Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-service-identity
pkgname=('python-service-identity' 'python2-service-identity')
pkgver=14.0.0
pkgrel=3
pkgdesc="Service identity verification for pyOpenSSL"
arch=('any')
license=('MIT')
url="http://pypi.python.org/pypi/service_identity"
makedepends=('python-pyopenssl' 'python2-pyopenssl' 'python-pyasn1' 'python2-pyasn1'
             'python-setuptools' 'python2-setuptools' 'python-pyasn1-modules' 'python2-pyasn1-modules'
             'python-characteristic' 'python2-characteristic' 'python-pytest' 'python2-pytest')
checkdepends=('python-idna' 'python2-idna')
source=("http://pypi.python.org/packages/source/s/service_identity/service_identity-${pkgver}.tar.gz")
md5sums=('cea0b0156d73b025ecef660fb51f0d9a')

prepare() {
   cp -a service_identity-${pkgver}{,-python2}
}

build() {
   # Build python 3 module
   cd service_identity-${pkgver}
   python3 setup.py build
 
   # Build python 2 module
   cd ../service_identity-${pkgver}-python2
   python2 setup.py build
}

check() {
   # Check python3 module
   cd "${srcdir}"/service_identity-${pkgver}
   py.test

   # Check python2 module
   cd "${srcdir}"/service_identity-${pkgver}-python2
   py.test2
}
 
package_python-service-identity() {
   depends=('python' 'python-pyopenssl' 'python-pyasn1' 'python-pyasn1-modules' 'python-characteristic')
   optdepends=('python-idna: for Internationalized Domain Names support')
 
   cd service_identity-${pkgver}
   python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
 
package_python2-service-identity() {
   depends=('python2' 'python2-pyopenssl' 'python2-pyasn1' 'python2-pyasn1-modules' 'python2-characteristic')
   optdepends=('python2-idna: for Internationalized Domain Names support')
 
   cd service_identity-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
