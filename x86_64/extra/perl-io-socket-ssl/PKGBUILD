# $Id: PKGBUILD 212767 2014-05-14 21:36:25Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-io-socket-ssl
pkgver=1.984
pkgrel=1
pkgdesc="Nearly transparent SSL encapsulation for IO::Socket::INET"
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/IO-Socket-SSL/"
depends=('perl-net-ssleay' 'perl' 'perl-uri')
checkdepends=('perl-io-socket-inet6')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/S/SU/SULLR/IO-Socket-SSL-${pkgver}.tar.gz)
sha1sums=('16f45ccb7c605591943465d55fa6fdec65a3e631')

build() {
  cd IO-Socket-SSL-${pkgver}
  yes | perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IO-Socket-SSL-${pkgver}
  make test
}

package() {
  cd IO-Socket-SSL-${pkgver}
  make DESTDIR="${pkgdir}" install
}
