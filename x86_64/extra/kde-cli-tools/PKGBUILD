# $Id: PKGBUILD 227673 2014-12-17 07:49:34Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=kde-cli-tools
pkgver=5.1.2
pkgrel=1
pkgdesc="Tools based on KDE Frameworks 5 to better interact with the system"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kde-cli-tools'
license=('LGPL')
depends=('kdesu' 'kdelibs4support' 'kcmutils')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('774eab3599c2d5455e066799e62b0d7d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  # conflicts with kdebase-runtime
  rm -r "$pkgdir"/usr/share/man
}
