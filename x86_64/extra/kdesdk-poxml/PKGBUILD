# $Id: PKGBUILD 250456 2015-11-09 22:15:16Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-poxml
pkgver=15.08.3
pkgrel=1
pkgdesc='Translates DocBook XML files using gettext po files'
url='https://projects.kde.org/projects/kde/kdesdk/poxml'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kdebase-runtime' 'gettext')
makedepends=('cmake' 'automoc4')
optdepends=('antlr2: required for po2xml and swappo tool')
source=("http://download.kde.org/stable/applications/${pkgver}/src/poxml-${pkgver}.tar.xz")
sha1sums=('0f932643634ece83d721425a65e65a2bffd4fb3a')

build() {
  mkdir -p build
  cd build
  cmake ../poxml-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
