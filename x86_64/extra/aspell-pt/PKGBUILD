# $Id: PKGBUILD 226310 2014-11-16 12:07:08Z fyan $
# Maintainer:

pkgname=aspell-pt
_pkgverPT=20141115
_pkgverBR=20090702
pkgver=$_pkgverPT
pkgrel=1
pkgdesc="Portuguese and Brazilian Portuguese dictionary for aspell"
arch=('i686' 'x86_64')
url="http://aspell.net/"
license=('GPL2')
depends=('aspell')
conflicts=('aspell-pt_br')
replaces=('aspell-pt_br')
source=(ftp://ftp.gnu.org/gnu/aspell/dict/pt_BR/aspell6-pt_BR-$_pkgverBR-0.tar.bz2
        http://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-$_pkgverPT.tar.bz2
        http://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-preao-$_pkgverPT.tar.bz2)
sha1sums=('add1db9a6a908dccaad13a7fd85c3b202299ff26'
          '59e86f87283d982ab7156e28a8d5da8f0a651b66'
          '4b39e1d9571f0a11a6370945d072baeb7a044512')

build() {
  cd aspell6-pt_BR-$_pkgverBR-0
  ./configure
  make

  cd ../aspell6-pt_PT-$_pkgverPT-0
  ./configure
  make

  cd ../aspell6-pt_PT-preao-$_pkgverPT-0
  ./configure
  make
}

package() {
  cd aspell6-pt_BR-$_pkgverBR-0
  make DESTDIR="$pkgdir" install

  cd ../aspell6-pt_PT-$_pkgverPT-0
  make DESTDIR="$pkgdir" install

  cd ../aspell6-pt_PT-preao-$_pkgverPT-0
  make DESTDIR="$pkgdir" install

  echo "add pt_BR.multi" > "$pkgdir"/usr/lib/aspell-0.60/brasileiro.alias
}
