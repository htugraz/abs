# $Id: PKGBUILD 204081 2014-01-15 08:57:35Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-common-internals
_pkgname=ktp-common-internals
pkgver=0.7.1
pkgrel=1
pkgdesc="Common components for KDE-Telepathy"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('libkpeople' 'telepathy-logger-qt' 'telepathy-mission-control')
makedepends=('cmake' 'automoc4' 'boost' 'doxygen')
install=${pkgname}.install
source=("http://download.kde.org/stable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('6b55bf5919d922502db8f21a2b1871e411e93699')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
