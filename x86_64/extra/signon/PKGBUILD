# $Id: PKGBUILD 202670 2013-12-24 08:58:48Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=signon
pkgname=('signon' 'signon-doc')
pkgver=8.55
pkgrel=1
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
makedepends=('qt4' 'libproxy' 'doxygen' 'graphviz' 'ttf-font')
options=('!emptydirs')
source=("http://accounts-sso.googlecode.com/files/$pkgbase-$pkgver.tar.bz2"
        'use_keyring.patch')
sha1sums=('e71bfe54bdd4ad50705eb7071c3a426293370b2a'
          'cfdd48b494ad267654ceba23f1b81bcb8f56b9a1')

prepare() {
  cd $pkgbase-$pkgver

  # Use gnome-keyring secrets storage if signon-keyring-extension is available
  patch -Np1 -i "$srcdir/use_keyring.patch"

  # qt4 fix
  sed -i 's/qdbusxml2cpp/qdbusxml2cpp-qt4/' src/signond/signond.pro
}

build() {
  cd $pkgbase-$pkgver
  qmake-qt4 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package_signon() {
  pkgdesc="Framework to provide credential storage and authentication service"
  depends=('qt4' 'libproxy')
  backup=('etc/signond.conf')
  install=$pkgname.install

  cd $pkgbase-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  # Split out documentation
  mv "$pkgdir/usr/share/doc" "$srcdir"
}

package_signon-doc() {
  pkgdesc="Developer documention of signon"

  mkdir -p "$pkgdir/usr/share"
  mv "$srcdir/doc" "$pkgdir/usr/share"
}
