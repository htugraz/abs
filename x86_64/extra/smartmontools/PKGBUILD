# $Id: PKGBUILD 219339 2014-08-05 17:28:35Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Jani Talikka <jani.talikka@gmail.com>
# Contributor: Ralf Barth <archlinux.org@haggy.org>

pkgname=smartmontools
pkgver=6.3
pkgrel=1
pkgdesc="Control and monitor S.M.A.R.T. enabled ATA and SCSI Hard Drives"
url="http://smartmontools.sourceforge.net"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'libcap-ng' 'bash')
optdepends=('heirloom-mailx: to get mail alerts to work')
backup=('etc/smartd.conf'
        'etc/conf.d/smartd')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'smartd.conf')
md5sums=('2ea0c62206e110192a97b59291b17f54'
         '28c1b80c41dac52d433961dcfb4f70e0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --sysconfdir=/etc \
              --enable-drivedb \
              --with-libcap-ng=yes \
              --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e "s:sysconfig/smartmontools:conf.d/smartd:g" smartd.service
  sed -i -e "s:smartd_opts:SMARTD_ARGS:g" smartd.service

  make DESTDIR="${pkgdir}" install

  rm -rf ${pkgdir}/etc/rc.d
  install -Dm644 ${srcdir}/smartd.conf "${pkgdir}/etc/conf.d/smartd"
}
