# $Id: PKGBUILD 211368 2014-04-18 18:01:49Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kmouth
pkgver=4.13.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Speech Synthesizer Frontend'
url='http://kde.org/applications/utilities/kmouth/'
license=('GPL' 'FDL')
groups=('kde' 'kdeaccessibility')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kmouth-${pkgver}.tar.xz")
sha1sums=('0281df733b9ce48ec75b3d253a093d3e3f4e2af8')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kmouth-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
