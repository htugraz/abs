# $Id: PKGBUILD 224175 2014-10-10 16:19:27Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=('protobuf' 'python2-protobuf')
pkgver=2.6.0
pkgrel=2
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=('i686' 'x86_64')
url='https://github.com/google/protobuf/'
license=('BSD')
depends=('gcc-libs' 'zlib')
makedepends=('python2-setuptools')
source=(https://protobuf.googlecode.com/svn/rc/protobuf-$pkgver.tar.gz)
md5sums=('9959d86087e64524d7f91e7a5a6e4fd7')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgbase-$pkgver check
}

package_protobuf() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-protobuf() {
  pkgdesc='Python 2 bindings for Google Protocol Buffers'
  depends=("python2" "protobuf=${pkgver}")
  provides=("protobuf-python")
  conflicts=("protobuf-python")
  replaces=("protobuf-python")

  cd $pkgbase-$pkgver/python
  python2 setup.py install --prefix=/usr --root="$pkgdir"

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s /usr/share/licenses/$pkgbase/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/
}
