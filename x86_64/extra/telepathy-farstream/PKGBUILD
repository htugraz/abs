# $Id: PKGBUILD 215891 2014-06-30 19:18:37Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=telepathy-farstream
pkgver=0.6.1
pkgrel=1
pkgdesc="A telepathy-backend to use stream engine."
arch=(i686 x86_64)
url="http://telepathy.freedesktop.org"
license=('LGPL2.1')
depends=('telepathy-glib' 'farstream')
makedepends=('dbus-glib' 'gobject-introspection')
conflicts=('telepathy-farsight')
replaces=('telepathy-farsight')
source=(http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('53e3a69bdee7b301e2fdd2f2d254e385')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/telepathy \
    --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
