# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xvidtune
pkgver=1.0.3
pkgrel=1
pkgdesc="Video mode tuner for Xorg"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxxf86vm' 'libxt' 'libxaw' 'libxmu' 'libx11')
makedepends=('xorg-util-macros')
source=("http://xorg.freedesktop.org/archive/individual/app/xvidtune-${pkgver}.tar.bz2")
sha256sums=('24e5e103933a04bfb2e1d18562619ed74031ab515e7343eb546bacdbcbeecf01')

build() {
  cd "${srcdir}/xvidtune-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xvidtune-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
