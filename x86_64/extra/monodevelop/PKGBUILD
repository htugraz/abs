# $Id: PKGBUILD 197646 2013-10-27 17:34:49Z daniel $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=monodevelop
pkgver=4.0.13
pkgrel=1
pkgdesc="An IDE primarily designed for C# and other .NET languages"

# should probably changed to "any" - no ELF files - OpenSUSE builds it as NOARCH 
arch=('i686' 'x86_64')

url="http://www.monodevelop.com"
license=('GPL')
depends=('mono>=2.10.8' 'mono-addins>=0.6.2' 'gnome-sharp' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('rsync' 'git')
options=(!makeflags)
install=monodevelop.install
source=(http://origin-download.mono-project.com/sources/${pkgname}/${pkgname}-${pkgver}-38.tar.bz2
        monodevelop-core-addins.pc.in.patch)
md5sums=('63ea0b389976f72d6f8b131a74c54b93'
         '8466d032735130e37d4091a793a7966c')

build() {
  export MONO_SHARED_DIR=$srcdir/src/.wabi
  mkdir -p $MONO_SHARED_DIR

  cd $srcdir/$pkgname-$pkgver
  # fix location for MonoDevelop.Gettext.dll - breaks gdb build
  # patch -Np0 -i ${srcdir}/monodevelop-core-addins.pc.in.patch
  
  ./configure --prefix=/usr
  LD_PRELOAD="" make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  
  LD_PRELOAD="" make DESTDIR=$pkgdir install
  # delete conflicting files
  find $pkgdir/usr/share/mime/ -type f -exec rm {} \;
  rm -r $MONO_SHARED_DIR
}

