# $Id: PKGBUILD 198866 2013-11-05 19:53:06Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kalzium
pkgver=4.11.3
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="http://kde.org/applications/education/kalzium/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'avogadro' 'ocaml' 'facile' 'chemical-mime-data')
makedepends=('cmake' 'automoc4' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kalzium-${pkgver}.tar.xz")
sha1sums=('5a83d5fd65b9486c982f68070ece63a3ce1e0b8d')

build() {
  mkdir build
  cd build
  cmake ../kalzium-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
