# $Id: PKGBUILD 233312 2015-03-10 08:02:07Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas

pkgname=kmix
pkgver=14.12.3
pkgrel=1
pkgdesc='KDE volume control program'
arch=('i686' 'x86_64')
url='http://kde.org/applications/multimedia/kmix/'
license=('GPL')
depends=('kdelibs4support' 'kcmutils' 'libcanberra' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdemultimedia-kmix')
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
install=$pkgname.install
sha256sums=('68be030da9a9255ee6bd637097a3ff5dd24e95523024ce965eeb8a567d38538c')

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKMIX_KF5_BUILD=ON \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
