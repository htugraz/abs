# $Id: PKGBUILD 230384 2015-02-03 05:57:40Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas

pkgname=kmix
pkgver=14.12.1
pkgrel=1
pkgdesc='KDE volume control program'
arch=('i686' 'x86_64')
url='http://kde.org/applications/multimedia/kmix/'
license=('GPL')
depends=('kdelibs4support' 'kcmutils' 'libcanberra' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdemultimedia-kmix')
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
install=$pkgname.install
sha256sums=('ca8d1f0ed4765c8b6e676c83aef9545387f64a52600c0123270bc61f6a6061fe')

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKMIX_KF5_BUILD=ON \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
