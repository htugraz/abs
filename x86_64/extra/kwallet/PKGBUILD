# $Id: PKGBUILD 221668 2014-09-14 13:02:23Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet
pkgver=5.2.0
pkgrel=1
pkgdesc='KWallet Framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kwallet'
license=('LGPL')
depends=('knotifications') # qgpgme
makedepends=('extra-cmake-modules' 'python')
groups=('kf5')
conflicts=('kwalletframeworks')
replaces=('kwalletframeworks')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('969f7be4cb7e483935855f9e8c12a041')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
