# $Id: PKGBUILD 227810 2014-12-20 02:15:41Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>

pkgname=ntp
_pkgname=ntp #-dev
_pkgver=4.2.8
pkgver=${_pkgver/p/.p}
pkgrel=1
pkgdesc='Network Time Protocol reference implementation'
url='http://www.ntp.org/'
license=('custom')
arch=('i686' 'x86_64')
depends=('openssl' 'libcap' 'libedit')
backup=('etc/ntp.conf')
#source=("http://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/${_pkgname}/${_pkgname}-${_pkgver}.tar.gz"
source=("http://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/${_pkgname}-${_pkgver}.tar.gz"
        'ntp.conf'
        'ntpd.service'
        'ntpdate.service')
sha1sums=('6d1c017a8a0e97d5cf8bf4e5e38333973ffd22d5'
          'ad1d6ee2e9aca64a84a7224c88bf6008ac6c69e1'
          '0cccca872385e0142888ab48d273fec0669b30a8'
          '059b382d1af0c55202e2d17f2ae065a2cbfec9ee')

options=('!emptydirs')
install=install

build() {
	cd "${srcdir}/${_pkgname}-${_pkgver}"

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-linuxcaps \
		--enable-ntp-signd \

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${_pkgver}"

	make DESTDIR="${pkgdir}" install

	install -Dm644 ../ntp.conf "${pkgdir}"/etc/ntp.conf
	install -Dm644 ../ntpd.service "${pkgdir}"/usr/lib/systemd/system/ntpd.service
	install -Dm644 ../ntpdate.service "${pkgdir}"/usr/lib/systemd/system/ntpdate.service
	install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -d -o 87 "${pkgdir}"/var/lib/ntp
	echo > "${pkgdir}/var/lib/ntp/.placeholder"
}
