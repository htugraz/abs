# $Id: PKGBUILD 216579 2014-07-06 19:40:32Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fluxbox
pkgver=1.3.5
pkgrel=4
pkgdesc="A lightweight and highly-configurable window manager"
arch=('i686' 'x86_64')
url="http://www.fluxbox.org"
license=('MIT')
depends=('libxft' 'libxpm' 'libxinerama' 'libxrandr' 'gcc-libs' 'imlib2' 'fribidi')
optdepends=('xorg-xmessage: for using the fbsetbg and fluxbox-generate_menu utilities')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.lzma \
        fluxbox.desktop)
sha1sums=('495d82ca428949cc2bc85814898b49cf4624b7cb'
          'f3f83b8ce84d79c2f8670ef687e0dd89ab0552b8')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/@EXEEXT@//g' util/fluxbox-generate_menu.in #share/fluxbox/menu}
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-xft --enable-xinerama \
    --enable-imlib2 --enable-nls
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/fluxbox.desktop" "${pkgdir}/usr/share/xsessions/fluxbox.desktop"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
