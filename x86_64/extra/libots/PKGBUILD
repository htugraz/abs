# $Id: PKGBUILD 206642 2014-03-03 23:27:24Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libots
pkgver=0.5.0
pkgrel=4
pkgdesc='Open Text Summarizer'
url='http://libots.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libxml2' 'glib2' 'popt')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/ots-${pkgver}.tar.gz")
sha1sums=('4468bc06470734848c2369a37c84f76ca4ac3f9a')

options=('!makeflags')

prepare() {
	cd "${srcdir}/ots-${pkgver}"
	touch gtk-doc.make
	sed 's/en.xml$//' -i dic/Makefile.am
	libtoolize --force
	aclocal
	automake --add-missing --force
	autoconf
}

build() {
	cd "${srcdir}/ots-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/ots-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
