# $Id: PKGBUILD 204963 2014-02-01 21:25:15Z eric $
# Maintainer: 

pkgname=i8kutils
pkgver=1.40
pkgrel=3
pkgdesc="Dell Inspiron/Latitude kernel driver and utilities"
arch=('i686' 'x86_64')
url="https://launchpad.net/i8kutils"
license=('GPL')
depends=('bash')
optdepends=('tk: for i8kmon deamon')
backup=('etc/i8kutils/i8kmon.conf')
source=(http://ftp.de.debian.org/debian/pool/main/i/${pkgname}/${pkgname}_${pkgver}.tar.gz
        i8kmon.service i8kutils-probe.diff)
sha1sums=('584edfbd90aa167885520b84848381585d9212fb'
          '5c20f5241de7e60b46117269f4e867b08734cdc8'
          'ab9058f92adb3e8b9306568005d5751908fb1765')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i "$srcdir"/i8kutils-probe.diff

  # conform to Arch Linux guidelines
  sed -i 's|/etc/i8kmon|/etc/i8kutils/i8kmon.conf|g' i8kmon.1
  sed -i 's|/etc/i8kmon.conf|/etc/i8kutils/i8kmon.conf|g' i8kmon
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  install -d "$pkgdir"/usr/{bin,share/man/man1}
  make DESTDIR="$pkgdir" install install-man
  install -D -m644 i8kmon.conf "$pkgdir/etc/i8kutils/i8kmon.conf"
  install -D -m644 ../i8kmon.service "$pkgdir/usr/lib/systemd/system/i8kmon.service"
}
