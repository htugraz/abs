# $Id: PKGBUILD 213297 2014-05-19 15:04:22Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kremotecontrol
pkgver=4.13.1
pkgrel=1
pkgdesc='Configure your remote controls for use with applications'
url='http://kde.org/applications/utilities/kremotecontrol/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
replaces=('kdeutils-kdelirc')
conflicts=('kdeutils-kdelirc')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kremotecontrol-${pkgver}.tar.xz")
sha1sums=('63edf67f4454d553a40edeeeb83e689879e22806')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kremotecontrol-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
