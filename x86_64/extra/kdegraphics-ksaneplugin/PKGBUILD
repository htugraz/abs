# $Id: PKGBUILD 241585 2015-06-30 21:00:25Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-ksaneplugin
pkgver=15.04.3
pkgrel=1
pkgdesc="A scan plugin that implements the scanning"
url="https://projects.kde.org/projects/kde/kdegraphics/ksaneplugin"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libksane')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/stable/applications/${pkgver}/src/ksaneplugin-${pkgver}.tar.xz")
sha1sums=('27a7d3e7e2547f8703d88ed29b4b4339147dd491')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../ksaneplugin-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
