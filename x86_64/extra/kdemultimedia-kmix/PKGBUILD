# $Id: PKGBUILD 214912 2014-06-10 17:55:11Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-kmix
pkgver=4.13.2
pkgrel=1
pkgdesc="KDE volume control program"
url='http://kde.org/applications/multimedia/kmix/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/kmix-${pkgver}.tar.xz")
sha1sums=('03f676426a9afbc9b0395239d8950319e786a019')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kmix-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
