# $Id: PKGBUILD 215303 2014-06-18 21:23:32Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.13.2
pkgrel=3
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs'
license=('GPL' 'LGPL' 'FDL')
depends=('attica' 'libxss' 'krb5' 'grantlee' 'qca' 'libdbusmenu-qt' 'polkit-qt'
        'shared-mime-info' 'enchant' 'giflib' 'jasper' 'openexr' 'strigi'
        'docbook-xsl' 'upower' 'udisks2' 'libxcursor' 'phonon-qt4'
        'media-player-info' 'libxtst' 'libutempter' 'qtwebkit' 'icu')
makedepends=('cmake' 'automoc4' 'avahi' 'libgl' 'hspell' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'kde-applications-menu.patch' 'archlinux-menu.patch' 'qt4.patch'
        'CVE-2014-3494.patch')
sha1sums=('c540edeb7da23f5a8feacb4d775bce43f2060a96'
          '86ee8c8660f19de8141ac99cd6943964d97a1ed7'
          '63a850ab4196b9d06934f2b4a13acd9f7739bc67'
          'ed1f57ee661e5c7440efcaba7e51d2554709701c'
          'c8b4010c68cee6352a68d97da3d5316f52207e83')

prepare() {
       mkdir build
       cd ${pkgname}-${pkgver}
       # avoid file conflict with gnome-menus
       patch -p1 -i "${srcdir}"/kde-applications-menu.patch
       # add Archlinux menu entry
       patch -p1 -i "${srcdir}"/archlinux-menu.patch
       # qmake refers to Qt5
       patch -p1 -i "${srcdir}"/qt4.patch
       # fix build with giflib 5.1.0
       sed -i "/DGifCloseFile/s:file:&, NULL:g" khtml/imload/decoders/gifloader.cpp
       patch -p1 -i "${srcdir}"/CVE-2014-3494.patch
}

build() {
       cd build
       cmake ../${pkgname}-${pkgver} \
               -DCMAKE_BUILD_TYPE=Release \
               -DKDE4_BUILD_TESTS=OFF \
               -DCMAKE_SKIP_RPATH=ON \
               -DKDE_DISTRIBUTION_TEXT='Arch Linux' \
               -DCMAKE_INSTALL_PREFIX=/usr \
               -DSYSCONF_INSTALL_DIR=/etc \
               -DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
               -DKDE_DEFAULT_HOME='.kde4' \
               -DWITH_FAM=OFF \
               -DWITH_SOLID_UDISKS2=ON
       make
}

package() {
       cd "${srcdir}"/build
       make DESTDIR="${pkgdir}" install

       # cert bundle seems to be hardcoded
       # link it to the one from ca-certificates
       rm -f "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
       ln -sf /etc/ssl/certs/ca-certificates.crt "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
}
