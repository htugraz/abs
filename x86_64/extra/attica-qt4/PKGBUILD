# $Id: PKGBUILD 225105 2014-10-20 17:31:52Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=attica-qt4
pkgver=0.4.2
pkgrel=1
pkgdesc='A Qt4 library that implements the Open Collaboration Services API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/attica'
license=('LGPL')
depends=('qt4')
makedepends=('cmake')
provides=('attica')
replaces=('attica')
conflicts=('attica')
source=("http://download.kde.org/stable/attica/attica-${pkgver}.tar.bz2")
md5sums=('d62c5c9489a68432e8d990dde7680c24')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../attica-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
