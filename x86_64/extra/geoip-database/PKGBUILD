# $Id: PKGBUILD 216824 2014-07-11 19:54:59Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=geoip-database
pkgver=20140702
pkgrel=1
pkgdesc="GeoLite country geolocation database compiled by MaxMind"
arch=('any')
url="http://www.maxmind.com/app/ip-location"
license=('custom:OPEN DATA LICENSE')
source=(GeoIP-$pkgver.dat.gz::http://geolite.maxmind.com/download/geoip/database/GeoLiteCountry/GeoIP.dat.gz
        GeoIPv6-$pkgver.dat.gz::http://geolite.maxmind.com/download/geoip/database/GeoIPv6.dat.gz
        http://geolite.maxmind.com/download/geoip/database/LICENSE.txt)
noextract=(GeoIP-$pkgver.dat.gz
           GeoIPv6-$pkgver.dat.gz)
sha256sums=('009b9db99799ee8f3bb8ab61a49e582e53b29aa956386f3f0b2281c5db8a8cde'
            '972ec74876324a5dc326fd4fc0246f8d992b6e67e2bae7246826baf3b72bd4f1'
            '83a4cb82ef8953c0107886b49bc39fcf77590a222d30e778d48f5d92f5b5e383')

prepare() {
  cd "$srcdir"

  for _database_name in GeoIP GeoIPv6; do
    gunzip -c $_database_name-$pkgver.dat.gz >$_database_name.dat
  done
}

check() {
  cd "$srcdir"

  for _database in {GeoIP,GeoIPv6}.dat; do
    if ! grep -q 'MaxMind Inc All Rights Reserved' $_database; then
      error "Copyright notice is missing from $_database; something is fishy!"
      return 1
    fi
  done
}

package() {
  cd "$srcdir"

  install -d "$pkgdir/usr/share/GeoIP"
  install -m644 -t "$pkgdir/usr/share/GeoIP" GeoIP.dat GeoIPv6.dat

  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
