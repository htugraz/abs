# $Id: PKGBUILD 250373 2015-11-09 21:46:03Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kblackbox
pkgver=15.08.3
pkgrel=1
pkgdesc="A game of hide and seek played on a grid of boxes"
url="http://kde.org/applications/games/kblackbox/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kblackbox')
replaces=('kdegames-kblackbox')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kblackbox-${pkgver}.tar.xz")
sha1sums=('6e10ff2f1379018bc14d9f8e60d2206bf6cf1e35')

build() {
  mkdir -p build
  cd build
  cmake ../kblackbox-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
