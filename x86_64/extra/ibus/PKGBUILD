# $Id: PKGBUILD 242298 2015-07-17 02:42:18Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=ibus
pkgname=(ibus libibus)
pkgver=1.5.11
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux"
arch=('i686' 'x86_64')
url="http://ibus.googlecode.com"
license=('LGPL')
depends=('dconf' 'gtk2' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'python-dbus' 'python-gobject' 'iso-codes' 'python2-gobject2' 'python2-dbus' 'python2-gobject' 'librsvg')
makedepends=('gobject-introspection' 'vala' 'intltool' 'git' 'gnome-common' 'gtk-doc' 'gconf' 'qt5-base')
options=('!emptydirs')
source=("git+https://github.com/ibus/ibus.git#tag=$pkgver")

build() {
  cd ${pkgbase}
  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --disable-gconf \
    --enable-dconf \
    --enable-wayland \
    --enable-gtk-doc \
    --disable-memconf \
    --enable-ui \
    --enable-python-library \
    --with-python=python3
  make
}

package_ibus() {
  depends+=("libibus=$pkgver")
  install=ibus.install

  cd ${pkgbase}
  make DESTDIR="${pkgdir}" install
  make -C src DESTDIR="${pkgdir}" uninstall
  make -C bindings DESTDIR="${pkgdir}" uninstall
  rm -r "$pkgdir/usr/lib/python2.7/site-packages/gi" # compiled pyc/pyo files were not cleaned
  make DESTDIR="${pkgdir}" uninstall-pkgconfigDATA
}

package_libibus() {
  pkgdesc="IBus support library"
  depends=('glib2')

  cd ${pkgbase}
  make -C src DESTDIR="${pkgdir}" install
  make -C bindings DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-pkgconfigDATA
}

sha512sums=('SKIP')
