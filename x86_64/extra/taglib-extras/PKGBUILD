# $Id: PKGBUILD 235749 2015-04-08 18:26:29Z arojas $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=taglib-extras
pkgver=1.0.1
pkgrel=3
pkgdesc="Additional taglib plugins for KDE"
arch=('i686' 'x86_64')
url="http://developer.kde.org/~wheeler/taglib.html"
license=('LGPL')
depends=('taglib')
makedepends=('pkgconfig' 'cmake' 'automoc4')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.gz")
md5sums=('e973ca609b18e2c03c147ff9fd9e6eb8')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
