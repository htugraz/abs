# $Id: PKGBUILD 221666 2014-09-14 13:02:04Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=knotifications
pkgver=5.2.0
pkgrel=1
pkgdesc='KNotifications'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/knotifications'
license=('LGPL')
depends=('phonon-qt5' 'libdbusmenu-qt5' 'kservice' 'kiconthemes')
makedepends=('extra-cmake-modules' 'qt5-tools' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('1e4affeb4319eca4a0afb885319ec060')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DLOCALE_INSTALL_DIR=/usr/share/locale/kf5 \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
