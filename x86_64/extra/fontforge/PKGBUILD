# $Id: PKGBUILD 197956 2013-10-30 11:54:42Z allan $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20120731_b
pkgrel=6
pkgdesc='Outline and bitmap font editor'
url='http://fontforge.sourceforge.net/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'python2' 'libspiro'
         'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme')
source=("http://downloads.sourceforge.net/project/fontforge/fontforge-source/fontforge_full-${pkgver//_/-}.tar.bz2"
        'http://fontforge.org/cidmaps.tgz')
sha1sums=('b520f532b48e557c177dffa29120225066cc4e84'
          'efbc7c9d3e95159f5600dc71f9cccb370e46bb94')

# git clone git://fontforge.git.sourceforge.net/gitroot/fontforge/fontforge; cd fontforge; git archive --prefix=${pkgname}-${pkgver}/ master | xz > ../${pkgname}-${pkgver}.tar.xz
# source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz")

options=('!makeflags')
install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver//_/-}"
	sed 's/python /python2 /g' -i Makefile.dynamic.in
	sed \
		-e'/^#include <gif_lib.h>/i #include <stddef.h>' \
		-e 's/\(DGifOpenFileName([^)]*\))/\1,NULL)/g' \
		-i configure gutils/gimagereadgif.c
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver//_/-}"
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-type3 \
		--enable-devicetables \
		--enable-double \
		--with-regular-link \
		--with-python=python2 \
		--enable-pyextension \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver//_/-}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/fontforge"
	install -m644 ../Adobe-* "${pkgdir}/usr/share/fontforge"

	install -d "${pkgdir}/usr/share/icons"
	cp -a Packaging/icons "${pkgdir}/usr/share/icons/hicolor"
	rm -fr "${pkgdir}/usr/share/icons/hicolor/src"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 Packaging/fontforge.desktop "${pkgdir}/usr/share/applications/fontforge.desktop"
}
