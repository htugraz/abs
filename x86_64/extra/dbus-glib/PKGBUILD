# $Id: PKGBUILD 206080 2014-02-17 10:22:31Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=dbus-glib
pkgver=0.102
pkgrel=1
pkgdesc="GLib bindings for DBUS"
arch=(i686 x86_64)
license=('GPL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
depends=('dbus-core' 'glib2')
makedepends=('pkgconfig')
options=('!emptydirs')
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
md5sums=('f76b8558fd575d0106c3a556eaa49184'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --enable-static=no --enable-bash-completion=no
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
