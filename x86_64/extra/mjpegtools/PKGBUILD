# $Id: PKGBUILD 198321 2013-10-30 14:09:25Z allan $
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Maintainer: Roberto Carvajal <roberto@archlinux.org>

pkgname=mjpegtools
pkgver=2.0.0
pkgrel=3
pkgdesc="Set of tools that can do recording of videos and playback, simple cut-and-paste editing and the MPEG compression of audio and video"
arch=(i686 x86_64)
license=('GPL')
url="http://mjpeg.sourceforge.net/"
depends=('libjpeg' 'libpng' 'sdl' 'gcc-libs' 'libdv')
makedepends=('gtk2' 'v4l-utils')
optdepends=('gtk2: glav GUI')
options=('!makeflags')
install=mjpegtools.install
source=(http://downloads.sourceforge.net/sourceforge/mjpeg/${pkgname}-${pkgver}.tar.gz
        mjpegtools-v4l-2.6.38.patch
        mjpegtools-2.0.0-gcc470.patch)
md5sums=('903e1e3b967eebcc5fe5626d7517dc46'
         '44006d6f844017fe57aa123aa43f4508'
         '7c9b3d4a1f3a10e3263a6a110b0f55cc')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np0 -i "${srcdir}/mjpegtools-v4l-2.6.38.patch"
  patch -Np1 -i "${srcdir}/mjpegtools-2.0.0-gcc470.patch"
  sed -i -e '/ARCHFLAGS=/s:=.*:=:' configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-largefile
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m644 mpeg2enc/mpeg2syntaxcodes.h "${pkgdir}/usr/include/mjpegtools/mpeg2enc/"
}
