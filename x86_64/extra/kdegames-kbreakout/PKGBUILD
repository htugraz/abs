# $Id: PKGBUILD 223266 2014-09-26 17:22:11Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kbreakout
pkgver=4.14.1
pkgrel=1
pkgdesc="A Breakout-like game"
url="http://kde.org/applications/games/kbreakout/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kbreakout-${pkgver}.tar.xz")
sha1sums=('62c484021dedd779265dc7a403c699b957c38b85')

build() {
  mkdir build
  cd build
  cmake ../kbreakout-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
