# $Id: PKGBUILD 211259 2014-04-18 11:54:05Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: M Rawash <mrawash@gmail.com>

pkgname=xf86-input-wacom
pkgver=0.24.0
pkgrel=1
pkgdesc="X.Org Wacom tablet driver"
arch=(i686 x86_64)
url="http://linuxwacom.sourceforge.net/"
license=(GPL)
backup=('etc/X11/xorg.conf.d/50-wacom.conf')
depends=(libxi libxinerama libxrandr systemd)
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=20' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.15.0' 'X-ABI-XINPUT_VERSION<20' 'X-ABI-XINPUT_VERSION>=21')
source=(http://downloads.sourceforge.net/project/linuxwacom/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('f487fffe1ba7d661aed84a9e36ea3b70')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
