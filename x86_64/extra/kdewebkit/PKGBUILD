# $Id: PKGBUILD 219428 2014-08-07 12:08:24Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdewebkit
pkgver=5.1.0
pkgrel=1
pkgdesc='KDE Webkit'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdewebkit'
license=('LGPL')
depends=('qt5-webkit' 'kparts')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('21787516593f0a8b0a1067ff82d4d9ac')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
