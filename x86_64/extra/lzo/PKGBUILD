# $Id: PKGBUILD 197768 2013-10-29 20:44:43Z eric $
# Maintainer:
# Contributor: Low Kian Seong <fastmail_low@speedymail.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=lzo
pkgver=1.08
pkgrel=8
pkgdesc='A portable lossless data compression library written in ANSI C'
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.oberhumer.com/opensource/lzo'
depends=('glibc')
makedepends=('nasm')
source=("http://www.oberhumer.com/opensource/lzo/download/LZO-v1/${pkgname}-${pkgver}.tar.gz"
        'nasm-gcc3.patch')
md5sums=('ab94d3da364c7cbd5b78d76f1875b0f6'
         '43fb13762383b572d22152f8815ea4a5')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p0 -i "${srcdir}"/nasm-gcc3.patch
}

build() {
  cd ${pkgname}-${pkgver}
  autoconf
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
