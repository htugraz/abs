# $Id: PKGBUILD 206559 2014-03-01 08:27:59Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: laloch <laloch@atlas.cz>

pkgname=qt-gstreamer
pkgver=0.10.2
pkgrel=3
pkgdesc="Qt4 bindings for GStreamer"
arch=('i686' 'x86_64')
url='http://gstreamer.freedesktop.org/modules/qt-gstreamer.html'
license=('LGPL2.1')
depends=('qt4' 'gstreamer0.10-base' 'libgles')
makedepends=('cmake' 'automoc4' 'boost' 'mesa' 'doxygen')
source=("http://gstreamer.freedesktop.org/src/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('0428aedf2874fd0884ad4c41db9da20e')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQTGSTREAMER_EXAMPLES=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
