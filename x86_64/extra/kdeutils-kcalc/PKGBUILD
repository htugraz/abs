# $Id: PKGBUILD 217695 2014-07-21 20:31:34Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kcalc
pkgver=4.13.3
pkgrel=1
pkgdesc='Scientific Calculator'
url='http://kde.org/applications/utilities/kcalc/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kcalc-${pkgver}.tar.xz")
sha1sums=('acca59a806e8d827bf46fc66faab713d9679c86c')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kcalc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
