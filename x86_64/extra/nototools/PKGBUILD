# $Id: PKGBUILD 249445 2015-10-16 23:55:30Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=nototools
_commit=580e5294
pkgver=git20151020
pkgrel=1
pkgdesc="Noto fonts support tools and scripts"
arch=(any)
url="https://www.google.com/get/noto/"
license=(Apache)
depends=(python2)
makedepends=(python2-setuptools)
source=("https://github.com/googlei18n/nototools/archive/$_commit.zip" data_path.patch)
sha256sums=('194c027b6e89febb7231daead7bc537ad7fed40fb3138f29df367b01d87a92d9'
            '8ef0f97a049a28d5b9dd8afa8b89ead06ae2cb81ebbf17da5e6f053c51b9b28c')

prepare() {
  cd nototools-*
  find -name '*.py' | xargs sed -e 's|/usr/bin/python|/usr/bin/python2|g' -e 's|/usr/bin/env python|/usr/bin/env python2|' -i

# Fix data files location
  patch -p0 -i ../data_path.patch
}

package() {
  cd nototools-*
  python2 setup.py install --root="$pkgdir" --optimize=1

  mkdir -p "$pkgdir"/usr/share/nototools
  cp -r third_party "$pkgdir"/usr/share/nototools
}
