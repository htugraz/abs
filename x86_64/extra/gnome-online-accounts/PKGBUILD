# $Id: PKGBUILD 239216 2015-05-12 20:13:54Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.16.2
pkgrel=1
pkgdesc="GNOME service to access online accounts"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL')
depends=('webkit2gtk' 'json-glib' 'libnotify' 'rest' 'libsecret' 'telepathy-glib' 'krb5' 'gcr')
makedepends=('intltool' 'libxslt' 'gobject-introspection' 'docbook-xsl')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('a50f2f0f54f85418c8274580a53fe6783a2b33d0c1fefb725b2dd2d00debd7e6')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --disable-static \
    --enable-exchange --enable-facebook --enable-flickr \
    --enable-google --enable-imap-smtp --enable-kerberos \
    --enable-owncloud --enable-telepathy --enable-windows-live \
    --enable-media-server --enable-foursquare
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
