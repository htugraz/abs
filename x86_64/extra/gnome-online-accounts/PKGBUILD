# $Id: PKGBUILD 211231 2014-04-18 11:19:32Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.12.1
pkgrel=1
pkgdesc="GNOME service to access online accounts"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL')
depends=('webkitgtk' 'json-glib' 'libnotify' 'rest' 'hicolor-icon-theme' 'libsecret'
         'telepathy-glib' 'krb5' 'gcr')
makedepends=('intltool' 'libxslt' 'gobject-introspection' 'docbook-xsl')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('2b8930e463c84d609a00ed9337db16550de89e7a7b8184ab18f2a63f150e7cef')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --disable-static \
    --enable-exchange --enable-facebook --enable-flickr \
    --enable-google --enable-imap-smtp --enable-kerberos \
    --enable-owncloud --enable-telepathy --enable-windows-live
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
