# $Id: PKGBUILD 227685 2014-12-17 07:51:32Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=libmm-qt5
pkgver=5.1.2
pkgrel=1
pkgdesc='Qt5 wrapper for ModemManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libmm-qt'
license=('LGPL')
depends=('modemmanager' 'qt5-base')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/stable/plasma/${pkgver}/libmm-qt-$pkgver.tar.xz")
md5sums=('e4623d229ae306db05cf0641235674d3')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libmm-qt-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
