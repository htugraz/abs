# $Id: PKGBUILD 239946 2015-05-30 18:13:30Z guillaume $
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=swt
# http://download.eclipse.org/eclipse/downloads/drops4
pkgver=4.4.2
pkgrel=1
_date=201502041700
pkgdesc='An open source widget toolkit for Java'
arch=('i686' 'x86_64')
url='http://www.eclipse.org/swt/'
license=('EPL')
depends=('java-runtime>=6' 'gtk2>=2.20.1' 'libxtst')
optdepends=('libgnomeui' 'glu' 'webkitgtk2')
makedepends=('java-environment' 'libxtst' 'glu' 'libgnomeui' 'unzip' 'pkgconfig' 'webkitgtk2' 'apache-ant')

source=(build-swt.xml)
source_i686=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86.zip)
source_x86_64=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86_64.zip)

sha256sums=('6bb48007a95e3d8c6b577cc9cc4b61a51ce928b04f4fcd393cf72f8f727fe923')
sha256sums_i686=('48f2ff63217d20a2abe3ce083f4e04da75aa6f9fa5675b1dd2a57e3151c6fe53')
sha256sums_x86_64=('fdfd66d4c2d5c92a2d505961b8e1738bedbdda0e910e45dafc2bd568c146307c')

# These examples don't even run anymore (Try Tux Guitar instead)!
# http://www.eclipse.org/swt/examples.php#standaloneOutsideEclipse
# http://download.eclipse.org/eclipse/downloads/

# http://mirror.ibcp.fr/pub/eclipse/eclipse/downloads/drops4/R-4.4-201406061215/org.eclipse.sdk.examples.source-4.4.zip

build() {
  cd ${srcdir}
  unzip -oq src.zip -d src

  export JAVA_HOME=/usr/lib/jvm/default
  . /etc/profile.d/apache-ant.sh

  # Shared objects
  cd src
  ./build.sh

  # SWT jar
  ant -f ../build-swt.xml compile
}

package() {
  cd ${srcdir}/src

  # Shared objects
  export OUTPUT_DIR=${pkgdir}/usr/lib
  install -dm755 ${OUTPUT_DIR} 
  make -f make_linux.mak install

  # SWT jar
  ant -f ../build-swt.xml jar
  install -Dm755 ../swt.jar ${pkgdir}/usr/share/java/swt-${pkgver}.jar
  ln -s swt-${pkgver}.jar ${pkgdir}/usr/share/java/swt.jar
}
