# $Id: PKGBUILD 249587 2015-10-20 20:50:16Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=noto-fonts-emoji
_pkgver=2015-09-29
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Google Noto emoji fonts"
arch=(any)
url="https://www.google.com/get/noto/"
license=(Apache)
depends=(fontconfig)
makedepends=(cairo optipng python2-fonttools python-fonttools nototools)
install=$pkgname.install
source=("https://github.com/googlei18n/noto-emoji/archive/v$_pkgver-license-apache.tar.gz")
sha256sums=('22d40e3b2a9f3f4dc0f1e758c2be8513e3e1a9d8843056466d7595b5476dbb86')

prepare() {
  cd noto-emoji-*
  find -name '*.py' | xargs sed -e 's|/usr/bin/env python|/usr/bin/env python2|' -i
  sed -e 's|python|python2|g' -i Makefile
  ln -rs add_emoji_gsub.py third_party/color_emoji/
}

package() {
  cd noto-emoji-*
  make

  mkdir -p "$pkgdir"/usr/share/fonts/noto
  install -m644 NotoColorEmoji.ttf NotoEmoji-Regular.ttf "$pkgdir"/usr/share/fonts/noto
}

