# $Id: PKGBUILD 199310 2013-11-10 09:59:15Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=phonon-vlc
pkgver=0.7.0
pkgrel=2
arch=('i686' 'x86_64')
url='http://phonon.kde.org/'
pkgdesc="Phonon VLC backend"
license=('LGPL')
depends=('vlc')
makedepends=('cmake' 'automoc4' 'phonon-qt4')
provides=('phonon-backend')
source=("http://download.kde.org/stable/phonon/phonon-backend-vlc/${pkgver}/src/phonon-backend-vlc-${pkgver}.tar.xz")
md5sums=('1a172744eb0fd9cb0dbe0ea8b69e50f1')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../phonon-backend-vlc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
