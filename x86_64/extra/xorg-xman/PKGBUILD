# $Id: PKGBUILD 113771 2011-03-10 15:39:50Z jgc $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xman
pkgver=1.1.4
pkgrel=1
pkgdesc="Unix manual page viewer"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxt')
makedepends=('xorg-util-macros' 'xproto' 'libxmu')
source=(http://xorg.freedesktop.org/archive/individual/app/xman-${pkgver}.tar.bz2{,.sig})
sha256sums=('b3fa9d2e3f3745c529eb0b6ee4f0e00d936457d11e5221b2efad06ff7c84df29'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xman-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xman-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
