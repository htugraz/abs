# $Id: PKGBUILD 191778 2013-07-30 17:02:46Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=whois
pkgver=5.0.26
pkgrel=1
pkgdesc="The whois client by Marco d'Itri"
arch=('i686' 'x86_64')
url="http://www.linux.it/~md/software/"
license=('GPL')
depends=('libidn')
makedepends=('perl')
source=("http://ftp.debian.org/debian/pool/main/w/whois/${pkgname}_${pkgver}.tar.xz")
md5sums=('236829eea77e85df2443a54815a1b41d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr CFLAGS="$CFLAGS" HAVE_LIBIDN=1 HAVE_ICONV=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr BASEDIR="${pkgdir}" install-whois
}
