# $Id: PKGBUILD 224958 2014-10-20 08:11:33Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-step
pkgver=4.14.2
pkgrel=1
pkgdesc="Interactive Physical Simulator"
url="http://kde.org/applications/education/step/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libqalculate' 'gsl')
makedepends=('cmake' 'automoc4' 'eigen2' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/step-${pkgver}.tar.xz")
sha1sums=('1092fedd58989a63357911ced063f12f77b792e1')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../step-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
