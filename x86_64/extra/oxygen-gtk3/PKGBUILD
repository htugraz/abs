# $Id: PKGBUILD 201464 2013-12-12 15:15:01Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=oxygen-gtk3
pkgver=1.3.0
pkgrel=1
pkgdesc="Port of the default KDE widget theme (Oxygen) to GTK3"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/artwork/oxygen-gtk/'
license=('LGPL')
depends=('gtk3')
makedepends=('cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('47901c08c1e1d2451da278552881ec06')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
