# $Id: PKGBUILD 241285 2015-06-26 03:11:29Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Firmicus <francois.archlinux.org> 

pkgname=perl-uri
_realname=URI
pkgver=1.68
pkgrel=1
pkgdesc="Uniform Resource Identifiers (absolute and relative)"
arch=('any')
url="http://search.cpan.org/dist/${_realname}/"
license=('PerlArtistic')
depends=('perl')
provides=('perl-uri-escape=3.30')
options=('!emptydirs')
source=("http://cpan.metacpan.org/authors/id/E/ET/ETHER/${_realname}-${pkgver}.tar.gz")
md5sums=('4a954ab8d79f831616ca817ba4b26cb9')

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"
}
# vim: ts=2 sw=2 et ft=sh
