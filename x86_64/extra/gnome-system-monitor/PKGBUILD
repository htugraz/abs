# $Id: PKGBUILD 249589 2015-10-21 01:15:53Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=3.18.0.1
pkgrel=1
pkgdesc="A system monitor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libgtop' 'gtkmm3' 'librsvg' 'systemd' 'libwnck3')
makedepends=('itstool' 'docbook-xsl' 'intltool' 'desktop-file-utils' 'appdata-tools')
options=(!emptydirs)
url="http://www.gnome.org"
groups=('gnome')
install=gnome-system-monitor.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('71ff8db2fa3eb53d8f54ffd612c6679b231a9d69c13adb91cf63421425953b10')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --enable-systemd --enable-wnck
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
