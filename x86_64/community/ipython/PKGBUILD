# $Id: PKGBUILD 106220 2014-02-25 15:22:43Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: PepeSmith
# Contributor: Aron Asor <aronasorman at gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=ipython 
pkgname=(ipython ipython2)
pkgver=1.2.1
pkgrel=1
pkgdesc="An enhanced Interactive Python shell."
arch=('any')
url="http://ipython.org"
license=('BSD')
depends=('python' 'sqlite' 'python-setuptools')
makedepends=('python-setuptools' 'python2-setuptools' 'sqlite')
optdepends=("wxpython: needed for ipythonx and ipython-wx"
            "python-nose: if you want to run IPython's test suite"
            "python-pyqt4: for ipython qtconsole"
            "python-sip: for ipython qtconsole"
            "python-pygments: for ipython qtconsole"
            "python-pyzmq: for ipython notebook and ipython qtconcole"
            "python-tornado: for ipython notebook"
            "python-jinja: for ipython notebook"
            "haskell-pandoc: ipython notebook conversion")
#source=("http://archive.ipython.org/release/$pkgver/ipython-$pkgver.tar.gz")
source=("https://pypi.python.org/packages/source/i/ipython/$pkgbase-$pkgver.tar.gz")
md5sums=('70d69c78122923879232567ac3c47cef')
md5sums=('4ffb36697f7ca8cb4a2de0f5b30bc89c')

build() {
  # feels hacky
  cd "$srcdir"
  cp -r ipython-$pkgver ipython2-$pkgver
}

package_ipython() {
  provides=('ipython3')
  replaces=('ipython3')
  cd "$srcdir/ipython-$pkgver"

  # see https://github.com/ipython/ipython/issues/2057
  #export LC_ALL=en_US.UTF-8
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
  find "$pkgdir/" -type d -empty -delete

  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython/LICENSE"

  # binary3 -> binary
  cd "$pkgdir/usr/bin/"
  for i in *; do
    mv $i ${i%3}
  done

  cd "$srcdir/ipython-$pkgver/examples/core"
  install -Dm644 ipython.desktop "$pkgdir/usr/share/applications/ipython.desktop"
  install -Dm644 ipython-qtconsole.desktop "$pkgdir/usr/share/applications/ipython-qtconsole.desktop"
}

package_ipython2() {
  pkgdesc="An enhanced Interactive Python2 shell."
  depends=('python2' 'sqlite' 'python2-setuptools')
  optdepends=("wxpython: needed for ipythonx and ipython-wx"
              "twisted: networking-related tasks"
              "python2-foolscap: for IPython's parallel computing features"
              "python2-pexpect: for irunner"
              "python2-nose: if you want to run IPython's test suite"
              "python2-pyqt4: for ipython qtconsole"
              "python2-sip: for ipython qtconsole"
              "python2-pygments: for ipython qtconsole"
              "python2-pyzmq: for ipython notebook and ipython qtconsole"
              "python2-tornado: for ipython notebook"
              "python2-jinja: for ipython notebook"
              "haskell-pandoc: ipython notebook conversion")

  cd "$srcdir/ipython2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  rm -rf "$pkgdir/usr/share/doc"
  find "$pkgdir" -name '*.py' -print0 | xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
  find "$pkgdir/" -name '*.pyc' -delete

  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython2/LICENSE"

  # hack to get around ipython collision
  cd "$pkgdir/usr/share/man/man1/"
  for i in *; do
    mv $i ${i/%.1/2.1}
  done
  cd "$pkgdir/usr/bin/"
  for i in *; do
    mv $i ${i}2
  done

  cd "$srcdir/ipython2-$pkgver/examples/core"
  sed -i 's/ython/ython2/g' *.desktop
  install -Dm644 ipython.desktop "$pkgdir/usr/share/applications/ipython2.desktop"
  install -Dm644 ipython-qtconsole.desktop "$pkgdir/usr/share/applications/ipython2-qtconsole.desktop"
}

