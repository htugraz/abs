# $Id: PKGBUILD 117970 2014-08-27 16:48:04Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Max Meyer <dev@fedux.org>

pkgbase=pacparser
pkgname=('pacparser' 'python2-pacparser')
pkgver=1.3.1
pkgrel=2
arch=(i686 x86_64)
url="http://pacparser.googlecode.com"
license=('LGPL')
makedepends=('python2')
options=('!makeflags')
source=("http://pacparser.googlecode.com/files/${pkgbase}-${pkgver}.tar.gz")

build() {
  cd $pkgbase-$pkgver
  PYTHON=python2 make all pymod -C src
}

package_pacparser() {
  pkgdesc="Library to parse proxy auto-config (PAC) files"

  cd $pkgbase-$pkgver
  make -C src DESTDIR="$pkgdir/" install

  # FS#41709
  mv "$pkgdir"/usr/share/man/man3/{,pacparser_}deprecated.3
}

package_python2-pacparser() {
  depends=('python2')
  pkgdesc="Python 2.x pacparser module"

  cd $pkgbase-$pkgver
  PYTHON=python2 make -C src DESTDIR="$pkgdir/" install-pymod
}

# vim:set ts=2 sw=2 et:
sha512sums=('20295667d92eb5385429c085c14ca8dc1ba216487ff474304433f99119216934be2dd1647452173418de31f5345679dd7159eafd331e40a2b87564f878d31349')
