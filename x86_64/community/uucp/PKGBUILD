# $Id: PKGBUILD 91914 2013-05-29 10:32:53Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jick Nan <jick.nan@gmail.com>

pkgname=uucp
pkgver=1.07
pkgrel=4
pkgdesc="Taylor UUCP is a free implementation of UUCP and is the standard UUCP used on the GNU system"
arch=(i686 x86_64)
url="http://www.gnu.org/software/uucp/uucp.html"
license=('GPL')
makedepends=('wget' 'sed' 'grep')
backup=('etc/uucp/config'
	'etc/uucp/port'
	'etc/uucp/sys')
source=(http://ftp.gnu.org/gnu/uucp/${pkgname}-${pkgver}.tar.gz)
md5sums=('64c54d43787339a7cced48390eb3e1d0')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|${CC-cc} -E|${CC-cc} -E -O2|g' configure
#  sed -i \
#    -e '1,1i#include <unistd.h>' \
#    -e '1,1i#include <fcntl.h>' \
#    -e '1,1i#include <sys/types.h>' \
#    uuconf/rdlocs.c unix/{efopen,cusub,init}.c
#  sed -i '1,1i#include <sys/statvfs.h>' unix/fsusg.c
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --with-newconfigdir=/etc/uucp --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 sample/config ${pkgdir}/etc/uucp/config
  install -D -m644 sample/sys1 ${pkgdir}/etc/uucp/sys
  install -D -m644 sample/port ${pkgdir}/etc/uucp/port
  mv ${pkgdir}/usr/info ${pkgdir}/usr/share/
}
