# $Id: PKGBUILD 137324 2015-07-23 11:36:36Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=1.0.1
pkgrel=1
pkgdesc="Professional vector animation program (CLI renderer only)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('libxml++' 'libsigc++2.0' 'etl' 'imagemagick' 'ffmpeg-compat' 'fontconfig' 'libpng'
	 'libtiff' 'libdv' 'libmng' 'cairo' 'pango' 'boost-libs' 'mlt')
makedepends=('boost')
optdepends=('openexr' 'libsigc++')
conflicts=('synfig-core')
replaces=('synfig-core')
source=(http://download.tuxfamily.org/synfig/releases/$pkgver/synfig-$pkgver.tar.gz
	build-fix.patch
	ffmpeg-0.8.patch)
md5sums=('0af2abd4ea9fd050d905c737fdc19567'
         '6d5277b28a3ba531e3e53e4a1f810fa7'
         'ebf41046097cfd778dee9a9854561dae')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir/build-fix.patch"
  patch -Np1 -i "$srcdir/ffmpeg-0.8.patch"
  sed -i 's|.*set_remove_alpha.*||g' src/modules/mod_libavcodec/trgt_av.cpp
}

build() {
  cd $pkgname-$pkgver
  export PKG_CONFIG_PATH=/usr/lib/ffmpeg-compat/pkgconfig:$PKG_CONFIG_PATH
  LDFLAGS="$LDFLAGS -Wl,-rpath -Wl,/usr/lib/ffmpeg-compat"
  CFLAGS="$CFLAGS -D__STDC_CONSTANT_MACROS"
  CXXFLAGS="$CXXFLAGS -D__STDC_CONSTANT_MACROS"
  [ -f configure ] || { libtoolize --ltdl --copy --force && autoreconf --install --force; }
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
