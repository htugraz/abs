# $Id: PKGBUILD 134777 2015-06-04 13:33:01Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>

pkgbase=python-ndg-httpsclient
pkgname=(python-ndg-httpsclient python2-ndg-httpsclient)
_pkgname=ndg_httpsclient
pkgver=0.4.0
pkgrel=2
pkgdesc="Provides enhanced HTTPS support for httplib and urllib2 using PyOpenSSL"
arch=(any)
url="https://pypi.python.org/pypi/ndg-httpsclient"
license=('BSD')
makedepends=(python2-setuptools python2-pyopenssl python2-pyasn1
             python-setuptools python-pyopenssl python-pyasn1)
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/n/ndg-httpsclient/$_pkgname-$pkgver.tar.gz
        https://github.com/cedadev/ndg_httpsclient/raw/v$pkgver/LICENSE)
sha256sums=('e8c155fdebd9c4bcb0810b4ed01ae1987554b1ee034dd7532d7b8fdae38a6274'
            '7be3995faaf78cebcf2cfcd77dbad990855d7a639243900d2208a824ced29e07')

build() {
  cd "$srcdir"/$_pkgname-$pkgver

  rm -rf ../buildpy3; mkdir ../buildpy3
  python setup.py build -b ../buildpy3

  rm -rf ../buildpy2; mkdir ../buildpy2
  python2 setup.py build -b ../buildpy2
}

package_python-ndg-httpsclient() {
  depends=(python-pyopenssl python-pyasn1)

  cd "$srcdir"/$_pkgname-$pkgver
  rm -rf build; ln -s ../buildpy3 build
  python setup.py install --skip-build -O1 --root="$pkgdir"
  install -m0644 -D "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-ndg-httpsclient() {
  depends=(python2-pyopenssl python2-pyasn1)

  cd "$srcdir"/$_pkgname-$pkgver
  rm -rf build; ln -s ../buildpy2 build
  python2 setup.py install --skip-build -O1 --root="$pkgdir"
  install -m0644 -D "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  mv "$pkgdir"/usr/bin/ndg_httpclient{,2}
}
