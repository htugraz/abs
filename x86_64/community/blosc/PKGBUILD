# $Id: PKGBUILD 147336 2015-11-21 16:39:09Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=blosc
pkgver=1.7.0
pkgrel=1
pkgdesc='A blocking, shuffling and loss-less compression library.'
arch=('i686' 'x86_64')
url='http://blosc.org/'
license=('MIT')
depends=('lz4' 'snappy' 'zlib')
makedepends=('cmake')
options=(staticlibs)
source=("https://github.com/Blosc/c-blosc/archive/v${pkgver}.tar.gz")
md5sums=('61256ab823b213a62d421512762e2a27')

build() {
  cd "${srcdir}"
  rm -rf build
  mkdir build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DPREFER_EXTERNAL_LZ4="ON" \
        -DBUILD_TESTS="OFF" \
        ../c-blosc-${pkgver}

  make
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir"/c-blosc-${pkgver}/LICENSES/BLOSC.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
