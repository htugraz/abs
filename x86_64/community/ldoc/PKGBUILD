# $Id: PKGBUILD 101737 2013-11-30 16:22:11Z seblu $
# Maintainer: SÃ©bastien Luttringer

pkgname=ldoc
pkgver=1.4.0
pkgrel=1
pkgdesc='A LuaDoc-compatible documentation generation system'
arch=('any')
url='http://stevedonovan.github.com/ldoc/'
license=('custom')
depends=('lua' 'lua-penlight')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stevedonovan/ldoc/archive/$pkgver.tar.gz")
md5sums=('b6643cb575b50cba4c3ae8694e9f0bab')

package() {
  cd LDoc-$pkgver
  # install library
  install -dm755 "$pkgdir/usr/share/lua/5.2"
  cp -a ldoc "$pkgdir/usr/share/lua/5.2"
  # install binaries
  install -Dm755 ldoc.lua "$pkgdir/usr/bin/ldoc.lua"
  ln -s ldoc.lua "$pkgdir/usr/bin/ldoc"
  # copy docs
  install -Dm644 readme.md "$pkgdir/usr/share/doc/$pkgname/readme.md"
  install -Dm644 doc/doc.md "$pkgdir/usr/share/doc/$pkgname/doc.md"
  # copy license
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
