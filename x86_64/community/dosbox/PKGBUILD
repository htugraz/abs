# $Id: PKGBUILD 98144 2013-10-06 16:29:33Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=dosbox
pkgver=0.74
pkgrel=6
pkgdesc='Emulator with builtin DOS for running DOS Games'
arch=('i686' 'x86_64')
url='http://dosbox.sourceforge.net/'
license=('GPL')
depends=('sdl_net' 'zlib' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'gcc-libs' 'glu')
makedepends=('mesa-libgl' 'gendesk')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        dosbox.png
        gcc46.patch)
sha256sums=('13f74916e2d4002bad1978e55727f302ff6df3d9be2f9b0e271501bd0a938e05'
            '491c42d16fc5ef7ee2eca1b736f7801249d4ca8c0b236a001aec0d3e24504f3b'
            'f4848d22982b47d1f123ff2c68493460e93aca593ea4a122d69d08625755359c')

prepare() {
  cd "$srcdir"

  gendesk --pkgname "$pkgname" --pkgdesc "$pkgdesc"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/gcc46.patch"
  sed -i 's/png_check_sig/png_sig_cmp/' configure 

  ./configure   --prefix=/usr --sysconfdir=/etc/dosbox  
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install 

# install docs, make does not install them
  install -Dm644 README "$pkgdir"/usr/share/doc/$pkgname/README
  install -Dm644 docs/README.video "$pkgdir"/usr/share/doc/$pkgname/README.video

  install -Dm644 "$srcdir/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
