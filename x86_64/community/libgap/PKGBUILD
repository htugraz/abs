# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libgap
pkgver=4.7.5.1
pkgrel=2
pkgdesc="A C library version of the GAP kernel"
arch=('i686' 'x86_64')
url="https://bitbucket.org/vbraun/libgap"
license=('GPL')
depends=('gmp')
#source=("https://bitbucket.org/vbraun/libgap/downloads/$pkgname-$pkgver.tar.gz")
source=("http://www.sagemath.org/packages/upstream/$pkgname/$pkgname-$pkgver.tar.gz" 'gap-4.7.6.patch')
md5sums=('f797ca3b8d3232cb24b5fe3be704a08a'
         '0233491508ee740c26030f97f51fe434')

prepare() {
  cd $pkgname-$pkgver
  
  # Sync source with GAP 4.7.6
  patch -p1 -i "$srcdir"/gap-4.7.6.patch
}

build() {
  cd $pkgname-$pkgver

  autoreconf
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}

