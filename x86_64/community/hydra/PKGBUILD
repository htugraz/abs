# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: pootzko <pootzko@gmail.com>
# Contributor: Jinx <jinxware@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>
 
pkgname=hydra
pkgver=8.1
pkgrel=3
pkgdesc='A very fast network logon cracker which support many different services'
url='http://www.thc.org/thc-hydra/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('postgresql-libs' 'subversion' 'libssh' 'libmariadbclient' 'libidn' 'libfbclient' 'afpfs-ng')
optdepends=(
  'gtk2: graphical front-end (xhydra)'
)
makedepends=('gtk2')
source=(${pkgname}-${pkgver}.tar.gz::http://www.thc.org/releases/${pkgname}-${pkgver}.tar.gz
        fix-locations.patch)
sha512sums=('7608dd365865e5976bea446faedaad9f0334e7572fd0ee47a601c471471fc7c8a922c7114f4df08705b34b29144d3a6552e66148adf6b0f92c67301119c94959'
            '229fdaff59c6970a030ec81a78a2d34861a6caa947e7c2f6f9186cfd3bc0bf35ec96deadaae58ff1eaa45574f268fa5d9b9688df8c033fcee35ddeb302fb2069')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../fix-locations.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}
 
package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" MANDIR="/share/man/man1" DATADIR="/share/${pkgname}" install
  install -Dm 644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim:set ts=2 sw=2 et:
