# $Id: PKGBUILD 152805 2015-12-11 03:42:07Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>
# Contributor: Peter Wu <lekensteyn at gmail dot com>

_npmname=babel-cli
pkgname=nodejs-$_npmname
pkgver=6.3.17
pkgrel=1
pkgdesc="A compiler for writing next generation JavaScript"
arch=('any')
url="https://babeljs.io/"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$_npmname/-/$_npmname-$pkgver.tgz"
        LICENSE)
noextract=($_npmname-$pkgver.tgz)
sha256sums=('0dc73016d043f2f1716b64ff012054c716539515857925623accae0702c52c6f'
            '8a5a22eb7991f5a022b91273b2395c2342f56fb43deddd29c248efb40c8ad62c')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$_npmname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
