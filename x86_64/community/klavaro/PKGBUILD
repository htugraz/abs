# $Id: PKGBUILD 98040 2013-10-04 13:46:45Z stativ $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=klavaro
pkgver=2.00
pkgrel=1
pkgdesc="Free touch typing tutor program"
arch=('i686' 'x86_64')
url="http://klavaro.sourceforge.net/"
license=('GPL2')
depends=('curl' 'gtkdatabox' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('espeakup: voice output')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('f7c804fbad7f9a053dfcfbea6d91dce6')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  sed -i 's|/usr/share/icons/hicolor/24x24/apps/klavaro.png|klavaro|' \
    "$pkgdir"/usr/share/applications/$pkgname.desktop
}
