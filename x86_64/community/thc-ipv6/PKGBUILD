# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: henning mueller <mail@nning.io>
# Contributor: Ariel Popper <a@arielp.com>
# Contributor: mortzu <me@mortzu.de>

pkgname=thc-ipv6
pkgver=3.0
pkgrel=3
pkgdesc='Complete tool set to attack the inherent protocol weaknesses of IPv6 and ICMP6'
url='https://thc.org/thc-ipv6/'
arch=('i686' 'x86_64')
license=('AGPL3')
depends=('libpcap' 'openssl' 'libnetfilter_queue')
source=(${pkgname}-${pkgver}.tar.gz::https://www.thc.org/releases/${pkgname}-${pkgver}.tar.gz)
sha512sums=('969ebbf4da4514cb83e3abcff637f7ba301c4f072f1f983b3cd3cfd3b0643b55af6eacca3c76e39e11ea9ef6e465be03dfa7ce1d6c988cabbd42cdd95d1e2552')
DLAGENTS=("https::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -o %o %u --insecure")

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|CFLAGS=|CFLAGS+=|g' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 README CHANGES HOWTO-INJECT -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
