# $Id: PKGBUILD 145237 2015-10-29 09:33:47Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-expects
pkgname=('python-expects' 'python2-expects')
pkgver=0.7.2
pkgrel=1
pkgdesc="Expressive and extensible TDD/BDD assertion library for Python"
arch=('any')
license=('Apache')
url="https://github.com/jaimegildesagredo/expects"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/jaimegildesagredo/expects.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a expects{,-py2}
}

build() {
  cd "$srcdir/expects"
  python setup.py build

  cd "$srcdir/expects-py2"
  python2 setup.py build
}

package_python-expects() {
  depends=('python')

  cd expects
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-expects() {
  depends=('python2')

  cd expects-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
