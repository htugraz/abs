# $Id: PKGBUILD 94870 2013-08-01 06:54:43Z bpiotrowski $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org> 
# Contributor: William Rea <sillywilly@gmail.com>, 
# Contributor: Bjorn Lindeijer <bjorn@lindeijer.nl>

pkgname=cegui
pkgver=0.8.2
pkgrel=5
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines"
arch=('i686' 'x86_64')
url="http://crayzedsgui.sourceforge.net"
license=("MIT")
depends=('pcre' 'glew' 'expat' 'freetype2' 'libxml2' 'devil' 'freeglut' 'lua51' 'silly' 'glfw')
makedepends=('cmake' 'python2' 'doxygen' 'ogre' 'gtk2' 'boost' 'graphviz' 'irrlicht' 'glm' 'mesa')
optdepends=("python2: python bindings"
            "ogre: ogre module"
            "gtk2: gtk2 module"
            "irrlicht: irrlicht module")
source=(http://downloads.sourceforge.net/crayzedsgui/cegui-$pkgver.tar.gz)
md5sums=('f72951f0cc0374bb2294035cb639bb83')

build() {
  cd "$srcdir/cegui-${pkgver}"

  sed -i "s/lib64/lib/g" CMakeLists.txt
  sed -i "s/lib32/lib/g" CMakeLists.txt

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCEGUI_LIB_INSTALL_DIR=lib \
      -DPYTHON_EXECUTABLE=/usr/bin/python2

  make
  make html
}

package() {
  cd "$srcdir/cegui-${pkgver}"
 
  cd build

  make DESTDIR="${pkgdir}" install

  cd ..

  install -Dm644 doc/COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
