# $Id: PKGBUILD 102242 2013-12-07 10:36:27Z bpiotrowski $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org> 
# Contributor: William Rea <sillywilly@gmail.com>, 
# Contributor: Bjorn Lindeijer <bjorn@lindeijer.nl>

pkgname=cegui
pkgver=0.8.2
pkgrel=6
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines"
arch=('i686' 'x86_64')
url="http://crayzedsgui.sourceforge.net"
license=("MIT")
depends=('pcre' 'glew' 'expat' 'freetype2' 'libxml2' 'devil' 'freeglut' 'lua51' 'silly' 'glfw')
makedepends=('cmake' 'python2' 'doxygen' 'ogre' 'gtk2' 'boost' 'graphviz' 'irrlicht' 'glm' 'mesa' 'mercurial' 'tolua++')
optdepends=("python2: python bindings"
            "ogre: ogre module"
            "gtk2: gtk2 module"
            "irrlicht: irrlicht module")
source=("hg+ssh://hg@bitbucket.org/cegui/cegui#branch=v0-8")
md5sums=('SKIP')

build() {
  cd "$srcdir/cegui"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCEGUI_LIB_INSTALL_DIR=lib \
      -DFREETYPE_INCLUDE_DIR=/usr/include/freetype2/ \
      -DCEGUI_BUILD_PYTHON_MODULES=OFF # currently broken :(

  make
  make html
}

package() {
  cd "$srcdir/cegui"
 
  cd build

  make DESTDIR="${pkgdir}" install

  cd ..
  install -Dm644 doc/COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
