# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=mate-terminal
pkgname=(${_pkgbase})
pkgver=${_ver}.1
pkgrel=1
pkgdesc="The MATE Terminal Emulator (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'dconf' 'libsm' 'mate-desktop>=1.10' 'vte')
makedepends=('docbook2x' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('72794300b9815de0e861e535e3c5601c19441c54')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --with-gtk=2.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
