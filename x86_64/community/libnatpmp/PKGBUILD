# $Id: PKGBUILD 109133 2014-04-10 15:59:00Z anatolik $
# Maintainer: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: Gustavo Alvarez <s1pkn07@gmail.com>
# Contributor: Kristjan Reinloo <mail at kreinloo dot net>
# Contributor: Pierre Bourdon <delroth@gmail.com>

pkgname=libnatpmp
pkgver=20140401
pkgrel=1
pkgdesc='A portable and fully compliant implementation of the NAT-PMP protocol'
arch=(i686 x86_64)
url='http://miniupnp.free.fr/libnatpmp.html'
license=(BSD)
depends=(glibc)
source=(http://miniupnp.tuxfamily.org/files/libnatpmp-$pkgver.tar.gz)
sha1sums=('e9511edecb9a75dbe8c9ed39c0469a18dc7e950b')

prepare() {
  sed -e 's/CFLAGS = /CFLAGS += /' -i $pkgname-$pkgver/Makefile
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALLPREFIX="$pkgdir/usr" INSTALLDIRINC="$pkgdir/usr/include" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 natpmpc.1 "$pkgdir/usr/share/man/man1/natpmpc.1"
}
