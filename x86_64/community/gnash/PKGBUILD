# $Id: PKGBUILD 107173 2014-03-14 00:16:07Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=gnash
pkgname=(gnash-common gnash-gtk)
pkgver=0.8.11dev
pkgrel=1
arch=(i686 x86_64)
url="http://www.gnu.org/software/gnash/"
license=(GPL3)
makedepends=(curl giflib libldap sdl agg libjpeg libpng libtool mesa glu
             speex fontconfig ffmpeg jemalloc
             gtk2 libldap xulrunner hicolor-icon-theme desktop-file-utils gconf gtkglext
             pkgconfig boost git)
options=(!emptydirs)
source=('gnash::git://git.sv.gnu.org/gnash.git#commit=a3180f2'
        nodebug.patch)
sha256sums=('SKIP'
            '0ebb104a7632af997c7e6b268755949fa0c1eea5e32015b95a22d63d0e431551')

prepare() {
  cd gnash

  # Disable very, very verbose debugging (gigabytes of logs)
  patch -Np1 -i ../nodebug.patch

  ./autogen.sh
}

build() {
  cd gnash
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-plugins-install=system \
    --with-npapi-plugindir=/usr/lib/mozilla/plugins \
    --enable-gui=sdl,gtk,dump \
    --enable-media=ffmpeg \
    --enable-renderer=all \
    --enable-device=x11

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package_gnash-common() {
  pkgdesc="A GNU Flash movie player"
  depends=(curl giflib sdl agg libjpeg libpng libltdl libgl glu
           speex fontconfig cairo ffmpeg jemalloc
           boost-libs)
  backup=(etc/gnashrc etc/gnashthumbnailrc)

  cd gnash
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  # Split gnash-gtk
  mkdir -p "$srcdir"/gtk/{etc,usr/{bin,share/man/man1}}
  mv "$pkgdir"/usr/bin/{gtk-gnash,gnash-gtk-launcher} \
    "$srcdir/gtk/usr/bin/"
  mv "$pkgdir"/usr/share/man/man1/{gtk-gnash,gnash-gtk-launcher}.1 \
    "$srcdir/gtk/usr/share/man/man1/"
  mv "$pkgdir"/usr/share/{applications,icons} "$srcdir/gtk/usr/share/"
  mv "$pkgdir"/etc/gnashpluginrc "$srcdir/gtk/etc/"
}

package_gnash-gtk() {
  pkgdesc="A GNU Flash movie player"
  depends=("gnash-common=$pkgver"
            gtk2 libldap hicolor-icon-theme desktop-file-utils gconf gtkglext)
  install=gnash-gtk.install
  backup=(etc/gnashpluginrc)

  mv gtk/* "$pkgdir"

  cd gnash
  make DESTDIR="$pkgdir" install-plugin

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/gnash.schemas" --domain gnash \
    "$pkgdir"/usr/share/applications/*.schemas
  rm -f "$pkgdir"/usr/share/applications/*.schemas
}
