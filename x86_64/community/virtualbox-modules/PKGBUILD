# $Id: PKGBUILD 143451 2015-10-08 20:24:25Z seblu $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: SÃ©bastien Luttringer

pkgbase=virtualbox-modules
pkgname=('virtualbox-host-modules' 'virtualbox-guest-modules')
pkgver=5.0.6
pkgrel=1
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux>=4.2' 'linux<4.3'
             'linux-headers>=4.2' 'linux-headers<4.3'
             "virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver")

# remember to also adjust the .install files and the package deps below
_extramodules=extramodules-4.2-ARCH

build() {
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
  # build guest modules
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-host-modules(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Host kernel modules for VirtualBox'
  depends=('linux>=4.2' 'linux<4.3')
  replaces=('virtualbox-modules')
  conflicts=('virtualbox-modules')
  install=virtualbox-host-modules.install

  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

package_virtualbox-guest-modules(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=('linux>=4.2' 'linux<4.3')
  replaces=('virtualbox-archlinux-modules')
  conflicts=('virtualbox-archlinux-modules')
  install=virtualbox-guest-modules.install

  cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

# vim:set ts=2 sw=2 et:



