# $Id: PKGBUILD 131015 2015-04-09 07:24:20Z lcarlier $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: shamrok <szamrok@gmail.com>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>

pkgname=krecipes
pkgver=2.1.0
pkgrel=1
pkgdesc="A tool designed to make organizing your personal recipes collection fast and easy"
arch=('i686' 'x86_64')
url="http://extragear.kde.org/apps/krecipes/"
license=('GPL')
depends=('kdebase-runtime' 'qimageblitz' 'hicolor-icon-theme')
makedepends=('cmake' 'automoc4' 'docbook-xsl')
optdepends=('mariadb' 'postgresql')
install=${pkgname}.install
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('422ff3df8ab5aac56617d87942762e88')

build() {
  cd ${srcdir}

  mkdir build
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
