# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-themes
pkgver=1.8.1
pkgrel=1
pkgdesc="Default themes for the MATE desktop"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('gtk2' 'gtk-engines' 'gtk-engine-murrine' 'librsvg' 'mate-icon-theme')
makedepends=('mate-common' 'icon-naming-utils' 'perl-xml-parser')
optdepends=('gtk-engine-unico: Support for some GTK3 themes.')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz")
sha1sums=('1e03cf7adc35a115d73ec76194f3bc98d0101d88')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --enable-all-themes \
        --enable-test-themes \
        --disable-placeholders
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
