# $Id: PKGBUILD 129470 2015-03-18 10:52:12Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=pound
pkgver=2.7
pkgrel=1
pkgdesc="A reverse proxy, load balancer, and SSL wrapper"
arch=('i686' 'x86_64')
url="http://www.apsis.ch/pound/index_html"
license=('GPL')
depends=('pcre' 'openssl')
backup=(etc/pound/pound.cfg)
source=(http://www.apsis.ch/pound/Pound-$pkgver.tgz
	pound.runit
	pound.service
	pound.cfg)
md5sums=('ec8298aa3e4aee3ffbecdc0639d7f14a'
         '2a555ee5871a849cb1f6e6ba2ff054df'
         'c0d0726a96dfda0e83843362e401b3dc'
         '8937808acd22c6391ebe4340af8df854')

build() {
  cd $srcdir/Pound-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc/pound --sbindir=/usr/bin
  make
}

package() {
  cd $srcdir/Pound-$pkgver
  install -d $pkgdir/usr/sbin
  make DESTDIR=$pkgdir install 
  chown root.root -R $pkgdir/usr
  install -d $pkgdir/etc/sv
  install -d $pkgdir/etc/sv/pound
  install -m0755 $srcdir/pound.runit $pkgdir/etc/sv/pound/run
  install -D -m0644 $srcdir/pound.cfg $pkgdir/etc/pound/pound.cfg
  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  rmdir $pkgdir/usr/sbin
}
