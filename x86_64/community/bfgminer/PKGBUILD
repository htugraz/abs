# $Id: PKGBUILD 128007 2015-02-20 05:04:25Z fyan $
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=bfgminer
pkgver=5.1.0
pkgrel=1
pkgdesc="Bitcoin miner featuring overclocking, monitoring, fan speed control and remote management. For FPGA/GPU/CPU Bitcoin mining."
arch=('i686' 'x86_64')
depends=('curl' 'jansson' 'libevent' 'libmicrohttpd' 'libusb' 'lm_sensors')
makedepends=('uthash' 'yasm')
optdepends=('opencl-nvidia: OpenCL implementation for NVIDIA')
url='http://bfgminer.org/'
license=('GPL3')
source=(http://luke.dashjr.org/programs/bitcoin/files/$pkgname/$pkgver/$pkgname-$pkgver.txz
        remove-dangerous-rpath.patch)
sha256sums=('436595e8b3af267e8fed023ac6b5f208a385d891d0c3c1c41b8aa13f1c1e69c9'
            '27d89695ff963dd8f66654b9d454aa725c7db769def708513188fe0a496fda4d')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  patch -p1 -i "${srcdir}"/remove-dangerous-rpath.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-cpumining \
    --enable-bfsb \
    --enable-metabank \
    --enable-scrypt \
    --enable-opencl \
    --with-udevrulesdir=/usr/lib/udev/rules.d
  # --enable-knc (FS#33640)
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
