# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=stuntrally-data
_pkgname=stuntrally
pkgver=2.4
pkgrel=1
pkgdesc="Stunt Rally game with track editor, based on VDrift (data files)"
arch=('any')
license=('GPL3')
url="http://code.google.com/p/vdrift-ogre"
makedepends=('cmake' 'boost' 'libvorbis' 'mygui' 'sdl2' 'enet' 'hicolor-icon-theme' 'libxcursor')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stuntrally/stuntrally/archive/${pkgver}.tar.gz"
        "$pkgname-tracks-$pkgver.tar.gz::https://github.com/stuntrally/tracks/archive/${pkgver}.tar.gz")
sha256sums=('273697fae75de64509aa26d8fc5fdfe662764c3ac7fc3fa9b8d72f8a76c7a677'
            'aeff544dad02751e1a7b174d0edc87a8c56d4b66ce1e43e423bde2b2d3c51cbb')

build() {
  # build the sources
  cd "${srcdir}/stuntrally-${pkgver}/"

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX="/usr" \
      -DSHARE_INSTALL="share/stuntrally"
  make

  # build the tracks
  cd "${srcdir}/tracks-${pkgver}/"

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  # install the sources
  cd "${srcdir}/stuntrally-${pkgver}/build/"
  make DESTDIR="${pkgdir}" install

  # install the tracks
  cd "${srcdir}/tracks-${pkgver}/build"
  make DESTDIR="${pkgdir}/usr/share/stuntrally/" install

  # clean up
  rm -rf "${pkgdir}/usr/share/stuntrally/tracks/build"
  rm -rf "${pkgdir}/usr/share/icons"
  rm -rf "${pkgdir}/usr/share/applications"
  rm -rf "${pkgdir}/usr/bin"
}
