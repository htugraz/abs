# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=stuntrally-data
_pkgname=stuntrally
pkgver=2.1
pkgrel=1
pkgdesc="Stunt Rally game with track editor, based on VDrift (data files)"
arch=('any')
license=('GPL3')
url="http://code.google.com/p/vdrift-ogre"
makedepends=('cmake' 'boost' 'libvorbis' 'mygui' 'sdl2' 'enet' 'hicolor-icon-theme' 'libxcursor')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stuntrally/stuntrally/archive/${pkgver}.tar.gz"
        "$pkgname-tracks-$pkgver.tar.gz::https://github.com/stuntrally/tracks/archive/${pkgver}.tar.gz")
sha256sums=('d987e8c05ed1f3f54468e75e2c1f68a9a4c59ceefa7429130d4f800b28821ae0'
            'a1ab9b11d9f5b6c4aa8e5a9028f5994e21446d96fda5762e427365e21cfe3d0e')

build() {
  # build the sources
  cd "${srcdir}/stuntrally-${pkgver}/"

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX="/usr" \
      -DSHARE_INSTALL="share/stuntrally"
  make

  # build the tracks
  cd "${srcdir}/tracks-${pkgver}/"

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  #install the sources
  cd "${srcdir}/stuntrally-${pkgver}/build/"
  make DESTDIR="${pkgdir}" install

  # install the tracks
  cd "${srcdir}/tracks-${pkgver}/build"
  make DESTDIR="${pkgdir}/usr/share/stuntrally/" install

  # clean up
  rm -rf "${pkgdir}/usr/share/stuntrally/tracks/build"
  rm -rf "${pkgdir}/usr/share/icons"
  rm -rf "${pkgdir}/usr/share/applications"
  rm -rf "${pkgdir}/usr/bin"
}
