# $Id: PKGBUILD 135923 2015-06-27 18:59:38Z jsteel $
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: megadriver <megadriver at gmx dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: kagan <juanynie@mi.madritel.es>
# Contributor: Hans-Kristian Arntzen <maister@archlinux.us>

pkgname=mednafen
pkgver=0.9.38.5
pkgrel=1
pkgdesc="Command-line multi-system gaming emulator"
url="http://mednafen.sourceforge.net"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libsndfile' 'sdl' 'zlib')
makedepends=('mesa' 'glu')
source=(http://downloads.sourceforge.net/mednafen/$pkgname-$pkgver.tar.bz2)
md5sums=('ad8d2bea9dd3ae85c8c380c2cd031632')

build() {
  cd $pkgname

  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  for i in ABOUT-NLS AUTHORS ChangeLog COPYING INSTALL NEWS \
           README{,.DOS,.PORTING} TODO; do
    install -Dm644 $i "$pkgdir"/usr/share/doc/$pkgname/$i
  done
}
