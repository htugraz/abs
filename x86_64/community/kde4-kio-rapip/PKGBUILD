# $Id: PKGBUILD 96052 2013-08-19 13:04:58Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ReNoM <renom@list.ru>

pkgname=kde4-kio-rapip
pkgver=0.4
pkgrel=1
pkgdesc="KIOSlave to browse through the PDA file system via the SynCE infrastructure"
arch=(i686 x86_64)
url="http://synce.sourceforge.net/"
license="MIT"
depends=('kdelibs' 'synce-core')
makedepends=('gcc' 'cmake' 'automoc4')
install=kde4-kio-rapip.install
source=(http://downloads.sourceforge.net/synce/$pkgname-$pkgver.tar.gz)
md5sums=('8411e62bcf7936c2fc9dddc4150c9f96')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` .
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR=$pkgdir install
  install -Dm0644 LICENSE $pkgdir/usr/share/licenses/kde4-kio-rapip/LICENSE
}
