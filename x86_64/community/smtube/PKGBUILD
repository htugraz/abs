# $Id: PKGBUILD 108589 2014-03-29 05:10:53Z speps $
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Andreas Schnaiter <rc.poison@gmail.com>

pkgname=smtube
pkgver=2.1
pkgrel=1
pkgdesc="A small youtube browser"
arch=('i686' 'x86_64')
url="http://smplayer.sourceforge.net"
license=('GPL')
depends=('qt4')
optdepends=('mplayer: play videos with MPlayer'
            'smplayer: play videos with SMPlayer'
            'gnome-mplayer: play videos with GNOME MPlayer'
            'kdemultimedia-dragonplayer: play videos with Dragon Player'
            'totem: play videos with Totem'
            'vlc: play videos with VLC')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/smplayer/$pkgname-$pkgver.tar.bz2")
md5sums=('9cd034d5abcbc66445478b5824af50f1')

build() {
  cd $pkgname-$pkgver
  make PREFIX=/usr QMAKE=qmake-qt4 LRELEASE=lrelease-qt4
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir/" install
}
