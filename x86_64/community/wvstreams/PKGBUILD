# $Id: PKGBUILD 152044 2015-12-10 04:12:56Z foutrelis $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=wvstreams
pkgver=4.6.1
pkgrel=8
pkgdesc="A network programming library written in C++"
arch=('i686' 'x86_64')
url="http://alumnit.ca/wiki/?page=WvStreams"
license=('LGPL')
depends=('zlib' 'pam' 'xplc' 'openssl' 'readline')
source=(http://wvstreams.googlecode.com/files/${pkgname}-${pkgver}.tar.gz 
wvstreams-4.6.1-glibc212.patch wvstreams-4.6.1-gcc47.patch openssl-buildfix.patch)
md5sums=('2760dac31a43d452a19a3147bfde571c'
         '5f6e24864209055239cb4e7c9bbd4a41'
         '217e5dedd7f1d50588a5a67d637fc8a1'
         'df423c68134a09439b3961c34635e0b4')
options=('!makeflags')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p0 -i "$srcdir/wvstreams-4.6.1-glibc212.patch"
  patch -p0 -i "$srcdir/wvstreams-4.6.1-gcc47.patch"
  patch -p1 -i "$srcdir/openssl-buildfix.patch"
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
     --sbin=/usr/bin \
     --sysconfdir=/etc \
     --localstatedir=/var \
     --without-dbus \
     --with-pam \
     --with-openssl \
     --without-tcl \
     --without-qt
  make COPTS="$CFLAGS -fPIC" CXXOPTS="$CXXFLAGS -fPIC -fpermissive -fno-tree-dce -fno-optimize-sibling-calls" VERBOSE=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  # FS#15974
  # --without-dbus still installs the pkg-config file
  rm "${pkgdir}/usr/lib/pkgconfig/libwvdbus.pc"
  # --without-qt still installs the pkg-config file
  rm "${pkgdir}/usr/lib/pkgconfig/libwvqt.pc"
}
