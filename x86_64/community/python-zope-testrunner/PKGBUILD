# $Id: PKGBUILD 146478 2015-11-10 07:17:40Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-testrunner' 'python2-zope-testrunner')
pkgver=4.4.10
pkgrel=1
pkgdesc="Zope testrunner script"
arch=('any')
url="https://github.com/zopefoundation/zope.testrunner"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-exceptions' 'python2-zope-exceptions'
             'python-six' 'python2-six' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-zope-testing' 'python2-zope-testing'
              'python2-subunit')
source=("git+https://github.com/zopefoundation/zope.testrunner.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.testrunner{,-py2}
}

build() {
  cd "$srcdir/zope.testrunner"
  python setup.py build

  cd "$srcdir/zope.testrunner-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.testrunner"
  python setup.py test

  cd "$srcdir/zope.testrunner-py2"
  python2 setup.py test
}

package_python-zope-testrunner() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-exceptions' 'python-six')

  cd "$srcdir/zope.testrunner"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-testrunner() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-exceptions' 'python2-six')

  cd "$srcdir/zope.testrunner-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  mv "$pkgdir/usr/bin/zope-testrunner"{,2}
}

# vim:set ts=2 sw=2 et:
