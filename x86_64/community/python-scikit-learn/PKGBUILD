# $Id: PKGBUILD 131486 2015-04-16 17:08:01Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-scikit-learn
pkgname=('python2-scikit-learn' 'python-scikit-learn')
pkgver=0.16.1
pkgrel=1
pkgdesc="A set of python modules for machine learning and data mining"
arch=('i686' 'x86_64')
url="http://scikit-learn.sourceforge.net/"
license=('BSD')
makedepends=('python2-scipy' 'python-scipy' 'python2-setuptools' 'python-setuptools')
options=(!emptydirs)

source=("https://pypi.python.org/packages/source/s/scikit-learn/scikit-learn-${pkgver}.tar.gz"
        "LICENSE")
md5sums=('363ddda501e3b6b61726aa40b8dbdb7e'
         '327083d2576cc0aad1b8f10b2bcd2974')

prepare() {
  cd "$srcdir"
  cp -a scikit-learn-$pkgver scikit-learn-py2-$pkgver
  cd scikit-learn-py2-$pkgver

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/scikit-learn-py2-$pkgver
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/scikit-learn-$pkgver
  python setup.py build
}

package_python2-scikit-learn() {
  depends=('python2-scipy')
  cd "$srcdir"/scikit-learn-py2-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -D "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-scikit-learn() {
  depends=('python-scipy')
  cd "$srcdir"/scikit-learn-$pkgver

  python setup.py install --root="$pkgdir"/ --optimize=1

  install -D "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

