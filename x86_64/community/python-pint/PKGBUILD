# $Id: PKGBUILD 132943 2015-05-08 09:19:48Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Matthew Sloan matthew@sloan.cc

pkgbase=python-pint
pkgname=('python-pint' 'python2-pint')
pkgver=0.6
pkgrel=1
pkgdesc="A unit library for Python"
arch=('any')
license=('BSD')
url="http://pint.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-numpy' 'python2-numpy')  # 'python-uncertainties' 'python2-uncertainties'
source=("https://pypi.python.org/packages/source/P/Pint/Pint-${pkgver}.zip")
md5sums=('88aa0918019071546b10c5fbdc91ae5e')

prepare() {
  cp -a Pint-$pkgver{,-py2}
}

check() {
  cd "$srcdir/Pint-${pkgver}"
  python setup.py test

  cd "$srcdir/Pint-${pkgver}-py2"
  python2 setup.py test
}

package_python-pint() {
  depends=('python')

  cd "${srcdir}/Pint-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pint() {
  depends=('python2')

  cd "${srcdir}/Pint-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
