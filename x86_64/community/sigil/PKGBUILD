# $Id: PKGBUILD 118573 2014-09-07 08:31:03Z foutrelis $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=sigil
pkgver=0.7.4
pkgrel=3
pkgdesc="WYSIWYG ebook editor"
arch=('i686' 'x86_64')
url="http://code.google.com/p/sigil/"
license=('GPL3')
depends=('qt5-webkit' 'boost-libs')
makedepends=('qt5-tools' 'qt5-svg' 'cmake' 'gendesk' 'boost')
install=sigil.install
source=("http://sigil.googlecode.com/files/Sigil-${pkgver}-Code.zip")
md5sums=('dde8f3b8daa0ccd37589ea2aa454e1bc')

build() {
  cd "${srcdir}"
  gendesk -n "$startdir"/PKGBUILD

  mkdir build
  cd build

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
    -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON ..

  make
}

package() {
  cd "${srcdir}"/build
  make install DESTDIR="${pkgdir}"

  install -d -m 0755 "${pkgdir}"/usr/share/applications
  install -m 0644 "${srcdir}"/${pkgname}.desktop \
    "${pkgdir}"/usr/share/applications

  for _pic in 16 32 48 128 256; do
    install -D -m 0644 ../src/Sigil/Resource_Files/icon/app_icon_${_pic}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${_pic}x${_pic}/apps/${pkgname}.png
  done
  install -Dm644 ../src/Sigil/Resource_Files/icon/app_icon_128.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
