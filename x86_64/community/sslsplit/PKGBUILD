# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sslsplit
pkgver=0.4.11
pkgrel=2
pkgdesc="Tool for man-in-the-middle attacks against SSL/TLS encrypted network connections"
url="https://www.roe.ch/SSLsplit"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libevent' 'openssl')
checkdepends=('check')
source=(https://mirror.roe.ch/rel/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha512sums=('c3339b4d22eb8f94ad1bc161b2c2d9efcb2f2828985712a5580437077f4c0bd74657d42274c4a9118c92721b9bd1d8150cf83d89afef6144b20aba497ac1d69a'
            'SKIP')
validpgpkeys=('BFF9C7D7EA0EAC7F1AA55B3EFABE3324B5D3397E') # Daniel Roethlisberger <daniel@roe.ch>

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 test
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}/usr" install
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 NEWS.md "${pkgdir}/usr/share/doc/${pkgname}/NEWS.md"
}

# vim: ts=2 sw=2 et:
