# $Id: PKGBUILD 144894 2015-10-24 14:38:38Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: XaBz <xabz.2k5 gmail.com>

pkgname=oblogout
pkgver=0.2
pkgrel=16
pkgdesc='Openbox logout script'
arch=('any')
url='https://launchpad.net/oblogout'
depends=('pygtk' 'cairo' 'python2-pillow' 'python2-distutils-extra'
         'dbus-python' 'upower')
license=('GPL2')
backup=(etc/oblogout.conf)
conflicts=('openboxlogout-gnome')
source=("http://launchpad.net/oblogout/$pkgver/$pkgver.0/+download/$pkgname-$pkgver.tar.bz2"
	      'oblogout.patch'
	      "$pkgname.conf"
	      'ru.po')
sha256sums=('f80003b85e9f4f1c588675453ea03f40a86fc50546d509ceb69706eb045782a9'
            'cea52a14df0ef924fcdd63b2e82c14b94cc2a7753994c176621ace9502bd9109'
            'e8e915973c2dd0ebfb6797e2b49b2769380a00b4c47229a264b58608b0b7a1e7'
            'aa8504055ec1e0661c3588cde85aad3267ffa7a42414c106e6ce8bff71f9d883')

build() {
  cd "$pkgname"

  patch -p0 -i ../oblogout.patch
  sed -i 's/fromstring/frombytes/' oblogout/__init__.py # FS#46587
	sed -i '0,/on/s//on2/' "$pkgname"/*.py
  cp "$srcdir/ru.po" po
}

package() {
  cd "$pkgname"

  python2 setup.py install --root="$pkgdir"
  install -m644 "$srcdir/oblogout.conf" "$pkgdir/etc/"
}

# vim:set ts=2 sw=2 et:
