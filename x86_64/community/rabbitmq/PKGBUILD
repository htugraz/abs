# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Marcello "mereghost" Rocha <marcello.rocha@gmail.com>
# Contributor: Gianni Vialetto <gianni at rootcube dot net>
# Contributor: Stefan J. Betz <info at stefan-betz dot net>
# Contributor: p2k <Patrick dot Schneider at uni-ulm dot de>
# Contributor: Jonathan Liu <net147@gmail.com>
# Contributor: Christopher Grebs <cg@webshox.org>
# Contributor: mutantmonkey

pkgname=rabbitmq
pkgver=3.5.5
pkgrel=1
pkgdesc="Highly reliable and performant enterprise messaging implementation of AMQP written in Erlang/OTP"
url="https://rabbitmq.com"
arch=('any')
license=('MPL')
depends=('erlang-nox' 'systemd')
backup=('etc/rabbitmq/rabbitmq-env.conf')
install=rabbitmq.install
source=(https://www.rabbitmq.com/releases/${pkgname}-server/v${pkgver}/${pkgname}-server-generic-unix-${pkgver}.tar.gz{,.asc}
        rabbitmq-env.conf
        rabbitmq.service)
sha512sums=('07061e91de566d4ed58175dbae1bfa2373064f086cf18ea2b45cd040b7b81e6b99b3976f92195b9b79014dac7c32b28487b32c702260285b219f0341da6ed7ec'
            'SKIP'
            '4872ff514c9f841fe4c93bc442ec4e3177d1bfa3834e8d8a26fc77a63aff1cdc14944827673a59556e26210b2b4c0091f93c8e93a334710e5bc3336a64e77140'
            'd51b43f948febb23cdd5b0c7736f966d8433b05ccd95778e90d280d45ccfd9a714e888762f458337a784fff4b86430151adb0f81d779d6b99c9154fcd7e0e319')
validpgpkeys=('F78372A06FF50C80464FC1B4F7B8CEA6056E8E56') # RabbitMQ Release Signing Key <info@rabbitmq.com>

prepare() {
  cd ${pkgname}_server-${pkgver}
  sed -r 's|^(SYS_PREFIX=).*$|\1""|' -i sbin/rabbitmq-defaults
}

package() {
  cd ${pkgname}_server-${pkgver}
  local libdir="${pkgdir}/usr/lib/rabbitmq/lib/rabbitmq-server-${pkgver}"

  install -d "${libdir}"
  install -d "${pkgdir}/usr/bin"
  install -dm 750 "${pkgdir}/var/log/rabbitmq"
  install -dm 750 "${pkgdir}/var/lib/rabbitmq"
   
  cp -R ebin ${libdir}
  cp -R include ${libdir}
  cp -R plugins ${libdir}
  cp -R sbin ${libdir}
  cp -R share "${pkgdir}/usr"

  for script in ${libdir}/sbin/*; do
    ln -s "${script#${pkgdir}}" "${pkgdir}/usr/bin/"
  done
 
  install -Dm 644 "${srcdir}/rabbitmq-env.conf" "${pkgdir}/etc/rabbitmq/rabbitmq-env.conf"
  install -Dm 644 "${srcdir}/rabbitmq.service" "${pkgdir}/usr/lib/systemd/system/rabbitmq.service"
}

# vim: ts=2 sw=2 et:
