# $Id: PKGBUILD 55537 2011-09-14 08:02:10Z andrea $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Aaron, phrakture, Griffin <aaron@archlinux.org>
# Contributor: Gustav Munkby <grddev@gmx.net>

pkgname=antiword
pkgver=0.37
pkgrel=3
pkgdesc="A free MS Word reader for Linux and RISC OS"
arch=('i686' 'x86_64')
url="http://www.winfield.demon.nl/"
license=('GPL')
depends=('bash')
install=antiword.install
source=("http://www.winfield.demon.nl/linux/$pkgname-$pkgver.tar.gz"
        'antiword-helper')
md5sums=('f868e2a269edcbc06bf77e89a55898d1'
         '01a988376cfd4bb268120ab597c9abe3')

build(){
  cd "${srcdir}"/$pkgname-$pkgver

  sed -i "s|GLOBAL_INSTALL_DIR =.*|GLOBAL_INSTALL_DIR = /usr/bin|g" \
    Makefile.Linux

  make OPT="${CFLAGS} -DNDEBUG" LDFLAGS="${LDFLAGS}"
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" global_install
  install -m755 $srcdir/antiword-helper $pkgdir/usr/bin/
  install -Dm644 Docs/antiword.1 $pkgdir/usr/share/man/man1/antiword.1
}
