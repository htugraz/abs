# $Id: PKGBUILD 112836 2014-06-08 21:07:40Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: David Moore <davidm@sjsoft.com>

pkgname=python2-xlwt
pkgver=0.7.5
pkgrel=1
pkgdesc="Library to create spreadsheet files compatible with MS Excel 97/2000/XP/2003 XLS files"
arch=('any')
url="http://www.python-excel.org/"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/source/x/xlwt/xlwt-${pkgver}.tar.gz")
md5sums=('59cb5efd55319465dfcd25e6a485f03c')

build() {
  cd "$srcdir/xlwt-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/xlwt-$pkgver"

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  find "$pkgdir" -name "*.py" -exec sed -i '1s/python[[:space:]]*$/python2/' {} \;
  install -Dm644 licences.py "$pkgdir/usr/share/licenses/$pkgname/licences.py"
}

