# $Id: PKGBUILD 140557 2015-09-17 20:53:41Z foutrelis $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Luis Henrique <lmello.009@gmail.com>

pkgname=ngspice
pkgver=26
pkgrel=3
pkgdesc='Mixed-level/Mixed-signal circuit simulator based on Spice3f5, Ciber1b1, and Xspice.'
url='http://ngspice.sourceforge.net'
license=('BSD')
arch=('i686' 'x86_64')
depends=('libxaw' 'libedit' 'gcc-libs')
source=("http://downloads.sourceforge.net/project/$pkgname/ng-spice-rework/$pkgver/$pkgname-$pkgver.tar.gz"
        "http://downloads.sourceforge.net/project/$pkgname/ng-spice-rework/$pkgver/$pkgname-doc-$pkgver.tar.gz")
sha1sums=('7c043c604b61f76ad1941defeeac6331efc48ad2'
          'bc163a47a2b4987c4617c47b132a30ffdc674350')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # FS#45230, create so lib
  # shared lib sets flags and modifies headers, needs dedicated pass
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --with-readline=yes \
              --enable-xspice \
              --enable-cider \
              --enable-openmp \
              --with-ngshared
  make
  #strip ./src/.libs/libngspice.so.0.0.0

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --with-readline=yes \
              --enable-xspice \
              --enable-cider \
              --enable-openmp
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644  "$srcdir/$pkgname-doc-$pkgver/manual.pdf" "$pkgdir/usr/share/doc/$pkgname/manual.pdf"

  install -Dm755 src/.libs/libngspice.so.0.0.0 "$pkgdir/usr/lib/libngspice.so.0.0.0"
  ln -s "libngspice.so.0.0.0" "$pkgdir/usr/lib/libngspice.so.0"
  ln -s "libngspice.so.0.0.0" "$pkgdir/usr/lib/libngspice.so"
}
