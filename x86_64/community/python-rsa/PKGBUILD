# $Id: PKGBUILD 105234 2014-02-03 09:57:08Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-rsa
pkgname=(python-rsa python2-rsa)
pkgver=3.1.3
pkgrel=1
_libname=rsa
pkgdesc="Pure-Python RSA implementation"
arch=(any)
url="http://stuvel.eu/rsa"
license=('BSD')
#source=(http://pypi.python.org/packages/source/r/$_libname/$_libname-$pkgver.tar.gz)
source=(https://bitbucket.org/sybren/$pkgname/get/version-$pkgver.tar.bz2)
makedepends=(python-distribute python2-distribute)

build() {
	#cd "$srcdir/$_libname-$pkgver"
	cd "$srcdir/sybren-python-rsa-8f50f506b4a2"

	rm -rf ../buildpy3; mkdir ../buildpy3
	python setup.py build -b ../buildpy3

	rm -rf ../buildpy2; mkdir ../buildpy2
	python2 setup.py build -b ../buildpy2
}

package_python-rsa() {
	depends=(python-pyasn1)

	#cd "$srcdir/$_libname-$pkgver"
	cd "$srcdir/sybren-python-rsa-8f50f506b4a2"
	rm -rf build; ln -s ../buildpy3 build
	python setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-rsa() {
	depends=(python2-pyasn1)

	#cd "$srcdir/$_libname-$pkgver"
	cd "$srcdir/sybren-python-rsa-8f50f506b4a2"
	rm -rf build; ln -s ../buildpy2 build
	python2 setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	find "$pkgdir/usr/bin" -type f | while read f; do mv "$f" "$f"2; done
}

sha256sums=('0a4a6f99b845b984101d1d959b7d1bf4e3c8dbdba8d1450eac6b76562c2ac4c3')
