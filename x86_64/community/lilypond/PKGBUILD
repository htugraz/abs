# $Id: PKGBUILD 103224 2013-12-30 15:18:05Z bgyorgy $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=lilypond
pkgver=2.16.2
pkgrel=5
pkgdesc="An automated music engraving system"
arch=('i686' 'x86_64')
url="http://lilypond.org"
license=('GPL')
depends=('guile1.8' 'python2' 'texlive-core' 'ghostscript' 'pango' 'fontconfig')
makedepends=('flex' 'bison' 'gettext' 'mftrace' 'texinfo' 'fontforge' 't1utils'
	     'gsfonts' 'texi2html')
options=('emptydirs')
#install=lilypond.install
source=(http://download.linuxaudio.org/lilypond/sources/v2.16/$pkgname-$pkgver.tar.gz
        lilypond.git-1ca9814191d16fd3c571d93035247db039254fc1.patch
        lilypond.git-7705e46966bfa05015fb9fb20c68da844ab88028.patch)
md5sums=('6db27f17d47e4f66a3b0716c65db3041'
         '121ac86f1004338ae20ad55a5690c0ad'
         'fa5073506f0e4dd96fb1858e3a75e08e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  sed -i 's|GUILE_CFLAGS=.*|GUILE_CFLAGS="`pkg-config --cflags guile-1.8`"|' configure
  sed -i 's|GUILE_LDFLAGS=.*|GUILE_LDFLAGS="`pkg-config --libs guile-1.8`"|' configure

  rm -rf lily/out/ python/out/

  # Fix build
  patch -Np1 -i "$srcdir/lilypond.git-1ca9814191d16fd3c571d93035247db039254fc1.patch"
  patch -Np1 -i "$srcdir/lilypond.git-7705e46966bfa05015fb9fb20c68da844ab88028.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=/usr/bin/python2
  export GUILE=/usr/bin/guile1.8
  export GUILE_CONFIG=/usr/bin/guile-config1.8
  export LDFLAGS="$LDFLAGS -lpthread"
  ./configure --prefix=/usr --disable-documentation
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" vimdir="/usr/share/vim/vimfiles" install
}
