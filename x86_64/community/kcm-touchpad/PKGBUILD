# $Id: PKGBUILD 106129 2014-02-23 16:02:33Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: A Rojas < nqn1976 @ gmail.com >
# Contributor: Alexander Mezin <mezin.alexander@gmail.com>

pkgname=kcm-touchpad
pkgver=0.9
pkgrel=2
pkgdesc='KCM, daemon and applet for touchpad'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/utils/kcm-touchpad'
license=('GPL')
depends=('kdebase-workspace' 'xf86-input-synaptics')
makedepends=('cmake' 'automoc4' 'xorg-server-devel')
install="${pkgname}.install"
source=("https://github.com/sanya-m/kde-touchpad-config/archive/v${pkgver}.tar.gz")
md5sums=('2d60b154b60a745a931ded129a6297d6')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kde-touchpad-config-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
