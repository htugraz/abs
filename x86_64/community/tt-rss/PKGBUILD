# $Id: PKGBUILD 137333 2015-07-23 15:25:44Z anatolik $
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: David Rosenstrauch <darose@darose.net>
# Contributor: Erik Mank <erik@braindisorder.org>

pkgname=tt-rss
pkgver=1.15.3.r257
pkgrel=1
pkgdesc='Web-based news feed (RSS/Atom) aggregator'
arch=('any')
url='http://tt-rss.org/redmine/'
license=('GPL')
depends=('php')
optdepends=('mysql'
            'postgresql'
            'php-gd: for coloured feed badges')
makedepends=('git')
install=tt-rss.install
_commit=2caeb0f875d78aee4aa55
# periodic releases are over https://tt-rss.org/forum/viewtopic.php?f=10&t=3262
source=(git://github.com/gothfox/Tiny-Tiny-RSS#commit=$_commit
        service)
md5sums=('SKIP'
         'cb9900413dd53d44a5ed1116c962dbcb')

pkgver() {
  cd Tiny-Tiny-RSS
  git describe --tags | sed 's/\([^-]*\)-g.*/r\1/;s/-/./g'
}

package() {
  cd Tiny-Tiny-RSS
  _instdir="$pkgdir"/usr/share/webapps/$pkgname

  install -d "$_instdir"
  cp -ra * "$_instdir/"
  rm -rf "$_instdir"/debian

  install -d "$pkgdir"/etc/webapps/tt-rss
  install -gm640 -g http config.php-dist "$pkgdir"/etc/webapps/tt-rss/config.php-dist
  ln -s /etc/webapps/tt-rss/config.php "$_instdir"/config.php

  install -d "$pkgdir"/var/lib/tt-rss
  mv "$_instdir"/{lock,feed-icons,cache} "$pkgdir"/var/lib/tt-rss
  ln -s /var/lib/tt-rss/lock "$_instdir"/lock
  ln -s /var/lib/tt-rss/feed-icons "$_instdir"/feed-icons
  ln -s /var/lib/tt-rss/cache "$_instdir"/cache
  chown -R 33:33 "$pkgdir"/var/lib/tt-rss
  chmod -R 777 "$pkgdir"/var/lib/tt-rss/*

  install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/tt-rss.service
}
