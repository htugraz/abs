# $Id: PKGBUILD 94923 2013-08-01 07:01:15Z bpiotrowski $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Serge Gielkens <gielkens.serge@mumeli.org>
pkgname=gimp-plugin-mathmap
pkgver=1.3.5
pkgrel=4
pkgdesc="A GIMP plug-in which allows distortion of images specified by mathematical formulae"
arch=('i686' 'x86_64')
url="http://www.complang.tuwien.ac.at/schani/mathmap/"
license=('GPL')
depends=('gimp' 'gsl' 'gtksourceview2' 'fftw' 'giflib')
makedepends=('doxygen' 'unzip')
provides=('gimp-mathmap')
replaces=('gimp-mathmap')
source=(http://www.complang.tuwien.ac.at/schani/mathmap/files/mathmap-${pkgver}.tar.gz \
        fix_libnoise_build.patch libpng15.patch giflib-5.0.patch)
md5sums=('6ff66d070ea410dee1a27283d05b8beb'
         'eca40de0ba0e6b8d34b7d1f904bc3d18'
         '9780d6354b7380b772c4616253ecee54'
         '9cc8eb13526056d8f3d06cc3e984a719')

build() {
  cd "$srcdir/mathmap-$pkgver"

  # fix problem with building libnoise
  patch -Np1 < "$srcdir"/fix_libnoise_build.patch
  # fix for libpng 1.5
  patch -Np1 < "$srcdir"/libpng15.patch
  # fix for giflib 5.0
  patch -Np1 < "$srcdir"/giflib-5.0.patch

  make
}

package() {
  cd "$srcdir/mathmap-$pkgver"
  make DESTDIR="$pkgdir" install
}
