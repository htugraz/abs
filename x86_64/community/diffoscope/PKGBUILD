# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=diffoscope
pkgver=40
pkgrel=3
pkgdesc='Tool for in-depth comparison of files, archives, and directories'
url='https://diffoscope.org/'
arch=('x86_64')
license=('GPL3')
depends=('python-magic' 'python-libarchive-c' 'python-setuptools')
optdepends=(
  'acl: access control list utilities support'
  'binutils: binary utilities support'
  'bzip2: bzip2 utilities support'
  'cdrkit: ISO utilities support'
  'cpio: cpio archive support'
  'e2fsprogs: Ext2/3/4 filesystem utilities support'
  'fpc: Free Pascal utilities support'
  'java-environment: java utilities support'
  'fontforge: bitmap font utilities support'
  'gettext: GNU internationalization utilities support'
  'ghc: haskell utilities support'
  'gnupg: GNU privacy guard support'
  'mono-tools: mono utilities support'
  'poppler: PDF utilities support'
  'sqlite: SQLite support'
  'squashfs-tools: squashfs filesystem support'
  'unzip: zip utilities support'
  'gzip: gzip utilities support'
  'tar: tar utilities support'
  'vim: Vi IMproved (xxd) utilities support'
  'xz: XZ and LZMA utilities support'
)
checkdepends=(
  'python-pytest' 'acl' 'binutils' 'bzip2' 'cdrkit' 'cpio' 'e2fsprogs' 'java-environment' 'fontforge' 'gettext' 'ghc' 'gnupg'
  'mono-tools' 'poppler' 'sqlite' 'squashfs-tools' 'unzip' 'gzip' 'tar' 'vim' 'xz')
source=(${pkgname}-${pkgver}.tar.xz::https://anonscm.debian.org/cgit/reproducible/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.xz
        ppu.patch::https://anonscm.debian.org/cgit/reproducible/diffoscope.git/patch/?id=5460a95556701712deb0fe5423344d5cd797ae5a
        fix-import.patch::https://anonscm.debian.org/cgit/reproducible/diffoscope.git/patch/?id=939b4b06b96ef6cfb910517070bda690f45f2cd3)
sha512sums=('b40e042769cc87056bdde5306c8630043995c16bd4f5b28a51eba3fd08c06f4975ff82b4b19c9fd7b203c5fa7d541ba69c734c787bdae09f0bf35facc376f533'
            'ba72fa3d46619ee2b36dff9e033a12bbd8e85dfd6599c9e5dc99180b004050eefc1760952ef906e0eef8ec321873b6dfcc2ae0a5047548cb209c76090f7ba9d8'
            '4d5ba2b0f9e6088ee6cf8fea5f2517dca31b1222b267e2e34418b8bc22b9bb478fa5517044e40f53972a62b9f48bf469d03cfd86a6481bab51b0545b2b38045a')

prepare() {
  cd ${pkgname}-${pkgver}
  sed '/python-magic/d' -i setup.py
  patch -p1 < "${srcdir}/ppu.patch"
  patch -p1 < "${srcdir}/fix-import.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  PYTHONPATH=".:${PYTHONPATH}" py.test
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
