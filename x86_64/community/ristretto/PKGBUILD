# $Id: PKGBUILD 78443 2012-10-18 17:04:59Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=ristretto
pkgver=0.6.3
pkgrel=3
pkgdesc='A fast and lightweight picture-viewer for Xfce'
arch=('i686' 'x86_64')
url='http://goodies.xfce.org/projects/applications/ristretto'
license=('GPL')
depends=('libxfce4ui' 'libexif' 'desktop-file-utils')
makedepends=('intltool')
optdepends=('librsvg: SVG support'
            'tumbler: thumbnailing support')
groups=('xfce4-goodies')
install=ristretto.install
source=(http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
md5sums=('6b8c9a5d7ed319e4c84a9fefaa18792c')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-debug
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
