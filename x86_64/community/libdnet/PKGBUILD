# $Id: PKGBUILD 100188 2013-11-01 21:06:01Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: James Fryman <jfryman@gmail.com>

pkgname=libdnet
pkgver=1.12
pkgrel=8
pkgdesc="A simplified, portable interface to several low-level networking routines"
arch=(i686 x86_64)
makedepends=(python2)
optdepends=(python2)
license=("BSD")
url="http://code.google.com/p/libdnet/"
source=(http://libdnet.googlecode.com/files/libdnet-$pkgver.tgz)
md5sums=('9253ef6de1b5e28e9c9a62b882e44cc9')

build() {
  cd $srcdir/$pkgname-$pkgver

  autoreconf -I config --force --install
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  cd python && \
  python2 setup.py install --root=$pkgdir

  install -D -m0644 ../LICENSE $pkgdir/usr/share/licenses/libdnet/license
}
