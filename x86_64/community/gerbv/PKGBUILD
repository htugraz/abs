# $Id: PKGBUILD 111554 2014-05-20 01:32:22Z eric $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jared Casper <jaredcasper@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=gerbv
pkgver=2.6.1
pkgrel=3
pkgdesc="An open source Gerber file (RS-274X only) viewer"
#url="http://gerbv.gpleda.org/"
url="http://gerbv.geda-project.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gtk2')
optdepends=('cairo: for better graphics')
makedepends=('cvs' 'gettext')
# cvs for autopoint
install=$pkgname.install
changelog=ChangeLog
source=("http://downloads.sourceforge.net/gerbv/$pkgname-$pkgver.tar.gz"
        "reautopointissues.diff")
md5sums=('9a8af19574025fb15f987c3df610f8ae'
         '55c3aac2af1f99a88dfa7b1508594e49')

build ()
{
  cd "$srcdir/$pkgname-$pkgver"
  # remove patches on 2.6.2
  patch -Np1 -i ../reautopointissues.diff
  ./autogen.sh
  sed -i 's|am_aux_dir=`cd $ac_aux_dir|am_aux_dir=`cd $srcdir|' configure
  ./configure --prefix=/usr \
              --disable-update-desktop-database 
  make 
}

package()
{
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install 
}

