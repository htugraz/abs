# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas at gmail dot com>
# Contributor: Laszlo Papp <djszapi2 at gmail dot com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Vladimir Cerny <blackvladimir@gmail.com>
# Contributor: 4javier <4javieregATgmailDOTcom>

_target=avr
pkgname=$_target-gdb
pkgver=7.9.1
pkgrel=1
pkgdesc='The GNU Debugger for AVR'
arch=(i686 x86_64)
url='http://www.gnu.org/software/gdb/'
license=(GPL3)
depends=(python2 guile xz)
options=(!emptydirs)
source=(ftp://ftp.gnu.org/gnu/gdb/gdb-$pkgver.tar.xz{,.sig})
sha256sums=('cd9c543a411a05b2b647dd38936034b68c2b5d6f10e0d51dc168c166c973ba40'
            'SKIP')
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3') # Joel Brobecker <brobecker@adacore.com>

prepare() {
  cd gdb-$pkgver
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd gdb-$pkgver

  ./configure \
    --disable-nls \
    --enable-languages=c,c++ \
    --prefix=/usr \
    --target=$_target \
    --with-python=/usr/bin/python2 \
    --with-system-readline \
    --with-system-gdbinit=/etc/gdb/gdbinit

  make
}

package() {
  cd gdb-$pkgver

  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/share/info
  rm -r "$pkgdir"/usr/share/man
  rm -r "$pkgdir"/usr/share/gdb
  rm -r "$pkgdir"/usr/include/gdb
}
