# $Id: PKGBUILD 121608 2014-10-30 20:13:29Z heftig $
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname=notification-daemon
pkgver=3.14.0
pkgrel=1
pkgdesc="Notification daemon for the desktop notifications framework"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org/"
depends=(gtk3 libcanberra)
makedepends=(intltool)
source=(http://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('6f55108e2964d0e0186c8886a545d0537052dc32010218cd657658534c316797')

prepare() {
    cd ${pkgname}-${pkgver}
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/notification-daemon-1.0 \
        --localstatedir=/var --disable-static
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
