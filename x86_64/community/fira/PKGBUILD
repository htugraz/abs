# $Id: PKGBUILD 129405 2015-03-17 09:57:53Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=fira
pkgname=(ttf-fira-sans ttf-fira-mono otf-fira-sans otf-fira-mono)
pkgver=4.003
_commit=98ab98d65f17767c111bba45b15d805e44e8947d
pkgrel=1
epoch=1
pkgdesc="Mozilla's typeface designed for Firefox OS"
arch=('any')
license=('custom:OFL')
url='https://github.com/mozilla/Fira'
makedepends=('git')
depends=('fontconfig' 'xorg-font-utils')
source=("git+https://github.com/mozilla/Fira.git#commit=$_commit"
        ttf.install otf.install)
md5sums=('SKIP'
         '9d795e681b14231a6f234c8d2a6d9957'
         '56bf7b135ff34a40513d5a832d882bff')

function _package {
    cd "$srcdir/Fira"

    case "$1" in
        ttf-fira-sans)
            pkgdesc="Mozilla's sans-serif typeface designed for Firefox OS"
            install=ttf.install
            cd ttf
            fonts=(FiraSans-*.ttf)
            installdir=TTF;;
        ttf-fira-mono)
            pkgdesc="Mozilla's monospace typeface designed for Firefox OS"
            install=ttf.install
            cd ttf
            fonts=(FiraMono-*.ttf)
            installdir=TTF;;
        otf-fira-sans)
            pkgdesc="Mozilla's sans-serif typeface designed for Firefox OS"
            install=otf.install
            cd otf
            fonts=(FiraSans-*.otf)
            installdir=OTF;;
        otf-fira-mono)
            pkgdesc="Mozilla's monospace typeface designed for Firefox OS"
            install=otf.install
            cd otf
            fonts=(FiraMono-*.otf)
            installdir=OTF;;
    esac

    # Prepare destination directory
    install -dm755 "$pkgdir/usr/share/fonts/$installdir"

    # Install fonts
    for font in "${fonts[@]}"; do
        install -m644 "$font" "$pkgdir/usr/share/fonts/$installdir"
    done

    install -D -m644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"   
}

for _pkgname in ${pkgname[@]}; do
    eval "function package_$_pkgname() { _package $_pkgname; }"
done
