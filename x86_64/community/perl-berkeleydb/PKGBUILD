# $Id: PKGBUILD 112168 2014-05-29 12:37:06Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-berkeleydb
pkgver=0.54
pkgrel=1
pkgdesc="Interface to Berkeley DB version 2, 3 or 4"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/BerkeleyDB/"
license=('GPL' 'PerlArtistic')
depends=('perl' 'db')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/P/PM/PMQS/BerkeleyDB-$pkgver.tar.gz)
md5sums=('7efc0aaaf6ca4a2a629eadea24d34fad')

build() {
  cd  $srcdir/BerkeleyDB-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  _dbver1=`pacman -Q db | cut -d\  -f2 | cut -d- -f1 | cut -f1 -d.`
  _dbver2=`pacman -Q db | cut -d\  -f2 | cut -d- -f1 | cut -f2 -d.`
  depends=('perl' "db>=${_dbver1}.${_dbver2}" "db<${_dbver1}.$(expr $_dbver2 + 1)")

  cd  $srcdir/BerkeleyDB-$pkgver
  make pure_install doc_install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
