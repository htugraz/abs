# $Id: PKGBUILD 115831 2014-07-17 17:30:07Z jsteel $
# Maintainer:  Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: tocer.deng <tocer.deng@gmail.com>

pkgname=apvlv
pkgver=0.1.4.20121023
pkgrel=4
pkgdesc='PDF/DJVU/TXT viewer which behaves like Vim'
arch=('i686' 'x86_64')
url="http://naihe2010.github.com/apvlv/"
license=('GPL')
depends=('gtk2' 'poppler-glib' 'djvulibre' 'desktop-file-utils')
makedepends=('cmake' 'webkitgtk2')
install=$pkgname.install
source=(ftp://ftp.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.xz
        $pkgname.desktop)
backup=('etc/apvlvrc')
md5sums=('f222fc4b09b15358eb4e7f996ba810d6'
         '54478fecb54f36d1c44d8fdc233ddbac')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  mkdir -p build
  cd build
  
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
        -DAPVLV_WITH_DJVU=yes -DAPVLV_WITH_TXT=yes \
        -DAPVLV_WITH_UMD=no  ..

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/build

  make DESTDIR="$pkgdir"/ install

  install -Dm644 "$srcdir"/$pkgname.desktop \
    "$pkgdir"/usr/share/applications/$pkgname.desktop
}
