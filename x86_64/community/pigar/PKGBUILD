# $Id: PKGBUILD 146817 2015-11-14 14:22:44Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pigar
pkgname=("$pkgbase" "python2-$pkgbase")
pkgver=0.6.3
pkgrel=1
pkgdesc="A fantastic tool to generate requirements for your Python project, and more than that"
arch=('any')
url="https://github.com/Damnever/pigar"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-colorama' 'python2-colorama'
             'python2-futures' 'git')
source=("git+https://github.com/Damnever/${pkgbase}.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a $pkgbase{,-py2}
}

build() {
  cd "${srcdir}/${pkgbase}"
  python setup.py build

  cd "${srcdir}/${pkgbase}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${pkgbase}"
  python -m unittest discover pigar/tests/ -t . -v

  cd "${srcdir}/${pkgbase}-py2"
  python2 -m unittest discover pigar/tests/ -t . -v
}

package_pigar() {
  depends=('python-setuptools' 'python-colorama')

  cd $pkgbase
  python3 setup.py install -O1 --root="$pkgdir/"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pigar() {
  depends=('python2-setuptools' 'python2-colorama' 'python2-futures')

  cd $pkgbase-py2
  python2 setup.py install -O1 --root="$pkgdir/"
  mv "$pkgdir/usr/bin/pigar" "$pkgdir/usr/bin/pigar2"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
