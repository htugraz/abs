# $Id: PKGBUILD 107093 2014-03-12 11:31:37Z speps $
# Maintainer: speps <speps at aur dot archlinux dot org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=sylpheed
pkgver=3.3.1
pkgrel=1
pkgdesc="Lightweight and user-friendly e-mail client"
arch=('i686' 'x86_64')
url="http://sylpheed.sraoss.jp/en/"
license=('GPL')
depends=('compface' 'gpgme' 'gtkspell' 'desktop-file-utils')
makedepends=('openssl')
install="$pkgname.install"
source=("http://sylpheed.sraoss.jp/$pkgname/v3.3/$pkgname-$pkgver.tar.bz2")
md5sums=('34551fa05722bac80bbe0688bb4a3341')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --enable-ldap
  make

  # Build Attachment-Tool Plug-in
  cd plugin/attachment_tool && make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # Install Attachment-Tool Plug-in
  cd plugin/attachment_tool
  make DESTDIR="$pkgdir/" install-plugin
}
