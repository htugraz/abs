# $Id: PKGBUILD 95114 2013-08-05 18:51:04Z jsteel $
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-data-optlist
pkgver=0.108
pkgrel=1
pkgdesc="Parse and validate simple name/value option pairs"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-params-util' 'perl-scalar-list-utils' 'perl-sub-install>=0.921')
makedepends=('perl-extutils-makemaker')
checkdepends=('perl-test-simple')
url="https://metacpan.org/release/Data-OptList"
source=(http://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Data-OptList-$pkgver.tar.gz)
md5sums=('f5c2d27987c1ec4b89b2702698881579')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""  \
      PERL_AUTOINSTALL=--skipdeps             \
      PERL_MM_OPT="INSTALLDIRS=vendor"        \
      PERL_MB_OPT="--installdirs vendor"      \
      MODULEBUILDRC=/dev/null
    cd "$srcdir"/Data-OptList-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$srcdir"/Data-OptList-$pkgver

    make test
  )
}

package() {
  cd "$srcdir"/Data-OptList-$pkgver

  make DESTDIR="$pkgdir"/ install
}
