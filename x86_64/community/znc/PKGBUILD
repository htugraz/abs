# $Id: PKGBUILD 131066 2015-04-09 18:55:17Z heftig $
# Maintainer: SÃ©bastien Luttringer
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: mickael9 <mickael9 at gmail dot com>

pkgname=znc
pkgver=1.6.0
pkgrel=3
pkgdesc='An IRC bouncer with modules & scripts support'
url='http://en.znc.in/wiki/index.php/ZNC'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libsasl' 'icu')
makedepends=('tcl' 'python' 'perl')
optdepends=('tcl: modtcl module'
            'python: modpython module'
            'perl: modperl module'
            'cyrus-sasl: saslauth module')
install=$pkgname.install
source=("http://znc.in/releases/$pkgname-$pkgver.tar.gz")
md5sums=('674d8c1277752dcc627b96e33a63376e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-cyrus \
    --enable-python \
    --enable-perl \
    --enable-tcl \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
