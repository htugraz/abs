# $Id: PKGBUILD 133755 2015-05-19 04:28:53Z anatolik $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=(lua-zlib lua51-zlib lua52-zlib)
pkgbase=lua-zlib
epoch=1
pkgver=0.4
pkgrel=4
url="http://github.com/brimworks/lua-zlib"
pkgdesc='Simple streaming interface to zlib for Lua.'
arch=('i686' 'x86_64')
license=('MIT')
makedepends=('cmake' 'lua' 'lua51' 'lua52' 'zlib' 'git')
conflicts=('lua-lzlib')
#source=("git://github.com/brimworks/lua-zlib.git")
source=("lua-zlib-$pkgver.tar.gz::https://github.com/brimworks/lua-zlib/archive/v$pkgver.tar.gz"
        lua53-compat.patch::https://github.com/brimworks/lua-zlib/commit/bc327e3ea486d0e9316d208a8d13fe0ed13deb68.patch)
md5sums=('1524ccc3a1c50c16a2955947a8f3b86b'
         'eab3012844a93639b19e374ac106348b')

prepare() {
  cd lua-zlib-$pkgver
  patch -p1 < $srcdir/lua53-compat.patch
}

build() {
  cp -a $srcdir/lua-zlib-$pkgver $srcdir/lua51-zlib-$pkgver
  cp -a $srcdir/lua-zlib-$pkgver $srcdir/lua52-zlib-$pkgver

  cd $srcdir/lua-zlib-$pkgver
  gcc -o zlib.so -O2 -fPIC -shared lua_zlib.c -llua -lz

  cd $srcdir/lua51-zlib-$pkgver
  gcc -o zlib.so -O2 -fPIC -shared -I/usr/include/lua5.1 lua_zlib.c -llua5.1 -lz

  cd $srcdir/lua52-zlib-$pkgver
  gcc -o zlib.so -O2 -fPIC -shared -I/usr/include/lua5.2 lua_zlib.c -llua5.2 -lz
}

package_lua-zlib() {
  depends=('zlib' 'lua')

  cd $srcdir/lua-zlib-$pkgver
  install -Dm0755 zlib.so $pkgdir/usr/lib/lua/5.3/zlib.so
  install -Dm644 "README" "$pkgdir/usr/share/licenses/$pkgname/README"
}

package_lua51-zlib() {
  depends=('zlib' 'lua51')

  cd $srcdir/lua51-zlib-$pkgver
  install -Dm0755 zlib.so $pkgdir/usr/lib/lua/5.1/zlib.so
  install -D -m644 "README" "$pkgdir/usr/share/licenses/$pkgname/README"
}

package_lua52-zlib() {
  depends=('zlib' 'lua52')

  cd $srcdir/lua52-zlib-$pkgver
  install -Dm0755 zlib.so $pkgdir/usr/lib/lua/5.2/zlib.so
  install -D -m644 "README" "$pkgdir/usr/share/licenses/$pkgname/README"
}
