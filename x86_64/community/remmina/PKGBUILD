# $Id: PKGBUILD 120832 2014-10-16 17:13:51Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
pkgver=1.1.1
pkgrel=2
pkgdesc="remote desktop client written in GTK+"
arch=(i686 x86_64)
url="http://freerdp.github.io/Remmina/"
license=('GPL')
depends=('gtk3' 'zlib' 'libjpeg' 'libssh' 'libunique' 'avahi' 'vte3' 'libgcrypt' 'libxdmcp')
makedepends=('intltool' 'pkgconfig' 'cmake' 'libgnome-keyring'
             'avahi' 'libxkbfile' 'freerdp' 'telepathy-glib'
             'libvncserver')
optdepends=('libxkbfile'
            'freerdp'
            'telepathy-glib'
            'gnome-keyring'
            'nxproxy'
            'xorg-server-xephyr: XDMCP support'
            'libvncserver')
replaces=('remmina-plugins')
provides=('remmina-plugins')
install=remmina.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/Remmina/archive/v1.1.1.tar.gz"
	"vte2.91.patch")
md5sums=('481dd96ebf7b14e4fd4df8648040b725'
         '5d8e0245ed23b856b35b02bf1a69c0a4')

prepare() {
  cd ${srcdir}/Remmina-$pkgver/
  for i in "${source[@]}"; do
    if [[ $i == *.patch ]]; then
      patch -F100 -p1 -i "${srcdir}/${i}"
    fi
  done
}

build() {
  cd ${srcdir}/Remmina-$pkgver/
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd ${srcdir}/Remmina-$pkgver/
  make DESTDIR=${pkgdir} install
  rm -rf $pkgdir/build
}
