# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>

pkgname=profanity
pkgver=0.4.0.rc1
pkgrel=2
pkgdesc='Console based XMPP client'
arch=('i686' 'x86_64')
url='http://www.profanity.im'
license=('GPL3')
depends=('curl' 'expat' 'glib2' 'libotr')
makedepends=('doxygen' 'libstrophe')
source=(https://github.com/boothj5/profanity/archive/$pkgver.tar.gz
        0001-Change-libotr-4.0.0-code-to-use-only-OTR-spec-v2.patch)
md5sums=('1b96b279bced5896c223d088c99c0ca6'
         'e957a737bb9d9f456f0b602815e6fe1e')

prepare() {
  cd $pkgname-$pkgver
  sed -i '21s/development/release/' configure.ac
  patch -p1 -i ../0001-Change-libotr-4.0.0-code-to-use-only-OTR-spec-v2.patch
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
