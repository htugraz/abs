# $Id: PKGBUILD 122344 2014-11-11 08:16:49Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Roberto Catini <roberto.catini@gmail.com>

pkgbase=python-requests-oauthlib
pkgname=('python-requests-oauthlib' 'python2-requests-oauthlib')
_pypiname=requests-oauthlib
pkgver=0.5.0.20151023
_commit=bf626f942188c62606f62b12ab1a08888e93bfe3
pkgrel=1
pkgdesc="First-class OAuth library support for Requests"
arch=('any')
url="https://pypi.python.org/pypi/requests-oauthlib"
license=('custom:ISC')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-oauthlib' 'python2-oauthlib' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock'
              'python-pyjwt' 'python2-pyjwt')
source=("git+https://github.com/requests/requests-oauthlib.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cp -a ${_pypiname}{,-py2}
}

build() {
  cd "$srcdir/${_pypiname}"
  python setup.py build

  cd "$srcdir/${_pypiname}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/${_pypiname}"
  py.test

  cd "$srcdir/${_pypiname}-py2"
  py.test2
}

package_python-requests-oauthlib() {
  depends=('python-requests' 'python-oauthlib')

  cd "$srcdir/${_pypiname}"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-requests-oauthlib() {
  depends=('python2-requests' 'python2-oauthlib')

  cd "$srcdir/${_pypiname}-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
