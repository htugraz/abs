# $Id: PKGBUILD 119301 2014-09-19 22:39:27Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

pkgname=ncmpcpp
pkgver=0.6beta4
pkgrel=1
pkgdesc='Almost exact clone of ncmpc with some new features'
arch=('i686' 'x86_64')
url='http://ncmpcpp.rybczak.net/'
license=('GPL')
depends=('curl' 'libmpdclient' 'taglib' 'ncurses' 'fftw' 'boost-libs')
makedepends=('boost')
source=(https://github.com/arybczak/ncmpcpp/archive/${pkgver/b/_b}.tar.gz
        missing-include.patch)
md5sums=('7cbd8c02889699b64d31d1375085d1e9'
         '14fa33349661c8e6c2a323970ef1e98c')

prepare() {
  cd $pkgname-${pkgver/b/_b}
  patch -p1 -i ../missing-include.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-${pkgver/b/_b}
  BOOST_LIB_SUFFIX='' ./configure --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-unicode \
    --enable-visualizer \
    --with-curl \
    --with-fftw \
    --with-taglib
  make
}

package() {
  cd $pkgname-${pkgver/b/_b}
  make DESTDIR="$pkgdir" install

  make -C extras
  install -Dm755 extras/artist_to_albumartist "$pkgdir"/usr/bin/
}
