# $Id: PKGBUILD 134084 2015-05-25 21:32:01Z anthraxx $
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

pkgname=ncmpcpp
pkgver=0.6.4
pkgrel=1
pkgdesc='Almost exact clone of ncmpc with some new features'
url='http://ncmpcpp.rybczak.net/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('curl' 'libmpdclient' 'taglib' 'ncurses' 'fftw' 'boost-libs')
makedepends=('boost')
source=(http://ncmpcpp.rybczak.net/stable/ncmpcpp-${pkgver}.tar.bz2)
sha512sums=('c60a6c2c89a9297d73360ec8819e8ae66ca53090c5ea5d3daf235e708467ee20006b1fff344c9312ccb0941c8307f3d84bcdc9cbf500955c74b10bd6a823c19f')

build() {
  cd ${pkgname}-${pkgver}
  BOOST_LIB_SUFFIX='' ./configure --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-unicode \
    --enable-visualizer \
    --with-curl \
    --with-fftw \
    --with-taglib
  make
  make -C extras
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm755 extras/artist_to_albumartist "${pkgdir}"/usr/bin/
}

# vim: ts=2 sw=2 et:
