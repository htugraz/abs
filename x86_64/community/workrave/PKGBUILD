# $Id: PKGBUILD 111968 2014-05-27 10:34:18Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>

pkgname=workrave
pkgver=1.10.4
pkgrel=1
pkgdesc="a tool to help RSI"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://workrave.org"
depends=('libxtst' 'gtkmm3' 'gconf' 'gdome2' 'gstreamer0.10' 'libsm' 'libice'
	 'python2-cheetah' 'libxss')
makedepends=('intltool' 'gobject-introspection')
source=($pkgname-$pkgver.tar.gz::https://github.com/rcaelers/workrave/archive/v${pkgver//./_}.tar.gz)
md5sums=('bd38a32f9a7c08665b1950b29659296e')

build() {
  cd $srcdir/workrave-${pkgver//./_}
  export PYTHON=python2
  ./autogen.sh
  ./configure --prefix=/usr --enable-dbus --sysconfdir=/etc --libexecdir=/usr/lib/workrave \
	--disable-pulse
  make
}

package() {
  cd $srcdir/workrave-${pkgver//./_}
  make DESTDIR=$pkgdir install
}
