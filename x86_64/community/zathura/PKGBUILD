# $Id: PKGBUILD 105878 2014-02-17 17:01:28Z spupykin $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

pkgname=zathura
pkgver=0.2.7
pkgrel=1
pkgdesc="a document viewer"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/zathura/"
license=('custom')
depends=('girara-gtk3' 'sqlite' 'desktop-file-utils' 'file')
makedepends=('python2-docutils')
optdepends=('zathura-djvu' 'zathura-pdf-poppler' 'zathura-pdf-mupdf' 'zathura-ps')
install=zathura.install
source=(http://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.gz
	bash-completion)
md5sums=('53124af80e974283a6678319a62e6666'
         'cac20c37f0e77ba62a8138788f4ccabb')

build() {
  cd $srcdir/zathura-$pkgver
  sed -i 's/rst2man/&2/' config.mk
  make
}

package() {
  cd $srcdir/zathura-$pkgver
  make install DESTDIR=$pkgdir
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 $srcdir/bash-completion $pkgdir/usr/share/bash-completion/completions/zathura
}
