# $Id: PKGBUILD 142773 2015-10-02 13:12:38Z foutrelis $
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=openimageio
pkgver=1.4.16
pkgrel=5
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=(i686 x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'intel-tbb')
makedepends=('cmake' 'qt4' 'python2' 'boost' 'mesa' 'git')
optdepends=('qt4: iv image viewer'
            'python2: bindings support')
source=(git+git://github.com/OpenImageIO/oiio.git#tag=Release-${pkgver}
        ustring-cxx11-std-string.patch
        fix-compiler-warning.patch
        stdmath.patch)
md5sums=('SKIP'
         'ec2e922a5297331901fd9331fcfb6bdb'
         'f41243f93ad9370060efdad931750289'
         '7b819bf20cf2c13cbde5fc6511e39f35')

prepare() {
  cd oiio

  patch -Np1 < "$srcdir/ustring-cxx11-std-string.patch"
  patch -Np1 < "$srcdir/fix-compiler-warning.patch"
  patch -Np1 < "$srcdir/stdmath.patch"
}

build() {
  cd oiio

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
      -DUSE_EXTERNAL_TBB=ON \
      -DOIIO_BUILD_TESTS=OFF \
      ..
  make
}

package() {
  cd oiio/build

  make DESTDIR="$pkgdir" install

  # license
  cd ..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
