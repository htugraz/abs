# $Id: PKGBUILD 106165 2014-02-24 13:55:16Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeff Mickey <j@codemac.net>
# Contributor: Dusty Phillips <dusty@buchuki.com>

pkgname=jedit
pkgver=5.1.0
pkgrel=4
pkgdesc='Text editor for programmers, written in Java'
arch=('any')
url='http://www.jedit.org/'
license=('GPL')
depends=('sh' 'java-runtime' 'desktop-file-utils')
makedepends=('gendesk' 'unzip' 'tar')
optdepends=('java-environment: for java development')
options=('!emptydirs')
install=$pkgname.install
noextract=("${pkgname}${pkgver}install.jar")
source=("http://downloads.sourceforge.net/$pkgname/${pkgname}${pkgver}install.jar"
        'jedit.sh'
        'jeditbg.sh'
        'jedit.props')
sha256sums=('96f229d91a1effd6b0da1aee209390d8a131bd68e0cf4cd274dca14245ba30d7'
            'b2e5a8f2f4818b336b5d71f677114f1bc01e929d7fa60918675cc2234dae1a3a'
            'a1dd1b688f08c5c967861fe80df13cac201afa6dcfcecc9a289b2c3bec7c8915'
            '5b428e95d76406fcfb81ac5c27f91397b9a990f597da5cbc4b7d60787cfd322e')

prepare() {
  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --exec 'jeditbg %U' \
    --name 'jEdit' --genericname 'Editor'

  # Extract the documentation so that it can be installed manually
  unzip "${pkgname}${pkgver}install.jar"
  tar jxf installer/jedit-api.tar.bz2
}

package() {
  install -d "$pkgdir/usr/share/java/$pkgdir"
  install -d "$pkgdir/usr/bin"
  install -d "$pkgdir/usr/share/man/man1"
  (java -jar "${pkgname}${pkgver}install.jar" auto \
    "$pkgdir/usr/share/java/$pkgname/" \
    unix-script="$pkgdir/usr/bin/" \
    unix-man="$pkgdir/usr/share/man/man1/" 2>&1) > inst.log
  install -Dm755 jedit.sh "$pkgdir/usr/bin/jedit"
  install -Dm755 jeditbg.sh "$pkgdir/usr/bin/jeditbg"
  install -Dm644 jedit.props "$pkgdir/usr/share/jedit/default.props"
  install -Dm644 jedit.desktop "$pkgdir/usr/share/applications/jedit.desktop"
  install -Dm644 "$pkgdir/usr/share/java/jedit/doc/jedit.png" \
    "$pkgdir/usr/share/pixmaps/jedit.png"

  # Install a few missing documentation files
  cp -ru "$srcdir/doc" "$pkgdir/usr/share/java/jedit/doc"
}

# vim:set ts=2 sw=2 et:
