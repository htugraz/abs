# $Id: PKGBUILD 90051 2013-05-06 19:38:01Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>

pkgname=kovpn
pkgver=0.3pre7
_pkgver=0.3.pre7
pkgrel=7
pkgdesc="An OpenVPN client management interface for KDE"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php?content=37043"
license=('GPL')
depends=('openvpn' 'kdelibs3' 'xdg-utils')
makedepends=('automake' 'autoconf')
options=('libtool')
install=kovpn.install
DLAGENTS=('ftp::/usr/bin/wget -c --passive-ftp -t 3 --waitretry=3 -O %o %u'
          'http::/usr/bin/wget -c -t 3 --waitretry=3 --no-check-certificate -O %o %u'
          'https::/usr/bin/wget -c -t 3 --waitretry=3 --no-check-certificate -O %o %u')
source=("https://projects.fslab.de/projects/kovpn/chrome/site/kovpn-${_pkgver}.tar.bz2"
	"kovpn-disable-dcop.patch::https://bugs.archlinux.org/task/15464?getfile=4173")
md5sums=('19b61cf9f84088baffbd5bb0a287422c'
         '21171b74f76289daea1cc61eedaa4dba')

build() {
  cd $srcdir/$pkgname-$_pkgver

  unset LDFLAGS
  export QTDIR=/usr
  export QMAKESPEC=/usr/share/qt3/mkspecs/linux-g++

  patch -Np1 -i ../kovpn-disable-dcop.patch
  sed -i "s#automake\*1.10\*#automake*`pacman -Q automake | cut -f2 -d\ |cut -f1-2 -d.`*#" admin/cvs.sh
  sed -i 's#AM_CONFIG_HEADER#AC_CONFIG_HEADER#g' admin/{cvs.sh,configure.in.min}
  make -f admin/Makefile.common cvs
  ./configure --prefix=/opt/kde --without-arts --with-qt-dir=/opt/qt
  make
}

package() {
  cd $srcdir/$pkgname-$_pkgver
  make prefix=$pkgdir/usr install
}
