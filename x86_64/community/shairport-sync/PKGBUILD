# $Id: PKGBUILD 144980 2015-10-26 00:46:31Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=shairport-sync
pkgver=2.6
pkgrel=1
pkgdesc='Emulates an AirPort Express for the purpose of streaming music from iTunes and compatible iPods and iPhones'
url='https://github.com/mikebrady/shairport-sync'
arch=(i686 x86_64)
license=(GPL)
backup=(etc/shairport-sync.conf)
install=shairport-sync.install
depends=(openssl avahi libsoxr popt alsa-lib libconfig libpulse)
source=(shairport-sync-$pkgver.zip::https://github.com/mikebrady/shairport-sync/archive/$pkgver.zip)
sha1sums=('e23914401f89e6ad27a1bf23f92451db8013e458')

build() {
  cd shairport-sync-$pkgver

  autoreconf -i -f
  ./configure --prefix=/usr --with-alsa --with-pulseaudio --with-avahi --with-ssl=openssl --with-soxr --with-dns_sd --with-pkg-config --with-systemd --with-configfiles
  make

  sed 's|/usr/local/bin/|/usr/bin/|' -i scripts/shairport-sync.service
}

package() {
  cd shairport-sync-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m664 LICENSES "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm "$pkgdir"/etc/shairport-sync.conf.sample
}
