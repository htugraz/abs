# $Id: PKGBUILD 101980 2013-12-03 09:48:38Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=libvirt-python
pkgver=1.2.0
pkgrel=1
pkgdesc="libvirt python binding"
arch=('i686' 'x86_64')
url="http://libvirt.org/"
license=('LGPL')
depends=('python2' 'libvirt')
options=('emptydirs')
install="libvirt-python.install"
source=("ftp://libvirt.org/libvirt/python/$pkgname-$pkgver.tar.gz")
md5sums=('38158e5740be65f17eef9f99ffa5dadf')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=`which python2`
  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
    sed -i 's_"python"_"python2"_g' $file
  done
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root=$pkgdir
}
