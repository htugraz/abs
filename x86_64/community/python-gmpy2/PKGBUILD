# $Id: PKGBUILD 138704 2015-08-23 22:29:13Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgbase=python-gmpy2
pkgname=(python2-gmpy2 python-gmpy2)
pkgver=2.0.7
pkgrel=1
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic."
arch=(i686 x86_64)
url="http://code.google.com/p/gmpy/"
license=(LGPL2.1)
makedepends=(python2 python libmpc)
source=("https://pypi.python.org/packages/source/g/gmpy2/gmpy2-${pkgver}.zip")
md5sums=('b5aada3ee5afb316ea94604f45192054')

package_python2-gmpy2() {
  depends=(python2 libmpc)

  cd gmpy2-$pkgver
  python2 setup.py install --root=$pkgdir --optimize=1

  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

package_python-gmpy2() {
  depends=(python libmpc)
  
  cd gmpy2-$pkgver 
  python setup.py install --root=$pkgdir --optimize=1
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

