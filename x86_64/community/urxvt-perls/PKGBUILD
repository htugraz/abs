# $Id: PKGBUILD 135270 2015-06-13 17:39:16Z jsteel $
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Bert Muennich <be.muennich at gmail.com>

pkgname=urxvt-perls
pkgver=2.2
pkgrel=1
pkgdesc="URL and Mouseless text selection for rxvt-unicode"
arch=('any')
url='http://www.github.com/muennich/urxvt-perls'
license=('GPL2')
depends=('rxvt-unicode' 'perl' 'xsel')
provides=('urxvt-url-select' 'urxvt-keyboard-select')
conflicts=('urxvt-url-select' 'urxvt-keyboard-select')
replaces=('urxvt-url-select' 'urxvt-keyboard-select')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/muennich/$pkgname/archive/$pkgver.tar.gz)
md5sums=('52332f67b93134d599f06e4d94915d52')

package() {
  cd $pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/lib/urxvt/perl
  install -t "$pkgdir"/usr/lib/urxvt/perl -m644 "${provides[@]#urxvt-}" clipboard
}
