# $Id: PKGBUILD 142306 2015-10-01 14:23:15Z spupykin $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Old maintainer: Gilbert Kennen <gilbert firewatcher org>

pkgname=elixir
pkgver=1.1.1
pkgrel=1
pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
url="http://elixir-lang.org"
arch=('any')
license=('Apache' 'custom:EPL')
depends=('erlang-nox')
source=("$pkgname-$pkgver.tar.gz::https://github.com/elixir-lang/elixir/tarball/v${pkgver}")
md5sums=('57f92587f33e985cb49fcdb88103b893')

build() {
  cd "${srcdir}/"elixir-lang*
  make
}

check() {
  cd "${srcdir}/"elixir-lang*
  make test_erlang
}

package() {
  cd "${srcdir}/"elixir-lang*
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 {LEGAL,LICENSE} "${pkgdir}/usr/share/licenses/${pkgname}"
  make DESTDIR=$pkgdir PREFIX=/usr install
}
