# $Id: PKGBUILD 138979 2015-08-30 10:57:33Z alucryd $
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>

pkgname=granite
pkgver=0.3.1
pkgrel=1
pkgdesc='An extension of GTK+ libraries'
arch=('i686' 'x86_64')
url='https://launchpad.net/granite'
license=('LGPL3')
depends=('cairo' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk3' 'hicolor-icon-theme'
         'libgee' 'pango')
makedepends=('cmake' 'gobject-introspection' 'vala')
provides=('libgranite.so')
install='granite.install'
source=("https://launchpad.net/granite/${pkgver%.*}/${pkgver}/+download/granite-${pkgver}.tar.xz")
sha256sums=('8ec1d61f9aba75f1b3a745e721288b0dfb34cb11d1307be80cef7b0571c2dec6')

prepare() {
  cd granite-${pkgver}

  sed '/demo/d' -i CMakeLists.txt

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd granite-${pkgver}/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make granite pot
}

package() {
  cd granite-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
