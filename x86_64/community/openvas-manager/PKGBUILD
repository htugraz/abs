# $Id: PKGBUILD 130001 2015-03-26 20:41:30Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-manager
pkgver=5.0.9
pkgrel=1
pkgdesc='A layer between the OpenVAS Scanner and various client applications'
arch=('i686' 'x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('openvas-libraries' 'rsync' 'sqlite3')
makedepends=('cmake' 'doxygen' 'xmltoman')
groups=(openvas)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/1963/$pkgname-${pkgver}.tar.gz"
        "http://wald.intevation.org/frs/download.php/1964/$pkgname-${pkgver}.tar.gz.asc")
md5sums=('da37223106a4f0b6dbefd090e9703feb'
         'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
