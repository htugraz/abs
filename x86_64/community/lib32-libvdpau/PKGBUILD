# $Id: PKGBUILD 126155 2015-01-18 21:10:43Z svenstaro $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libvdpau
pkgver=0.9
pkgrel=1
pkgdesc='Nvidia VDPAU library'
arch=('x86_64')
url='http://cgit.freedesktop.org/~aplattner/libvdpau'
license=('custom')
depends=('lib32-gcc-libs' 'lib32-libxext' "${pkgname#*-}")
conflicts=('lib32-nvidia-utils<190.42-2')
makedepends=('dri2proto')
source=("http://people.freedesktop.org/~aplattner/vdpau/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('e32957140835a545a01a3972db1143e01174db3d9c0017e51f6a3bf8b09c0e6e')

build() {
  cd ${pkgname#*-}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --sysconfdir='/etc'
  make
}

package() {
  cd ${pkgname#*-}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/include}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
