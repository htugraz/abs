# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: EVorster <evorster@gmail.com>

pkgname=vid.stab
pkgver=0.98b
pkgrel=2
pkgdesc="Video stabilize library for fmpeg, mlt or transcode"
arch=('i686' 'x86_64')
url="http://public.hronopik.de/vid.stab"
license=('GPL')
depends=()
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/georgmartius/vid.stab/archive/release-$pkgver.tar.gz")
md5sums=('299b2f4ccd1b94c274f6d94ed4f1c5b8')

build() {
  cd "$srcdir/$pkgname-release-$pkgver"
  rm -rf build
  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$srcdir/$pkgname-release-$pkgver/build"
  make DESTDIR="$pkgdir" install
}
