# $Id: PKGBUILD 115317 2014-07-08 17:15:28Z jsteel $
# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Joker-jar <joker-jar@yandex.ru>

pkgname=dgen-sdl
pkgver=1.32
pkgrel=3
pkgdesc="An emulator for Sega Genesis/Mega Drive systems ported to SDL"
arch=('i686' 'x86_64')
url="http://dgen.sourceforge.net"
license=('BSD')
depends=('sdl' 'mesa-libgl' 'libarchive')
[[ $CARCH == i686 ]] && makedepends=('nasm')
source=(http://downloads.sourceforge.net/dgen/$pkgname-$pkgver.tar.gz)
md5sums=('81b6f1c4bd612e81a9b8663c38d51890')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir"/ install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
