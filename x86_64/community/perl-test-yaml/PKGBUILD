# $Id: PKGBUILD 117018 2014-08-08 04:18:01Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=perl-test-yaml
pkgver=1.01
pkgrel=1
pkgdesc="Testing Module for YAML Implementations"
arch=('any')
url="http://search.cpan.org/dist/Test-YAML/"
license=('GPL' 'PerlArtistic')
depends=('perl-test-base')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/I/IN/INGY/Test-YAML-${pkgver}.tar.gz")
sha1sums=('861622acd6f2ca81da23e0df8f85f90d26098e28')

build() {
  cd Test-YAML-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-YAML-${pkgver}
  make test
}

package() {
  cd Test-YAML-${pkgver}
  make DESTDIR="${pkgdir}" install
}
