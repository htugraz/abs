# $Id: PKGBUILD 137680 2015-08-02 01:30:59Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=hardening-wrapper
pkgver=10
pkgrel=1
pkgdesc='Wrapper scripts for building hardened executables by default'
arch=(i686 x86_64)
url='https://github.com/thestinger/hardening-wrapper'
license=('MIT')
depends=(bash)
backup=(etc/hardening-wrapper.conf)
source=("$pkgname-$pkgver.tar.gz::https://github.com/thestinger/hardening-wrapper/archive/$pkgver.tar.gz"
        path.sh hardening-wrapper-i686.conf hardening-wrapper-x86_64.conf)
sha1sums=('61e8c7e3062e6830cd7b190aa6b81834138a7137'
          '1e5f6d9931f01b26bb4b6fbb839e21d34d534cdc'
          '6729f0a6d2af72b6def9383e8104d0f763d3f01d'
          '9453d7984a4a90aa884a51a7bf1bd2c72cbddc9f')

package() {
  install -Dm644 hardening-wrapper-${CARCH}.conf "$pkgdir/etc/hardening-wrapper.conf"
  install -Dm644 path.sh "$pkgdir/etc/profile.d/hardening-wrapper.sh"

  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir" PREFIX=/usr
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
