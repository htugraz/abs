# $Id: PKGBUILD 137644 2015-07-31 16:13:55Z anatolik $
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

# Build order: avr-binutils -> avr-gcc -> avr-libc

pkgname=avr-libc
pkgver=1.8.1
pkgrel=4
pkgdesc='The C runtime library for the AVR family of microcontrollers'
arch=(any)
url='http://savannah.nongnu.org/projects/avr-libc/'
license=(BSD)
depends=(avr-gcc)
makedepends=(subversion)
options=(!strip) # it corrupts avr libraries
# 1.8.1 does not support gcc-5.1 http://savannah.nongnu.org/bugs/?44869
source=(svn://svn.sv.gnu.org/avr-libc/trunk/avr-libc#revision=2480)
#source=(http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
md5sums=('SKIP')
validpgpkeys=('5E84F980C3CAFD4BB5841070F48CA81B69A85873') # Joerg Wunsch

build() {
  cd $pkgname
  ./bootstrap
  ./configure --build=$(./config.guess) --host=avr --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
