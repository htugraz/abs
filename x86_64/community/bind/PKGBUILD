# $Id: PKGBUILD 111392 2014-05-17 21:38:15Z seblu $
# Maintainer: SÃ©bastien Luttringer
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Mario Vazquez <mario_vazq@hotmail.com>

pkgname=bind
_pkgver=9.10.0-P1
pkgver=${_pkgver//-/.}
pkgrel=4
pkgdesc='The ISC BIND nameserver'
url='http://www.isc.org/software/bind/'
license=('custom:ISC')
arch=('i686' 'x86_64')
options=('!makeflags')
depends=('openssl' 'krb5' 'libxml2')
provides=('dns-server')
backup=('etc/logrotate.d/named'
        'etc/named.conf')
install=$pkgname.install
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz"{,.asc}
        '01-fix-forgotten-log.patch'
        'root.hint::http://www.internic.net/zones/named.root'
        'tmpfiles.d'
        'named.conf'
        'named.service'
        'named.logrotate'
        'localhost.zone'
        '127.0.0.zone')
sha1sums=('2404524f321107d7654e4c64f4174f6c464debb9'
          'SKIP'
          '4d96c9b9ef0aebdad7e79c749536858a344779b5'
          '029f89c49550c40ec7a95116b6a33f0e5a041094'
          'c5a2bcd9b0f009ae71f3a03fbdbe012196962a11'
          'c71a7fc02d4bf0d55e8e29d1e014607ac1d58726'
          'cb2e81b4cbf9efafb3e81e3752f0154e779cc7ec'
          '3fe1f0b5c1a51dc1db9ebe5e173d18c52c97169b'
          '76a0d4cd1b913db177a5a375bebc47e5956866ec'
          '53be0f1437ebe595240d8dbdd819939582b97fb9')

prepare() {
  # remove dig to avoid conflict with dnsutils
  sed -i 's/dig//' $pkgname-$_pkgver/bin/Makefile.in
  # https://lists.isc.org/pipermail/bind-users/2014-May/093124.html
  patch -p1 -d bind-$_pkgver < 01-fix-forgotten-log.patch
}

build() {
  cd bind-$_pkgver
  # for gcc 4.8 rebuild
  export CFLAGS="-march=${CARCH/_/-} -mtune=generic -O2 -pipe -fstack-protector --param=ssp-buffer-size=4"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --disable-static \
    --with-openssl \
    --with-libxml2 \
    --with-libtool
  make
}

package() {
  cd "bind-$_pkgver"

  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  make DESTDIR="$pkgdir" install

  rmdir "$pkgdir/var/run"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install doc/arm/*.html "$pkgdir/usr/share/doc/$pkgname"

  cd "$srcdir"
  install -D -m644 tmpfiles.d "$pkgdir/usr/lib/tmpfiles.d/named.conf"
  install -D -m644 named.service "$pkgdir/usr/lib/systemd/system/named.service"
  install -D -m600 named.logrotate "$pkgdir/etc/logrotate.d/named"
  install -D -m640 -o 0 -g 40 named.conf "$pkgdir/etc/named.conf"

  install -d -m770 -o 0 -g 40 "$pkgdir/var/named"
  install    -m640 -o 0 -g 40 root.hint "$pkgdir/var/named"
  install    -m640 -o 0 -g 40 127.0.0.zone "$pkgdir/var/named"
  install    -m640 -o 0 -g 40 localhost.zone "$pkgdir/var/named"
}

# vim:set ts=2 sw=2 et:
