# $Id: PKGBUILD 108643 2014-03-31 00:53:21Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Tianjiao Yin <ytj000+AUR@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: TDY <tdy@gmx.com>
# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>

pkgbase=python-pyflakes
pkgname=(python-pyflakes python2-pyflakes)
_reponame=pyflakes
pkgver=0.8.1
pkgrel=1
pkgdesc='A lint-like tool for Python to identify common errors quickly without executing code'
arch=('any')
url='http://pypi.python.org/pypi/pyflakes'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/p/pyflakes/${_reponame}-${pkgver}.tar.gz")
sha1sums=('d44dc00c271b825b64c270a867c0c9327886e857')

prepare() {
  cp -r "${_reponame}-${pkgver}"{,-py2}
}

build() {
  cd "$srcdir/${_reponame}-${pkgver}"
  python setup.py build

  cd "$srcdir/${_reponame}-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/${_reponame}-${pkgver}"
  python setup.py test

  cd "$srcdir/${_reponame}-${pkgver}-py2"
  python2 setup.py test
}

package_python-pyflakes() {
  depends=('python')

  cd "${_reponame}-${pkgver}"

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  # We have python 3 as default python, and want to keep compatibility with the old pyflakes3k naming
  ln -s pyflakes "${pkgdir}/usr/bin/pyflakes3k"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyflakes() {
  depends=('python2')

  cd "${_reponame}-${pkgver}-py2"

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  mv "${pkgdir}/usr/bin/pyflakes" "${pkgdir}/usr/bin/pyflakes-python2"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
