# $Id: PKGBUILD 137005 2015-07-16 15:51:55Z tredaelli $

# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Det <nimetonmaili g-mail>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=npapi-vlc
pkgver=2.2.1
pkgrel=1
pkgdesc="The modern VLC Mozilla (NPAPI) plugin"
arch=('i686' 'x86_64')
url="http://git.videolan.org/?p=$pkgname.git;a=summary"
license=('GPL')
depends=('gtk2' 'vlc')
makedepends=('git' 'npapi-sdk')
# This package uses version tags from Git, because there are no official releases
source=("git://git.videolan.org/$pkgname.git#tag=$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
}
