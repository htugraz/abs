# $Id: PKGBUILD 118102 2014-08-31 14:39:27Z speps $
# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=harvid
pkgver=0.7.4
pkgrel=1
pkgdesc="HTTP Ardour Video Daemon"
arch=('i686' 'x86_64')
url="http://x42.github.io/harvid/"
license=('GPL')
depends=('ffmpeg' 'ffmpeg-compat')
source=("https://github.com/x42/harvid/archive/v$pkgver.tar.gz")
md5sums=('545c6b657a70f62d5f895b9866d020f9')

build() {
  cd $pkgname-$pkgver

  # prevent GNU executable stack
  CFLAGS+=" -Wl,-znoexecstack"
  make PREFIX=/usr
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir/" install

  # ffmpeg and ffprobe symlinks (for ardour)
  ln -s ffmpeg "$pkgdir/usr/bin/ffmpeg_harvid"
  ln -s ffprobe "$pkgdir/usr/bin/ffprobe_harvid"

  # doc
  install -Dm644 README.md \
    "$pkgdir/usr/share/doc/$pkgname/README"
}
