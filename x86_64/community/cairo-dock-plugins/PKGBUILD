# $Id: PKGBUILD 129128 2015-03-12 13:55:57Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: zhuqin <zhuqin83@gmail.com>
# Contributor: tri1976 <trile7@gmail.com>
# Contributor: snoopy33 <snoopy33@no-log.org>

pkgname=cairo-dock-plugins
pkgver=3.4.1
pkgrel=2
pkgdesc='Plugins for Cairo-Dock'
arch=('i686' 'x86_64')
url='http://glx-dock.org'
license=('GPL')
depends=('cairo-dock')
makedepends=('alsa-lib' 'cmake' 'dbus-sharp-glib' 'fftw' 'gnome-menus'
             'gtk-sharp-2' 'gvfs' 'libetpan' 'libexif' 'libical' 'libpulse'
             'libxklavier' 'libzeitgeist' 'lm_sensors' 'python' 'python2'
             'ruby' 'vala' 'vte3' 'webkitgtk')
optdepends=('alsa-lib: Sound Control, Sound Effects applets'
            'dbus-sharp-glib: Mono API'
            'fftw: Impulse applet'
            'gnome-menus: Applications Menu applet'
            'gtk-sharp-2: Mono API'
            'gvfs: GVFS integration'
            'libetpan: Mail applet'
            'libexif: Slider applet'
            'libical: Clock applet'
            'libpulse: Impulse applet'
            'libxklavier: Keyboard Indicator applet'
            'libzeitgeist: Recent Events applet'
            'lm_sensors: System Monitor applet'
            'python: Python 3 API'
            'python2: Python 2 API'
            'ruby: Ruby API'
            'vte3: Terminal applet'
            'webkitgtk3: Weblets applet')
source=("cairo-dock-plugins-${pkgver}.tar.gz::https://github.com/Cairo-Dock/cairo-dock-plug-ins/archive/${pkgver}.tar.gz"
        'cairo-dock-plugins-mono.patch')
sha256sums=('321113fb8fa214d965c9827b82890651dc4cd0beb23aef0e2d3f5caf806ebdc2'
            'b3e4d5384f2293d2f9ac05440350ba076a8db40b2b7628811af2cb102171c10a')

prepare() {
  cd cairo-dock-plug-ins-${pkgver}

  patch -Np1 -i ../cairo-dock-plugins-mono.patch
}

build() {
  cd cairo-dock-plug-ins-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd cairo-dock-plug-ins-${pkgver}/build

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/lib/{cli,mono}
}

# vim: ts=2 sw=2 et:
