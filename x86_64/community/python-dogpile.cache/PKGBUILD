# $Id: PKGBUILD 138808 2015-08-27 08:24:22Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-dogpile.cache
pkgname=(python-dogpile.cache python2-dogpile.cache)
_pypiname=dogpile.cache
pkgver=0.5.7
pkgrel=2
pkgdesc="A caching front-end based on the Dogpile lock"
arch=('any')
url="http://pypi.python.org/pypi/dogpile.cache"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-dogpile.core' 'python2-dogpile.core')
checkdepends=('python-mako' 'python2-mako' 'python-redis' 'python2-redis' 'python2-memcached'
              'memcached' 'redis' 'python-pytest-runner' 'python2-pytest-runner'
              'python-mock' 'python2-mock' 'python-pytest-cov' 'python2-pytest-cov'
              'python-pylibmc' 'python2-pylibmc')
source=("http://pypi.python.org/packages/source/d/$_pypiname/$_pypiname-$pkgver.tar.gz")
md5sums=('3e58ce41af574aab41d78e9c4190f194')

prepare() {
  cp -a "$_pypiname-$pkgver"{,-py2}
}

build() {
  cd $_pypiname-$pkgver
  python setup.py build

  cd ../$_pypiname-$pkgver-py2
  python2 setup.py build
}

check() {
  _run() {
    rm -f "$srcdir/memcached.pid" "$srcdir/redis.pid"
    memcached -d -P "$srcdir/memcached.pid"
    redis-server --daemonize yes --pidfile "$srcdir/redis.pid"
    "$@"
    kill $(cat "$srcdir/memcached.pid") $(cat "$srcdir/redis.pid")
  }

  cd $_pypiname-$pkgver
  _run python setup.py ptr

  cd ../$_pypiname-$pkgver-py2
  _run python2 setup.py ptr
}

package_python-dogpile.cache() {
  depends=('python-dogpile.core' 'python-setuptools')

  cd "$srcdir/$_pypiname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-dogpile.cache() {
  depends=('python2-dogpile.core' 'python2-setuptools')

  cd "$srcdir/$_pypiname-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
