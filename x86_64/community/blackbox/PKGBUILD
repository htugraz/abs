# $Id: PKGBUILD 118799 2014-09-11 05:19:50Z bpiotrowski $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: durbatuluk <dvdscripter@gmail.org>
# Contributor: Steve Ponsford <sp4d@lavabit.com>

pkgname=blackbox
pkgver=0.70.1
pkgrel=12
pkgdesc="A window manager for X11"
url="http://blackboxwm.sourceforge.net/"
arch=('i686' 'x86_64')
depends=('gcc-libs' 'libxft' 'libxext' 'libx11' 'libxt')
license=('MIT')
source=(http://downloads.sourceforge.net/blackboxwm/$pkgname-$pkgver.tar.gz
	gcc4.3.patch
	bbdock.patch
	bsetbg-feh.patch
	textpropertytostring-unconditional.patch
        blackbox-0.70.1-asneeded.patch)
md5sums=('2d173b95ca5e64ef478c6a5d2deee9df'
         'bc7a1335d77cdeb6f85cba4d01e68e63'
         '69c5a5e981b087b2d8267cab996b48dd'
         'e7fb0d5bfd95820c0194ec73df66dadc'
         '3b92ad246f07126dd55ae9634d6e875e'
         '55d2437d58aeb76392fe66caab13840c')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  patch -Np1 -i ../gcc4.3.patch
  patch -Np0 -i ../bbdock.patch
  patch -Np0 -i ../bsetbg-feh.patch
  patch -Np1 -i ../textpropertytostring-unconditional.patch
  patch -Np0 -i ../blackbox-0.70.1-asneeded.patch

  autoreconf -fi
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
