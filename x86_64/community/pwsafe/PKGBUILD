# $Id: PKGBUILD 114346 2014-07-01 14:42:12Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=pwsafe
pkgver=0.2.0
pkgrel=9
pkgdesc="A commandline program for managing encrypted password databases"
arch=('i686' 'x86_64')
url="http://nsd.dyndns.org/pwsafe/"
license=('GPL')
depends=('openssl' 'libxmu' 'readline')
source=(${url}/releases/${pkgname}-${pkgver}.tar.gz
	pwsafe-XChangeProperty.patch)
md5sums=('4bb36538a2772ecbf1a542bc7d4746c0'
         'cff6aee2e43f5fbe82e8cd7ccfefb099')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Patch from fedora, fixes FS#28339
  patch -Np0 -i ../pwsafe-XChangeProperty.patch

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # Make pwsafe suid root so it can seed rng as a user
  chmod +s ${pkgdir}/usr/bin/pwsafe
}
