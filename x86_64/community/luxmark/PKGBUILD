# $Id: PKGBUILD 133574 2015-05-16 10:21:07Z stativ $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=luxmark
pkgver=3.0
_scenever=3.0beta2
_pkgver=985b948aa5c7
pkgrel=2
pkgdesc="OpenCL benchmark tool"
arch=('x86_64') # because of embree only x86_64 is supported
url="http://www.luxrender.net/"
license=('GPL3')
depends=('boost-libs' 'freetype2' 'freeglut' 'glew' 'libcl' 'libgl' 'libpng' 'openimageio' 'qt4')
makedepends=('cmake' 'boost' 'luxrays' 'mesa' 'opencl-headers' 'embree' 'freeglut')
optdepends=('opencl-nvidia: OpenCL support for nVidia GPUs' \
            'amdapp-sdk: OpenCL support for AMD GPUs' \
            'intel-opencl-runtime: OpenCL support for Intel CPUs')
source=(https://bitbucket.org/luxrender/luxmark/get/${_pkgver}.tar.bz2 \
        https://bitbucket.org/luxrender/luxmark/downloads/scenes-v${_scenever}.zip \
        system_scenes.diff)
md5sums=('8bf391a882c00c290372a9d67fea87af'
         '504724fde889d0e46a8b971636a0703b'
         '0635afe0dc5dc3e4e58f25190808942e')

prepare() {
  cd "$srcdir"/luxrender-luxmark-$_pkgver

  patch -Np1 < "$srcdir/system_scenes.diff"
}

build() {
  cd "$srcdir/luxrender-luxmark-$_pkgver"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    .
  make
}

package() {
  cd "$srcdir/luxrender-luxmark-$_pkgver"
  install -D -m755 bin/luxmark "$pkgdir"/usr/bin/luxmark

  install -d "$pkgdir/usr/share/luxmark"
  cp -a "$srcdir/scenes" "$pkgdir/usr/share/luxmark"
}

# vim:set ts=2 sw=2 et:
