# $Id: PKGBUILD 102663 2013-12-17 12:06:27Z bgyorgy $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=pyneighborhood
pkgver=0.5.4
pkgrel=3
pkgdesc="A GTK+ 2 rewrite of the well-known LinNeighborhood tool"
license=('GPL')
url="https://launchpad.net/pyneighborhood"
source=("http://launchpad.net/pyneighborhood/0.5/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
depends=('pygtk' 'smbclient')
optdepends=('gksu: mount/unmount Samba shares')
arch=('any')
options=('!emptydirs')
sha1sums=('b0d899230ed0212aa50c17f406b0ee7e3d6ac17a')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # python2 fix
  for file in $(find . -type f -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make PYTHON=python2 DESTDIR=${pkgdir}/usr install
}
