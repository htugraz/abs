# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: János Illés <ijanos@gmail.com>

pkgname=vim-fugitive
_sourceid=15542
pkgver=1.2
pkgrel=1
pkgdesc="A git wrapper so awesome, it should be illegal."
arch=('any')
url="http://www.vim.org/scripts/script.php?script_id=2975"
license=('custom:vim')
groups=('vim-plugins')
depends=('vim')
install=vimdoc.install
source=("http://www.vim.org/scripts/download_script.php?src_id=$_sourceid"
        'license.txt')
md5sums=('e13a9eab4606d503429a2ff146964048'
         'cd49910a3dde6fe0a7e24ebe8cbf7066')

package() {
  cd "$srcdir"

  local installpath="$pkgdir/usr/share/vim/vimfiles"

  install -Dm644 doc/fugitive.txt "$installpath/doc/fugitive.txt"
  install -Dm644 plugin/fugitive.vim "$installpath/plugin/fugitive.vim"
  install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
