# $Id: PKGBUILD 115399 2014-07-10 15:20:13Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: János Illés <ijanos@gmail.com>

pkgname=vim-fugitive
pkgver=2.1
pkgrel=1
pkgdesc="A Git wrapper so awesome, it should be illegal"
arch=('any')
url="https://github.com/tpope/vim-fugitive"
license=('custom:vim')
groups=('vim-plugins')
depends=('vim')
install=vimdoc.install
source=("https://github.com/tpope/$pkgname/archive/v${pkgver}.tar.gz"
        'license.txt')
md5sums=('aaa768541c6c6ab7a02e6d40c54f6ee5'
         'cd49910a3dde6fe0a7e24ebe8cbf7066')

package() {
  cd $pkgname-$pkgver

  local installpath="$pkgdir/usr/share/vim/vimfiles"
  install -Dm644 doc/fugitive.txt "$installpath/doc/fugitive.txt"
  install -Dm644 plugin/fugitive.vim "$installpath/plugin/fugitive.vim"
  install -Dm644 ../license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
