# $Id: PKGBUILD 122294 2014-11-10 10:25:11Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=beets
pkgver=1.3.8
pkgrel=2
pkgdesc='Flexible music library manager and tagger'
arch=('any')
url='http://beets.radbox.org/'
license=('MIT')
depends=('mutagen' 'python2-enum34' 'python2-munkres' 'python2-musicbrainzngs'
         'python2-setuptools' 'python2-unidecode' 'python2-yaml')
optdepends=('bash-completion: Bash completion'
            'gst-plugins-bad: ReplayGain plugin'
            'gst-plugins-good: ReplayGain plugin'
            'gst-plugins-ugly: ReplayGain plugin'
            'gst-libav: ReplayGain plugin'
            'gst-python: ReplayGain plugin'
            'gstreamer0.10-bad-plugins: Chromaprint/Acoustid/BPD plugins'
            'gstreamer0.10-good-plugins: Chromaprint/Acoustid/BPD plugins'
            'gstreamer0.10-ugly-plugins: Chromaprint/Acoustid/BPD plugins'
            'gstreamer0.10-ffmpeg: Chromaprint/Acoustid/BPD plugins'
            'gstreamer0.10-python: Chromaprint/Acoustid/BPD plugins'
            'python2-flask: Web plugin'
            'python2-gobject: ReplayGain plugin'
            'python2-pyacoustid: Chromaprint/Acoustid plugin'
            'python2-pylast: LastGenre plugin'
            'python2-requests: FetchArt plugin')
source=("https://github.com/sampsyo/beets/releases/download/v${pkgver}/beets-${pkgver}.tar.gz")
sha256sums=('102ab9067d230c6d30a8f2e1139df8ddd4cf37873fd19cf6f32991fd403da21b')

build() {
  cd beets-${pkgver}

  python2 setup.py build
}

package() {
  cd beets-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'

  install -dm 755 "${pkgdir}"/usr/share/man/man{1,5}
  install -m 644 man/beet.1 "${pkgdir}"/usr/share/man/man1/
  install -m 644 man/beetsconfig.5 "${pkgdir}"/usr/share/man/man5/

  install -dm 755 "${pkgdir}"/usr/share/licenses/beets
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/beets/
}

# vim: ts=2 sw=2 et:
