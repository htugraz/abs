# $Id: PKGBUILD 115375 2014-07-09 13:28:03Z fyan $
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>

pkgbase=uzbl
pkgname=('uzbl-core' 'uzbl-browser' 'uzbl-tabbed')
pkgver=2012.05.14
pkgrel=3
arch=('i686' 'x86_64')
url="http://www.uzbl.org"
license=('GPL3')
makedepends=('pkgconfig' 'webkitgtk2>=1.3.13')
source=(uzbl-$pkgver.tar.gz::https://github.com/uzbl/uzbl/archive/$pkgver.tar.gz)
md5sums=('8d644ecb28cb27fbc971c771156dee6f')

build() {
    cd "$srcdir"/uzbl-$pkgver

    # python2 fix
    for file in examples/data/scripts/uzbl*; do
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
    done
    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
           $(find ./ -name '*.py') bin/*

    # NEW_BG_TAB event fix (merged upstream)
    sed -i -e "s/tab_bg/bg_tab/" bin/uzbl-tabbed

    make
}

package_uzbl-core() {
   pkgdesc="Webpage interface component meant for integration with other tools and scripts"
   depends=('webkitgtk2>=1.3.13')
   optdepends=('socat: to interface with the socket' 'dmenu: to run some of the example scripts'
               'zenity: to run some of the example scripts'
               'python2: to run some of the example scripts'
               'xclip: for clipboard related keybindings'
	       'pygtk: for uzbl-tabbed')
   install=uzbl.install

   cd "$srcdir"/uzbl-$pkgver

   make DESTDIR="$pkgdir" PREFIX=/usr  install-uzbl-core
}

package_uzbl-browser() {
   pkgdesc="A complete browser experience based on uzbl-core"
   depends=("uzbl-core=$pkgver" 'socat' 'zenity' 'xclip' 'dmenu' 'python2')

   cd "$srcdir"/uzbl-$pkgver

   make DESTDIR="$pkgdir" PREFIX=/usr  install-uzbl-browser
   rm -r $pkgdir/usr/bin/uzbl-core $pkgdir/usr/share/uzbl/docs
}

package_uzbl-tabbed() {
   pkgdesc="Tabbing manager providing multiple uzbl-browser instances in 1 window"
   depends=("uzbl-browser=$pkgver" 'pygtk')

   cd "$srcdir"/uzbl-$pkgver

   make DESTDIR="$pkgdir" PREFIX=/usr install-uzbl-tabbed
}

