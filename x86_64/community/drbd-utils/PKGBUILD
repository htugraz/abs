# $Id: PKGBUILD 137578 2015-07-28 21:54:10Z seblu $
# Maintainer: SÃ©bastien Luttringer

pkgname=drbd-utils
pkgver=8.9.3
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='Userland tools for Distributed Replicated Block Device'
url='http://www.drbd.org'
license=('GPL2')
depends=('perl' 'bash')
makedepends=('libxslt' 'systemd')
conflicts=('drbd')
replaces=('drbd')
source=("http://oss.linbit.com/drbd/$pkgname-$pkgver.tar.gz")
backup=('etc/drbd.conf' 'etc/drbd.d/global_common.conf')
md5sums=('81ba3ac48786602b4eddd637654ba24c')

#prepare() {
#  # fix hardcoded path in udev rules
#  sed -i 's,/sbin/drbdadm,/usr/bin/drbdadm,' $pkgname-$pkgver/scripts/drbd.rules
#}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libdir=/usr/lib \
    --with-distro=generic \
    --with-bashcompletion \
    --with-udev \
    --without-rgmanager \
    --without-pacemaker \
    --without-heartbeat \
    --with-initscripttype=systemd \
    --without-xen
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install

  # move /lib files under /usr/lib
  cd "$pkgdir"
  mv lib/drbd/* usr/lib/drbd
  rmdir lib/drbd lib

  # move bash completion
  install -dm 755 usr/share/bash-completion
  mv etc/bash_completion.d usr/share/bash-completion/completions

  # remove /var/lock
  rmdir var/lock

  # autoload module
  install -Dm 644 /dev/null usr/lib/modules-load.d/drbd.conf
  echo 'drbd' > usr/lib/modules-load.d/drbd.conf

}

# vim:set ts=2 sw=2 et:
