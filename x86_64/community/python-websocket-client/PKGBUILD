# $Id: PKGBUILD 145233 2015-10-29 09:07:16Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matmas <matmas@matmas.net>
# Contributor: Thomas Gubler <thomasgubler@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Florian Bruhin <archlinux.org@the-compiler.org>

pkgname=(python-websocket-client python2-websocket-client)
pkgver=0.34.0
pkgrel=1
pkgdesc="WebSocket client library for Python"
arch=(any)
url="https://github.com/liris/websocket-client"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=(http://pypi.python.org/packages/source/w/websocket-client/websocket_client-$pkgver.tar.gz
        use-system-ca.patch)
md5sums=('80079d5f068e7c77516c1730822eba71'
         'c34c7f6fddbe5758d84b9d3cfdd92108')

prepare() {
  (cd websocket_client-$pkgver; patch -p1 -i ../use-system-ca.patch)
  rm websocket_client-$pkgver/websocket/cacert.pem

  cp -a websocket_client-$pkgver{,-py2}
}

build() {
  cd "$srcdir/websocket_client-$pkgver"
  python setup.py build

  cd "$srcdir/websocket_client-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/websocket_client-$pkgver"
  python setup.py test

  cd "$srcdir/websocket_client-$pkgver-py2"
  python2 setup.py test
}

package_python-websocket-client() {
  depends=('python-six')

  cd "$srcdir/websocket_client-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-websocket-client() {
  depends=('python2-six')

  cd "$srcdir/websocket_client-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir"/usr/bin/wsdump{,2}.py
}

# vim:set ts=2 sw=2 ft=sh et:
