# $Id: PKGBUILD 113102 2014-06-13 15:40:40Z anatolik $
# Maintainer: Anatol Pomozov <anatol pomozov at gmail> 
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Konstantin Plotnikov <kostyapl at gmail dot com>

pkgname=fossil
pkgver=1.29
_date_pkgver=20140612172556
# NOTE: update _date_pkgver from http://www.fossil-scm.org/download.html
pkgrel=1
pkgdesc='Simple, high-reliability, distributed software configuration management'
arch=(i686 x86_64)
license=(BSD)
url='http://www.fossil-scm.org'
depends=(openssl zlib)
backup=(etc/xinetd.d/fossil)
source=(
  fossil.socket
  fossil.service
  fossil-xinetd
  bash.completion
  http://www.fossil-scm.org/download/fossil-src-$_date_pkgver.tar.gz
)
sha256sums=('f885e17998dc1eece1688a75e516663462fe72a7f4f132def4132055777c7ff8'
            'c4973079facf209d3757db81df08f8d0843ede242f2a0c12281720da65e3b166'
            '2f9a141fadfb7cb5225f0d65a308949bbc592bff9d567b5c472edb413c81f5aa'
            'e22e19dba1a4be6a62af0b77ff0267c2a0f19edf2f6a9133ee64246fae5d87da'
            'c9882f9beb95695db7402dbd03d8fbf082e1109429175af4d57e089bcd2db387')

build() {
  cd fossil-src-$_date_pkgver
  ./configure --prefix=/usr
  # headers and translate targets are problematic with parallel jobs
  make -j1 bld bld/headers
  make
}

package() {
  install -Dm644 fossil-xinetd "$pkgdir"/etc/xinetd.d/fossil
  install -Dm644 fossil.socket "$pkgdir"/usr/lib/systemd/system/fossil.socket
  install -Dm644 fossil.service "$pkgdir"/usr/lib/systemd/system/fossil@.service
  install -Dm644 bash.completion "$pkgdir"/usr/share/bash-completion/completions/fossil

  cd fossil-src-$_date_pkgver
  install -Dm755 fossil "$pkgdir"/usr/bin/fossil
  install -Dm644 COPYRIGHT-BSD2.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
