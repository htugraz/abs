# $Id: PKGBUILD 137381 2015-07-24 13:00:30Z idevolder $
# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-audioencoder-lame
pkgver=20150601.b283cd5
pkgrel=2
pkgdesc="kodi audioencoder addon for lame (mp3)"
arch=('i686' 'x86_64')
url='https://github.com/xbmc/audioencoder.lame'
license=('GPL')
groups=('kodi-addons')
depends=('kodi' 'lame')
makedepends=('git' 'cmake')
source=("$pkgname::git://github.com/xbmc/audioencoder.lame.git")
sha256sums=('SKIP')

pkgver() {
	cd "$pkgname"
	git log -1 --date=short --format="%cd.%h" | tr -d '-'
}

build() {
	cd "$pkgname"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_SHARED_LIBS=1 \
		-DUSE_LTO=1
	make
}

package() {
	cd "$pkgname"
	make DESTDIR="$pkgdir/" install
}

