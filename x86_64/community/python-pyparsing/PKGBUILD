# $Id: PKGBUILD 145447 2015-11-01 13:52:05Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>

pkgname=python-pyparsing
pkgname=('python-pyparsing' 'python2-pyparsing')
pkgver=2.0.5
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='http://pyparsing.wikispaces.com/'
makedepends=('python-setuptools' 'python2-setuptools')
license=('MIT')
source=("http://downloads.sourceforge.net/pyparsing/pyparsing-$pkgver.tar.gz")
sha256sums=('58756bf33e989d84ac72142e4ca558cf10c778a3233edb0a86632f271409ba9e')

prepare() {
  cp -r "pyparsing-$pkgver" py2
}

package_python-pyparsing() {
  depends=('python')
  cd "pyparsing-$pkgver"

  rm -rf build
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyparsing() {
  depends=('python2')
  cd py2

  rm -rf build
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: pypi.python.org/pypi/pyparsing
# vim:set ts=2 sw=2 et:
