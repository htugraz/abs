# $Id: PKGBUILD 68685 2012-03-31 16:54:07Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Babets <fbabetz@yahoo.it>
# Contributor: Leslie P. Polzer <polzer at stardawn dot org>

pkgname=minizip
pkgver=1.2.8
pkgrel=1
pkgdesc='ZIP file extraction library'
url='http://www.winimage.com/zLibDll/minizip.html'
license=('ZLIB' 'custom')
arch=('x86_64' 'i686')
depends=('zlib')
source=("http://zlib.net/current/zlib-${pkgver}.tar.gz"
        'LICENSE')
sha256sums=('36658cb768a54c1d4dec43c3116c27ed893e88b02ecfcb44f2166f9c0b7f2a0d'
            '3b66c863472c2107984f383f34412578b479e7f8fa111ded78190ff0cb3f2eb7')

prepare() {
  cd "$srcdir/zlib-$pkgver/contrib/$pkgname"

  rm -f Makefile
  autoreconf -i
  autoconf
  automake --add-missing
  automake
}

build() {
  cd "$srcdir/zlib-$pkgver/contrib/$pkgname"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/zlib-$pkgver/contrib/$pkgname"

  make install DESTDIR="$pkgdir"
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
