# $Id: PKGBUILD 149784 2015-12-07 09:09:11Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-qiniu
pkgname=(python-qiniu python2-qiniu)
_pkgname=qiniu
pkgver=7.0.6
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/q/qiniu/qiniu-$pkgver.tar.gz"
        LICENSE)
sha512sums=('d57b803862b8359b523da484af4375bdd229193e3957fa077d6f36f36a3aa0359847441bf0b8b7128921695abf60b031f78b275b84017784f077e111c1d2a165'
            '4fbb1090b3ee0d230d40b45db7bcd164873872d1860bd83b51197ed5d03e9d4be7785a400f1d314d6e3c0a6fcba7e1afdf3fdab04ae9970df59f96b18862c529')

prepare() {
  cp -a ${_pkgname}-$pkgver{,-py2}
}

package_python-qiniu() {
  depends=('python-requests' 'python-setuptools')

  cd ${_pkgname}-$pkgver
  python setup.py install -O1 --root "${pkgdir}"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-qiniu() {
  depends=('python2-requests' 'python2-setuptools')

  cd ${_pkgname}-$pkgver-py2
  python2 setup.py install -O1 --root "${pkgdir}"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Don't conflict with python 3.x version
  mv "$pkgdir"/usr/bin/qiniupy{,2}
}
