# $Id: PKGBUILD 148302 2015-12-04 05:20:41Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=0.1.1.20151201
_commit=46ff346af5e8a968ee74834c5e7c8cc24a24f922
#_golibver=0.4.2
_golibrev=902e89b94c9d657f523bcdabad399dc789dcf86e
#_deepinapiver=2.92.1
_deepinapirev=c555d82e94275a6ce808ddc4c4e71460c6822f04
#_dbusfactoryver=2.92.1
_deepinfactoryrev=7257936bb7ed172fcec2c22e5253ab50eb84b85b
pkgrel=1
pkgdesc='deepin file manager backend'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-file-manager"
license=('GPL3')
depends=('deepin-metacity' 'libcanberra' 'poppler-glib' 'gcc-go')
makedepends=('git' 'deepin-dbus-generator')
replaces=('deepin-file-manager-backend')
install="${pkgname}.install"
source=("git+https://github.com/linuxdeepin/deepin-file-manager.git#commit=$_commit"
        #"git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver"
        "git+https://github.com/linuxdeepin/go-lib.git#commit=$_golibrev"
        #"git+https://github.com/linuxdeepin/dde-api.git#tag=$_deepinapiver"
        "git+https://github.com/linuxdeepin/dde-api.git#commit=$_deepinapirev"
        #"git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://github.com/linuxdeepin/dbus-factory.git#commit=$_deepinfactoryrev")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/

  go get github.com/howeyc/fsnotify github.com/mattn/go-sqlite3
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/$pkgname"
  make USE_GCCGO=1
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" do-install
}
