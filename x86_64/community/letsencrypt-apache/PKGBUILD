# $Id: PKGBUILD 148217 2015-12-03 14:35:51Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=letsencrypt-apache
pkgver=0.1.0
pkgrel=1
pkgdesc="Apache plugin for Letâ€™s Encrypt client"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/${pkgname}"
depends=("letsencrypt=$pkgver" "python2-acme=$pkgver" 'python2-augeas' 'python2-setuptools'
         'python2-mock' 'python2-zope-component' 'python2-zope-interface')
checkdepends=('python2-nose')
source=("https://pypi.python.org/packages/source/l/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
md5sums=('b7b0207b456007c0aacdeedcb15bcad1'
         'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  nosetests2
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}"
}
