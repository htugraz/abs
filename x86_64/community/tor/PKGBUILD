# $Id: PKGBUILD 130858 2015-04-07 05:23:06Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Sid Karunaratne

pkgname=tor
pkgver=0.2.5.12
pkgrel=1
pkgdesc='Anonymizing overlay network.'
arch=('i686' 'x86_64')
url='http://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'bash')
optdepends=('torsocks: for torify')
makedepends=('ca-certificates')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist')
install='tor.install'
source=("https://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'torrc'
        'tor.service')
md5sums=('89745069a7efb7aafd01ae263bd0fe5c'
         'SKIP'
         'c1dd4004c63edaeaa829e01be5413cfc'
         '503cedd9679f9817b2c27916ba989a74')
validpgpkeys=('B35BF85BF19489D04E28C33C21194EBB165733EA') # Nick Mathewson

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -dm0700 -o 43 -g 43 "${pkgdir}/var/lib/tor"

  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -Dm0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"
  install -Dm0644 "${srcdir}/tor.service" "${pkgdir}/usr/lib/systemd/system/tor.service"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
