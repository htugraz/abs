# $Id: PKGBUILD 108745 2014-04-02 16:15:39Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Michael Jakl <jakl.michael@gmail.com>
# Contributor: devmotion <nospam-archlinux.org@devmotion.de>

pkgname=julia
pkgver=0.3.0_prerelease
pkgrel=1
pkgdesc='High-level, high-performance, dynamic programming language'
arch=('x86_64' 'i686')
url='http://julialang.org'
license=('GPL')
depends=('arpack' 'fftw' 'git' 'gmp' 'libunwind' 'mpfr' 'pcre' 'readline' 'zlib' 'llvm') # 'suitesparse'  'utf8proc' (AUR) 'intel-mkl' (AUR)
makedepends=('gcc-fortran')
optdepends=('gnuplot: If using the Gaston Package from julia')
provides=('julia')
conflicts=('julia')
options=('!emptydirs' 'staticlibs')
source=("git://github.com/JuliaLang/julia.git#commit=0684a320da")
sha256sums=('SKIP')

build() {
  make -C "$pkgname" prefix=/usr sysconfdir=/etc \
    USE_SYSTEM_LLVM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_READLINE=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_LIBM=1 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_OPENSPECFUN=0 \
    USE_SYSTEM_BLAS=1 \
    USE_SYSTEM_LAPACK=1 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_ARPACK=1 \
    USE_SYSTEM_SUITESPARSE=0 \
    USE_SYSTEM_ZLIB=1 \
    USE_SYSTEM_GRISU=0 \
    USE_SYSTEM_RMATH=0 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_UTF8PROC=0 \
    USE_MKL=0 \
    USE_BLAS64=0 \
    USE_LLVM_SHLIB=0
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" \
    prefix=/usr sysconfdir=/etc  \
    USE_SYSTEM_LLVM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_READLINE=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_LIBM=1 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_OPENSPECFUN=0 \
    USE_SYSTEM_BLAS=1 \
    USE_SYSTEM_LAPACK=1 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_ARPACK=1 \
    USE_SYSTEM_SUITESPARSE=0 \
    USE_SYSTEM_ZLIB=1 \
    USE_SYSTEM_GRISU=0 \
    USE_SYSTEM_RMATH=0 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_UTF8PROC=0 \
    USE_MKL=0 \
    USE_BLAS64=0 \
    USE_LLVM_SHLIB=0 \
    install
    
    # Remove duplicate man-page from julia/doc
    rm -rv $pkgdir/usr/share/julia/doc/man
}

# vim:set ts=2 sw=2 et:
