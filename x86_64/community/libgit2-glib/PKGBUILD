# $Id: PKGBUILD 116312 2014-07-24 19:00:05Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgit2-glib
pkgver=0.0.20
pkgrel=1
pkgdesc="GLib wrapper for libgit2"
url="http://wiki.gnome.org/Projects/Libgit2-glib"
license=(LGPL2.1)
arch=(i686 x86_64)
depends=(glib2 libgit2)
makedepends=(gobject-introspection vala)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('f8a10c8d3fcad14eed080dff6b8db0c72866c11a05b9731af31cb7258bcc8d95')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
