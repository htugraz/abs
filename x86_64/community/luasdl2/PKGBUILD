# $Id: PKGBUILD 135876 2015-06-25 20:39:55Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgbase=luasdl2
pkgname=('lua-sdl2')
pkgver=v2.0.3_4.0
pkgrel=1
pkgdesc='Pure C binding of SDL 2.0 for Lua'
arch=('x86_64' 'i686')
url='https://github.com/Tangent128/luasdl2'
license=('MIT')
depends=('lua' 'sdl2_gfx' 'sdl2_image' 'sdl2_mixer' 'sdl2_net' 'sdl2_ttf')
makedepends=('lua' 'git' 'sdl2_gfx' 'sdl2_image' 'sdl2_mixer' 'sdl2_net' 'sdl2_ttf' 'cmake' 'ninja')
_luaver=53
source=("git://github.com/Tangent128/luasdl2#tag=${pkgver/_/-}")
md5sums=('SKIP')

build() {
  cd luasdl2
  mkdir -p build
  cd build
  cmake \
    -DWITH_LUAVER=$_luaver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -GNinja \
    ..
  ninja
}

package_lua-sdl2() {
  DESTDIR="$pkgdir" ninja -C luasdl2/build install
  install -Dm644 luasdl2/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  mv "$pkgdir/usr/share/docs/Lua-SDL2" "$pkgdir/usr/share/docs/$pkgname"
}

# vim:set ts=2 sw=2 et:
