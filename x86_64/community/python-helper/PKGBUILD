# $Id: PKGBUILD 142451 2015-10-01 16:05:56Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-helper
pkgname=('python-helper' 'python2-helper')
pkgver=2.4.1
pkgrel=2
pkgdesc="Development library for quickly writing configurable applications and daemons"
arch=('any')
license=('BSD')
url="https://helper.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-yaml' 'python2-yaml' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock')
source=("git+https://github.com/gmr/helper.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a helper{,-py2}
}

build() {
  cd "$srcdir/helper"
  python setup.py build

  cd "$srcdir/helper-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/helper"
  nosetests3 || warning "Expected: Tests do not support Python 3"

  cd "$srcdir/helper-py2"
  nosetests2
}

package_python-helper() {
  depends=('python-yaml')

  cd helper
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-helper() {
  depends=('python2-yaml')

  cd helper-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

