# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lcalc
pkgver=1.23
pkgrel=3
pkgdesc="C++ L-function class library and command line interface"
arch=('i686' 'x86_64')
url="http://oto.math.uwaterloo.ca/~mrubinst/L_function_public/L.html"
license=('GPL2')
depends=('pari')
makedepends=('chrpath')
source=("http://oto.math.uwaterloo.ca/~mrubinst/L_function_public/CODE/L-$pkgver.tar.gz" 'Lcommon.h.patch' 'gcc-4.9.patch' 'pari-2.7.patch')
md5sums=('8262d4495e0bbe0283e5341ef8694c23'
         '717f630a387fb1a4569e843a421f30cc'
         '436dd35b06766dffad0941bcfb511e89'
         '5b1937955ca9ceb5d635e57af2364ed2')

prepare() {
  cd L-$pkgver
  patch -p1 -i "$srcdir"/Lcommon.h.patch  
  patch -p1 -i "$srcdir"/gcc-4.9.patch
# port to PARI 2.7 API
  patch -p1 -i "$srcdir"/pari-2.7.patch
# link to PARI
  sed -e 's|#PARI_DEFINE = -DINCLUDE_PARI|PARI_DEFINE = -DINCLUDE_PARI|' -i src/Makefile
  sed -e 's|#include "pari.h"|#include "pari/pari.h"|' -i include/Lcommandline.h -i include/Lcommandline_elliptic.h
}

build() {
  cd L-$pkgver/src
  make
}

package() {
  cd L-$pkgver/src
  mkdir -p "$pkgdir"/usr/{bin,include,lib}
  make INSTALL_DIR="$pkgdir"/usr install

# remove leftover files
  rm "$pkgdir"/usr/include/Lfunction/{Lexplicit_formula.h.swap.crap,.Lexplicit_formula.h.swp}
# fix wrong permissions
  chmod 644 "$pkgdir"/usr/include/Lfunction/Ldokchitser.h
# fix insecure rpath
  chrpath -d "$pkgdir"/usr/bin/lcalc
}

