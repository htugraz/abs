# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lcalc
pkgver=1.23
pkgrel=2
pkgdesc="C++ L-function class library and command line interface"
arch=('i686' 'x86_64')
url="http://oto.math.uwaterloo.ca/~mrubinst/L_function_public/L.html"
license=('GPL2')
depends=('gcc-libs')
makedepends=('chrpath')
source=("http://oto.math.uwaterloo.ca/~mrubinst/L_function_public/CODE/L-$pkgver.tar.gz" 'Lcommon.h.patch' 'gcc-4.9.patch')
md5sums=('8262d4495e0bbe0283e5341ef8694c23'
         '717f630a387fb1a4569e843a421f30cc'
         '436dd35b06766dffad0941bcfb511e89')

prepare() {
  cd L-$pkgver
  patch -p1 -i $srcdir/Lcommon.h.patch  
  patch -p1 -i $srcdir/gcc-4.9.patch
}

build() {
  cd L-$pkgver/src
  make
}

package() {
  cd L-$pkgver/src
  mkdir -p "$pkgdir"/usr/{bin,include,lib}
  make INSTALL_DIR="$pkgdir"/usr install

# remove leftover files
  rm "$pkgdir"/usr/include/Lfunction/{Lexplicit_formula.h.swap.crap,.Lexplicit_formula.h.swp}
# fix wrong permissions
  chmod 644 "$pkgdir"/usr/include/Lfunction/Ldokchitser.h
# fix insecure rpath
  chrpath -d "$pkgdir"/usr/bin/lcalc
}

