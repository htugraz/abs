# $Id: PKGBUILD 136935 2015-07-15 02:29:21Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=fira-mono
pkgname=(ttf-fira-mono otf-fira-mono)
pkgver=3.204
_commit=54736c99038cc308a5dbbddc2101630f26a6b333
pkgrel=1
epoch=2
pkgdesc="Mozilla's typeface designed for Firefox OS (Monospace)"
arch=('any')
license=('custom:OFL')
url='https://github.com/mozilla/Fira'
makedepends=('git')
depends=('fontconfig' 'xorg-font-utils')
source=("git+https://github.com/mozilla/Fira.git#commit=$_commit"
        ttf.install otf.install)
md5sums=('SKIP'
         '9d795e681b14231a6f234c8d2a6d9957'
         '56bf7b135ff34a40513d5a832d882bff')

function _package {
    cd "$srcdir/Fira"

    case "$1" in
        ttf-fira-mono)
            pkgdesc="Mozilla's monospace typeface designed for Firefox OS"
            install=ttf.install
            cd ttf
            fonts=(FiraMono-*.ttf)
            installdir=TTF;;
        otf-fira-mono)
            pkgdesc="Mozilla's monospace typeface designed for Firefox OS"
            install=otf.install
            cd otf
            fonts=(FiraMono-*.otf)
            installdir=OTF;;
    esac

    # Prepare destination directory
    install -dm755 "$pkgdir/usr/share/fonts/$installdir"

    # Install fonts
    for font in "${fonts[@]}"; do
        install -m644 "$font" "$pkgdir/usr/share/fonts/$installdir"
    done

    install -D -m644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"   
}

for _pkgname in ${pkgname[@]}; do
    eval "function package_$_pkgname() { _package $_pkgname; }"
done
