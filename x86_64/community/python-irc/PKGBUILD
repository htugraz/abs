# $Id: PKGBUILD 103447 2014-01-06 02:50:42Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Pierre Chapuis <catwell@archlinux.us>
# Contributor: Stefano Esposito <ragnarok@email.it>

pkgbase=python-irc
pkgname=('python-irc' 'python2-irc')
pkgver=8.5.4
pkgrel=1
pkgdesc="IRC (Internet Relay Chat) protocol client library for Python"
depends=('python-six')
makedepends=('python-setuptools' 'python2-setuptools')
#checkdepends=('python-pytest' 'python2-pytest')  # AUR packages
arch=('any')
url="http://pypi.python.org/pypi/irc"
license=('LGPL')
install='python-irc.install'
source=(http://pypi.python.org/packages/source/i/irc/irc-$pkgver.zip)
md5sums=('5d9456d8861ef409252a6184adba2617')

build() {
  cd "$srcdir"
  cp -a "irc-$pkgver" "irc2-$pkgver"
}

package_python2-irc()
{
  depends=('python2-six')
  install='python2-irc.install'

  cd "$srcdir/irc2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
}

package_python-irc()
{
  depends=('python-six')
  install='python-irc.install'

  cd "$srcdir/irc-$pkgver"
  export LC_ALL=en_US.UTF-8
  python3 setup.py install --root="$pkgdir" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
  find "$pkgdir/" -type d -empty -delete
}

#check()
#{
#  cd "$srcdir/irc-$pkgver"
#  export LC_ALL=en_US.UTF-8
#  python3 setup.py test 
#
#  cd "$srcdir/irc2-$pkgver"
#  python2 setup.py test 
#}
