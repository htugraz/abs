# $Id: PKGBUILD 123316 2014-12-01 09:07:07Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alexander 'gridcol' Griesbaum <agrsbm@gmail.com>
# Contributor: Ravenman <correo.cuervo@gmail.com>
# Contributor: Anton Bazhenov <anton.bazhenov@gmail>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.com.ve>

pkgname=mantisbt
pkgver=1.2.17
pkgrel=5
pkgdesc='Web-based issue tracking system'
arch=('any')
url='http://www.mantisbt.org/'
license=('GPL')
depends=('php')
optdepends=('apache: Web server to run MantisBT'
            'curl: Twitter integration'
            'gd: Graphs support'
            'lighttpd: Web server to run MantisBT'
            'mariadb: SQL database'
            'nginx: Web server to run MantisBT'
            'php-pgsql: PostgreSQL database')
backup=('etc/webapps/mantisbt/config_inc.php')
install='mantisbt.install'
source=("http://downloads.sourceforge.net/mantisbt/mantisbt-${pkgver}.tar.gz"
        'CVE-2014-7146.patch'
        'CVE-2014-8554.patch'
        'CVE-2014-8598.patch'
        'CVE-2014-9089.patch'
        'fix-17742.patch'
        'fix-17870.patch')
sha256sums=('4305295a1d3910516b6fa238e03e710c0bb5b30a01b3a908865799096207b243'
            '5660d838efa89f5cc391df902979faa024a26faa698ab0845a458bf3a5fdcd08'
            '3183477bcc3b69fc969b9d9502070816b2f8bd1ec387d02805b1bd901b471908'
            '3bfb9a6e118678f80a244ca13f527d5589da094491e910d95c53dd5c10d048ed'
            '6ecb79495337243971945e95c7c1ed4264ed17b49d1fdc256fb3406f793d56a5'
            'b04ad80fe28ee8b247ec61433c6515e40ef6a4ca50f4aab039242858100efcca'
            '9f879930bfe31baf7b8449de65123c96db5444187bc637bb45af57fc290bb7fc')

prepare() {
  cd mantisbt-${pkgver}

  patch -Np1 -i ../CVE-2014-7146.patch
  patch -Np1 -i ../CVE-2014-8554.patch
  patch -Np1 -i ../CVE-2014-8598.patch
  patch -Np1 -i ../CVE-2014-9089.patch
  patch -Np1 -i ../fix-17742.patch
  patch -Np1 -i ../fix-17870.patch
}

package() {
  install -dm 755 "${pkgdir}"/{etc/webapps/mantisbt,usr/share/webapps}
  cp -dr --no-preserve='ownership' mantisbt-${pkgver} "${pkgdir}"/usr/share/webapps/mantisbt

  for f in {config_inc.php,custom_strings_inc.php,custom_constants_inc.php,custom_functions_inc.php}; do
    ln -s /etc/webapps/mantisbt/${f} "${pkgdir}"/usr/share/webapps/mantisbt/
  done
  cp "${pkgdir}"/usr/share/webapps/mantisbt/config_inc.php.sample "${pkgdir}"/etc/webapps/mantisbt/config_inc.php

  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
  chown http:http -R "${pkgdir}"/usr/share/webapps/mantisbt
}

# vim: ts=2 sw=2 et:
