# $Id: PKGBUILD 147451 2015-11-23 10:31:59Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=2.92.1
_golibver=0.4.2
_deepinapiver=2.92.1
_dbusfactoryver=2.92.1
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('gcc-go' 'deepin-wm' 'deepin-daemon')
makedepends=('cmake' 'coffee-script' 'deepin-dbus-generator' 'python2' 'webkitgtk' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/startdde.git#tag=$pkgver"
        "git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver"
        "git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://github.com/linuxdeepin/dde-api.git#tag=$_deepinapiver")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  sed -i '/GOLDFLAGS +=/ s/)$/ libcanberra)/' startdde/Makefile

  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/

  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/startdde"
  make USE_GCCGO=1
}

package() {
  cd startdde
  make DESTDIR="${pkgdir}" install
}
