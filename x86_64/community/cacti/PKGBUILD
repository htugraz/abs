# $Id: PKGBUILD 137219 2015-07-21 14:51:29Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tuxbubling <tuxbubling@jabber.fr>

pkgname=cacti
pkgver=0.8.8_f
pkgrel=2
pkgdesc="complete network graphing solution designed to harness the power of RRDTool's data storage and graphing functionality"
arch=(any)
url="http://www.cacti.net"
license=('GPL')
depends=('rrdtool' 'mariadb' 'php' 'php-gd' 'net-snmp' 'xorg-fonts-100dpi')
backup=('etc/webapps/cacti/.htaccess')
install=cacti.install
source=("http://www.cacti.net/downloads/$pkgname-${pkgver/_/}.tar.gz")
md5sums=('8d90642b362f80c46c489c5531e2ba90')

prepare() {
  cd $srcdir/$pkgname-${pkgver/_/}
}

package() {
  mkdir -p $pkgdir/usr/share/webapps/cacti $pkgdir/etc/webapps/cacti
  mv $srcdir/$pkgname-${pkgver/_/}/* $pkgdir/usr/share/webapps/cacti
  echo "Require all denied" >> $pkgdir/etc/webapps/cacti/.htaccess
  mv $pkgdir/usr/share/webapps/cacti/include/config.php $pkgdir/etc/webapps/cacti
  ln -s /etc/webapps/cacti/.htaccess $pkgdir/usr/share/webapps/cacti/.htaccess
  ln -s /etc/webapps/cacti/config.php $pkgdir/usr/share/webapps/cacti/include/config.php

  install -dm0755 $pkgdir/var/log/ $pkgdir/var/lib/cacti/

  mv $pkgdir/usr/share/webapps/cacti/rra $pkgdir/var/lib/cacti/rra
  ln -s /var/lib/cacti/rra $pkgdir/usr/share/webapps/cacti/rra

  mv $pkgdir/usr/share/webapps/cacti/log $pkgdir/var/log/cacti
  ln -s /var/log/cacti $pkgdir/usr/share/webapps/cacti/log
}
