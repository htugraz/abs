# $Id: PKGBUILD 124809 2014-12-29 04:02:19Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Nicolas Pouillard https://nicolaspouillard.fr

pkgname=python2-netlib
pkgver=0.11.2
pkgrel=1
pkgdesc="A collection of network utilities used by pathod and mitmproxy"
url="https://github.com/mitmproxy/netlib"
license=('MIT')
arch=('any')
depends=("python2-passlib" 'python2-pyasn1' 'python2-pyopenssl')
checkdepends=('python2-nose' 'python2-mock' 'pathod')
source=("http://pypi.python.org/packages/source/n/netlib/netlib-$pkgver.tar.gz")
sha256sums=('66dac408eccb528b284e6a6fa5bc52aa40d1c2a53d74179d3cb2253b3120851e')

build() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir/netlib-$pkgver"
  nosetests2
}

package() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py install -O1 --skip-build --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
