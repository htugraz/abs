# $Id: PKGBUILD 145765 2015-11-03 09:09:49Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-testtools
pkgname=('python-testtools' 'python2-testtools')
pkgver=1.8.0
pkgrel=2
pkgdesc="Extensions to the Python standard library unit testing framework"
arch=('any')
license=('MIT')
url="https://github.com/testing-cabal/testtools"
makedepends=('python-setuptools' 'python2-setuptools' 'python-yaml' 'python2-yaml'\
             'git' 'python-pbr' 'python2-pbr' 'python-extras' 'python2-extras'
             'python-mimeparse' 'python2-mimeparse' 'python2-traceback2')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock')
source=("git+https://github.com/testing-cabal/testtools.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  # Since we are always latest...
  sed -i 's|unittest2|unittest|' testtools/testtools/tests/test_run.py testtools/testtools/tests/test_testsuite.py testtools/testtools/tests/test_deferredruntest.py
  sed -i '/unittest2>=1.0.0/d' testtools/requirements.txt

  cp -a testtools{,-py2}

  # These are for python 2.x and old python 3.x only
  sed -i 's|import linecache2 as linecache|import linecache|' testtools/testtools/tests/test_compat.py
  sed -i "s|traceback = try_import('traceback2')|import traceback|" testtools/testtools/content.py
  sed -i '/traceback2/d' testtools/requirements.txt
}

build() {
  cd "$srcdir/testtools"
  python setup.py build

  cd "$srcdir/testtools-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/testtools"
  python -m testtools.run testtools.tests.test_suite

  cd "$srcdir/testtools-py2"
  python2 -m testtools.run testtools.tests.test_suite
}

package_python-testtools() {
  depends=('python-pbr' 'python-extras' 'python-mimeparse')

  cd testtools
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-testtools() {
  depends=('python2-pbr' 'python2-extras' 'python2-mimeparse' 'python2-traceback2')

  cd testtools-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
