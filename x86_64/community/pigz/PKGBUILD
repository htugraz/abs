# $Id: PKGBUILD 98523 2013-10-12 20:12:05Z jsteel $
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski
# Contributor: Laszlo Papp <djszapi2@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pigz
pkgver=2.3.1
pkgrel=1
pkgdesc='Parallel implementation of the gzip file compressor'
arch=('i686' 'x86_64')
url='http://www.zlib.net/pigz/'
license=('custom')
depends=('zlib')
source=(http://www.zlib.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('e803f8bc0770c7a5e96dccb1d2dd2aab')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  install -Dm755 "$srcdir"/$pkgname-$pkgver/$pkgname \
    "$pkgdir"/usr/bin/$pkgname 

  pushd "$pkgdir"/usr/bin
    ln -s pigz unpigz 
  popd

  install -Dm644 "$srcdir"/$pkgname-$pkgver/$pkgname.1 \
    "$pkgdir"/usr/share/man/man1/$pkgname.1
  install -Dm644 "$srcdir"/$pkgname-$pkgver/$pkgname.pdf \
    "$pkgdir"/usr/share/doc/$pkgname/$pkgname.pdf
  install -Dm644 "$srcdir"/$pkgname-$pkgver/README \
    "$pkgdir"/usr/share/licenses/$pkgname/README
}
