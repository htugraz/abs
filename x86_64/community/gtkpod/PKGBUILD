# $Id: PKGBUILD 122944 2014-11-22 20:18:51Z bpiotrowski $
# Maintainer:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=gtkpod
pkgver=2.1.4
pkgrel=3
pkgdesc="A platform independent GUI for Apple's iPod using GTK3"
arch=('i686' 'x86_64')
url="http://gtkpod.sourceforge.net"
license=('GPL')
depends=('libanjuta' 'curl' 'flac' 'libid3tag' 'libgpod' 'awk' 'hicolor-icon-theme' 'webkitgtk')
makedepends=('flex' 'intltool' 'faad2' 'libvorbis')
optdepends=('libmp4v2: MP4/h264 support'
            'vorbis-tools: OGG support'
	    'libvorbis: OGG support'
            'id3v2: mp3 conversion support'
            'faad2: m4a conversion support')
install=gtkpod.install
source=("http://downloads.sourceforge.net/gtkpod/$pkgname-$pkgver.tar.gz")
md5sums=('66c12ccb529c0f7de9a48a7f0d277c80')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's#python#python2#' scripts/sync-palm-jppy.py

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
