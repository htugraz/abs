# $Id: PKGBUILD 146227 2015-11-06 01:50:16Z flexiondotorg $
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
_pkgbase=mate-terminal
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="The MATE Terminal Emulator (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'dconf' 'libsm' 'mate-desktop-gtk3>=1.12' 'vte290')
makedepends=('docbook2x' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('58d526e98a86653ddc49c88f03ad52b0cf907b7a')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --with-gtk=3.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
