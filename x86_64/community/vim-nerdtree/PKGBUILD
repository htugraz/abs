# $Id: PKGBUILD 114851 2014-07-04 09:11:35Z fyan $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kwpolska <kwpolska@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Laszlo Papp <djszapi @ gmail at com>

pkgname=vim-nerdtree
pkgver=4.2.0
_scriptid=17123
pkgrel=4
pkgdesc='Tree explorer plugin for navigating the filesystem'
arch=('any')
url='http://www.vim.org/scripts/script.php?script_id=1658'
license=('custom:WTFPL')
depends=('vim')
groups=('vim-plugins')
install='vimdoc.install'
source=("$pkgname-$pkgver.zip::http://www.vim.org/scripts/download_script.php?src_id=$_scriptid"
        'COPYING')
md5sums=('39e61096eb0c54a4d386eaba49d6de3d'
         '389a9e29629d1f05e115f8f05c283df5')

package() {
  install -d \
    "$pkgdir/usr/share/vim/vimfiles/"{doc,nerdtree_plugin,plugin,syntax}
  install -m644 doc/NERD_tree.txt "$pkgdir/usr/share/vim/vimfiles/doc/"
  install -m644 nerdtree_plugin/{exec_menuitem,fs_menu}.vim \
    "$pkgdir/usr/share/vim/vimfiles/nerdtree_plugin/"
  install -m644 plugin/NERD_tree.vim "$pkgdir/usr/share/vim/vimfiles/plugin/"
  install -m644 syntax/nerdtree.vim "$pkgdir/usr/share/vim/vimfiles/syntax/"
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
