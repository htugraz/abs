# $Id: PKGBUILD 133841 2015-05-19 17:15:18Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: sracker <smb.sac@gmail.com>

pkgname=xdebug
pkgver=2.3.2
pkgrel=2
pkgdesc="PHP debugging extension"
arch=('i686' 'x86_64')
url="http://www.xdebug.org"
license=('GPL')
depends=('php')
backup=('etc/php/conf.d/xdebug.ini')
source=(http://www.xdebug.org/files/$pkgname-$pkgver.tgz
	https://github.com/remicollet/xdebug/commit/419834a441cc65bb4e7220e1128356dc99d86211.patch
	xdebug.ini)
md5sums=('647b862291fe900f40d25a80fcf50026'
         'cd211deafab05c1d9e4b596c308c82cf'
         '6a1f2ef91c632d4c9b7b218cd2e278ef')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/419834a441cc65bb4e7220e1128356dc99d86211.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  phpize
  ./configure --prefix=/usr --enable-xdebug
  make

  cd debugclient
  ./configure --prefix=/usr
  make
}

package() {
  local PHPVER=`php -r 'echo phpversion();'`

  cd $srcdir/$pkgname-$pkgver/debugclient
  make DESTDIR=$pkgdir install

  cd $srcdir/$pkgname-$pkgver
  make INSTALL_ROOT=$pkgdir install
  install -D -m 644 $srcdir/xdebug.ini $pkgdir/etc/php/conf.d/xdebug.ini
}
