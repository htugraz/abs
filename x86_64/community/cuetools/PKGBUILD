# $Id: PKGBUILD 117846 2014-08-24 16:41:05Z stativ $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=cuetools
pkgver=1.4.0
pkgrel=3
pkgdesc="Cue and toc file parsers and utilities"
arch=('i686' 'x86_64')
url="https://github.com/svend/cuetools"
#url="http://developer.berlios.de/projects/cuetools/"
license=('GPL')
depends=('glibc')
optdepends=('id3v2: mp3 support in cuetag (does not require mutagen)'
            'mutagen: mp3 support in cuetag (does not require id3v2)')
source=("https://github.com/svend/cuetools/archive/${pkgver}.tar.gz"
        "cuetag-fix_spaces.diff" "fix_build_with_automake-1.12.diff")
md5sums=('4492dae2b3f9e077f6455a1f1cddef3b'
         '60f4aba26e529e48dbedd2adef917117'
         '67a3b32711d1a1f508be6ed0f1bdadf3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 < "$srcdir/fix_build_with_automake-1.12.diff" || true
  patch -Np1 < "$srcdir/cuetag-fix_spaces.diff" || true

  aclocal
  autoheader
  automake --force-missing --add-missing
  autoconf
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
