# $Id: PKGBUILD 141739 2015-09-28 06:36:56Z tpowa $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=vhba-module
pkgver=20140928
_extramodules=extramodules-4.2-ARCH
pkgrel=18
pkgdesc="Kernel module that emulates SCSI devices"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('linux>=4.2' 'linux<4.3')
makedepends=('linux-headers>=4.2' 'linux-headers<4.3')
options=(!makeflags)
install=$pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2"
        '60-vhba.rules')
md5sums=('967007230bb028424216d9b35da422c0'
         '4dc37dc348b5a2c83585829bde790dcc')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  make KDIR=/usr/lib/modules/$_kernver/build
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 vhba.ko "$pkgdir/usr/lib/modules/$_extramodules/vhba.ko"
  install -Dm644 ../60-vhba.rules "$pkgdir/usr/lib/udev/rules.d/60-vhba.rules"

  cd $startdir
  cp -f $install ${install}.pkg
  true && install=${install}.pkg
  sed -i "s/EXTRAMODULES=.*/EXTRAMODULES=$_extramodules/" $install
}

# vim:set ts=2 sw=2 et:
