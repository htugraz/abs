# $Id: PKGBUILD 117442 2014-08-15 16:10:44Z dicebot $
# Maintainer: Mihails Strasuns <public@dicebot.lv>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=('ldc' 'liblphobos-devel')
groups=('dlang' 'dlang-ldc')
pkgver=0.14.0
epoch=1
pkgrel=1
pkgdesc="A D Compiler based on the LLVM Compiler Infrastructure including D runtime and libphobos2"
arch=('i686' 'x86_64')
url="https://github.com/ldc-developers/ldc"
license=('BSD')
depends=('libconfig')
makedepends=('git' 'cmake' 'llvm')
source=("git://github.com/ldc-developers/ldc.git#tag=v${pkgver}"
        "ldc2.conf"
       )
sha1sums=('SKIP'
          '290689ded66a12a243666d0bac3ffb74180fa490'
         )

build() {
    cd $srcdir/ldc

    git submodule update --init --recursive

    mkdir build && cd build
    cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINCLUDE_INSTALL_DIR=/usr/include/dlang/ldc \
    ..
    make
}

package_ldc() {
    depends=('liblphobos-devel')
    backup=("etc/ldc2.conf")
    provides=("d-compiler")

    # binaries
    install -D -m755 $srcdir/ldc/build/bin/ldmd2 $pkgdir/usr/bin/ldmd2 
    install -D -m755 $srcdir/ldc/build/bin/ldc2 $pkgdir/usr/bin/ldc2 

    # supplementaries
    install -D -m644 $srcdir/ldc/bash_completion.d/ldc $pkgdir/usr/share/bash-completion/completions/ldc

    # licenses
    install -D -m644 $srcdir/ldc/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE    

    # default configuration files
    install -D -m644 $srcdir/ldc2.conf $pkgdir/etc/ldc2.conf
}

package_liblphobos-devel() {
    provides=("d-runtime" "d-stdlib")
	options=("staticlibs")

    # licenses
    install -D -m644 $srcdir/ldc/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE    

    # libraries
    install -D -m644 $srcdir/ldc/build/lib/libphobos2-ldc.a $pkgdir/usr/lib/liblphobos2.a
    install -D -m644 $srcdir/ldc/build/lib/libdruntime-ldc.a $pkgdir/usr/lib/libldruntime.a
    install -D -m644 $srcdir/ldc/build/lib/libphobos2-ldc-debug.a $pkgdir/usr/lib/liblphobos2-debug.a
    install -D -m644 $srcdir/ldc/build/lib/libdruntime-ldc-debug.a $pkgdir/usr/lib/libldruntime-debug.a

    # imports
    mkdir -p $pkgdir/usr/include/dlang/ldc
    cp -r $srcdir/ldc/build/import/* $pkgdir/usr/include/dlang/ldc/
    cp -r $srcdir/ldc/runtime/phobos/std $pkgdir/usr/include/dlang/ldc/
    cp -r $srcdir/ldc/runtime/phobos/etc $pkgdir/usr/include/dlang/ldc/
}
