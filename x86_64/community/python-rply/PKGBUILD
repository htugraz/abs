# $Id: PKGBUILD 144962 2015-10-25 15:11:16Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-rply
pkgname=('python-rply' 'python2-rply')
pkgver=0.7.4
pkgrel=1
pkgdesc="A pure Python Lex/Yacc that works with RPython"
arch=('any')
license=('BSD')
url="https://rply.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-appdirs' 'python2-appdirs' 'git')
checkdepends=('python-pytest' 'python2-pytest')
source=("git+https://github.com/alex/rply.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a rply{,-py2}
}

build() {
  cd "$srcdir/rply"
  python setup.py build

  cd "$srcdir/rply-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/rply"
  py.test

  cd "$srcdir/rply-py2"
  py.test2
}

package_python-rply() {
  depends=('python-appdirs')

  cd rply
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-rply() {
  depends=('python2-appdirs')

  cd rply-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

