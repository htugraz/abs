# $Id: PKGBUILD 105534 2014-02-09 21:18:21Z bluewind $
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=filezilla
pkgver=3.7.4
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
arch=('i686' 'x86_64')
url="http://filezilla-project.org/"
license=('GPL')
depends=('dbus-core' 'xdg-utils' 'wxgtk2.8' 'libidn' 'hicolor-icon-theme' 'sqlite' 'gnutls')
install=filezilla.install
source=("http://downloads.sourceforge.net/project/filezilla/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_src.tar.bz2")

build() {
  cd "${pkgname}-${pkgver}"
  export WX_CONFIG_PATH='/usr/bin/wx-config-2.8'
  export WXRC='/usr/bin/wxrc-2.8'
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --with-tinyxml=builtin

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
md5sums=('8419418f1f473bf153369d7a834e39e7')
