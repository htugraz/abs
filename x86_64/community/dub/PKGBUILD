# Maintainer:  Mihails Strasuns <public@dicebot.lv>
# Contributor: Moritz Maxeiner <moritz@ucworks.org>
pkgname=dub
pkgver=0.9.21
pkgrel=1
pkgdesc="Developer package manager for D programming language"
arch=('i686' 'x86_64')
url="https://github.com/rejectedsoftware/dub"
license=('MIT')
makedepends=('dmd' 'libphobos-devel' 'git')
depends=('d-compiler' 'd-runtime' 'curl')
conflicts=('dub-git')
source=("git+http://github.com/rejectedsoftware/dub.git#tag=v$pkgver")
sha256sums=('SKIP')

build()
{
  cd "${srcdir}/${pkgname}"

  echo Generating version file...
  GITVER=$(git describe) || GITVER=unknown
  echo "module dub.version_; enum dubVersion = \"$GITVER\";" > source/dub/version_.d

  dmd -ofbin/dub -g -debug -w -version=DubUseCurl -Isource -L-lcurl @build-files.txt
}

package()
{
  cd "${srcdir}/${pkgname}"
  install -D -m755 bin/dub "${pkgdir}/usr/bin/dub"
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
