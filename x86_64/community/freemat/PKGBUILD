# $Id: PKGBUILD 97998 2013-10-02 21:37:00Z bgyorgy $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=freemat
pkgver=4.2
pkgrel=3
pkgdesc="A free environment for rapid engineering, scientific prototyping and data processing"
arch=('i686' 'x86_64')
url="http://freemat.sourceforge.net"
license=('GPL')
depends=('arpack' 'fftw' 'glu' 'portaudio' 'qtwebkit')
makedepends=('cmake' 'python2' 'suitesparse')
install=freemat.install
source=(http://downloads.sourceforge.net/project/freemat/FreeMat4/FreeMat-$pkgver-Source.tar.gz)
md5sums=('ace147e49273ae935d363da8e2a56d4d')

build() {
  cd $srcdir/FreeMat-$pkgver-Source

  unset LDFLAGS
  rm -f CMakeCache.txt
  find . -type f -name '*.moc.cpp' -exec rm -f {} \;
  find . -type f -name 'add.so' -exec rm -f {} \;
  echo >libs/libMatC/CJitFuncClang.hpp
  echo >libs/libMatC/CJitFuncClang.cpp

  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DUSE_LLVM=OFF \
	-DFORCE_BUNDLED_UMFPACK=ON \
	-DFFI_INCLUDE_DIR=/usr/lib/libffi-`pacman -Q libffi | cut -f2 -d\ |cut -f1 -d-`/include/ \
	-DPYTHON_EXECUTABLE=/usr/bin/python2 \
	.
  make
}
package() {
  cd $srcdir/FreeMat-$pkgver-Source

  make DESTDIR=$pkgdir install -j1
  sed -i "s|/FreeMat-.*/|/FreeMat-$pkgver/|g" $startdir/freemat.install
  rm $pkgdir/usr/bin/blas.ini
}
