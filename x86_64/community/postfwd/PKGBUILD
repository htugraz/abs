# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>

pkgname=postfwd
pkgver=1.35
pkgrel=2
pkgdesc="Combines complex postfix restrictions in a ruleset similar to those of the most firewalls"
arch=(any)
url="http://postfwd.org/"
license=('bsd')
install="install"
depends=('perl' 'perl-io-multiplex' 'perl-net-server' 'perl-net-dns')
source=("http://postfwd.org/$pkgname-$pkgver.tar.gz"
        "service")
md5sums=('8e01328416c735d55efec90dede8bff2'
         '1c9295de6da98241eb55e62809746030')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i bin/postfwd-script.sh \
      -e 's|PFWCMD=/usr/local/postfwd/sbin/postfwd|PFWCMD=/usr/bin/postfwd|g'
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -dm755 "$pkgdir"/{etc/$pkgname,usr/{bin,share/{man,$pkgname}}}

  cp -ra etc/*    "$pkgdir/etc/$pkgname/"
  cp -ra -t "$pkgdir/usr/bin/" bin/* sbin/*
  cp -ra man/*    "$pkgdir/usr/share/man/"
  cp -ra tools/*  "$pkgdir/usr/share/$pkgname/"
  cp -ra doc/*    "$pkgdir/usr/share/$pkgname/"

  install -Dm644 $srcdir/service "$pkgdir/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 $srcdir/service "$pkgdir/usr/lib/systemd/system/${pkgname}2.service"
  sed -i 's|/usr/bin/postfwd|/usr/bin/postfwd2|' "$pkgdir/usr/lib/systemd/system/${pkgname}2.service"

  install -dm755 $pkgdir/usr/share/licenses/$pkgname
  mv $pkgdir/usr/share/$pkgname/LICENSE $pkgdir/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
