# $Id: PKGBUILD 99628 2013-10-30 16:34:52Z arodseth $
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=libnsgif
pkgver=0.1.0
pkgrel=2
pkgdesc='Decoding library for the GIF image file format'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libnsgif/'
license=('MIT')
depends=('glibc')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('e7dc7ee887fefc19e7c7985c5a2b057870e0fa3dd0dcfc1fdb4e98da1f36593c')

build() {
  cd "$pkgname-$pkgver"

  CFLAGS="$CFLAGS -fno-strict-aliasing" 
  make PREFIX=/usr COMPONENT_TYPE="lib-shared" 
  #make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
  cd "$pkgname-$pkgver"

  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"   
  #make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-static"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
