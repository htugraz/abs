# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: MThinkCpp <mtc.maintainer[at]outlook.com>
pkgname=libc++
pkgver=3.4
pkgrel=1
pkgdesc='A new implementation of the C++ standard library, targeting C++11.'
url='http://libcxx.llvm.org'
license=('custom:University of Illinois/NCSA Open Source License')
arch=('i686' 'x86_64')
# gcc-libs needed for libgcc_s, because Arch doesn't use a pure compiler-rt clang
depends=('glibc' 'libc++abi' 'gcc-libs')
makedepends=('clang' 'subversion' 'cmake')
source=("http://www.llvm.org/releases/${pkgver}/libcxx-${pkgver}.src.tar.gz")
md5sums=('85c3c4e2233893b037256bf9f7077857')
install=${pkgname}.install

build() {
  mkdir -p libcxx-${pkgver}/build
  cd libcxx-${pkgver}/build
  CC=clang CXX=clang++ cmake -G "Unix Makefiles" -DLIBCXX_CXX_ABI=libcxxabi \
    -DLIBCXX_LIBCXXABI_INCLUDE_PATHS=/usr/include \
    -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr "$srcdir/libcxx-${pkgver}"
  make
}
package() {
  install -Dm644 libcxx-${pkgver}/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/license.txt"
  cd libcxx-${pkgver}/build
  make DESTDIR="$pkgdir" install
}
