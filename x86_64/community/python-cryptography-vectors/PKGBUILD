# $Id: PKGBUILD 119864 2014-09-30 07:20:27Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-cryptography-vectors
pkgname=('python-cryptography-vectors' 'python2-cryptography-vectors')
pkgver=0.6
pkgrel=1
pkgdesc="Test vectors for the cryptography package"
arch=('any')
license=('Apache')
url="http://pypi.python.org/pypi/cryptography-vectors"
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/c/cryptography-vectors/cryptography_vectors-${pkgver}.tar.gz")
md5sums=('1e31a05a84c16c6e41bd43c05a7dfbec')

prepare() {
   cp -a cryptography_vectors-${pkgver}{,-python2}
}

package_python-cryptography-vectors() {
   depends=('python')
 
   cd cryptography_vectors-${pkgver}
   python3 setup.py install --root="${pkgdir}" --optimize=1
}
 
package_python2-cryptography-vectors() {
   depends=('python2')
 
   cd cryptography_vectors-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1
}
