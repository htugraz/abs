# $Id: PKGBUILD 145208 2015-10-29 02:04:32Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cryptography-vectors
pkgname=('python-cryptography-vectors' 'python2-cryptography-vectors')
pkgver=1.1
pkgrel=1
pkgdesc="Test vectors for the cryptography package"
arch=('any')
license=('Apache')
url="http://pypi.python.org/pypi/cryptography-vectors"
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/c/cryptography-vectors/cryptography_vectors-${pkgver}.tar.gz")
md5sums=('0ad422501074929c06b7edd40df41844')

prepare() {
   cp -a cryptography_vectors-$pkgver{,-python2}
}

package_python-cryptography-vectors() {
   depends=('python')
 
   cd cryptography_vectors-$pkgver
   python3 setup.py install --root="$pkgdir" --optimize=1
}
 
package_python2-cryptography-vectors() {
   depends=('python2')
 
   cd cryptography_vectors-$pkgver-python2
   python2 setup.py install --root="$pkgdir" --optimize=1
}
