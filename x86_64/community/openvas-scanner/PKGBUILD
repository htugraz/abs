# $Id: PKGBUILD 119157 2014-09-18 05:59:59Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-scanner
pkgver=4.0.3
pkgrel=1
pkgdesc='The OpenVAS scanning Daemon'
arch=('i686' 'x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('openvas-libraries')
makedepends=('cmake' 'doxygen')
groups=(openvas)
# This URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/1726/$pkgname-${pkgver}.tar.gz"
        "http://wald.intevation.org/frs/download.php/1727/$pkgname-${pkgver}.tar.gz.asc"
        "$pkgname.service")
md5sums=('850facc6e7f550d1a75925563b0e5caf'
         'SKIP'
         '5cc11ac11a7454aaa9148eab844b81af')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  install -Dm644 ../$pkgname.service "$pkgdir/usr/lib/systemd/system/${pkgname}.service"
}
