# $Id: PKGBUILD 117517 2014-08-18 11:58:33Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Rorschach <r0rschach@lavabit.com>

pkgname=torsocks
pkgver=2.0.0
pkgrel=1
pkgdesc='Wrapper to safely torify applications'
arch=('i686' 'x86_64')
url='http://code.google.com/p/torsocks'
license=('GPL2')
depends=('tor')
makedepends=('git')
backup=("etc/${pkgname}.conf")
source=("git://git.torproject.org/torsocks.git#tag=1.3")
md5sums=('SKIP')

build() {
  cd $pkgname
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir/" install
  find "$pkgdir/usr/share" -maxdepth 1 -type f -delete
}
