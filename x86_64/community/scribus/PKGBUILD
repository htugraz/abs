# $Id: PKGBUILD 119430 2014-09-22 18:19:35Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=scribus
pkgver=1.4.4
pkgrel=2
pkgdesc='Desktop publishing software'
url='http://www.scribus.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('cmake' 'boost' 'mesa')
optdepends=('tk: scripts based on tkinter')
depends=('qt4' 'cairo' 'lcms2' 'libcups' 'libxml2' 'hyphen'
         'ghostscript' 'python2' 'podofo' 'boost-libs'
         'desktop-file-utils' 'shared-mime-info')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('7bd3b4a7d6e6262126079847c617b3a25ba8ab06')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's|Icon=scribus|Icon=/usr/share/scribus/icons/scribus.png|' -i scribus.desktop
	sed \
		-e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
		-e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
		-i scribus/plugins/scriptplugin/{samples,scripts}/*
	install -d ../build
}

build() {
	cd "${srcdir}/build"
	cmake "../${pkgname}-${pkgver}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DLIB_SUFFIX=""
	make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install
	install -Dm644 "../${pkgname}-${pkgver}/scribus.desktop" "${pkgdir}/usr/share/applications/scribus.desktop"
}
