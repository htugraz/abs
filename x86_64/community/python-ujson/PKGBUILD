# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-ujson
pkgname=('python-ujson' 'python2-ujson')
_libname=${pkgname/python-/}
pkgver=1.33
pkgrel=3
pkgdesc="Ultra fast JSON encoder and decoder for Python"
url="https://github.com/esnme/ultrajson"
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
sha512sums=('0f1f66212fbf94c03e048ba64c3bd817c50443d1a29b87f6a3a38f697a050f38821be4ba36a3b17a96930c69ee92973ac31bdd41851dea071af14cd4bbaf8480'
            '74a87d69556e526768fe1d0d0b6ff888b8b34ab7e5c651a96b4e4c42599ce67825cd5a12222ad3a20745faf230eb778737b03afb5fe944057cd7dc705c4b5b6f')
source=(https://pypi.python.org/packages/source/${_libname:0:1}/${_libname}/${_libname}-${pkgver}.zip
        LICENSE.txt)

package_python-ujson() {
  cd ${_libname}-${pkgver}
  depends=('python')
  
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 "${srcdir}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

package_python2-ujson() {
  cd ${_libname}-${pkgver}
  depends=('python2')
  
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 "${srcdir}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
