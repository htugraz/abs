# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Beej Jorgensen <beej@beej.us>

pkgname=wavemon
pkgver=0.7.6
pkgrel=3
pkgdesc='Ncurses-based monitoring application for wireless network devices'
url='http://eden-feed.erg.abdn.ac.uk/wavemon/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('ncurses' 'libcap')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/uoaerg/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('d5863bd5ea4ead763b07eb723a2017d1ad53708f5a92cd90522aa508e144f02b60a7ad1123291e635e83b2e88d03be09f54e1ea7e2c2831bd07dc907d2d621af')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's|CFLAGS="$CFLAGS -pthread"||g' -i configure.ac
  sed -r 's|(/share)|\1/doc|g' -i Makefile.in
  ./configure --prefix=/usr --mandir=/usr/share/man
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
