# $Id: PKGBUILD 127508 2015-02-10 17:02:35Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: scj <scj(at)archlinux(dot)us>

pkgname=incron
pkgver=0.5.10
pkgrel=7
pkgdesc="Like the regular cron but is driven by filesystem events instead of time periods"
arch=('i686' 'x86_64')
url="http://incron.aiken.cz/"
license=('GPL')
depends=('gcc-libs' 'bash')
install=incron.install
options=('emptydirs')
source=("http://inotify.aiken.cz/download/incron/$pkgname-$pkgver.tar.gz"
	'incrond.service')
md5sums=('67f8b671db8169edccc8ef11c7d2c8be'
         '4be206ba6ecacd9d2006cdc72b4604db')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  sed -i '1,1i#include <unistd.h>' inotify-cxx.cpp icd-main.cpp
  sed -i '1,1i#include <stdio.h>' icd-main.cpp inotify-cxx.cpp usertable.cpp appargs.cpp
  sed -i 's|strchr(s,|(char*)strchr(s,|' incroncfg.cpp
  sed -i 's|$(DESTDIR)$(PREFIX)/sbin/|$(DESTDIR)$(PREFIX)/bin/|g' Makefile
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  install -d "${pkgdir}"/usr/share/man "${pkgdir}"/usr/{s,}bin

  make PREFIX="${pkgdir}"/usr \
    MANPATH="${pkgdir}"/usr/share/man \
    USERDATADIR="${pkgdir}"/var/spool/incron \
    CFGDIR="${pkgdir}"/etc \
    SYSDATADIR="${pkgdir}"/etc/incron.d install

  install -Dm0644 "${srcdir}/incrond.service" "${pkgdir}/usr/lib/systemd/system/incrond.service"
  rmdir $pkgdir/usr/sbin
}
