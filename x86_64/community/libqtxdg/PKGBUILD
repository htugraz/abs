# $Id: PKGBUILD 127407 2015-02-09 15:21:00Z jleclanche $
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=libqtxdg
pkgver=1.1.0
pkgrel=2
pkgdesc="Library providing freedesktop.org XDG specs implementations for Qt."
arch=("i686" "x86_64")
url="http://lxqt.org"
license=("GPL2")
depends=("qt5-base")
makedepends=("cmake")
source=("http://downloads.lxqt.org/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=("5c90989fcd13387cc8a7905a03e0b65f76aa28545cb9733625e1d88fd6ff4002")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DUSE_QT5=true
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
