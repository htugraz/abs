# $Id: PKGBUILD 110897 2014-05-09 11:31:23Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tomas A. Schertel <tschertel@gmail.com>

pkgname=cherrytree
pkgver=0.33.0
pkgrel=1
pkgdesc='Hierarchical note taking application featuring rich text and syntax highlighting'
arch=('any')
url='http://www.giuspen.com/cherrytree/'
license=('GPL3')
depends=('pygtksourceview2' 'desktop-file-utils')
optdepends=('python2-pyenchant: for spell checking support'
            'p7zip: for password protection support')
install=cherrytree.install
source=(http://www.giuspen.com/software/$pkgname-$pkgver.tar.xz)
sha256sums=('d136eb8f20ec6647ea0454947b47c447bcbf45904ca9a9f43345e9b816a0cf56')

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
