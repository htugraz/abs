# $Id: PKGBUILD 104801 2014-01-26 12:17:39Z andyrtr $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=nomacs
pkgver=1.6.2
pkgrel=3
pkgdesc="A Qt image viewer"
arch=(i686 x86_64)
url="http://www.nomacs.org/"
license=('GPL3')
depends=('qt4' 'exiv2' 'libraw' 'opencv')
makedepends=('cmake')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver-source.tar.bz2")
md5sums=('3e8782b7bf792933ba673a2e9340841b')

prepare() {
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4
  make
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
