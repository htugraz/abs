# $Id: PKGBUILD 116077 2014-07-22 00:54:22Z speps $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=nomacs
pkgver=2.0.0
pkgrel=1
pkgdesc="A Qt image viewer"
arch=(i686 x86_64)
url="http://www.nomacs.org/"
license=('GPL3')
depends=('qt4' 'exiv2' 'libraw' 'opencv')
makedepends=('cmake')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver-source.tar.bz2")
md5sums=('9c666fa1bb1efe814fc70d1df810b9fd')

prepare() {
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
