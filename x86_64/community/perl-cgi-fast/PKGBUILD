# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cgi-fast
pkgver=2.02
pkgrel=1
pkgdesc='CGI Interface for Fast CGI'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' 'perl-cgi>=4' 'perl-fcgi>=0.67')
checkdepends=('perl-test-deep>=0.11')
url=https://metacpan.org/release/CGI-Fast
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEEJO/CGI-Fast-$pkgver.tar.gz")
md5sums=(779a5152f44cab2e3e3856459837fe78)
sha512sums=(558113b2bcbfa9d2915b12e882f9f305f96c0da5f6f6c4eb5e68bbb4ce3871f368a74927a9348f92b0d80a5cc857c4e5eb7228f962b1abcb87a155f8b18e5d5b)
_ddir="CGI-Fast-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
