# $Id: PKGBUILD 128638 2015-03-03 15:35:48Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=ser2net
pkgver=2.10.0
pkgrel=2
pkgdesc="A proxy that allows telnet/tcp connections to be made to serial ports"
arch=(i686 x86_64)
url="http://sourceforge.net/projects/ser2net"
license=('GPL')
depends=('glibc')
backup=(etc/ser2net.conf)
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
	ser2net.service)
md5sums=('cd937041144de83d41d811521e72158c'
         '137032ba9927b7c4ef41facebfa727b8')

build() {
  cd $srcdir/${pkgname}-${pkgver/rc/-rc}
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd $srcdir/${pkgname}-${pkgver/rc/-rc}
  make DESTDIR=$pkgdir install
  install -Dm0644 ser2net.conf $pkgdir/etc/ser2net.conf
  install -Dm0644 $srcdir/ser2net.service $pkgdir/usr/lib/systemd/system/ser2net.service
}
