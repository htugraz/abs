# $Id: PKGBUILD 100158 2013-11-01 20:25:26Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Kalidarn

pkgname=freeradius-client
pkgver=1.1.6
pkgrel=6
pkgdesc="FreeRADIUS Client Software"
arch=('i686' 'x86_64')
url="http://wiki.freeradius.org/Radiusclient"
license=('BSD')
depends=('sh')
source=(ftp://ftp.freeradius.org/pub/radius/$pkgname-$pkgver.tar.bz2)
md5sums=('2e46564e450ae13aedb70dc133b158ac')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m0644 COPYRIGHT $pkgdir/usr/share/licenses/$pkgname/license
  mv $pkgdir/etc/radiusclient $pkgdir/etc/radiusclient.default
}
