# $Id: PKGBUILD 97844 2013-09-30 10:04:25Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=xboard
pkgver=4.7.2
pkgrel=3
pkgdesc="Graphical user interfaces for chess"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/xboard/"
license=('GPL3')
depends=('gnuchess' 'texinfo' 'xdg-utils'
	 'xorg-fonts-75dpi' 'xorg-fonts-100dpi'
	 'gtk2' 'cairo' 'librsvg')
optdepends=('perl')
install=${pkgname}.install
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('83ec086cbf5c4bc362a188654a9332fc')

build() {
  cd "${pkgname}-${pkgver}"
  sed -i 's/fairymax/gnuchess/g' {xaw,gtk}/xboard.h xboard.conf.in
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --with-gtk \
    --enable-zippy
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm755 ${srcdir}/${pkgname}-${pkgver}/cmail \
    ${pkgdir}/usr/bin/cmail
}
