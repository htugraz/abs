# $Id: PKGBUILD 123401 2014-12-03 12:28:05Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=bspwm
pkgver=0.8.9
pkgrel=4
pkgdesc='Tiling window manager based on binary space partitioning'
arch=('x86_64' 'i686')
url='https://github.com/baskerville/bspwm'
license=('custom:BSD')
depends=('libxcb' 'xcb-util' 'xcb-util-wm')
optdepends=('sxhkd: to define keyboard and pointer bindings'
            'xtitle-git: for the example panel'
	    'sutils-git: for the example panel'
	    'bar-aint-recursive: for the example panel'
	    'terminus-font: for the example panel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/baskerville/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('750c76132914661d8d5edf7809e9b601977215d31e747dd780c60fd562913d55')

build() {
  make -C "$pkgname-$pkgver" PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
