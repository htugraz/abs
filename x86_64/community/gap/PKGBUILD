# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgbase=gap
pkgname=('gap' 'gap-packages')
pkgver=4.7.6
pkgrel=3
pkgdesc="GAP: a system for computational discrete algebra"
arch=('i686' 'x86_64')
url="http://www.gap-system.org/"
license=('GPL')
source=("http://www.gap-system.org/pub/gap/gap47/tar.bz2/gap4r7p6_2014_11_15-20_02.tar.bz2")
sha256sums=('cfcc6efb6ea79a38efad6031eb8bedbc6474f47dc32c53bd7bada2febdb32b4a')

build() {
  cd gap4r7
  ./configure --prefix=/usr --with-gmp=system
  make
}

package_gap() {
  depends=('gmp')
  optdepends=('gap-packages: extra packages')
  replaces=('gap-math')
  conflicts=('gap-math')
  cd gap4r7
  
  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r * "$pkgdir"/usr/lib/gap

# move pkg to gap-packages, except for GAPDoc
  mv "$pkgdir"/usr/lib/gap/pkg{,0}
  mkdir "$pkgdir"/usr/lib/gap/pkg
  mv "$pkgdir"/usr/lib/gap/pkg0/GAPDoc-* "$pkgdir"/usr/lib/gap/pkg
  rm -r "$pkgdir"/usr/lib/gap/pkg0

# remove leftover source files
  rm -r "$pkgdir"/usr/lib/gap/extern

  mkdir -p "$pkgdir"/usr/bin
  ln -s /usr/lib/gap/bin/gap.sh "$pkgdir"/usr/bin/gap

# fix location
  sed -e 's|/build/gap/src/gap4r7|/usr/lib/gap|' -i "$pkgdir"/usr/lib/gap/bin/gap.sh  
}

package_gap-packages() {
  depends=('gap')
  pkgdesc="Extra packages for GAP"
  cd gap4r7

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r pkg "$pkgdir"/usr/lib/gap

# GAPDoc is in main package
  rm -r "$pkgdir"/usr/lib/gap/pkg/GAPDoc-*
}
