# $Id: PKGBUILD 138196 2015-08-11 05:55:02Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-betamax
pkgname=('python-betamax' 'python2-betamax')
pkgver=0.5.0
pkgrel=1
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"z
arch=('any')
license=('GPL')
url='https://github.com/sigmavirus24/betamax'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python2-mock')
source=("git+https://github.com/sigmavirus24/betamax.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a betamax{,-py2}
}

check() {
  cd "$srcdir/betamax"
  python setup.py ptr

  cd "$srcdir/betamax-py2"
  python2 setup.py ptr
}

package_python-betamax() {
  depends=('python-requests')

  cd "${srcdir}/betamax"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-betamax() {
  depends=('python2-requests')

  cd "${srcdir}/betamax-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
