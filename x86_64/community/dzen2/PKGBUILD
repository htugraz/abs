# $Id: PKGBUILD 107017 2014-03-11 11:33:34Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Mark Taylor <skymt0@gmail.com>

pkgname=dzen2
pkgver=0.9.5git
pkgrel=2
arch=(i686 x86_64)
pkgdesc="X notification utility"
url="https://github.com/robm/dzen"
license=("MIT")
depends=('libx11' 'libxpm' 'libxinerama' 'libxft')
makedepends=('gcc' 'git')
source=("$pkgname-$pkgver::git://github.com/robm/dzen")
md5sums=('SKIP')

build() {
  cd $srcdir/$pkgname-$pkgver

  CFLAGS=" -Wall -Os ${INCS} -DVERSION=\"${VERSION}\" -DDZEN_XINERAMA -DDZEN_XPM -DDZEN_XFT `pkg-config --cflags xft`"
  LIBS=" -L/usr/lib -lc -lXft -lXpm -lXinerama -lX11"

  echo "CFLAGS=$CFLAGS" >>config.mk
  echo "LIBS=$LIBS" >>config.mk
  echo "LDFLAGS=$LIBS" >>config.mk

  make X11INC=/usr/include X11LIB=/usr/lib
  (cd gadgets && make X11INC=/usr/include X11LIB=/usr/lib)
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr MANPREFIX=/usr/man DESTDIR=$pkgdir install
  (cd gadgets && make PREFIX=/usr MANPREFIX=/usr/man DESTDIR=$pkgdir install)
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/dzen2/COPYING
}
