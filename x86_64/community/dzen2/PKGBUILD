# $Id: PKGBUILD 118860 2014-09-12 12:13:20Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Mark Taylor <skymt0@gmail.com>

pkgname=dzen2
pkgver=0.9.5git
pkgrel=4
arch=(i686 x86_64)
pkgdesc="X notification utility"
url="https://github.com/robm/dzen"
license=("MIT")
depends=('libx11' 'libxpm' 'libxinerama' 'libxft')
makedepends=('gcc' 'git')
source=("$pkgname-$pkgver::git://github.com/robm/dzen")
md5sums=('SKIP')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's|DZEN_XMP|DZEN_XPM|g' main.c

  CFLAGS="$CFLAGS -Wall -Os \${INCS} -DVERSION=\\\"\${VERSION}\\\" -DDZEN_XINERAMA -DDZEN_XPM -DDZEN_XFT `pkg-config --cflags xft`"
  LIBS=" -L/usr/lib -lc -lXft -lXpm -lXinerama -lX11"

  echo "CFLAGS=$CFLAGS" >>config.mk
  echo "LIBS=$LIBS" >>config.mk
  echo "LDFLAGS=$LDFLAGS $LIBS" >>config.mk
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include X11LIB=/usr/lib
  (cd gadgets && make X11INC=/usr/include X11LIB=/usr/lib)
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr MANPREFIX=/usr/man DESTDIR=$pkgdir install
  (cd gadgets && make PREFIX=/usr MANPREFIX=/usr/man DESTDIR=$pkgdir install)
  install -Dm0644 LICENSE $pkgdir/usr/share/licenses/dzen2/COPYING

  install -dm0644 $pkgdir/usr/share/doc/dzen2
  install -m0644 README* $pkgdir/usr/share/doc/dzen2/
}
