# $Id: PKGBUILD 138470 2015-08-18 07:33:25Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
pkgver=1.6.0
pkgrel=1
pkgdesc='All the stuff that is not good/big enough for a real library'
arch=('any')
url='https://github.com/flori/tins'
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/tins-${pkgver}.gem")
noextract=("tins-${pkgver}.gem")
sha256sums=('1f82e41851f343147e8acda8b02a67e990e38d4755e1e7a8de7cd913fcce0a0b')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} tins-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/ruby-tins
  ln -s ${_gemdir}/gems/tins-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/ruby-tins/
}

# vim: ts=2 sw=2 et:
