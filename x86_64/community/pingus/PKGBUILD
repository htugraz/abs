# $Id: PKGBUILD 104769 2014-01-25 18:55:45Z lcarlier $
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=pingus
pkgver=0.7.6
pkgrel=11
pkgdesc="A Lemmings clone, i.e. a level-based puzzle game."
arch=('i686' 'x86_64')
url="http://pingus.seul.org"
license=('GPL')
depends=('sdl_image' 'sdl_mixer' 'libgl' 'boost-libs>=1.49')
makedepends=('scons' 'boost>=1.49' 'mesa' 'glu')
source=("http://pingus.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2"
        'pingus.desktop'
        'pingus-0.7.6-gcc470-udl.patch')
sha1sums=('b5f5a25d71beb197c9466fb8928018a377f56487'
          '579a1144f161ce89e6e024cea37210149b89c0c0'
          '143dd9969e8d100d29ed4f5b2039dde627afeb06')

build() {
  cd ${pkgname}-${pkgver}

  # fedora patch to fix error due to new standards
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=51282
  patch -Np0 -i ${srcdir}/pingus-0.7.6-gcc470-udl.patch
  # manpage must be in man6, not man1
  sed -i "s#/man1/#/man6/#g" Makefile

  scons prefix=/usr
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR="${pkgdir}" PREFIX="/usr"

  install -D -m644 "${srcdir}/pingus.desktop" "${pkgdir}/usr/share/applications/pingus.desktop"
}
