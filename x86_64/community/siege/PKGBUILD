# $Id: PKGBUILD 139938 2015-09-11 22:23:36Z bluewind $
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Sven Kauber <celeon@gmail.com>

pkgname=siege
pkgver=3.1.1
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="An http regression testing and benchmarking utility"
url="http://www.joedog.org/JoeDog/Siege"
depends=('openssl')
source=("http://download.joedog.org/siege/siege-${pkgver}.tar.gz")
backup=(etc/siegerc etc/urls.txt)

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  install -d -m755 "$pkgdir/etc"
  install -d -m755 "$pkgdir/usr/share"
  make DESTDIR="$pkgdir" install
  sed -i 's/^# file =$/file = \/etc\/urls.txt/' "$pkgdir/etc/siegerc"
}

md5sums=('800b34cf0dbeda177196583588a2e731')
