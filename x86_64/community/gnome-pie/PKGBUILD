# $Id: PKGBUILD 132696 2015-05-04 10:09:15Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-pie
_pkgname=Gnome-Pie
pkgver=0.6.2
pkgrel=1
pkgdesc="Circular application launcher for GNOME"
arch=('i686' 'x86_64')
url="http://gnome-pie.simonschneegans.de/"
license=('GPL')
depends=('bamf' 'libgee' 'gnome-menus')
makedepends=('cmake' 'vala')
install=$pkgname.install
source=($_pkgname-$pkgver.tar.gz::https://github.com/Simmesimme/$_pkgname/archive/v$pkgver.tar.gz)
md5sums=('e733bdf0670726221ac9eab1256a7e5d')

build() {
  cd $_pkgname-$pkgver
  [[ -d build ]] || mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd $_pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
