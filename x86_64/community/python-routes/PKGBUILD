# $Id: PKGBUILD 137260 2015-07-22 06:17:10Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgbase=python-routes
pkgname=(python-routes python2-routes)
pkgver=2.2
pkgrel=1
pkgdesc="Routing recognition and generation tools"
arch=("any")
url="https://pypi.python.org/pypi/Routes"
license=("custom:BSD")
makedepends=('python-setuptools' 'python2-setuptools' 'python-repoze.lru' 'python2-repoze.lru' 'python-six' 'python2-six')
checkdepends=('python-nose' 'python2-nose' 'python-webob' 'python2-webob' 'python-coverage' 'python2-coverage')
source=("https://pypi.python.org/packages/source/R/Routes/Routes-${pkgver}.tar.gz")
md5sums=('d62bb225ba7919b5470095528932648b')

prepare() {
  cp -a Routes-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/Routes-${pkgver}/"
  python setup.py build

  cd "${srcdir}/Routes-${pkgver}-py2/"
  python2 setup.py build
}

check() {
  cd "${srcdir}/Routes-${pkgver}/"
  nosetests3

  cd "${srcdir}/Routes-${pkgver}-py2/"
  nosetests2
}

package_python-routes() {
  depends=("python-repoze.lru" 'python-six')

  cd "${srcdir}/Routes-${pkgver}/"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-routes() {
  depends=("python2-repoze.lru" 'python2-six')

  cd "${srcdir}/Routes-${pkgver}-py2/"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
