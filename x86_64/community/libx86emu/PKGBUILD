# $Id: PKGBUILD 111085 2014-05-13 12:20:38Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=libx86emu
pkgver=1.1.21.6
pkgrel=1
pkgdesc='x86 emulation library'
arch=('x86_64' 'i686')
url='http://download.opensuse.org/source/factory/repo/oss/suse/src/'
license=('BSD')
depends=('glibc')
makedepends=('rpmextract')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/$pkgname-${pkgver%.*.*}-${pkgver#*.*.}.src.rpm")
sha256sums=('220465fd4380c1a91dbd394a67301b409efaa255af54a33ddfc0d4fc9f583582')

prepare() {
  # Wish they would just provide a normal $pkgname-$pkgver.tar.gz...
  rpmextract.sh "${pkgname}-${pkgver%.*.*}-${pkgver#*.*.}.src.rpm"
  tar jxf "$pkgname-${pkgver%.*.*}.tar.bz2"
}

build() {
  make -C "$pkgname-${pkgver%.*.*}" LIBDIR=/usr/lib
}

package() {
  make -C "$pkgname-${pkgver%.*.*}" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname-${pkgver%.*.*}/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
