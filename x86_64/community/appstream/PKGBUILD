# $Id: PKGBUILD 143996 2015-10-14 18:51:41Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.8.5
pkgrel=1
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(i686 x86_64)
url="http://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(xapian-core polkit libyaml libxml2 protobuf)
makedepends=(cmake xmlto intltool gobject-introspection python2 docbook-xsl qt5-base)
source=("http://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz")
sha256sums=('d7fdc9dbef7450ddec23dd5bb577103f0469a53dbcf6d3a61e61fa73f1f46155')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../AppStream-$pkgver -DQT=ON -DAPPSTREAM_QT_VERSION=5 -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package_appstream() {
  cd build

  make DESTDIR="$pkgdir" install
  
# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppstreamQt,lib/cmake,lib/libAppstreamQt.*}
}

package_appstream-qt() {
  pkgdesc=('Qt5 interface for AppStream')
  depends=(appstream qt5-base)

  cd build/qt
  make DESTDIR="$pkgdir" install
}
