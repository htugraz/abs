# $Id: PKGBUILD 127866 2015-02-17 19:20:58Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Xilon <xilonmu@gmail.com>

pkgname=powertop
pkgver=2.7
pkgrel=2
pkgdesc='A tool to diagnose issues with power consumption and power management'
arch=('i686' 'x86_64')
url='https://01.org/powertop/'
license=('GPL2')
depends=('gcc-libs' 'libnl' 'ncurses' 'pciutils')
changelog=$pkgname.changelog
source=(https://01.org/sites/default/files/downloads/powertop/$pkgname-$pkgver.tar.gz
        $pkgname-autotune.patch)
sha256sums=('8d4b1490e2baad4467c0ded3c423db4472dcbf7b2dd8f8f2a928f54047c678ca'
            'ff1ae08afb4ab611d7782de8d2ea7398b8d8289e7f45f21b7327317157fa5e46')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  patch -Np1 -i "${srcdir}"/$pkgname-autotune.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

#  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
