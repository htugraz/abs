# $Id: PKGBUILD 121579 2014-10-29 18:33:38Z speps $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=firefox-firebug
pkgver=2.0.4
pkgrel=1
pkgdesc="Firefox extension with tools for web development."
arch=(any)
url="http://getfirebug.com/"
license=('custom:BSD')
groups=('firefox-addons')
depends=('firefox')
provides=('firebug')
source=("${url}releases/firebug/${pkgver%.*}/firebug-$pkgver.xpi")
md5sums=('0cce65acf14629a003424b9b7ff1b06f')

package() {
  # extension
  _dest="$pkgdir/usr/lib/firefox/browser/extensions/firebug@software.joehewitt.com"
  find . -type d -exec install -d {} $_dest/{} \;
  find . -type f -exec install -Dm644 {} $_dest/{} \;

  # license
  install -Dm644 license.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
