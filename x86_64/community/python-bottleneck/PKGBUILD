# $Id: PKGBUILD 142403 2015-10-01 16:04:00Z fyan $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Michael Schubert <mschu.dev at gmail>
# Contributor: Panagiotis Mavrogiorgos (pmav99) <> (gmail)

pkgbase=python-bottleneck
pkgname=('python2-bottleneck' 'python-bottleneck')
pkgver=1.0.0
pkgrel=3
pkgdesc="Fast NumPy array functions written in Cython"
arch=('i686' 'x86_64')
url="http://berkeleyanalytics.com/bottleneck/"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools' 'python-numpy' 'python2-numpy')
source=("https://pypi.python.org/packages/source/B/Bottleneck/Bottleneck-${pkgver}.tar.gz")
md5sums=('380fa6f275bd24f27e7cf0e0d752f5d2')

build() {
  cd "$srcdir"
  cp -a Bottleneck-${pkgver} Bottleneck-py2-${pkgver}

  msg "Building Python2"
  cd "$srcdir"/Bottleneck-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/Bottleneck-${pkgver}
  python setup.py build
}

package_python2-bottleneck() {
  depends=('python2-numpy')
  optdepends=('python2-nose: needed for integrated test suite'
              'python2-scipy: needed for integrated benchmarks')

  cd "$srcdir"/Bottleneck-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-bottleneck() {
  depends=('python-numpy')
  optdepends=('python-nose: needed for integrated test suite'
              'python-scipy: needed for integrated benchmarks')

  cd "$srcdir"/Bottleneck-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

