# $Id: PKGBUILD 109977 2014-04-22 16:20:55Z spupykin $
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>

pkgname=dia
pkgver=0.97.2
pkgrel=6
pkgdesc="A GTK+ based diagram creation program"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/Dia"
install=dia.install
depends=('libxslt' 'desktop-file-utils' 'libart-lgpl' 'gtk2' 'hicolor-icon-theme')
makedepends=('intltool' 'python2' 'docbook-xsl')
optdepends=('python2')
options=('docs' '!emptydirs')
source=("ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.97/${pkgname}-${pkgver}.tar.xz"
	"dia-crash.patch::https://git.gnome.org/browse/dia/patch/?id=213bdfe956bf8fe57c86316f68a09408fef1647e")
md5sums=('1e1180a513fb567709b09bc19f12105e'
         '9ae7a0f48498962240208b9f7c86b1ff')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  for file in `find -type f -name '*.py'`; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' "$file"
  done
  sed -i 's#python2\.1#python2 python2.1#' configure
  patch -p1 <$srcdir/dia-crash.patch
  sed -i 's|#include <freetype/|#include <freetype2/|g' plug-ins/postscript/diapsft2renderer.c
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr \
    --with-cairo \
    --with-python \
    --disable-gnome \
    --with-hardbooks
  sed -i 's#SUBDIRS = lib objects plug-ins shapes app bindings samples po sheets data doc tests installer#SUBDIRS = lib objects plug-ins shapes app bindings samples po sheets data tests installer#' Makefile
  make
  cd doc
  make html
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  cd doc
  make DESTDIR="${pkgdir}" install-html
  ln -sf ../doc/dia/html "${pkgdir}"/usr/share/dia/help
}
