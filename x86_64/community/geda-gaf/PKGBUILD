# $Id: PKGBUILD 126157 2015-01-18 21:22:20Z eric $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jared Casper <jaredcasper@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=geda-gaf
pkgver=1.8.2
pkgrel=1
pkgdesc="gEDA/gaf suite - Contains gschem, gnetlist, gsymcheck, gattrib, utilities and documentation from the gEDA project"
arch=('i686' 'x86_64')
url="http://geda-project.org/"
license=('GPL')
depends=('gtk2' 'guile' 'libstroke' 'shared-mime-info' 'hicolor-icon-theme')
makedepends=('pkg-config' 'perl-xml-parser' 'flex' 'gawk')
optdepends=('python2: for two of the commands (garchive, tragesym)'
  'gawk: for sw2asc')
replaces=('geda-suite' 'geda-libs')
install=geda-gaf.install
source=(http://ftp.geda-project.org/geda-gaf/stable/v1.8/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('fa8a807615decf47027e22a217dd0ded')

build ()
{
  cd "$srcdir/$pkgname-$pkgver/"
  ./configure --prefix=/usr --disable-update-xdg-database 
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver/"
  make DESTDIR="$pkgdir/" install 
  sed -i 's+/usr/bin/env python+/usr/bin/env python2+' \
    "$pkgdir/usr/bin/garchive"
  sed -i 's+/usr/bin/python+/usr/bin/python2+' \
    "$pkgdir/usr/bin/tragesym"
  sed -i 's+/usr/bin/python+/usr/bin/python2+' \
    "$pkgdir/usr/share/doc/$pkgname/examples/lightning_detector/bom"
}

