# $Id: PKGBUILD 111089 2014-05-13 13:23:00Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ner0

pkgname=activity-log-manager
pkgver=0.9.7
pkgrel=2
pkgdesc="A graphical user interface which lets you easily control what gets logged by Zeitgeist"
arch=('i686' 'x86_64')
url="https://launchpad.net/activity-log-manager"
license=('GPL2')
depends=('gtk3' 'libgee06' 'zeitgeist' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('vala' 'intltool')
install=$pkgname.install
source=(https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130725.tar.gz
        fix-build.patch)
md5sums=('d886be32db0fa351aaf0eb59912ee377'
         'f7a284bca152011500cb845aebad8d14'
         '243c71b429e12172691dcb748055247d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  # Regenerate C files
  rm src/activity_log_manager_vala.stamp

  # Fix build
  patch -Np1 -i ../fix-build.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
