# $Id: PKGBUILD 143729 2015-10-12 02:11:22Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgbase='python-webob'
pkgname=('python-webob' 'python2-webob' 'python-webob-docs')
pkgver=1.5.0
pkgrel=1
pkgdesc="WSGI request and response object"
arch=('any')
url="http://pythonpaste.org/webob/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-sphinx')
checkdepends=('python-nose' 'python2-nose')
optdepends=('python-webob-docs: documentation')
source=("http://pypi.python.org/packages/source/W/WebOb/WebOb-${pkgver}.tar.gz")
md5sums=('dff7f401d9a7fac47b160c36a6d62e63')

prepare() {
  cp -a "$srcdir/WebOb-${pkgver}"{,-py2}
  cp -a "$srcdir/WebOb-${pkgver}"{,-docs}
}

build() {
  cd "$srcdir/WebOb-${pkgver}"
  python setup.py build

  cd "$srcdir/WebOb-${pkgver}-py2"
  python2 setup.py build

  cd "$srcdir/WebOb-${pkgver}-docs"
  python setup.py build_sphinx
}

check() {
  cd "$srcdir/WebOb-${pkgver}"
  python setup.py test

  cd "$srcdir/WebOb-${pkgver}-py2"
  python2 setup.py test
}

package_python-webob() {
  depends=('python')

  cd "$srcdir/WebOb-${pkgver}"
  python setup.py install -O1 --root="$pkgdir"
  install -D -m644 "docs/license.txt" \
                   "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

package_python2-webob() {
  depends=('python2')

  cd "$srcdir/WebOb-${pkgver}-py2"
  python2 setup.py install -O1 --root="$pkgdir"
  install -D -m644 "docs/license.txt" \
                   "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

package_python-webob-docs() {
  optdepends=()

  cd "$srcdir/WebOb-${pkgver}-docs"
  _doc_dir="$pkgdir/usr/share/docs/python-webob"
  mkdir -p "$_doc_dir"
  cp -a "build/sphinx/html" "$_doc_dir"
  install -D -m644 "docs/license.txt" \
                   "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
