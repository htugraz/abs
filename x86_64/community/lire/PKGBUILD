# $Id: PKGBUILD 91514 2013-05-23 11:14:10Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=lire
pkgver=2.1.1
pkgrel=2
pkgdesc="log file analyzer"
arch=(any)
url="http://www.logreport.org/"
license=('GPL')
depends=('perl-dbd-sqlite' 'perl-libintl-perl' 'perl-curses-ui' 'perl-curses' 'perl-xml-parser')
makedepends=('postfix')
backup=(etc/lire/address.cf
	etc/lire/defaults
	etc/lire/disclaimer
	etc/lire/explanation
	etc/lire/profile_lean
	etc/lire/signature)
options=(zipman docs)
source=(http://download.logreport.org/pub/lire-$pkgver.tar.gz)
md5sums=('075949a72e7d0e7e133dcfe2e04eec59')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--with-spooldir=/var/spool/lire  --libexecdir=/usr/lib/ --sbindir=/usr/bin
  sed -i 's|$(dtd_infos)/%:|$(dtd_infos):|' doc/Makefile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
