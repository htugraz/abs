# $Id: PKGBUILD 124976 2014-12-30 18:19:36Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason R Begley (jayray@digitalgoat.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=freeradius
pkgver=3.0.6
pkgrel=1
pkgdesc="The premier open source RADIUS server"
arch=('i686' 'x86_64')
url="http://www.freeradius.org/"
license=('GPL')
depends=('krb5' 'pth' 'net-snmp' 'postgresql-libs' 'libmariadbclient' 'talloc' 'libpcap')
makedepends=('unixodbc' 'python2' 'curl')
optdepends=('unixodbc: for ODBC'
	    'python2: for Python'
	    'curl: for REST')
options=('!makeflags')
install=$pkgname.install
source=("ftp://ftp.freeradius.org/pub/radius/freeradius-server-$pkgver.tar.bz2"
	freeradius.tmpfiles
        freeradius.service
        "https://github.com/FreeRADIUS/freeradius-server/commit/6d296f2c4f3c58742543cc0508642c6d06747aea.patch")
md5sums=('579d553049c53b4d4e9af88745ed367a'
         'f959e89812bedfc9f8308076f78cd74e'
         'e3f18e3a25df3b692e59f60605354708'
         'a51da2299e4bb1979ad6fc54683f173e')

prepare() {
  cd $srcdir/freeradius-server-$pkgver
#  patch -p1 <$srcdir/6d296f2c4f3c58742543cc0508642c6d06747aea.patch
}

build() {
  cd $srcdir/freeradius-server-$pkgver
  ./configure --with-system-libtool --with-system-libltdl \
	      --prefix=/usr --enable-heimdal-krb5 \
	      --localstatedir=/var \
	      --sysconfdir=/etc \
	      --sbindir=/usr/bin \
	      --libdir=/usr/lib/freeradius \
	      --with-udpfromto
  make
}

package() {
  cd $srcdir/freeradius-server-$pkgver
  make install R=$pkgdir
  chmod o+r $pkgdir/etc/raddb/*
  mv $pkgdir/etc/raddb $pkgdir/etc/raddb.default
  rm -rf $pkgdir/var/run

  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  install -Dm0644 $srcdir/$pkgname.tmpfiles $pkgdir/usr/lib/tmpfiles.d/$pkgname.conf
}
