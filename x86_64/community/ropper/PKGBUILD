# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=ropper
_pkgname=Ropper
pkgver=1.7.2
pkgrel=1
pkgdesc="Show information about binary files and find gadgets to build rop chains for different architectures"
url="https://github.com/sashs/Ropper"
arch=('any')
license=('GPL2')
depends=('python-capstone')
optdepends=('sqlite: gadgets database support')
makedepends=('python-setuptools')
checkdepends=('sqlite')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sashs/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('6ba6fb8de35ba73ff54b2eb469487cf05129641220b34d263f23fbd42d3d93eae4c00d25c471632839a00383ea4049a6e6f1de984b7b6e931c3b335c1f736070')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python test.py
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 sample.py "${pkgdir}/usr/share/doc/${pkgname}/sample.py"
}

# vim: ts=2 sw=2 et:
