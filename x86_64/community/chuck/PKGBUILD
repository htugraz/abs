# $Id: PKGBUILD 109394 2014-04-14 11:34:22Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=chuck
pkgver=1.3.4.0
pkgrel=1
pkgdesc='Concurrent, on-the-fly audio programming language'
arch=('x86_64' 'i686')
url='http://chuck.cs.princeton.edu/'
license=('GPL')
  depends=('gcc-libs' 'libsndfile' 'jack')
source=("http://chuck.cs.princeton.edu/release/files/$pkgname-$pkgver.tgz")
sha256sums=('b65ee7c512192c732d638777fa1e1d885b95f315f3f36e059c168518169a8b33')

build() {
  # Other build alternatives are: linux-alsa linux-oss osx win32
  make -C "$pkgname-$pkgver/src" linux-jack
}

package() {
  cd "$pkgname-$pkgver/src"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r ../examples "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
