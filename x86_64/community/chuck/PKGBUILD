# $Id: PKGBUILD 129855 2015-03-24 16:44:31Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=chuck
pkgver=1.3.5.0
pkgrel=1
pkgdesc='Concurrent, on-the-fly audio programming language'
arch=('x86_64' 'i686')
url='http://chuck.cs.princeton.edu/'
license=('GPL')
  depends=('gcc-libs' 'libsndfile' 'jack')
source=("http://chuck.cs.princeton.edu/release/files/$pkgname-$pkgver.tgz")
sha256sums=('658e361ceb2ef263c38432e8182664dfe7bf0473fc4580a392a4326b66a14266')

build() {
  # Other build alternatives are: linux-alsa linux-oss osx win32
  make -C "$pkgname-$pkgver/src" linux-jack
}

package() {
  cd "$pkgname-$pkgver/src"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r ../examples "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
