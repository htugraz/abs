# $Id: PKGBUILD 144919 2015-10-24 16:29:02Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.br>

pkgbase=python-pyro
pkgname=("$pkgbase" 'python2-pyro')
pkgver=4.40
pkgrel=1
pkgdesc='Distributed object middleware for Python (RPC)'
url='http://pypi.python.org/pypi/Pyro4/'
arch=('any')
license=('MIT')
depends=('python' 'python2')
source=("https://pypi.python.org/packages/source/P/Pyro4/Pyro4-$pkgver.tar.gz")
sha256sums=('00423d3710f60b2da146075a59e17bfa837f556ed2c8acafe05bc209dcaac3e9')

package_python-pyro() {
  depends=('python')
  cd "Pyro4-$pkgver"
  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyro() {
  depends=('python2')
  cd "Pyro4-$pkgver"
  python2 setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
