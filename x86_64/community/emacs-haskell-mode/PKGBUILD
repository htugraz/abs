# $Id: PKGBUILD 104252 2014-01-16 18:10:58Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gleidson <gleidson.echeli@gmail.com>

pkgname=emacs-haskell-mode
pkgver=13.10
pkgrel=1
pkgdesc="Haskell mode package for Emacs"
arch=(any)
license=('GPL')
url="https://github.com/haskell/haskell-mode"
install=emacs-haskell-mode.install
makedepends=('emacs')
source=($pkgname-$pkgver.tar.gz::https://github.com/haskell/haskell-mode/archive/v$pkgver.tar.gz)
md5sums=('44326d23f0211f8f7f0959177839f449')

build() {
  cd $srcdir/haskell-mode-$pkgver
  make
}

package() {
  cd $srcdir/haskell-mode-$pkgver
  install -dm0755 $pkgdir/usr/share/emacs/site-lisp/haskell-mode
  install -dm0755 $pkgdir/usr/share/doc/$pkgname
  install -m0644 *.el{,c} -t $pkgdir/usr/share/emacs/site-lisp/haskell-mode
  install -m0644 NEWS README.md $pkgdir/usr/share/doc/$pkgname
  install -Dm0644 haskell-mode.info $pkgdir/usr/share/info/haskell-mode.info
  cp -a examples $pkgdir/usr/share/doc/$pkgname/
}
