# $Id: PKGBUILD 108919 2014-04-07 16:09:08Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libuv
pkgver=0.11.23
pkgrel=1
pkgdesc="A new platform layer for Node.JS"
arch=('i686' 'x86_64')
url="https://github.com/joyent/libuv"
license=('custom')
depends=('glibc')
source=("https://github.com/joyent/libuv/archive/v$pkgver.tar.gz")

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./autogen.sh
	./configure --prefix=/usr
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 AUTHORS \
		"$pkgdir/usr/share/doc/$pkgname/AUTHORS"
	install -Dm644 README.md \
		"$pkgdir/usr/share/doc/$pkgname/README.md"
	install -Dm644 ChangeLog \
		"$pkgdir/usr/share/doc/$pkgname/ChangeLog"
}

sha256sums=('041beb3cc3ae6791e7469984324b4c7dd18001f9be82eac617f244dfbb332e39')
