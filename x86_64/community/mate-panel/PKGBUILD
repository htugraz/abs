# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=mate-panel
pkgname=${_pkgbase}
pkgver=${_ver}.1
pkgrel=1
pkgdesc="The MATE Panel (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'gtk2' 'libwnck' 'libcanberra' 'libice'
         'libmateweather>=1.10' 'librsvg' 'libsm' 'libsoup' 'libxau'
         'mate-menus>=1.10' 'mate-desktop>=1.10')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
groups=('mate')
conflicts=("${_pkgbase}-gtk3")
sha1sums=('e82156c5b732c22779ffcb03bbbcd3c01e3ce146')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
