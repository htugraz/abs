# $Id:
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Sauyon Lee <sauyonl@sauyon.com>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: korjjj <korjjj+aur@gmail.com>

pkgname=syncthing
pkgver=0.9.13
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing"
url="http://syncthing.net/"
license=('MIT')
arch=('i686' 'x86_64')
depends=('glibc')
makedepends=('git' 'go' 'godep' 'inetutils' 'sed')
source=("$pkgname-$pkgver::git+https://github.com/syncthing/syncthing.git#tag=v${pkgver}"
        "syncthing@.service"
        "syncthing.1")
sha1sums=('SKIP'
          '204cad3823306c793a14a17a23220e26199160d4'
          '17be8b3f99c577e325765abc251f597e204d7711')
install=${pkgname}.install

prepare() {
    cd "${srcdir}"
    mkdir -p "src/github.com/syncthing"
    mv "${pkgname}-${pkgver}" "src/github.com/syncthing/${pkgname}"
}

build() {
    export GOPATH="${srcdir}"
    cd "${srcdir}/src/github.com/syncthing/${pkgname}"
    ./build.sh "" -tags noupgrade
}

check() {
    export GOPATH="${srcdir}"
    cd "${srcdir}/src/github.com/syncthing/${pkgname}"
    ./build.sh test
}

package() {
    cd "${srcdir}/src/github.com/syncthing/${pkgname}"
    for FILE in bin/*; do
        BASE_FILE=$(basename ${FILE})
        install -D -m 755 bin/${BASE_FILE} "${pkgdir}/usr/bin/${BASE_FILE}"
    done
    install -D -m 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -D -m 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
    install -D -m 644 "${srcdir}/${pkgname}@.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}@.service"
    install -D -m 644 "${srcdir}/${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
