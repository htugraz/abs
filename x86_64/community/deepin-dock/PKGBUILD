# $Id: PKGBUILD 147625 2015-11-24 07:28:40Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=2.90.1
pkgrel=2
pkgdesc='Deepin desktop-environment - dock module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('gtk2' 'libdui' 'qt5-svg')
makedepends=('git')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-dock.git#tag=$pkgver")
sha256sums=('SKIP')

build(){
  cd dde-dock
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-dock
  make INSTALL_ROOT="${pkgdir}" install
}
