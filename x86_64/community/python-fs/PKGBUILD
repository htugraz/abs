# $Id: PKGBUILD 146809 2015-11-14 12:44:27Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fs
pkgname=('python-fs' 'python2-fs')
pkgver=0.5.4
pkgrel=1
pkgdesc="Filesystem abstraction layer"
arch=('any')
license=('custom:BSD')
url="http://pypi.python.org/pypi/fs/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'fuse' 'python2-paramiko')
source=("https://pypi.python.org/packages/source/f/fs/fs-$pkgver.tar.gz")
sha512sums=('d67c85f2816cf1a84c25664a6cf71dac551c3eec4eb34154bc92d52eb0efa9739adca6d0ec86b9978ff6af408cfa17b913ee8fd4c143cd2adf862ca0a1dd4c5d')

prepare() {
  cp -a fs-$pkgver{,-py2}
}

build() {
  cd "$srcdir/fs-$pkgver"
  python setup.py build

  cd "$srcdir/fs-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/fs-$pkgver"
  LC_CTYPE=en_US.utf8 python setup.py ptr || warning "Tests failed"

  cd "$srcdir/fs-$pkgver-py2"
  LC_CTYPE=en_US.utf8 python2 setup.py ptr || warning "Tests failed"
}

package_python-fs() {
  depends=('python-setuptools' 'python-six')

  cd fs-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-fs() {
  depends=('python2-setuptools' 'python2-six')

  cd fs-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1

  for f in fscat fscp fsinfo fsls fsmkdir fsmount fsmv fsrm fsserve fstree; do
    mv "$pkgdir/usr/bin/$f"{,2}
  done
}
