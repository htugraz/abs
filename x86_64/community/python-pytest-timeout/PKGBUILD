# $Id: PKGBUILD 142552 2015-10-01 16:08:51Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-timeout
pkgname=('python-pytest-timeout' 'python2-pytest-timeout')
_pypiname=pytest-timeout
pkgver=0.5
pkgrel=3
pkgdesc='py.test plugin to abort hanging tests'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-timeout'
makedepends=('python-pytest' 'python2-pytest' 'mercurial')
checkdepends=('python-pexpect' 'python2-pexpect')
source=("hg+https://bitbucket.org/pytest-dev/pytest-timeout#tag=$pkgver"
        pytest-2.8.patch)
md5sums=('SKIP'
         'aa75f95f3347cdcdd05eba5255ec3c74')

prepare() {
  # https://bitbucket.org/pytest-dev/pytest-timeout/issues/12/tests-failed-with-pytest-280
  (cd $_pypiname; patch -p1 -i ../pytest-2.8.patch)

  cp -a "${srcdir}/${_pypiname}"{,-py2}
}

build() {
  cd "$srcdir/$_pypiname"
  python setup.py build

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir/$_pypiname"
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.5/site-packages:$PYTHONPATH" py.test

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-timeout() {
  depends=('python-pytest')

  cd "$srcdir/$_pypiname"
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pytest-timeout() {
  depends=('python2-pytest')

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
