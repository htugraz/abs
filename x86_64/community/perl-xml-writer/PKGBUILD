# $Id: PKGBUILD 105811 2014-02-15 20:35:53Z spupykin $
# Maintainer: Justin Davis <jrcd83@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-xml-writer
pkgver=0.624
pkgrel=1
pkgdesc="Module for writing XML documents"
arch=('any')
url="http://search.cpan.org/dist/XML-Writer"
license=('custom')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/J/JO/JOSEPHW/XML-Writer-$pkgver.tar.gz)
md5sums=('ac797a3f49b837f39131240869b9d789')

build() {
  cd XML-Writer-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-Writer-$pkgver
  make test
}

package() {
  cd  XML-Writer-$pkgver
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
