# $Id: PKGBUILD 99213 2013-10-25 20:22:05Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=flam3
pkgver=3.0.1
pkgrel=4
pkgdesc="Tools to create/display fractal flames: algorithmically generated images and animations"
arch=('i686' 'x86_64')
url="http://flam3.com/"
license=('GPL3')
depends=('libjpeg' 'libpng' 'libxml2')
source=(http://flam3.googlecode.com/files/${pkgname}-${pkgver}.tar.gz flam3-3.0.1-libpng15.patch)
sha1sums=('8814515f2e49e034e47cf97e9d2c0e932844abb9'
          '17ece6cc00899e5135dad2bc79f97dfbd73107a6')

prepare() {
  cd ${pkgname}-${pkgver}/src
  patch -p1 -i "${srcdir}/flam3-3.0.1-libpng15.patch"
}

build() {
  cd ${pkgname}-${pkgver}/src
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd ${pkgname}-${pkgver}/src
  make DESTDIR="${pkgdir}" install
}
