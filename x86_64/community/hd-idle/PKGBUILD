# $Id: PKGBUILD 123358 2014-12-02 14:09:03Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Tilman Blumenbach <tilman@ax86.net>

pkgname=hd-idle
pkgver=1.04
pkgrel=4
pkgdesc='Utility program for spinning-down external disks after a period of idle time.'
arch=('i686' 'x86_64')
backup=(etc/conf.d/hd-idle)
url="http://hd-idle.sourceforge.net/"
license=('GPL')
depends=('glibc')
source=("http://downloads.sf.net/$pkgname/$pkgname-$pkgver.tgz"
        "hd-idle.service")
md5sums=('41e52e669fc59fa82ee0c2bcce1336d3'
         '6e9162a075a7a99c3d10e9cde9c499df')

build() {
    cd "$srcdir/$pkgname"
    sed -i 's|/sbin|/bin|' Makefile
    make
}

package() {
    cd "$srcdir/$pkgname"

    make TARGET_DIR="$pkgdir/usr" install

    install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
    install -Dm644 debian/hd-idle.default "$pkgdir/etc/conf.d/hd-idle"

    cd "$srcdir"
    install -Dm644 hd-idle.service "$pkgdir/usr/lib/systemd/system/hd-idle.service"
}

