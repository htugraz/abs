# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-file-tail
pkgver=1.0
pkgrel=1
pkgdesc='Perl extension for reading from continously updated files'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl-io)
url=https://metacpan.org/release/File-Tail
source=("http://search.cpan.org/CPAN/authors/id/M/MG/MGRABNAR/File-Tail-$pkgver.tar.gz")
md5sums=('c87f648d138c176b5d224c8d1b59838e')
sha512sums=('8ad120e83fcf18d05f686c637f96af7dd0b5b04186e0b3fbfdf802bf82effaa16377fce15887d395abd03f9ebe4db2685d1d8e40f0b7b19b08ad71a93042551f')
_ddir="File-Tail-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
