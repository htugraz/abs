# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-file-tail
pkgver=0.99.3
pkgrel=6
pkgdesc='Perl extension for reading from continously updated files'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl-io)
url=https://metacpan.org/release/File-Tail
source=("http://search.cpan.org/CPAN/authors/id/M/MG/MGRABNAR/File-Tail-$pkgver.tar.gz")
md5sums=(ef0fb7bcb4181ba593f4a09940f61d1c)
sha512sums=(8ecc2c7e6ed4a6d6578635034e66922c927d6965659606e6d9f8816d4ddefe441c3299126d713d4a5232d96042da8990bb4b2b2118333cc0871f1c4a8c0d44bc)
_ddir="File-Tail-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
