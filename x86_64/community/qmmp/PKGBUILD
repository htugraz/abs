#$Id: PKGBUILD 144565 2015-10-21 21:09:22Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=qmmp
pkgver=1.0.1
pkgrel=1
pkgdesc="Qt5 based audio-player"
arch=('i686' 'x86_64')
url="http://qmmp.ylsoftware.com/"
license=('GPL')
depends=('alsa-lib' 'curl' 'hicolor-icon-theme' 'libmad' 'libvorbis' 'libogg' 'libxkbcommon-x11' 'taglib' 'xdg-utils' 'qt5-x11extras')
makedepends=('cmake' 'flac' 'jack' 'libmpcdec' 'pulseaudio' 'ffmpeg' 'libcdio-paranoia' 'libcddb' 'libmms' 'libsamplerate' 'libmodplug'
             'libsndfile' 'wavpack' 'projectm' 'mesa' 'faad2' 'libgme' 'libsidplayfp' 'opusfile' 'wildmidi' 'qt5-tools')
optdepends=('flac: native FLAC support'
            'jack: JACK sound output'
            'libmpcdec: Musepack support'
            'pulseaudio: PulseAudio output'
            'libcdio-paranoia: Compact Disc input and control support'
            'libcddb: CDDB server support'
            'libmms: MMS stream protocol support'
            'libmodplug: MOD playing library'
            'libsndfile: sampled sound support'
            'projectm: visual efects'
            'faad2: ADTS AAC support'
            'libgme: support for chiptunes from various platforms'
            'libsidplayfp: C64 music support'
            'opusfile: Opus support'
            'wildmidi: MIDI support'
            'ffmpeg' 'libsamplerate' 'wavpack')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://qmmp.ylsoftware.com/files/$pkgname-$pkgver.tar.bz2)
sha256sums=('010923f2fbba8b5a75c422eadf9b762b8b3eee10849e49272271262549457e66')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=/usr/lib \
          -DUSE_HAL:BOOL=FALSE
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
