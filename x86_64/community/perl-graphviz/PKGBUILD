# $Id: PKGBUILD 146663 2015-11-13 02:11:12Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: antoniolucas <antoniolucasnobar at gmail dot com>

_perlmod=GraphViz
pkgname=perl-graphviz
pkgver=2.19
pkgrel=1
pkgdesc="GraphViz - Interface to AT&T's GraphViz. Deprecated. See GraphViz2"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('graphviz' 'perl-file-which' 'perl-ipc-run' 'perl-parse-recdescent'
         'perl-xml-twig' 'perl-xml-xpath' 'perl-libwww')
makedepends=('perl-module-build')
checkdepends=('perl-test-pod')
source=("https://cpan.metacpan.org/authors/id/R/RS/RSAVAGE/$_perlmod-$pkgver.tgz")
md5sums=('a394f6a0366e6561af45b4c2d00688df')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  perl Build.PL installdirs=vendor destdir="$pkgdir/"
  perl Build
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  perl Build test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  perl Build install
}

# vim:set ts=2 sw=2 et:
