# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: fnord0 <fnord0 AT riseup DOT net>

pkgname=zaproxy
pkgver=2.4.0
pkgrel=2
pkgdesc="Integrated penetration testing tool for finding vulnerabilities in web applications"
url="https://www.owasp.org/index.php/ZAP"
arch=('any')
license=('Apache')
depends=('java-runtime' 'ttf-font')
source=(${pkgname}-${pkgver}.tar.gz::https://downloads.sourceforge.net/project/zaproxy/${pkgver}/ZAP_${pkgver}_Linux.tar.gz)
sha512sums=('7b61ac7ebaf6bd98bfe647b4583da5e1d488b1eea093dd4174c45a0ad51268ce7d6f5ea10d3e3ac27aaa299bf94980e78cc6ccb538d186b96612764e1161fbb1')

prepare() {
  cd ZAP_${pkgver}
  cat > ${pkgname} <<EOF
#!/bin/sh
cd /usr/share/zaproxy
./zap.sh
cd -
EOF
}

package() {
  cd ZAP_${pkgver}
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -pR . "${pkgdir}/usr/share/${pkgname}"
  install -Dm 755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
}

# vim: ts=2 sw=2 et:
