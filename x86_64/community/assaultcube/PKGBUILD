# $Id: PKGBUILD 100613 2013-11-06 13:34:16Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Rabyte <rabyte [at] gmail (dot) com>

pkgname=assaultcube
pkgver=1.2.0.1
pkgrel=1
pkgdesc='A realistic team oriented multiplayer FPS based on the Cube engine'
arch=('i686' 'x86_64')
url='http://assault.cubers.net/'
license=('ZLIB' 'custom')
depends=('sdl' 'sdl_mixer' 'sdl_image' 'openal' 'zlib' 'gcc-libs' 'libgl' 'desktop-file-utils' 'glu')
makedepends=('mesa' 'clang')
source=("http://downloads.sourceforge.net/actiongame/AssaultCube_v${pkgver}.tar.bz2"
        "http://downloads.sourceforge.net/actiongame/AssaultCube_v${pkgver}.source.tar.bz2"
        'assaultcube'
        'assaultcube-server'
        'assaultcube.desktop'
        'assaultcube.png')
md5sums=('d159e60bc7cc09e7675c0db941906f56'
         '22118d5dbb1ef7ab3369da4ba11c236f'
         'd658ad3ee476bfe92afa5b5a04a7b4f5'
         '553e2c3b38c4d13e5d77c23efd51a6a6'
         'a37dfbe8263f4ef8fe41120196194eae'
         'f688c59ecee2ebc5c589720aa1480765')
install=assaultcube.install

build() {
  cd AssaultCube_v${pkgver}.source/source/src

  make
}

package() {
  cd AssaultCube_v${pkgver}.source/source/src
  install -Dm755 ac_client ${pkgdir}/usr/bin/ac_client
  install -Dm755 ac_server ${pkgdir}/usr/bin/ac_server

  cd ${srcdir}/AssaultCube_v${pkgver}
  mkdir -p ${pkgdir}/usr/share/assaultcube
  cp -rf config packages docs mods ${pkgdir}/usr/share/assaultcube
  install -Dm644 ${srcdir}/assaultcube.png ${pkgdir}/usr/share/pixmaps/assaultcube.png
  install -Dm644 ${srcdir}/assaultcube.desktop ${pkgdir}/usr/share/applications/assaultcube.desktop
  install -Dm644 docs/package_copyrights.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm755 ${srcdir}/assaultcube ${pkgdir}/usr/bin/assaultcube
  install -Dm755 ${srcdir}/assaultcube-server ${pkgdir}/usr/bin/assaultcube-server
}

# vim: sw=2:ts=2 et:
