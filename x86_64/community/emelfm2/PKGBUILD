# $Id: PKGBUILD 106313 2014-02-27 17:50:17Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: contrasutra

pkgname=emelfm2
pkgver=0.9.1
pkgrel=1
pkgdesc="A file manager that implements the popular two-pane design"
arch=('i686' 'x86_64')
url="http://emelfm2.net/"
depends=('gtk2')
license=('GPL3')
source=("http://emelfm2.net/rel/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('62eb6299d0a33281feae2e6ec4ddab2194c36963')

# WITH_SYSTEM_ICONS=1 added 0.9.1 ?

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX="${pkgdir}/usr" install install_i18n

  install -m 755 "${pkgdir}/usr/share/pixmaps/emelfm2/emelfm2_48.png" \
      "${pkgdir}/usr/share/pixmaps/emelfm2.png"
  sed -e '/Icon=/ s^emelfm2/emelfm2_48.png^emelfm2.png^' \
      -e '/Categories=/ s^;$^;Application;Utility;^' \
      -i "${pkgdir}/usr/share/applications/emelfm2.desktop"
}
