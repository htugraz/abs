# $Id: PKGBUILD 130519 2015-04-02 08:54:24Z tredaelli $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=mldonkey
pkgver=3.1.5
pkgrel=2
pkgdesc='A multi-network P2P client'
arch=(i686 x86_64)
url='http://mldonkey.sourceforge.net/'
license=(GPL)
depends=(file gd miniupnpc libnatpmp)
makedepends=(lablgtk2 librsvg ocaml)
optdepends=('librsvg: GUI support'
            'gtk2: GUI support')
backup=(etc/conf.d/mldonkey)
install=mldonkey.install
source=(http://downloads.sourceforge.net/sourceforge/mldonkey/$pkgname-$pkgver.tar.bz2
        mldonkey.conf
        mldonkey.service
        mldonkey.tmpfiles
        http://caml.inria.fr/distrib/ocaml-3.12/ocaml-3.12.0.tar.gz
        https://forge.ocamlcore.org/frs/download.php/561/lablgtk-2.14.2.tar.gz)
sha256sums=('74f9d4bcc72356aa28d0812767ef5b9daa03efc5d1ddabf56447dc04969911cb'
            'f1d9401cefd591662d49011c53fdb2788755a6f745a963e46d8037b990edeb6a'
            '778cebe8edcffd63db3594054c2daa62ce571644a96ad235b8c95470b55c0415'
            '45f45d2a3c064d5bbc0b6b9b70fa3497ed47cfecdd1ec52e175311f48eaa89dc'
            'd964b385e110c1517c9de1c6331476d5e7a3585cc692b6b6b8973f3a9be7da4b'
            '4981abedabdc462303f345104042c88af227ccd50fd30a9bf48fd353ab02d0ba')

build() {
  cd mldonkey-$pkgver
  cp ../ocaml-3.12.0.tar.gz patches/
  cp ../lablgtk-2.14.2.tar.gz patches/
  # mldonkey requires the old version of ocaml
  ./configure --prefix=/usr --enable-gui=newgui2 --enable-upnp-natpmp --enable-batch
  make
}

package() {
  cd mldonkey-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 icons/rsvg/type_source_normal.svg "$pkgdir"/usr/share/icons/mldonkey.svg
  install -Dm644 distrib/mldonkey.desktop "$pkgdir"/usr/share/applications/mldonkey.desktop

  install -Dm644 "$srcdir"/mldonkey.conf "$pkgdir"/etc/conf.d/mldonkey
  install -Dm644 "$srcdir"/mldonkey.service "$pkgdir"/usr/lib/systemd/system/mldonkey.service
  install -Dm644 "$srcdir"/mldonkey.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/mldonkey.conf
}
