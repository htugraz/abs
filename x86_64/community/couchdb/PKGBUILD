# $Id: PKGBUILD 109076 2014-04-10 09:01:51Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=couchdb
pkgver=1.5.1
pkgrel=1
pkgdesc="A document-oriented database that can be queried and indexed in a MapReduce fashion using JSON"
arch=('i686' 'x86_64')
url="http://couchdb.apache.org"
license=('APACHE')
depends=('icu' 'erlang-nox' 'js185' 'openssl' 'curl')
makedepends=('autoconf-archive' 'git')
install=couchdb.install
backup=('etc/couchdb/local.ini'
	'etc/conf.d/couchdb'
	'etc/logrotate.d/couchdb')
#source=("git://github.com/apache/couchdb.git#commit=8d7ab8b18dd20f8785e69f4420c6f93a2edbfa60"
source=("http://www.eu.apache.org/dist/couchdb/source/$pkgver/apache-couchdb-$pkgver.tar.gz"{,.asc}
	"couchdb.service"
	"couchdb.tmpfiles")
md5sums=('1118a9f4ca6f58b751d3187838248b3c'
         'SKIP'
         '8914a7ffc6745f94106d96206709acb4'
         '1e254ebe32eeb061be64193bafa35dbf')

build() {
  cd "$srcdir/apache-couchdb-$pkgver"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "$srcdir/apache-couchdb-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 etc/default/couchdb $pkgdir/etc/conf.d/couchdb
  sed -i 's|\(COUCHDB_OPTIONS=\)|\1"-p /var/run/couchdb/couchdb.pid"|' $pkgdir/etc/conf.d/couchdb

  rm -rf $pkgdir/etc/default/ $pkgdir/var/run

  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  install -Dm0644 $srcdir/$pkgname.tmpfiles $pkgdir/usr/lib/tmpfiles.d/$pkgname.conf
}
