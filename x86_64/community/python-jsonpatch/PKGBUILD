# $Id: PKGBUILD 111729 2014-05-22 21:36:59Z dwallace $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgbase=python-jsonpatch
_pkgname=jsonpatch
pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=1.5
pkgrel=2
pkgdesc="Python object model built on JSON schema and JSON patch"
arch=(any)
url=https://github.com/stefankoegl/python-json-patch
license=(BSD)
makedepends=(python2-setuptools python2-jsonschema python-setuptools python-jsonschema)
source=(http://pypi.python.org/packages/source/j/${_pkgname#*-}/${_pkgname#*-}-$pkgver.tar.gz)
sha256sums=('5b8874a5bd0b7201d8e917f820bbb3c8bbd55174ae9036220f83ac6dadc2c4df')
sha512sums=('5f542620139abc61d835392b7885367979607c2b67ce29cd8279d60b9485228e7e2d5ec9badb25acbb820689910ff0fbe38cda6ddaad217e543eb8c06186e354')

prepare(){
    cp -a $_pkgname-$pkgver $_pkgname-$pkgver-2
    find $_pkgname-$pkgver-2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build() {
    cd $srcdir/$_pkgname-$pkgver
    python setup.py build
    cd $srcdir/$_pkgname-$pkgver-2
    python2 setup.py build
}

package_python-jsonpatch() {
    cd $srcdir/$_pkgname-$pkgver
    depends=(python-jsonschema)
    python setup.py install --prefix=/usr --root="$pkgdir"
}
package_python2-jsonpatch() {
    cd $srcdir/$_pkgname-$pkgver-2
    depends=(python2-jsonschema)
    python2 setup.py install --prefix=/usr --root="$pkgdir"
}
