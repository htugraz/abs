# $Id: PKGBUILD 127669 2015-02-13 15:59:55Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-authen-radius
pkgver=0.24
pkgrel=2
pkgdesc="perl radius client"
arch=('any')
url="http://search.cpan.org/dist/Authen-Radius/"
#url="http://search.cpan.org/dist/RadiusPerl"
license=('GPL' 'PerlArtistic')
depends=('perl-data-hexdump>=0.02')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/P/PO/PORTAONE/Authen-Radius-$pkgver.tar.gz
	undef-warn-fix.patch)
md5sums=('35541556f8ec48bf0b60f2e8d284957f'
         '7a4bf593c8ab0e4631e74c6637605066')

prepare() {
  cd  $srcdir/Authen-Radius-$pkgver
  patch -p1 <$srcdir/undef-warn-fix.patch
}

build() {
  cd  $srcdir/Authen-Radius-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  $srcdir/Authen-Radius-$pkgver
  make install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
