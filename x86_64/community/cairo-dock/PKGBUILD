# $Id: PKGBUILD 120781 2014-10-16 12:06:59Z jgc $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: erm67 <erm67@yahoo.it>

pkgname=cairo-dock
pkgver=3.3.2.r1818
pkgrel=1
pkgdesc='Light eye-candy fully themable animated dock'
arch=('i686' 'x86_64')
url='https://launchpad.net/cairo-dock-core'
license=('GPL')
depends=('curl' 'dbus-glib' 'glu' 'gtk3' 'librsvg')
makedepends=('bzr' 'cmake')
optdepends=('cairo-dock-plugins: Plugins for Cairo-Dock')
#source=("https://launchpad.net/cairo-dock-core/${pkgver%.?}/${pkgver}/+download/cairo-dock-${pkgver}.tar.gz")
source=("cairo-dock::bzr+lp:cairo-dock-core#revision=${pkgver#*.r}")
sha256sums=('SKIP')

build() {
  cd cairo-dock

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd cairo-dock/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
