# $Id: PKGBUILD 112216 2014-05-30 04:55:36Z arcanis $
# Maintainer: Evgeniy Alekseev <arcanis.arch at gmail dot com>
# Contributor: RaphaÃ«l Doursenaud <rdoursenaud at free dot fr>

pkgbase=python-mysql-connector
pkgname=('python-mysql-connector' 'python2-mysql-connector')
_pkgname=mysql-connector-python
pkgver=1.2.2
pkgrel=1
pkgdesc="A standardized database driver for Python platforms and development"
arch=('any')
url="http://dev.mysql.com/downloads/connector/python/"
license=('GPL')
depends=('python' 'python2')
source=("http://cdn.mysql.com/Downloads/Connector-Python/${_pkgname}-${pkgver}.tar.gz")
md5sums=('15b0e95e6224ed7c9bfb685a9d54d356')

prepare() {
  cp -r "${_pkgname}-${pkgver}" "${_pkgname}-${pkgver}-py2"
  # change python to python2
  find "${_pkgname}-${pkgver}-py2" -name '*.py' -type f -exec \
      sed -i 's|env python|env python2|' {} \;
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py build
}

package_python-mysql-connector() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-mysql-connector() {
  depends=('python2')

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
