# $Id: PKGBUILD 134544 2015-06-02 13:43:27Z eworm $
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubikey-personalization
pkgver=1.17.1
pkgrel=2
pkgdesc='Yubico YubiKey Personalization library and tool'
arch=('i686' 'x86_64')
url='https://github.com/Yubico/yubikey-personalization'
license=('BSD')
depends=('libusb' 'yubico-c-client')
makedepends=('git')
provides=('ykpers')
conflicts=('ykpers')
install=yubikey-personalization.install
source=("git://github.com/Yubico/yubikey-personalization.git#tag=v${pkgver}")
sha256sums=('SKIP')

build() {
	cd yubikey-personalization/

	autoreconf -fi
	./configure --prefix=/usr --with-udevrulesdir=/usr/lib/udev/rules.d/
	make
}

check() {
	cd yubikey-personalization/

	make check
}

package() {
	cd yubikey-personalization/

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/yubikey-personalization/COPYING"
	install -D -m0644 README "${pkgdir}/usr/share/doc/yubikey-personalization/README"
	make DESTDIR="${pkgdir}/" install
}

