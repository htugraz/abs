# $Id: PKGBUILD 99811 2013-10-31 01:51:20Z allan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-anthy
pkgver=1.5.4
pkgrel=1
pkgdesc='Japanese input method Anthy IMEngine for IBus Framework'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://ibus.googlecode.com'
depends=('ibus' 'anthy' 'python2' 'pygtk' 'gtk-update-icon-cache')
makedepends=('swig' 'intltool')
install=$pkgname.install
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")

build() {
  cd $srcdir/$pkgname-$pkgver

  # python2 fix
  for file in setup/ibus-setup-anthy.in engine/ibus-engine-anthy.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir/ install
}

sha512sums=('8478e9ea14472d0153cc7a35feae4edea791476dc98cfc266b598f915376d267bd5db48ea0a676209d7a6fa680628116a30fec59afaf696477cfaa14082aad8d')
