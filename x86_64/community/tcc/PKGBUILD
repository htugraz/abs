# $Id: PKGBUILD 99550 2013-10-29 14:08:21Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Gergely Imreh <imrehgATgmailDOTcom>
# Contributor : dschauer <dschauerATgmailDOTcom>
# Contributor : Stefan Husmann <stefan-husmann@t-online.de>
# Contributor : Jeremy Cowgar <jeremy@cowgar.com>

pkgname=tcc
pkgver=0.9.26
pkgrel=2
pkgdesc="Tiny C Compiler"
arch=('i686' 'x86_64')
url="http://bellard.org/tcc/"
license=('LGPL')
makedepends=('gcc')
options=('docs' 'staticlibs')
source=(http://download.savannah.nongnu.org/releases/tinycc/${pkgname}-${pkgver}.tar.bz2)
md5sums=('5fb28e4abc830c46a7f54c1f637fb25d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # sed fixes from Gentoo ebuild, cheers!
  # Don't strip
  sed -i -e 's|$(INSTALL) -s|$(INSTALL)|' Makefile

  # Fix examples
  sed -i -e '1{
    i#! /usr/bin/tcc -run
    /^#!/d
  }' examples/ex*.c
  sed -i -e '1s/$/ -lX11/' examples/ex4.c

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make tccdir=${pkgdir}/usr/lib/tcc libdir=${pkgdir}/usr/lib \
    mandir=${pkgdir}/usr/share/man bindir=${pkgdir}/usr/bin \
    includedir=${pkgdir}/usr/include \
    docdir=${pkgdir}/usr/share/doc/tcc \
    install

  make clean
  make CFLAGS="-fPIC"
  ld -shared -soname libtcc.so -o libtcc.so *.o
  install -D -m 755 libtcc.so "${pkgdir}/usr/lib/libtcc.so"
}
