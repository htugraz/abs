# $Id: PKGBUILD 81295 2012-12-16 16:14:21Z arodseth $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=fillets-ng
pkgver=1.0.1
pkgrel=4
pkgdesc="Port of the wonderful puzzle game Fish Fillets"
url="http://fillets.sf.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('fillets-ng-data' 'lua51' 'fribidi' 'gcc-libs' 'libx11' 'sdl_mixer' 'sdl_ttf' 'sdl_image' 'desktop-file-utils')
makedepends=('gendesk')
install=$pkgname.install
changelog=$pkgname.changelog
source=("http://downloads.sourceforge.net/fillets/$pkgname-$pkgver.tar.gz"
       "$pkgname.png::http://img.1mobile.com/market/i/4/9/49ec5e5520844b66c189c7e0ef6eedfc.png"
        'lua-config')
sha256sums=('329a4d9515d60bebdb657d070824933b993b85864b9d3e302e6361accab992da'
            '338c9e1b8ac4ef5bb167f4a6090c341897bd2c7f8b4e5afc3ac3131a57bb8c88'
            '849fb1410387dc7df0617e12a9472cfbe3b1179600011c0365f9099ff5af6214')
_exec=('fillets')

build() {
  gendesk
  export PATH=$PATH:$PWD
  chmod a+rx lua-config
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --datadir=/usr/share/$pkgname
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  # desktop and icon files
  install -D -m644 "${srcdir}/$pkgname.png" \
    "${pkgdir}/usr/share/pixmaps/$pkgname.png"
  install -D -m644 "${srcdir}/$pkgname.desktop" \
    "${pkgdir}/usr/share/applications/$pkgname.desktop"
}
