# $Id: PKGBUILD 110076 2014-04-23 17:29:50Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=vyqchat
pkgver=0.2.8
pkgrel=5
pkgdesc="vypress compatible qt chat (can work without server)"
arch=('i686' 'x86_64')
url="http://linux.bydg.org/~yogin/"
license=('GPL')
depends=('qt3' 'libao' 'libxi')
source=(http://www.uic.unn.ru/~soed/programs/chat/$pkgname-$pkgver.tar.gz
	build-fix.patch)
md5sums=('67974bc5df1ed0d63785d04325444d4f'
         'de3af70da3743c27929f04f72c837412')

build() {
  cd $srcdir/$pkgname-${pkgver}
  patch -Np1 <../build-fix.patch
  # Use _LDADD to properly compile with --as-needed
  sed -i 's/vyqchat_LDFLAGS/vyqchat_LDADD/' src/Makefile.am
  # Correct acinclude.m4 to avoid empty else block
  sed -i 's:# Leave bnv_qt_lib_dir defined:/bin/true:' acinclude.m4
  autoreconf -fiv
  ./configure --prefix=/usr --with-Qt-bin-dir=/usr/lib/qt3/bin --with-Qt-include-dir=/usr/include/qt3 --with-Qt-lib-dir=/usr/lib
  make
}

package() {
  cd $srcdir/$pkgname-${pkgver}
  make DESTDIR=$pkgdir install
}
