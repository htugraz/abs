# $Id: PKGBUILD 129339 2015-03-16 00:16:01Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Christoph 'delmonico' Neuroth <delmonico@gmx.net>

pkgname=librsync
pkgver=1.0.0
pkgrel=1
pkgdesc="A free software library that implements the rsync remote-delta algorithm (rdiff)"
arch=('i686' 'x86_64')
url="http://librsync.sourceforge.net/"
license=('LGPL')
depends=('popt' 'zlib' 'bzip2')
provides=('librsync.so')
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('2195998516960ce84d93f88ee3bfd92f430a16cdba4b5d34560a39fa13fcafd9')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-shared
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
