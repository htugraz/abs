# $Id: PKGBUILD 131030 2015-04-09 13:54:42Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Olivier Biesmans <olivier at biesmans dot fr>

pkgbase=python-pyamf
pkgname=('python2-pyamf')  # 'python-pyamf' Python 3 version doesn't work so far
pkgver=0.7.2
pkgrel=1
pkgdesc="AMF support for Python"
arch=('i686' 'x86_64')
url="https://github.com/hydralabs/pyamf"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python2-django' 'python2-sqlalchemy' 'python2-twisted' 'python2-lxml' 'python2-pillow')
source=("http://pypi.python.org/packages/source/P/PyAMF/PyAMF-${pkgver}.tar.gz")
md5sums=('3350e8254a0a6b22b3576a190de665a3')

prepare() {
  cp -a PyAMF-$pkgver{,-py2}
}

build() {
  cd "$srcdir/PyAMF-$pkgver"
  # python setup.py build

  cd "$srcdir/PyAMF-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/PyAMF-$pkgver"
  # python setup.py test

  cd "$srcdir/PyAMF-$pkgver-py2"
  # Tests failed due to Django 1.8 https://github.com/hydralabs/pyamf/issues/53
  python2 setup.py test || warning "Tests failed"
}

package_python-pyamf() {
  depends=('python')

  cd "$srcdir/PyAMF-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-pyamf() {
  depends=('python2')

  cd "$srcdir/PyAMF-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
