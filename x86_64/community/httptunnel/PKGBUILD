# $Id: PKGBUILD 116094 2014-07-22 08:57:17Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=httptunnel
pkgver=3.3
pkgrel=4
pkgdesc="Creates a bidirectional virtual data connection tunnelled in HTTP requests"
arch=('i686' 'x86_64')
url="http://www.nocrew.org/software/httptunnel"
license=('GPL2')
depends=('glibc')
source=("http://www.nocrew.org/software/${pkgname}/$pkgname-$pkgver.tar.gz"
        'httptunnel-3.3-build-fix.patch')
md5sums=('493cc0f5f21e9955db27ee9cd9a976d5'
         '65c1f8a2a847fb4af106c9358f9e0be8')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -p0 -i ../httptunnel-3.3-build-fix.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
