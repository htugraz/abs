# $Id: PKGBUILD 126643 2015-01-27 14:06:03Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>

pkgname=kscreen4
_pkgname=kscreen
pkgver=1.0.2.1
pkgrel=1
pkgdesc="KDE's screen management software"
arch=('i686' 'x86_64')
url='https://projects.kde.org/kscreen'
license=('GPL' 'LGPL')
depends=('kdebase-runtime' 'libkscreen4')
conflicts=('kscreen' 'kscreen-frameworks')
replaces=('kscreen<5')
makedepends=('cmake' 'automoc4')
install=$pkgname.install
source=("http://download.kde.org/stable/${_pkgname}/${pkgver}/src/${_pkgname}-${pkgver}.tar.xz")
md5sums=('cd3d6ffdb0e44c418c2804fc72b8391b')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
