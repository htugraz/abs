# $Id: PKGBUILD 138163 2015-08-09 21:00:38Z jlichtblau $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=colordiff
pkgver=1.0.15
pkgrel=1
pkgdesc="A Perl script wrapper for 'diff' that produces the same output but with pretty 'syntax' highlighting"
arch=('any')
url="http://www.colordiff.org/"
license=('GPL')
depends=('perl' 'diffutils')
backup=('etc/colordiffrc' 'etc/colordiffrc-lightbg')
source=(http://www.colordiff.org/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('595ee4e9796ba02fad0b181e21df3ee34ae71d1611e301e146c0bf00c5269d45'
            'SKIP')
validpgpkeys=('CF3A93EF01E616C5AE7A1D2745E1E473378BB197') #Dave Ewart <davee@sungate.co.uk>

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" INSTALL_DIR=/usr/bin MAN_DIR=/usr/share/man/man1 install
  install -m644 colordiffrc-lightbg "${pkgdir}/etc/colordiffrc-lightbg"
}
