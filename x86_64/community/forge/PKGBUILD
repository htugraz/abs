# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=forge
pkgdesc="High Performance Visualizations"
url='https://github.com/arrayfire/forge'
pkgver=3.1.2
arch=('i686' 'x86_64')
pkgrel=1
license=('BSD')
makedepends=('cmake' 'doxygen' 'graphviz')
depends=('glew' 'glfw' 'freetype2' 'fontconfig' 'glm')
source=(https://github.com/arrayfire/forge/archive/af${pkgver}.tar.gz)
md5sums=('dae835aae60327c1cda693cb8ede5cfa')

build() {
  cd "${srcdir}/forge-af${pkgver}/"

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DUSE_SYSTEM_GLM=ON \
      -DUSE_SYSTEM_FREETYPE=ON \
      -DBUILD_EXAMPLES=ON \
      -DBUILD_DOCUMENTATION=ON

  make
}

package() {
  cd "${srcdir}/forge-af${pkgver}/"

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  cd build

  make DESTDIR="${pkgdir}/" install
}
