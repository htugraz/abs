# $Id: PKGBUILD 105637 2014-02-12 15:41:55Z alucryd $$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=ffms2
pkgver=2.19
pkgrel=2
pkgdesc='A libav/ffmpeg based source library and Avisynth plugin for easy frame accurate access'
arch=('i686' 'x86_64')
url='https://github.com/FFMS/ffms2'
license=('GPL')
depends=('ffmpeg')
provides=('vapoursynth-plugin-ffms2')
source=("https://github.com/FFMS/ffms2/archive/${pkgver}.tar.gz")
sha256sums=('6f1379514f03a14092fdfc9941960a9b86f5b78d86f9e000eeddf3bf50d42811')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --enable-shared --disable-static --enable-avresample
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/lib/vapoursynth
  ln -s ../libffms2.so "${pkgdir}"/usr/lib/vapoursynth/
}

# vim: ts=2 sw=2 et:
