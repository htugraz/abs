# $Id: PKGBUILD 128117 2015-02-22 21:46:34Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcmsystemd
pkgver=1.0.0
pkgrel=1
pkgdesc="Systemd control module for KDE"
arch=('i686' 'x86_64')
url='https://github.com/rthomsen/kcmsystemd'
license=(GPL)
depends=(kconfigwidgets boost-libs)
makedepends=(extra-cmake-modules python boost)
source=("https://github.com/rthomsen/kcmsystemd/archive/$pkgver.tar.gz")
md5sums=('6233faf3cd8569b360c21c38d0d06d7f')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_BUILD_TYPE=Release \
   -DCMAKE_INSTALL_LIBDIR=lib \
   -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
