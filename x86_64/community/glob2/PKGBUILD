# $Id: PKGBUILD 94023 2013-07-13 12:32:16Z svenstaro $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=glob2
pkgver=0.9.4.4
pkgrel=19
pkgdesc='RTS game which minimizes micro-management by automatically assigning tasks to units'
arch=('x86_64' 'i686')
url='http://www.globulation2.org/'
license=('GPL3')
depends=('boost-libs' 'sdl_net' 'sdl_image' 'sdl_ttf' 'libvorbis' 'speex' 'glu'
         'fribidi' 'xdg-utils' 'libgl')
makedepends=('scons' 'boost' 'mesa-libgl')
install="$pkgname.install"
source=("http://dl.sv.nongnu.org/releases/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.gz"
        "$pkgname-${pkgver%.*}.1-gcc44.patch")
sha256sums=('0f4d898ec6b05ce27b4a12ef242cc26571304b90d2509932a4743c71311314b8'
            '3a0cfc06696d1aaf2d7948b5f6a09a8ce96492767039dc09bfc25182b92d7b7a')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np0 -i "$srcdir/$pkgname-${pkgver%.*}.1-gcc44.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  scons LINKFLAGS="-lboost_system"
}

package () {
  cd "$srcdir/$pkgname-$pkgver"

  scons install BINDIR="$pkgdir/usr/bin" INSTALLDIR="$pkgdir/usr/share"
  find "$pkgdir/usr/share" -type f -exec chmod 644 '{}' \;
  install -Dm644 "data/icons/$pkgname-icon-48x48.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname-icon-48x48.png"
}

# vim:set ts=2 sw=2 et:
