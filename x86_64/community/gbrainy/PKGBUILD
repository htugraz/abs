# $Id: PKGBUILD 132694 2015-05-04 09:57:06Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>

pkgname=gbrainy
pkgver=2.2.5
pkgrel=1
epoch=1
pkgdesc="A brain teaser game and trainer to have fun and to keep your brain trained"
arch=('any')
url="https://wiki.gnome.org/Apps/gbrainy"
license=('GPL2')
depends=('gtk-sharp-2' 'mono-addins' 'librsvg' 'hicolor-icon-theme'
         'desktop-file-utils')
makedepends=('intltool' 'gettext' 'gnome-doc-utils')
install=gbrainy.install
source=(http://gent.softcatala.org/jmas/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('74d1c66ef4c8eaf7cfc3e09525224296f684ca1ccb8529c067a03cd7189be3cd')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export MONO_SHARED_DIR="$srcdir"
  ./configure \
    --prefix=/usr \
    --enable-addins
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
