# $Id: PKGBUILD 128516 2015-03-02 02:59:58Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bartosz Fe≈Ñski <fenio@debian.org>

pkgname=reaver
pkgver=1.4
pkgrel=2
pkgdesc="Brute force attack against Wifi Protected Setup"
arch=('i686' 'x86_64')
url="http://code.google.com/p/reaver-wps/"
license=('GPL2')
depends=('libpcap' 'sqlite3')
backup=('etc/reaver/reaver.db')
source=(http://reaver-wps.googlecode.com/files/$pkgname-$pkgver.tar.gz)
md5sums=('05441dda7bacfcbe1e831c85d1ea3bc9')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  install -Dm755 "$srcdir/$pkgname-$pkgver/src/reaver" "$pkgdir/usr/bin/reaver"
  install -Dm755 "$srcdir/$pkgname-$pkgver/src/wash" "$pkgdir/usr/bin/wash"
  install -Dm644 "$srcdir/$pkgname-$pkgver/src/reaver.db" "$pkgdir/etc/reaver/reaver.db"
  install -Dm644 "$srcdir/$pkgname-$pkgver/docs/README" "$pkgdir/usr/share/doc/reaver/README"
  install -Dm644 "$srcdir/$pkgname-$pkgver/docs/reaver.1.gz" "$pkgdir/usr/share/man/man1/reaver.1.gz"
}
