# $Id: PKGBUILD 145270 2015-10-30 06:48:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jim Bridgewater <jwbwater at gmail dot com>
# Contributor: delta48 <dark.magician.48[at]gmail[dot]com> 

pkgbase=python-pyperclip
pkgname=('python-pyperclip' 'python2-pyperclip')
_pypiname=pyperclip
pkgver=1.5.19
_commit=74ebe1f0bf8f05add1590ebe58e60e27b620461d
pkgrel=1
pkgdesc="A cross-platform clipboard module for Python"
arch=('any')
url="https://github.com/asweigart/pyperclip"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'xclip' 'git')
checkdepends=('xorg-server-xvfb')
source=("git+https://github.com/asweigart/pyperclip.git#commit=$_commit"
        fix-test_copyUnicode.patch)
md5sums=('SKIP'
         '40675a4461359c8e2c22ee4bd240d76c')

prepare() {
  # https://github.com/asweigart/pyperclip/pull/28
  (cd $_pypiname; patch -p1 -i ../fix-test_copyUnicode.patch)
  cp -a $_pypiname{,-py2}
}

check() {
  cd "$srcdir/$_pypiname"
  xvfb-run python tests/basicTests.py

  cd "$srcdir/$_pypiname-py2"
  xvfb-run python2 tests/basicTests.py
}

package_python-pyperclip() {
  depends=('python' 'xclip')

  cd "$srcdir/$_pypiname"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-pyperclip() {
  depends=('python2' 'xclip')

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
