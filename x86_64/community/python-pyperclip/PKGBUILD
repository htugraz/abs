# $Id: PKGBUILD 143901 2015-10-13 10:24:12Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jim Bridgewater <jwbwater at gmail dot com>
# Contributor: delta48 <dark.magician.48[at]gmail[dot]com> 

pkgbase=python-pyperclip
pkgname=('python-pyperclip' 'python2-pyperclip')
_pypiname=pyperclip
pkgver=1.5.15
_commit=c57ed8ea81d5658b88f5e4dbe35938bb8be1da53
pkgrel=1
pkgdesc="A cross-platform clipboard module for Python"
arch=('any')
url="https://github.com/asweigart/pyperclip"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'xclip' 'git')
checkdepends=('xorg-server-xvfb')
source=("git+https://github.com/asweigart/pyperclip.git#commit=$_commit"
        fix-test_copyUnicode.patch)
md5sums=('SKIP'
         '473ee868530cc1d6e579f9df63085588')

prepare() {
  (cd $_pypiname; git apply ../fix-test_copyUnicode.patch)
  cp -a $_pypiname{,-py2}
}

check() {
  cd "$srcdir/$_pypiname"
  xvfb-run python tests/basicTests.py

  cd "$srcdir/$_pypiname-py2"
  xvfb-run python2 tests/basicTests.py
}

package_python-pyperclip() {
  depends=('python' 'xclip')

  cd "$srcdir/$_pypiname"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-pyperclip() {
  depends=('python2' 'xclip')

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
