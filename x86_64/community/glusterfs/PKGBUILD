# $Id: PKGBUILD 100446 2013-11-04 12:12:13Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributors:
#   Andrei Antoukh - niwi@niwi.be - http://www.niwi.be
#   henning mueller <henning@orgizm.net>

pkgname=glusterfs
pkgver=3.4.1
_basever=`echo $pkgver | cut -f1-2 -d.`
pkgrel=1
pkgdesc='Is a cluster file-system capable of scaling to several peta-bytes.'
arch=(i686 x86_64)
url='http://www.gluster.org/'
license=(GPL2 LGPL3)
depends=(fuse python2 libxml2)
makedepends=(flex bison)
source=(http://download.gluster.org/pub/gluster/glusterfs/$_basever/$pkgver/glusterfs-$pkgver.tar.gz
	glusterd.service)
sha256sums=('2a2a43fb52a7f6b8b00331acc5ea2c86a067a2f2901cca886a2348e919c0cbd8'
            '18e6c3bcca396a4524d866977d3fa86135377d7bf5d28a130e697ae2e85f043b')
sha256sums=('b5f1b4e3c6b86ecdfc5ff1b870ae3d3384bb2e3943565d6907c29ee6e21ab739'
            '18e6c3bcca396a4524d866977d3fa86135377d7bf5d28a130e697ae2e85f043b')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-mountutildir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --libexecdir=/usr/lib/$pkgname \
    PYTHON=python2

  make

  sed -i s:env\ python:env\ python2: \
    xlators/features/marker/utils/syncdaemon/gsyncd.py
  sed -i s:/usr/bin/python:/usr/bin/python2: \
    contrib/ipaddr-py/ipaddr.py
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make -j1 DESTDIR=$pkgdir install

  install -D -m 644 \
    $srcdir/$pkgname-$pkgver/{README,INSTALL,COPYING*} \
    $pkgdir/usr/share/doc/glusterfs/

  install -D -m 644 \
    $srcdir/glusterd.service \
    $pkgdir/usr/lib/systemd/system/glusterd.service

  cp -rf \
    $srcdir/$pkgname-$pkgver/doc/* \
    $pkgdir/usr/share/doc/glusterfs/
}

# vim:set ts=2 sw=2 et:
