# $Id: PKGBUILD 142896 2015-10-03 14:56:55Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=vte3-ng
_basever=0.42.0
pkgver=$_basever.a
pkgrel=1
pkgdesc="Enhanced Virtual Terminal Emulator widget for use with GTK3"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!emptydirs')
depends=('gtk3' 'vte-common')
makedepends=('intltool' 'gobject-introspection' 'gtk3' 'vala' 'gtk-doc' 'gperf')
url="https://github.com/thestinger/vte-ng"
source=(https://github.com/thestinger/vte-ng/archive/$pkgver.tar.gz)
sha256sums=('865783c97e16f70dbaf49383eff2a152e72539b1744d21e2a08e370870b3e833')
provides=(vte3=$_basever vte3-select-text=$_basever)
conflicts=(vte3 vte3-select-text)
replaces=(vte3-select-text)

build() {
  cd "vte-ng-$pkgver"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection
  make
}

package() {
  cd "vte-ng-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir/etc/profile.d/vte.sh"
}
