# $Id: PKGBUILD 123386 2014-12-03 11:19:31Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-xlsxwriter
pkgname=('python2-xlsxwriter' 'python-xlsxwriter')
pkgver=0.6.4
pkgrel=2
pkgdesc="A Python module for creating Excel XLSX files"
arch=('any')
url="https://github.com/jmcnamara/XlsxWriter"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/source/X/XlsxWriter/XlsxWriter-${pkgver}.tar.gz")
md5sums=('b3702816516257a03f432b8c978406f0')

prepare() {
  cd "$srcdir"
  cp -a XlsxWriter-${pkgver} XlsxWriter-py2-${pkgver}
}

build() {
  msg "Building Python2"
  cd "$srcdir"/XlsxWriter-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/XlsxWriter-${pkgver}
  python setup.py build
}

package_python2-xlsxwriter() {
  depends=('python2')

  cd "$srcdir"/XlsxWriter-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir"/usr/bin/vba_extract{,2}.py
}

package_python-xlsxwriter() {
  depends=('python')

  cd "$srcdir"/XlsxWriter-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

