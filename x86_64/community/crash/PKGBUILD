# $Id: PKGBUILD 136804 2015-07-13 20:38:15Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Bernhard Walle <bernhard.walle@gmx.de>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Contributor: Mathieu Pasquet <mathieui@mathieui.net>

pkgname=crash
pkgver=7.1.2
pkgrel=1
pkgdesc='Linux kernel crashdump analysis tool'
url='http://people.redhat.com/~anderson'
arch=(i686 x86_64)
license=(GPL)
depends=(xz ncurses zlib)
source=(http://people.redhat.com/~anderson/crash-$pkgver.tar.gz)
sha256sums=('037f3bdde8ad0602150f7a09a7a2870966b863d689e73e91d0d8d2a4cf28845e')

prepare() {
  cd crash-$pkgver

  make gdb_unzip
  sed -i '/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/' gdb-7.6/libiberty/configure
}

build() {
  cd crash-$pkgver
  make
}

package() {
  cd crash-$pkgver

  mkdir -p "$pkgdir"/usr/bin "$pkgdir"/usr/share/man/man8
  install crash "$pkgdir"/usr/bin/
  install crash.8 "$pkgdir"/usr/share/man/man8/
}
