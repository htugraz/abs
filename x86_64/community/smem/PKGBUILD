# $Id: PKGBUILD 112258 2014-05-30 18:58:49Z jelle $
# Maintainer:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Dave Reisner <d@falconindy.com>
# Contributor: Isaac Dupree <id@isaac.cedarswampstudios.org>
# Contributor: Sebastien Duquette <ekse.0x@gmail.com>

pkgname=smem
pkgver=1.4
pkgrel=1
pkgdesc="Generate reports on memory usage."
url="http://www.selenic.com/smem/"
license=("GPL")
depends=('python2')
optdepends=('python2-matplotlib: for chart generation')
arch=('i686' 'x86_64')
source=("http://www.selenic.com/smem/download/$pkgname-$pkgver.tar.gz")
md5sums=('fe79435c3930389bfdb560255c802162')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '1s/python$/python2/' smem
  make smemcap
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 smem "$pkgdir/usr/bin/smem"
  install -Dm755 smemcap "$pkgdir/usr/bin/smemcap"
  install -Dm644 smem.8 "$pkgdir/usr/share/man/man8/smem.8"
}

