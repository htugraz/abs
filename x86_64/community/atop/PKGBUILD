# $Id: PKGBUILD 110162 2014-04-25 13:27:08Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: John Gerritse <john.gerritse@gmail.com>

pkgname=atop
pkgver=2.0.2
pkgrel=2
pkgdesc="A system and process level monitor."
arch=(i686 x86_64)
url="http://www.atoptool.nl/"
license=('GPL')
depends=('ncurses' 'sh' 'zlib')
source=(http://www.atoptool.nl/download/atop-${pkgver}.tar.gz)
md5sums=('f1dd1984584f9635712cb7196e25620c')

prepare() {
  cd $srcdir/$pkgname-${pkgver/_/-}
  sed -i 's#root ##' atop.cron
  sed -i 's|%|%%|g' atop.service
}

build() {
  cd $srcdir/$pkgname-${pkgver/_/-}
  make
}

package() {
  cd $srcdir/$pkgname-${pkgver/_/-}
  make DESTDIR=$pkgdir INIPATH=/etc/rc.d install
  rm -f $pkgdir/etc/cron.d/atop
  install -dm0755 $pkgdir/etc/cron.daily
  mv $pkgdir/etc/atop/atop.daily $pkgdir/etc/cron.daily/atop
  install -Dm0644 atop.service $pkgdir/usr/lib/systemd/system/atop.service
  rm -rf $pkgdir/etc/rc.d
}
