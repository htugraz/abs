# $Id: PKGBUILD 104918 2014-01-28 11:06:38Z flexiondotorg $
# Maintainer: Martin Wimpress <code@flexion.org>

pkgname=bip
pkgver=0.8.9
pkgrel=1
pkgdesc="Multiuser IRC proxy that supports replaying of logged conversations."
arch=('i686' 'x86_64')
url="http://bip.milkypond.org/"
license=('GPL')
depends=('openssl')
source=("http://pkgs.fedoraproject.org/repo/pkgs/${pkgname}/${pkgname}-${pkgver}.tar.gz/04158ad020db221ed87cdc03660ef6c9//$pkgname-$pkgver.tar.gz"
        ${pkgname}.service)
md5sums=('04158ad020db221ed87cdc03660ef6c9'
         'e9a445ed7b0cf12e57d77cf42021160c')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # Don't treat warnings as errors.
    sed -i 's/-Werror//g' Makefile.in
    # Make the sample configuration reflect the PKGBUILD defaults
    sed -i 's:/var/proxy/logs:/var/log/bip:' samples/$pkgname.conf
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m755 ${srcdir}/${pkgname}-${pkgver}/scripts/bipgenconfig "${pkgdir}"/usr/bin/bipgenconfig
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/samples/$pkgname.conf "${pkgdir}"/usr/share/$pkgname/$pkgname.conf.sample
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/samples/$pkgname.vim "${pkgdir}"/usr/share/vim/syntax/$pkgname.vim
    install -D -m 0644 ../${pkgname}.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
