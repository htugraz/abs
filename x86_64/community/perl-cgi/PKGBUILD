# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cgi
pkgver=4.03
pkgrel=1
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1')
url=https://metacpan.org/release/CGI
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEEJO/CGI.pm-$pkgver.tar.gz")
md5sums=(265c8133a02e194d3189573835de51f4)
sha512sums=(e064d5d40cf284347454774b2bcadd54d39fa1433be0d619d18067e614f8570e072f1995206e9233f1b5a4a3e61fb0748aefc0d535fd89355d4afb8bc0bd879c)
_ddir="CGI.pm-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
