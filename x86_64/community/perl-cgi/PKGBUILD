# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cgi
pkgver=4.22
pkgrel=1
pkgdesc='Handle Common Gateway Interface requests and responses'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' perl-html-parser)
checkdepends=(perl-test-deep perl-test-warn)
url=https://metacpan.org/release/CGI
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEEJO/CGI-$pkgver.tar.gz")
md5sums=('8289aa6f2cba9589134d517f4a25da9e')
sha512sums=('becd37e56ca3fe8a6ab6069d99ddc65838d4aa00b57b6e30a5e2bcf73e84dff9a3f5f2841549f6627fe29d46607254197f3a03f308dc5fb6add3ece60719871d')
_ddir="CGI-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
