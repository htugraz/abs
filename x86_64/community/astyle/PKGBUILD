# $Id: PKGBUILD 111974 2014-05-27 10:49:10Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Thomas Mader <thezema@gmail.com>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=astyle
pkgver=2.04
pkgrel=2
pkgdesc='A free, fast and small automatic formatter for C, C++, C#, and Java source code.'
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/astyle/'
license=('LGPL')
depends=('gcc-libs')
source=("http://downloads.sourceforge.net/sourceforge/astyle/${pkgname}_${pkgver}_linux.tar.gz")
md5sums=('30b1193a758b0909d06e7ee8dd9627f6')

build() {
  cd "$srcdir/$pkgname/build/gcc"
  make release shared
}

package() {
  cd "$srcdir/$pkgname/build/gcc"

  install -Dm0755 bin/astyle "$pkgdir/usr/bin/astyle"
  install -Dm0755 bin/libastyle.so "$pkgdir/usr/lib/libastyle.so"

  # install documentation (implements FS#23290)
  install -d "$pkgdir/usr/share/doc/$pkgname/"
  for f in ../../doc/*.html; do
    install -m0644 "$f" "$pkgdir/usr/share/doc/$pkgname/"
  done
}
