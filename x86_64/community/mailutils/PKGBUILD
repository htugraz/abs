# $Id: PKGBUILD 110825 2014-05-08 12:08:11Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=mailutils
pkgver=2.99.98
pkgrel=1
pkgdesc="MUA command line tool (mailx)"
arch=(i686 x86_64)
url="http://www.gnu.org/software/mailutils/"
license=('GPL')
depends=('libldap' 'pam' 'gnutls')
makedepends=('emacs')
install=mailutils.install
options=(zipman !emptydirs)
source=(http://alpha.gnu.org/gnu/mailutils/mailutils-$pkgver.tar.xz
	build-fix.patch)
md5sums=('7d87795ff24afa966ad5d6785f974141'
         'e83709ffdcdb257dfab443ef77c89574')

prepare() {
  cd "$srcdir/mailutils-$pkgver"
#  patch -p1 <$srcdir/build-fix.patch
}

build() {
  cd "$srcdir/mailutils-$pkgver"
  ./configure --prefix=/usr --with-gdbm --with-gnutls \
	--libexecdir=/usr/lib/$pkgname --without-fribidi \
	--without-tcp-wrappers --sbindir=/usr/bin \
	--without-python --without-guile
  make
}

package() {
  cd "$srcdir/mailutils-$pkgver"
  make -j1 DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/info/dir
  mv $pkgdir/usr/bin/mail $pkgdir/usr/bin/gnu-mail
  mv $pkgdir/usr/share/man/man1/mail.1 $pkgdir/usr/share/man/man1/gnu-mail.1
  chown -R root:root $pkgdir
}
