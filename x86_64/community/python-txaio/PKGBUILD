# $Id: PKGBUILD 150098 2015-12-07 19:36:52Z anatolik $
# Maintainer: Anatol Pomozov

pkgbase=python-txaio
pkgname=(python-txaio python2-txaio)
pkgver=2.2.0
pkgrel=1
pkgdesc='Utilities to support code that runs unmodified on Twisted and asyncio'
arch=(any)
url='https://github.com/tavendo/txaio'
license=(MIT)
makedepends=(python-setuptools python2-setuptools python-twisted python2-twisted)
source=(https://pypi.python.org/packages/source/t/txaio/txaio-$pkgver.tar.gz)
sha1sums=('2ad680eedcf3858243e217bfbdb1b38238235a36')

prepare() {
  cp -a txaio-$pkgver{,-py2}
}

build() {
  cd "$srcdir/txaio-$pkgver"
  python setup.py build

  cd "$srcdir/txaio-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/txaio-$pkgver"
  python setup.py test

  cd "$srcdir/txaio-$pkgver-py2"
  python2 setup.py test
}

package_python-txaio() {
  depends=(python python-six python-twisted)

  cd "$srcdir/txaio-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-txaio() {
  depends=(python2 python2-six python2-twisted)

  cd "$srcdir/txaio-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
