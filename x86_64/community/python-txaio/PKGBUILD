# $Id: PKGBUILD 144377 2015-10-19 16:22:26Z anatolik $
# Maintainer: Anatol Pomozov

pkgbase=python-txaio
pkgname=(python-txaio python2-txaio)
pkgver=2.0.4
pkgrel=1
pkgdesc='Utilities to support code that runs unmodified on Twisted and asyncio'
arch=(any)
url='https://github.com/tavendo/txaio'
license=(MIT)
makedepends=(python-setuptools python2-setuptools)
source=(https://pypi.python.org/packages/source/t/txaio/txaio-$pkgver.tar.gz)
sha1sums=('fd43ddbac7442cfd0ef582473c5751bf666e44e3')

prepare() {
  cp -a txaio-$pkgver{,-py2}
}

build() {
  cd "$srcdir/txaio-$pkgver"
  python setup.py build

  cd "$srcdir/txaio-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/txaio-$pkgver"
  python setup.py test

  cd "$srcdir/txaio-$pkgver-py2"
  python2 setup.py test
}

package_python-txaio() {
  depends=(python python-six)

  cd "$srcdir/txaio-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-txaio() {
  depends=(python2 python2-six)

  cd "$srcdir/txaio-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
