# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=drumkv1
pkgver=0.4.0
pkgrel=1
pkgdesc="An old-school drum-kit sampler"
arch=(i686 x86_64)
url="http://$pkgname.sourceforge.net/$pkgname-index.html"
license=('GPL')
groups=('lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt5-base'
         'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info')
install="$pkgname.install"
source=("http://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('d2be11a7c06d62ad789bc4bf7884e1eb')

prepare() {
  cd $pkgname-$pkgver

  # x86_64 lib path fix
  sed -i "s/lib64/lib/" src/src_lv2{,ui}.pro

  # i686 -reduce-relocations
  if [[ $CARCH = i686 ]]; then
    export CXXFLAGS+=" -fPIE"
  fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
