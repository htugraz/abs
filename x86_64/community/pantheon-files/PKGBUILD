# $Id: PKGBUILD 138983 2015-08-30 12:43:39Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=0.2.2.1
pkgrel=1
pkgdesc='The Pantheon File Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-files'
license=('GPL3')
depends=('atk' 'cairo' 'dbus-glib' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2'
         'glibc' 'gtk3' 'libgee' 'libnotify' 'pango' 'plank' 'sqlite'
         'zeitgeist'
         'libgranite.so')
makedepends=('cmake' 'gnome-common' 'vala')
optdepends=('contractor: Various context menu entries'
            'tumbler: Thumbnails generation')
install='pantheon-files.install'
source=("https://launchpad.net/pantheon-files/${pkgver%.*.*}.x/${pkgver}/+download/pantheon-files-${pkgver}.tar.xz")
sha256sums=('a8b39e3eb34c5aeab38b9152043485f8df187ba27204275fcdb02d03bfee1833')

prepare() {
  cd pantheon-files-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd pantheon-files-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DCMAKE_SKIP_RPATH='TRUE' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  cd pantheon-files-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
