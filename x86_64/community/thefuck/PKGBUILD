# $Id: PKGBUILD 145778 2015-11-03 10:41:12Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=thefuck
pkgver=3.2
pkgrel=1
pkgdesc="Magnificent app which corrects your previous console command"
arch=('any')
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python-setuptools' 'python-psutil' 'python-six' 'python-colorama' 'python-decorator')
makedepends=('git')
checkdepends=('python-pytest-mock' 'python-mock' 'python-pexpect')
source=("git+https://github.com/nvbn/thefuck.git#tag=$pkgver")
sha256sums=('SKIP')

build() {
  cd thefuck
  python setup.py build
}

check() {
  cd thefuck

  # Hack distribution test by installing it and set PYTHONPATH afterwards
  python setup.py install --root="$PWD/tmp_install" --optimize=1

  # Hack $HOME as it's / in the chroot and not writable
  cp tests/test_conf.py "$srcdir/test_conf.py.bak"
  sed -i "s|data = {}|data = {'HOME': '$srcdir'}|" tests/test_conf.py
  LC_CTYPE=en_US.utf8 PYTHONPATH="$PWD/tmp_install/usr/lib/python3.5/site-packages:$PYTHONPATH" py.test tests
  mv "$srcdir/test_conf.py.bak" tests/test_conf.py
}

package() {
  cd thefuck
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir/"
  mv "$pkgdir"/usr/lib/python3.5/site-packages/{tests,thefuck/}

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
