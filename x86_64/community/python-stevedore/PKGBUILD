# $Id: PKGBUILD 151224 2015-12-09 09:26:09Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com

pkgname=('python-stevedore' 'python2-stevedore')
pkgver=1.10.0
pkgrel=1
pkgdesc="Manage dynamic plugins for Python applications"
arch=('any')
url="https://github.com/openstack/stevedore"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
options=('!emptydirs')
source=(http://pypi.python.org/packages/source/s/stevedore/stevedore-$pkgver.tar.gz)
sha256sums=('f5d689ef38e0ca532d57a03d1ab95e89b17c57f97b58d10c92da94699973779f')

prepare() {
  cd "$srcdir/stevedore-$pkgver"

  # Remove dependency on argparse; it's in Python 2.7+/3.2+ stdlib (FS#42063)
  # https://bugs.launchpad.net/python-stevedore/+bug/1372433
  sed -i '/argparse/d' requirements.txt

  # Remove dependency on pbr; it's not a runtime dependency
  sed -i '/pbr/d' requirements.txt
}

package_python-stevedore() {
  depends=('python-setuptools' 'python-six')

  cd "$srcdir/stevedore-$pkgver"
  python3 setup.py install --root="$pkgdir" -O1
}

package_python2-stevedore() {
  depends=('python2-setuptools' 'python2-six')

  cd "$srcdir/stevedore-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
