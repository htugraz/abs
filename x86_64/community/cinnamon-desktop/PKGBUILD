# $Id$
# Maintainer: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor:  Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=2.4.0
pkgrel=1
pkgdesc="Library with common API for various Cinnamon modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(gtk3 libxkbfile python2)
makedepends=(intltool gobject-introspection gnome-common python2)
url="https://github.com/linuxmint/cinnamon-desktop"
install=cinnamon-desktop.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-desktop/archive/${pkgver}.tar.gz"
        "gnome-3.14.patch")
sha256sums=('4ddfa1d73287f419c825eb0174eab18b87a96e5d895b59ea996ae17f27771acc'
            'c4df93c49fd3f9f8bc359cbad5d5526eef80ee5c34680f8ba62c67c3dbb7eca5')

prepare() {
  cd $pkgname-$pkgver
  # Update configuration for GNOME 3.14
  patch -Np1 -i ../gnome-3.14.patch
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
