# $Id: PKGBUILD 117001 2014-08-07 17:38:35Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk3
pkgver=0.24
pkgrel=3
pkgdesc="Gtk client and libraries for SPICE remote desktop servers (gtk3 version)"
arch=('i686' 'x86_64')
url="http://spice-space.org"
license=('LGPL2.1')
depends=('celt0.5.1' 'dbus-glib' 'gstreamer0.10-good' 'gtk3' 'libcacard' 'libpulse' 'usbredir' 'libsoup')
makedepends=('gobject-introspection' 'intltool' 'python2-pyparsing' 'spice-protocol' 'usbutils' 'vala')
source=("http://www.spice-space.org/download/gtk/spice-gtk-$pkgver.tar.bz2")
sha256sums=('6efe500845f910945670f11664f584fd13b9db494075c41137eb7bacd60d1ed1')

prepare() {
  cd "$srcdir/spice-gtk-$pkgver"
  sed -i 's,/usr/bin/env python,/usr/bin/python2,' spice-common/spice_codegen.py
}

build() {
  cd "$srcdir/spice-gtk-$pkgver"
  PYTHON=python2 ./configure --prefix=/usr \
    --disable-static --enable-vala --with-gtk=3.0 \
    --with-audio=pulse --with-audio=gstreamer \
    --with-coroutine=gthread --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/spice-gtk-$pkgver"
  make DESTDIR="$pkgdir/" install
}
