# $Id: PKGBUILD 144649 2015-10-22 00:38:16Z dwallace $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=cloud-init
pkgver=0.7.6
pkgrel=2
pkgdesc="Cloud Bootstrapping Application"
arch=(any)
license=("GPL3")
url=https://launchpad.net/cloud-init
# cloud-init python lib requirements (according to Requires file):
depends=(systemd python2-yaml python2-cheetah python2-prettytable python2-oauth python2-boto python2-configobj python2-jsonpatch python2-jsonpointer net-tools)
makedepends=('python2' 'python2-setuptools')
options=(!emptydirs)
# Archlinux specific cloud.cfg
source=(https://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
sha1sums=('9af02f68d68abce91463bec22b17964d1618e1da')
backup=(etc/cloud/cloud.cfg etc/cloud/cloud.cfg.d/05_logging.cfg)

prepare(){
    find $pkgname-$pkgver -name \*.py -exec sed -i '1s/python$/&2/' {} +
    sed -i '1s/python$/&2/' $pkgname-$pkgver/tools/read*
    sed -i 's:/etc/systemd:/usr/lib/systemd:g' $pkgname-$pkgver/setup.py
    sed -i 's,name: ubuntu,name: archlinux,;s,gecos: Ubuntu,gecos: Archlinux,' $srcdir/$pkgname-$pkgver/config/cloud.cfg
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 ./setup.py install --root=${pkgdir} --init-system systemd
}
