# $Id: PKGBUILD 117310 2014-08-12 16:15:33Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=labyrinth
pkgver=0.6
pkgrel=2
pkgdesc="Lightweight mind-mapping tool with support for image import and drawing"
arch=('any')
url="https://github.com/labyrinth-team/labyrinth"
license=('GPL')
depends=('python2-gconf' 'python2-xdg')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/labyrinth-team/$pkgname/archive/$pkgver.tar.gz
        no-numpy.patch)
md5sums=('e008ebfc1eaeab7f40cff05cb1865c45'
         'c993d413288a1c68bee8e4b0ea187972')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's@^#.*python$@#!/usr/bin/python2@' labyrinth_lib/*.py

  # Eliminate numpy dependency
  patch -Np1 -i ../no-numpy.patch
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
  make -C po
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root "$pkgdir" --optimize=1
  make -C po localedir=$pkgdir/usr/share/locale install
  install -Dm644 data/$pkgname-16.png "$pkgdir/usr/share/icons/hicolor/16x16/apps/$pkgname.png"
  install -Dm644 data/$pkgname-22.png "$pkgdir/usr/share/icons/hicolor/22x22/apps/$pkgname.png"
  install -Dm644 data/$pkgname-24.png "$pkgdir/usr/share/icons/hicolor/24x24/apps/$pkgname.png"
  install -Dm644 data/$pkgname.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  install -Dm644 data/$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
}
