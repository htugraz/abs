# $Id: PKGBUILD 109002 2014-04-09 10:28:04Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk3-greeter
pkgver=1.8.4
pkgrel=1
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('exo' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=("${pkgname/3/}")
conflicts=("${pkgname/3/2}")
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
install="${pkgname/3/}.install"
source=("https://launchpad.net/${pkgname/3/}/${pkgver%.?}/${pkgver}/+download/${pkgname/3/}-${pkgver}.tar.gz")
sha256sums=('cb3e93bfcb8fbaf109e01080dc75ec9a6803afcb68acbf9eef3799be927a2c6c')

build() {
  cd ${pkgname/3/}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' --libexecdir='/usr/lib/lightdm' --disable-static
  make
}

package() {
  cd ${pkgname/3/}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
