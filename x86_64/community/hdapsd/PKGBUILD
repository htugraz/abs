# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Emil Renner Berthing <esmil@mailme.dk>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=hdapsd
pkgver=20090401
pkgrel=8
pkgdesc='HDAPS userspace hard drive protection daemon'
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/hdaps/'
license=('GPL')
depends=('glibc' 'bash')
source=("http://downloads.sourceforge.net/hdaps/hdapsd-${pkgver}.tar.gz"
        'hdapsd.systemd')
md5sums=('897cee8b0192febd127470f3e9506aeb'
         'f514cca78059477c4f33faf97d4aaa06')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  install -Dm0644 "${srcdir}/hdapsd.systemd" "${pkgdir}/usr/lib/systemd/system/hdapsd.service"
}
