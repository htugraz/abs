# $Id: PKGBUILD 101429 2013-11-25 14:11:30Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>

pkgname=powerdns
pkgver=3.4.2
pkgrel=1
pkgdesc='Authoritative DNS server'
arch=('x86_64' 'i686')
url='http://www.powerdns.com/'
license=('GPL')
depends=('boost-libs' 'gcc-libs' 'postgresql-libs' 'libmariadbclient' 'sqlite' 'libldap' 'lua')
makedepends=('boost' 'setconf')
options=('!makeflags')
install='pdns.install'
backup=('etc/powerdns/pdns.conf')
source=("http://downloads.powerdns.com/releases/pdns-$pkgver.tar.bz2"
        'pdns.service')
sha256sums=('a6ab05459a0118cb921092deee06362722c45fa69ed0166ebc3696d526014b5b'
            'b2358a5951ac944feee15ac03d42ffe9c2a72fa6f57746f77ef8997d54a82040')

prepare() {
  touch "pdns-$pkgver/AUTHORS" "pdns-$pkgver/NEWS"

  # Make changes to pdns.conf, while basing it on pdns.conf-dist
  cp "pdns-$pkgver/pdns/pdns.conf-dist" pdns.conf

  # Setting the default configuration options.
  for keyvalue in \
    allow-recursion=127.0.0.1 \
    cache-ttl=6 \
    chroot=/var/empty \
    config-dir=/etc \
    daemon=yes \
    distributor-threads=3 \
    guardian=yes \
    local-port=53 \
    loglevel=3 \
    module-dir=/usr/lib/powerdns \
    negquery-cache-ttl=60 \
    setgid=nobody \
    setuid=nobody \
    socket-dir=/var/run \
    webserver=no
  do
    setconf -a pdns.conf "$keyvalue"
  done
}

build() {
  cd "pdns-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/powerdns \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib \
    --mandir=/usr/share/man \
    --with-modules='' \
    --with-dynmodules="ldap pipe gmysql gpgsql gsqlite3 geo bind" \
    --docdir=/usr/share/doc/powerdns \
    --sbindir=/usr/bin \
    --bindir=/usr/bin
  make
}

package() {
  make -C "pdns-$pkgver" DESTDIR="$pkgdir" install
  install -m644 "pdns.conf" "$pkgdir/etc/powerdns/"
  install -Dm644 "pdns.service" \
    "$pkgdir/usr/lib/systemd/system/pdns.service"
  rm -f "$pkgdir/etc/powerdns/pdns.conf-dist"
  mv "$pkgdir/usr/lib/pdns" "$pkgdir/usr/lib/powerdns"
}

# vim:set ts=2 sw=2 et:
