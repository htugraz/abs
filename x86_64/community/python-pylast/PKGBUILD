# $Id: PKGBUILD 135477 2015-06-16 12:38:24Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: Amr Hassan <amr.hassan@gmail.com>

pkgbase=python-pylast
pkgname=('python-pylast' 'python2-pylast')
pkgver=1.2.1
pkgrel=2
pkgdesc='A Python interface to Last.fm and Libre.fm'
arch=('any')
url='https://github.com/pylast/pylast'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/p/pylast/pylast-$pkgver.tar.gz")
sha256sums=('fd87ac3932ad6f137848fe36b4062ecf07cb043d93a6798e9acd00accccdea10')

prepare() {
  cp -r pylast-${pkgver} python2-pylast-${pkgver}
}

build() {
  cd pylast-${pkgver}

  python setup.py build

  cd ../python2-pylast-${pkgver}

  python2 setup.py build
}

package_python-pylast() {
  depends=('python-six')

  cd pylast-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
}

package_python2-pylast() {
  depends=('python2-six')

  cd python2-pylast-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
