# $Id: PKGBUILD 108434 2014-03-27 14:47:39Z fyan $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-cchardet
pkgname=(python-cchardet python2-cchardet)
pkgver=0.3.5
pkgrel=2
pkgdesc="Universal encoding detector faster than chardet."
url="http://pypi.python.org/pypi/cchardet/"
makedepends=('cython' 'cython2' 'python-setuptools' 'python2-setuptools')
license=('MIT')
arch=('i686' 'x86_64')
source=(http://pypi.python.org/packages/source/c/cchardet/cchardet-$pkgver.tar.gz)
sha256sums=('51094c573d248a4908a968e75edd05bab136f10fcc8a70b87c8243b6d45731f8')

build() {
    cd "$srcdir/cchardet-$pkgver"

    rm -rf ../buildpy3; mkdir ../buildpy3
    python setup.py build -b ../buildpy3

    rm -rf ../buildpy2; mkdir ../buildpy2
    python2 setup.py build -b ../buildpy2
    find ../buildpy2 -name \*.py -exec sed -r 's|^#!(.*)python$|#!\1python2|' -i {} +
}

package() {
    cd "$srcdir/cchardet-$pkgver"
    python setup.py install --root="$pkgdir"
	install -m0644 -D "README.rst" "$pkgdir/usr/share/licenses/$pkgname/README.rst"
}

package_python-cchardet() {
    depends=(python)

    cd "$srcdir/cchardet-$pkgver"
    rm -rf build; ln -s ../buildpy3 build
    python setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "README.rst" "$pkgdir/usr/share/licenses/$pkgname/README.rst"
}

package_python2-cchardet() {
    depends=(python2)

    cd "$srcdir/cchardet-$pkgver"
    rm -rf build; ln -s ../buildpy2 build
    python2 setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "README.rst" "$pkgdir/usr/share/licenses/$pkgname/README.rst"
}

