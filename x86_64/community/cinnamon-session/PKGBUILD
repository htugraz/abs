# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=2.4.3
pkgrel=2
pkgdesc="The Cinnamon Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(dbus-glib cinnamon-desktop libsm libcanberra)
makedepends=(intltool mesa json-glib xtrans gnome-common)
options=('!emptydirs')
install=cinnamon-session.install
url="https://github.com/linuxmint/cinnamon-session"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-session/archive/${pkgver}.tar.gz
        timeout.patch
        blacklist_gnome-initial-setup.patch
        gtk316.patch)
sha256sums=('07afa992ae1c513b3a1e83a2fd29f83305f697dd45ba04121521ba8a7c005733'
            'ef421a14814c7858490b3a806568ab4ec8a0dc21d390e94f801771c1261bb24a'
            '0664375a725144b9e0cabfbcd9a424bf1cc65504cc30f71d1d7ea4a8cfa5a778'
            '012feb2ed16570a54e3b1d26b6e45677a16fbd86a3bea828b58b516885ddb871')

prepare() {
  cd $pkgname-$pkgver

  # Increase timeout, for slow machines
  patch -Np1 -i ../timeout.patch

  # Blacklist gnome-initial-setup from autostart, it's not designed for Cinnamon
  patch -Np1 -i ../blacklist_gnome-initial-setup.patch

  patch -Np1 -i ../gtk316.patch
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/cinnamon-session \
      --disable-schemas-compile --enable-systemd --disable-gconf
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
