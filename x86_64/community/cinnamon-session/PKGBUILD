# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=2.2.2
pkgrel=2
pkgdesc="The Cinnamon Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(dbus-glib cinnamon-desktop libsm libcanberra)
makedepends=(intltool mesa json-glib xtrans gnome-common)
options=('!emptydirs')
install=cinnamon-session.install
url="https://github.com/linuxmint/cinnamon-session"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-session/archive/${pkgver}.tar.gz
        timeout.patch
        0001-Fix-misplaced-gtk_widget_show-s-for-gtk-3.14.patch)
sha256sums=('16e94442dc8c3d666ca2b576088b7dd4b179e48a3af96ac92ac94864c1f51288'
            'ef421a14814c7858490b3a806568ab4ec8a0dc21d390e94f801771c1261bb24a'
            '78ef4ca45fd03ec9921b1ce02a340376223423ad54d3b256e2b7205a4f0d6d74')

prepare() {
  cd $pkgname-$pkgver

  # Increase timeout, for slow machines
  patch -Np1 -i ../timeout.patch

  # Fix cinnamon-session-properties for GTK+ 3.14
  patch -Np1 -i ../0001-Fix-misplaced-gtk_widget_show-s-for-gtk-3.14.patch
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/cinnamon-session \
      --disable-schemas-compile --enable-systemd --disable-gconf
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
