# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
_pkgbase=marco
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.1
pkgrel=1
pkgdesc="A window manager for MATE  (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libcanberra' 'libgtop' 'libice' 'libsm' 'libxt'
         'mate-desktop-gtk3>=1.12' 'startup-notification' 'zenity')
makedepends=('mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('8cf33e096f8cb77627e3fdad7ae3350a78f77227')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --enable-startup-notification \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
