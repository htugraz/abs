# $Id: PKGBUILD 107273 2014-03-16 03:10:55Z svenstaro $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt
pkgver=3.3.2
_pkgver=3.3.2
pkgrel=1
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=('i686' 'x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost-libs' 'libharu' 'graphicsmagick' 'pango' 'zlib')
makedepends=('boost' 'cmake>=2.8' 'postgresql-libs' 'fcgi' 'sqlite' 'mysql++' 'qt4')
optdepends=('openssl: for SSL support in built-in webserver'
	    'fcgi: for FastCGI support'
	    'postgresql-libs: for PostgreSQL Dbo support'
	    'sqlite: for Sqlite Dbo support'
	    'mysql++: for the hangman example'
	    'qt4: for the Wt/Qt interopability example (wtwithqt)')
backup=('etc/wt/wt_config.xml')
source=(http://downloads.sourceforge.net/witty/$pkgname-${_pkgver}.tar.gz)
md5sums=('dd061399c9b4366085619038f59a2e02')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}

  [[ -d build ]] && rm -r build
  mkdir -p build && cd build
  cmake \
      -DCONNECTOR_HTTP=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DUSE_SYSTEM_SQLITE3=ON \
      -DINSTALL_EXAMPLES=ON \
      ..
  make
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}/build
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/usr/share/cmake-2.8/Modules
  mv ${pkgdir}/usr/cmake/FindWt.cmake ${pkgdir}/usr/share/cmake-2.8/Modules/
  rm -r ${pkgdir}/usr/cmake
  rm -rf $pkgdir/var/run
}
