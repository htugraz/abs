# $Id: PKGBUILD 99809 2013-10-31 01:50:39Z allan $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=hercules
pkgver=3.08
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Software implementation of System/370 and ESA/390'
url='http://www.hercules-390.eu/'
source=("http://downloads.hercules-390.eu/$pkgname-$pkgver.tar.gz")
license=('custom')
depends=('bzip2' 'libgcrypt' 'zlib' 'perl')
sha256sums=('85e217773587f2278d4b6cb6bc815e042cfc982d0fc14baa0da4a84dea399e9d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Change module extension from .la to .so.
  sed '/HDL_MODULE_SUFFIX/ s/\.la/.so/' -i hdl.h
  ./configure --prefix=/usr
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make prefix="$pkgdir/usr" install
  install -Dm644 COPYRIGHT \
    "$pkgdir/usr/share/licenses/hercules/qpl1"
}

# vim:set ts=2 sw=2 et:
