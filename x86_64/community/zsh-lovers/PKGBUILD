# $Id: PKGBUILD 110913 2014-05-09 13:51:20Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>
# Contributor: Patrick Leslie Polzer <polzer@gnu.org>
# Contributor: Aaron 'venisonslurpee' Laursen <venisonslurpee@gmail.com>

pkgname=zsh-lovers
pkgver=0.9.0
pkgrel=1
pkgdesc="A collection of tips, tricks and examples for the Z shell."
arch=('any')
url='http://grml.org/zsh/#zshlovers'
license=('GPL')
source=("https://github.com/grml/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('96de41a9b20a93b46f4adcfcf9948682')
makedepends=('asciidoc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/"
  a2x -vv --no-xmllint -f manpage zsh-lovers.1.txt
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/"
  install -Dm0644 "${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
