# Contributor: dickeny@gmail.com
# Maintainer:  cuihao <cuihao dot leo at gmail dot com>

pkgbase=man-pages-zh
pkgname=(man-pages-zh_cn man-pages-zh_tw)
_pkgname=manpages-zh
pkgver=1.5.2
pkgrel=3
arch=('any')
url=('https://github.com/lidaobing/manpages-zh')
license=('GPL2')
makedepends=("zh-autoconvert")
depends=('man-db')
conflicts=('man-pages-zh', 'manpages-zh')
source=("https://github.com/downloads/lidaobing/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2")
md5sums=('cab232c7bb49b214c2f7ee44f7f35900')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    ./configure --prefix=/usr
    make
}

package_man-pages-zh_cn() {
    pkgdesc='Simplified Chinese Linux man pages'

    cd "${srcdir}/${_pkgname}-${pkgver}"
    make install DESTDIR="${pkgdir}"

    rm -r "${pkgdir}/usr/share/man/zh_TW"

    # remove conflicted files with shadow
    cd "${pkgdir}/usr/share/man/zh_CN"
    rm -f \
        ./man1/chfn* \
        ./man1/chsh* \
        ./man1/groups* \
        ./man1/newgrp* \
        ./man5/passwd* \
        ./man8/chpasswd* \
        ./man8/groupadd* \
        ./man8/groupdel* \
        ./man8/groupmod* \
        ./man8/useradd* \
        ./man8/userdel* \
        ./man8/usermod*
}

package_man-pages-zh_tw() {
    pkgdesc='Traditional Chinese Linux man pages'

    cd "${srcdir}/${_pkgname}-${pkgver}"
    make install DESTDIR="${pkgdir}"

    rm -r "${pkgdir}/usr/share/man/zh_CN"

    # remove conflicted files with shadow
    cd "${pkgdir}/usr/share/man/zh_TW"
    rm -f \
        ./man1/chfn* \
        ./man1/chsh* \
        ./man1/groups* \
        ./man1/newgrp* \
        ./man5/passwd* \
        ./man8/chpasswd* \
        ./man8/groupadd* \
        ./man8/groupdel* \
        ./man8/groupmod* \
        ./man8/useradd* \
        ./man8/userdel* \
        ./man8/usermod*
}
