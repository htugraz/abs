# $Id: PKGBUILD 107169 2014-03-13 21:45:37Z seblu $
# Maintainer: Sébastien Luttringer <seblu@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=glances
pkgver=1.7.5
pkgrel=1
pkgdesc='CLI curses-based monitoring tool'
arch=('any')
url='https://github.com/nicolargo/glances'
license=('LGPL')
depends=('python2' 'python2-psutil' 'python2-setuptools')
optdepends=('python2-jinja: HTML export'
            'python2-sensors: temperature sensors support'
            'hddtemp: HDD temperature monitoring support')
source=("$url/archive/v$pkgver.tar.gz")
sha256sums=('1598d1c10f1d2b59ea8d90d5be899fd151b4af28640c6daae23258dc76f659fe')

prepare() {
  cd $pkgname-$pkgver
  sed 's|^#!/usr/bin/env python$|&2|' -i glances/glances.py
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
