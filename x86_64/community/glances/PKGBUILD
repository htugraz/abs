# $Id: PKGBUILD 113434 2014-06-22 19:17:21Z seblu $
# Maintainer: Sébastien Luttringer <seblu@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=glances
pkgver=2.0.1
pkgrel=1
pkgdesc='CLI curses-based monitoring tool'
arch=('any')
url='https://github.com/nicolargo/glances'
license=('LGPL')
depends=('python' 'python-psutil' 'python-setuptools')
optdepends=('python-jinja: HTML export'
            'python-bottle: web server support'
            'hddtemp: HDD temperature monitoring support')
source=("https://pypi.python.org/packages/source/G/Glances/Glances-$pkgver.tar.gz")
md5sums=('313bc2a161fb6820f554bd3e850f3f8d')

package() {
  cd Glances-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  # remove html doc
  rm -r "$pkgdir/usr/share/doc/$pkgname"/{glances-doc.html,images}
}

# vim:set ts=2 sw=2 et:
