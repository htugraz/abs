# $Id: PKGBUILD 106785 2014-03-07 15:58:35Z svenstaro $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=phpldapadmin
pkgver=1.2.3
pkgrel=7
pkgdesc="A PHP and hence web-based tool to administrate LDAP over the WWW"
arch=('any')
url="http://phpldapadmin.sourceforge.net/"
license=('GPL')
depends=('libldap' 'php' 'php-ldap')
makedepends=('optipng')
backup=("etc/webapps/phpldapadmin/config.php")
source=(http://downloads.sourceforge.net/project/phpldapadmin/phpldapadmin-php5/$pkgver/phpldapadmin-$pkgver.tgz
        phpldapadmin-1.2.3-php5_5.patch )
md5sums=('59a49054039ceb852c2da377d8742360'
         '45ebc875c588ff6756369b949ffe7401')

build() {
  cd $srcdir/phpldapadmin-${pkgver}/
  find -name '*.png' -exec optipng -quiet -force -fix {} \;
  # patch from http://pkgs.fedoraproject.org/cgit/phpldapadmin.git/tree/
  patch -p1 -i $srcdir/phpldapadmin-1.2.3-php5_5.patch
}

package() {
  _instdir=$pkgdir/usr/share/webapps/phpldapadmin
  mkdir -p ${_instdir} $pkgdir/etc/webapps/phpldapadmin
  cd ${_instdir}

  cp -ra $srcdir/phpldapadmin-${pkgver}/* .

  ln -s /etc/webapps/phpldapadmin/config.php ${_instdir}/config/config.php
  cp ${_instdir}/config/config.php.example $pkgdir/etc/webapps/phpldapadmin/config.php
}
