# $Id: PKGBUILD 116686 2014-08-01 10:13:26Z foutrelis $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Christopher Rosell <chrippa@tanuki.se>
pkgname=livestreamer
pkgver=1.9.0
pkgrel=1
pkgdesc='CLI program that launches streams from various streaming services in a custom video player'
arch=('any')
url='https://github.com/chrippa/livestreamer'
license=('BSD')
depends=('python-requests' 'rtmpdump' 'python-setuptools')
makedepends=('python-sphinx')
provides=('livestreamer')
conflicts=('livestreamer-git')
options=(!emptydirs)
source=(http://pypi.python.org/packages/source/l/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('d450f1352fe8f9c65cbecf6415c6d404c45334870321224253b4c1a3dac4e28c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py build_sphinx -b man
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 build/sphinx/man/livestreamer.1 \
    "$pkgdir/usr/share/man/man1/livestreamer.1"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
