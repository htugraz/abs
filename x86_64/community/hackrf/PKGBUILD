# $Id: PKGBUILD 138100 2015-08-08 03:37:48Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=hackrf
pkgver=2015.07.2
pkgrel=1
pkgdesc="Driver for HackRF, allowing general purpose software defined radio (SDR)."
arch=('i686' 'x86_64')
url="https://github.com/mossmann/hackrf"
license=('GPL2')
depends=('libusb')
makedepends=('cmake')

#source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz"
source=("https://github.com/mossmann/hackrf/releases/download/v${pkgver}/hackrf-$pkgver.tar.xz"
        'hackrf.conf')
md5sums=('67595d2dad1c299138baf9a85ec37341'
         '01ad1ba4a27d820e8f9abeb5fa88bfc6')

build() {
  cd "$srcdir/$pkgname-$pkgver/host"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  pushd host/build
  make DESTDIR="$pkgdir" install
  popd
  pushd host/libhackrf
  install -vD -m644 53-hackrf.rules "$pkgdir/usr/lib/udev/rules.d/53-hackrf.rules"
  popd
  pushd firmware-bin
  install -vD -m644 hackrf_jawbreaker_usb_rom_to_ram.bin "$pkgdir/usr/share/hackrf/hackrf_jawbreaker_usb_rom_to_ram.bin"
  install -vD -m644 hackrf_one_usb_rom_to_ram.bin "$pkgdir/usr/share/hackrf/hackrf_usb_rom_to_ram.bin"
  install -Dm644 "$srcdir/hackrf.conf" "$pkgdir/etc/modprobe.d/hackrf.conf"
}
