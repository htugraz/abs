# $Id: PKGBUILD 95853 2013-08-17 15:15:02Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: wain <wain.at.gmx.net>

pkgname=calcurse
pkgver=3.1.4
pkgrel=2
pkgdesc='A text-based personal organizer.'
arch=('i686' 'x86_64')
url='http://calcurse.org/'
license=('BSD')
depends=('ncurses')
install='calcurse.install'
source=("http://calcurse.org/files/${pkgname}-${pkgver}.tar.gz"{,.asc})
md5sums=('e354bdb7636daffa99ffb98534d04cb2'
         '44baaf17198e2fd75d2d160dedee814f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --enable-docs --without-asciidoc --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
