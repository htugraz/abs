# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: spider-mario <spidermario@free.fr>

pkgname=afl
pkgver=1.92b
pkgrel=1
pkgdesc='Security-oriented fuzzer using compile-time instrumentation and genetic algorithms'
arch=('i686' 'x86_64')
url="http://lcamtuf.coredump.cx/afl/"
license=('Apache')
depends=('glibc' 'bash')
optdepends=(
  'gcc: gcc instrumentation support'
  'clang: clang instrumentation support'
)
provides=('american-fuzzy-lop')
replaces=('american-fuzzy-lop')
options=('!emptydirs' '!strip')
source=(${pkgname}-${pkgver}.tgz::http://lcamtuf.coredump.cx/${pkgname}/releases/${pkgname}-${pkgver}.tgz)
sha512sums=('abb2c6cc9d28d522b59d28821e2eb7b82e4cc37212f1a02c848e9ef05fed008a0f3f57c8defb3384880700f9da23412a9ac77bd6afd7fbb30f0e4ad501e04148')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
