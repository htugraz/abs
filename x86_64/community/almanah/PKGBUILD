# $Id: PKGBUILD 98185 2013-10-07 12:08:18Z jgc $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=almanah
pkgver=0.10.1
pkgrel=4
pkgdesc="Small GTK+ application to allow you to keep a diary of your life"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Almanah_Diary"
license=('GPL')
depends=('evolution-data-server' 'gtkspell3' 'libcryptui' 'xdg-utils')
makedepends=('intltool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        eds-3.8.patch
        update-gtkspell3-support.patch)
sha256sums=('122789c1baea1713102b200c0401d25395ec177a66ae22635d3ae2fecc1f08bd'
            '2407cb2cc11e61b7863bf20fc13d4614a3384e8cb7c5b534f1e3ce7a3afb2d2d'
            '60fedd6be05ef2619c3b50539b9fdc717480e4f03a2920d8b68b09e63dc7e7db')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Port to evolution-data-server 3.8
  patch -Np1 -i "$srcdir/eds-3.8.patch"

  # Port to the new gtkspell3
  patch -Np1 -i "$srcdir/update-gtkspell3-support.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
