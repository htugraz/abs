# $Id: PKGBUILD 132739 2015-05-04 17:06:46Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Bj?indeijer <bjorn@lindeijer.nl>

pkgname=enet
pkgver=1.3.13
pkgrel=1
pkgdesc='A relatively thin, simple and robust network communication layer on top of UDP'
arch=('i686' 'x86_64')
url='http://enet.bespin.org/'
license=('custom')
depends=('glibc')
source=("http://enet.bespin.org/download/${pkgname}-${pkgver}.tar.gz")
sha1sums=('5bcf9aa1a8f2e54cc85c1435d7dac0970d382b7e')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
