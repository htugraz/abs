# $Id: PKGBUILD 103275 2014-01-02 08:50:55Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv
pkgver=0.3.1
pkgrel=2
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=(
         'portaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libquvi'
         'libxinerama' 'mpg123' 'libxv' 'libxkbcommon' 'libva' 'lirc-utils'
         'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua' 'libdvdnav'
        )
makedepends=('mesa' 'python-docutils' 'waf')
options=('!emptydirs' '!buildflags')
install=mpv.install
source=(https://github.com/mpv-player/$pkgname/archive/v$pkgver.tar.gz
        mpv-0.3.1-always_respect_sector_size.patch)
md5sums=('f7e4091eb9ce058ce35b4bc1b6c4f3a2'
         'afe01937e9bff1f241d37774eda39b7a')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../mpv-0.3.1-always_respect_sector_size.patch
}

build() {
  cd $pkgname-$pkgver

  waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-joystick \
    --enable-wayland \
    --disable-radio \
    --disable-radio-capture \
    --disable-radio-v4l2 \
    --disable-libbs2b \
    --disable-direct3d \
    --disable-corevideo \
    --disable-cocoa \
    --disable-coreaudio
  waf build
}

package() {
  cd $pkgname-$pkgver
  waf install --destdir="$pkgdir"

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
