# $Id: PKGBUILD 108557 2014-03-28 15:46:57Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv
pkgver=0.3.6
pkgrel=2
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=(
  'portaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libquvi' 'libgl'
  'libxinerama' 'mpg123' 'libxv' 'libxkbcommon' 'libva' 'lirc-utils' 'wayland'
  'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua' 'libdvdnav'
)
makedepends=('mesa' 'python-docutils' 'waf' 'ladspa')
options=('!emptydirs' '!buildflags')
install=mpv.install
source=(https://github.com/mpv-player/$pkgname/archive/v$pkgver.tar.gz)
md5sums=('11a8bb5f10056b7cc0dbedd38fe6e581')

build() {
  cd $pkgname-$pkgver

  waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-joystick

  waf build
}

package() {
  cd $pkgname-$pkgver
  waf install --destdir="$pkgdir"

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
