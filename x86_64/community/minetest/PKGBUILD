# $Id: PKGBUILD 116783 2014-08-03 07:42:02Z bpiotrowski $
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Konsta Kokkinen <kray@tsundere.fi>

pkgname=minetest
pkgver=0.4.10
pkgrel=2
pkgdesc='An Infiniminer/Minecraft inspired game'
arch=('i686' 'x86_64')
url='http://minetest.net/'
license=('GPL')
depends=('sqlite' 'freetype2' 'leveldb' 'openal' 'libvorbis' 'curl' 'irrlicht'
         'hicolor-icon-theme' 'luajit')
makedepends=('cmake')
install=minetest.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/minetest/${pkgname}/tarball/${pkgver}
	${pkgname}_game-${pkgver}.tar.gz::https://github.com/minetest/${pkgname}_game/tarball/${pkgver})
sha256sums=('05d15c8e2c8e53ee9d3006e79c2b59200311a7b879220d8b6f6a6fd2acf806b6'
            '7624a1e64124c4438256719ccecfc44fe5139733a076394aedad1f785e50019f')

build() {
  cd "$srcdir"/minetest-minetest-*

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_GETTEXT=on \
    -DENABLE_FREETYPE=1 \
    -DENABLE_LEVELDB=on \
    -DLUA_INCLUDE_DIR=/usr/include/luajit-2.0 \
    -DLUA_LIBRARY=//usr/lib/libluajit-5.1.so.2
  make
}

package() {
  cd "$srcdir"/minetest-minetest-*

  make DESTDIR="$pkgdir" install

  mv -v "$srcdir"/minetest-minetest_game-* "$pkgdir"/usr/share/minetest/games/minetest
  
  # small cleanup
  rm "$pkgdir"/usr/share/minetest/games/minetest/.gitignore
}
