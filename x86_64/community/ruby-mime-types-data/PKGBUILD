# $Id: PKGBUILD 150041 2015-12-07 16:38:01Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

_gemname=mime-types-data
pkgname=ruby-$_gemname
pkgver=3.2015.1120
pkgrel=1
pkgdesc='provides a registry for information about MIME media type definitions'
arch=(any)
url='https://github.com/mime-types/mime-types-data/'
license=(MIT)
depends=(ruby)
options=(!emptydirs)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha1sums=('7e0ba3069053eb0aa536fa9bf08f9de59d7bc5df')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
}
