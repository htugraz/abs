# $Id: PKGBUILD 121398 2014-10-25 12:53:18Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: xgdgsc<xgdgsc@gmail.com>
# Contributor: Philanecros Heliostein <philanecros@gmail.com>

pkgname=isatapd
pkgver=0.9.7
pkgrel=4
pkgdesc="Creates and maintains an ISATAP tunnel (rfc5214) in Linux"
arch=("i686" "x86_64")
url="http://www.saschahlusiak.de/linux/isatap.htm"
license=('GPL')
depends=('glibc')
source=("http://www.saschahlusiak.de/linux/${pkgname}-${pkgver}.tar.gz"
        isatapd@.service)
md5sums=('79f13a360b9a14cb0afcdf7a2af9c6de'
         'e33e0a5b31f958b9d52c345439b455af')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -Dm644 "${srcdir}/${pkgname}@.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}@.service"
}
