# $Id: PKGBUILD 142348 2015-10-01 16:02:36Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Allen Li <darkfeline at abagofapples.com>

pkgbase=flake8
pkgname=(flake8 python2-flake8)
pkgver=2.4.1
pkgrel=3
epoch=1
pkgdesc="The modular source code checker: pep8, pyflakes and co"
arch=('any')
url="http://bitbucket.org/tarek/flake8"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyflakes' 'python2-pyflakes'
             'pep8' 'python2-pep8' 'python-mccabe' 'python2-mccabe')
checkdepends=('python-nose' 'python2-nose' 'python2-mock')
source=("https://pypi.python.org/packages/source/f/${pkgbase}/${pkgbase}-${pkgver}.tar.gz"
        'fix-flake8-tests-using-mock-to-work-with-1.1.x-branch.patch')
md5sums=('ed45d3db81a3b7c88bd63c6e37ca1d65'
         '76004de2511275c09fba49c43691aaf0')

prepare() {
  # We have patched pep8 to fix the problem, allowing 1.6.2)
  sed -e 's/pep8 >= 1.5.7, != 1.6.0, != 1.6.1, != 1.6.2/pep8 >= 1.5.7, != 1.6.0, != 1.6.1/' \
      -e 's/pyflakes >= 0.8.1, < 0.9/pyflakes >= 0.8.1/' \
      -i $pkgbase-$pkgver/setup.py

  # Fix test to work with Python 3.5's unittest.mock
  patch -d ${pkgbase}-${pkgver} -Np1 <fix-flake8-tests-using-mock-to-work-with-1.1.x-branch.patch

  cp -a ${pkgbase}-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py test

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py test
}

package_flake8() {
  depends=('python-pyflakes' 'python-mccabe' 'pep8')
  provides=('flake8-python3')
  replaces=('flake8-python3')
  conflicts=('flake8-python3')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --prefix=/usr --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-flake8() {
  depends=('python2-pyflakes' 'python2-mccabe' 'python2-pep8')
  provides=('flake8-python2')
  replaces=('flake8-python2')
  conflicts=('flake8-python2')

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}/" --prefix=/usr --optimize=1
  mv "${pkgdir}/usr/bin/flake8" "${pkgdir}/usr/bin/flake8-python2"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
