# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Allen Li <darkfeline at abagofapples.com>

pkgbase=flake8
pkgname=(flake8 flake8-python2)
pkgver=2.1.0
pkgrel=3
epoch=1
pkgdesc="The modular source code checker: pep8, pyflakes and co"
arch=('any')
url="http://bitbucket.org/tarek/flake8"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyflakes' 'python2-pyflakes'
             'pep8-python3' 'pep8-python2' 'python-mccabe' 'python2-mccabe')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock')
source=("http://pypi.python.org/packages/source/f/${pkgbase}/${pkgbase}-${pkgver}.tar.gz")
md5sums=('cf326cfb88a1db6c5b29a3a6d9efb257')

prepare() {
  cp -r ${pkgbase}-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py test

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py test
}

package_flake8() {
  depends=('python' 'python-pyflakes' 'python-mccabe' 'pep8-python3')
  provides=('flake8-python3')
  replaces=('flake8-python3')
  conflicts=('flake8-python3')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --prefix=/usr --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_flake8-python2() {
  depends=('python2' 'python2-pyflakes' 'python2-mccabe' 'pep8-python2')
  provides=('python2-flake8')
  replaces=('python2-flake8')
  conflicts=('python2-flake8')

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}/" --prefix=/usr --optimize=1
  mv "${pkgdir}/usr/bin/flake8" "${pkgdir}/usr/bin/flake8-python2"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
