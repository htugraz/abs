# $Id: PKGBUILD 145753 2015-11-03 08:08:50Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-extras
pkgname=('python-extras' 'python2-extras')
pkgver=0.0.3
pkgrel=1
pkgdesc="Useful extra bits for Python - things that shold be in the standard library"
arch=('any')
license=('MIT')
url="https://github.com/testing-cabal/extras"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-testtools' 'python2-testtools')
source=("git+https://github.com/testing-cabal/extras.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a extras{,-py2}
}

build() {
  cd "$srcdir/extras"
  python setup.py build

  cd "$srcdir/extras-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/extras"
  python -m testtools.run testtools.tests.test_suite

  cd "$srcdir/extras-py2"
  python2 -m testtools.run testtools.tests.test_suite
}

package_python-extras() {
  depends=('python')

  cd extras
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-extras() {
  depends=('python2')

  cd extras-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
