# $Id: PKGBUILD 103786 2014-01-11 19:56:49Z stativ $
# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
# Contributor: Bogdan Szczurek <thebodzio(at)gmail.com>
# Contributor: Vaclav Kramar <vaclav.kramar@tiscali.cz>
# Contributor: Archie <mymaud@gmail.com>

pkgname=rawtherapee
pkgver=4.0.12
pkgrel=1
epoch=1
pkgdesc="RAW photo editor"
arch=('i686' 'x86_64')
url="http://www.rawtherapee.com/"
license=('GPL3')
depends=('fftw' 'gtkmm' 'libcanberra' 'libiptcdata' 'lcms2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('cmake' 'mercurial')
#makedepends=('cmake')
install=rawtherapee.install
source=("rawtherapee-$pkgver::hg+https://code.google.com/p/rawtherapee/#tag=$pkgver")
md5sums=('SKIP')
#source=("http://rawtherapee.googlecode.com/files/rawtherapee-$pkgver.tar.xz")
#md5sums=('d30a54e6446326d8299c06ec76c281d8')

build() {
  mkdir "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
