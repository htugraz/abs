# $Id: PKGBUILD 109419 2014-04-14 18:46:41Z jelle $
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Jelle van der Waa <jelle vdwaa nl>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Thomas Holmquist <thomas@vorget.com>
# Contributor: Jan Boehringer <ja(a)bm-boehringer.de>
# Contributor: Alex Griffin <griffin.aj(a)gmail.com>

pkgname=polipo
pkgver=1.1.0
pkgrel=1
pkgdesc="A small and fast caching web proxy."
arch=('i686' 'x86_64')
url="http://www.pps.jussieu.fr/~jch/software/polipo/"
license=('GPL')
depends=('bash')
makedepends=('texinfo')
install=polipo.install
source=("http://www.pps.univ-paris-diderot.fr/~jch/software/files/polipo/polipo-$pkgver.tar.gz"
        "polipo.service")
md5sums=('55c6fc19e8d484997f223a56f0bd26ca'
         '3861a067f6cdd2b8afcee1de97678902')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make 	PREFIX="$pkgdir/usr" \
        MANDIR="$pkgdir/usr/share/man" \
        INFODIR="$pkgdir/usr/share/info" \
        LOCAL_ROOT="$pkgdir/usr/share/polipo/www" \
        DISK_CACHE_ROOT="$pkgdir/var/cache/polipo" \
        install

  # install config files
  install -Dm 644 config.sample "$pkgdir/etc/polipo/config.sample"
  install -Dm 644 forbidden.sample "$pkgdir/etc/polipo/forbidden.sample"

  # install license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/polipo/LICENSE"

  # install systemd service
  install -Dm 644 "$srcdir/polipo.service" "$pkgdir/usr/lib/systemd/system/polipo.service"

  install -dm 700 -o 185 -g 185 "$pkgdir/var/cache/polipo"
}

# vim:set ts=2 sw=2 et:
