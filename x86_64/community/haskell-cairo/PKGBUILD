# $Id: PKGBUILD 92370 2013-06-04 12:29:46Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-cairo
pkgver=0.12.4
pkgrel=5
pkgdesc="Binding to the cairo library for Gtk2Hs."
url="http://hackage.haskell.org/package/cairo"
license=('LGPL2.1')
arch=('i686' 'x86_64')
# deps adjusted to specific versions in package()
depends=('ghc' 'cairo' 'haskell-mtl')
makedepends=("gtk2hs-buildtools")
options=('strip')
conflicts=('gtk2hs-cairo')
provides=('gtk2hs-cairo')
replaces=('gtk2hs-cairo')
install=gtk2hs-cairo.install
source=(http://hackage.haskell.org/packages/archive/cairo/$pkgver/cairo-$pkgver.tar.gz)
md5sums=('b9d7e8de6ba4db680c222958622d6ee6')

build() {
    cd ${srcdir}/cairo-$pkgver
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/haskell-cairo \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
    _hmtlver=`pacman -Q haskell-mtl | cut -f2 -d\  | cut -f1 -d-`
    depends=("ghc=${_ghcver}" "cairo" "haskell-mtl=${_hmtlver}")

    cd ${srcdir}/cairo-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/gtk2hs-cairo/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/gtk2hs-cairo/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/haskell-cairo/html ${pkgdir}/usr/share/doc/ghc/html/libraries/cairo
    runhaskell Setup copy --destdir=${pkgdir}
}
