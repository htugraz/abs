# $Id: PKGBUILD 100263 2013-11-02 08:45:17Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

pkgname=openmotif
pkgver=2.3.4
pkgrel=3
pkgdesc="Open Motif"
arch=('i686' 'x86_64')
url="http://www.motifzone.org/"
license=('GPL')
depends=(libxext libxp libxft libxt libxmu libpng libjpeg)
makedepends=(xbitmaps xproto printproto xextproto automake)
conflicts=(lesstif)
source=("http://downloads.sourceforge.net/project/motif/Motif%20$pkgver%20Source%20Code/motif-$pkgver-src.tgz")
md5sums=('612bb8127d0d31da6e5474edf8a5c247')

build() {
  cd $srcdir/motif-$pkgver
  export LDFLAGS="-lX11"
  sed -i \
    -e 's|AM_CONFIG_HEADER|AC_CONFIG_HEADER|g' \
    -e 's|AM_PROG_CC_STDC|AC_PROG_CC|g' \
    configure.ac
  [ -x configure ] || ./autogen.sh --prefix=/usr
  [ -f Makefile ] || ./configure --prefix=/usr
  make -j1
}

package() {
  cd $srcdir/motif-$pkgver
  make DESTDIR=$pkgdir install
}
