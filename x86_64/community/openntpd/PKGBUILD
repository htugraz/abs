# $Id: PKGBUILD 143008 2015-10-05 14:02:15Z anatolik $
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Mark Rosenstand <mark@borkware.net>
# Contributor: Giorgio Lando <patroclo7@gmail.com> (adjtimex patch)
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=openntpd
pkgver=5.7p4
pkgrel=1
pkgdesc='Free, easy to use implementation of the Network Time Protocol.'
url='http://www.openntpd.org/'
arch=('x86_64' 'i686')
license=('BSD')
depends=('openssl')
conflicts=('ntp')
backup=('etc/ntpd.conf')
install=$pkgname.install
source=("ftp://ftp.openbsd.org/pub/OpenBSD/OpenNTPD/$pkgname-$pkgver.tar.gz"
        'openntpd.tmpfiles'
        'openntpd.service')

sha256sums=('a993d95976e375acc0ab1a677fd268f55024477835633c8ae404895046bccb23'
            'fe12841110c3c080519e248988c4b6334f54bd9646b015753c7e15de2a9600c5'
            '3239fc6f69d661cd9233233da9e68bebdf7b12888febbc2f2d794742db2d8ed1')

build() {
  cd $pkgname-$pkgver
  autoreconf -fi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --with-privsep-user=ntp \
    --with-privsep-path=/run/openntpd/ \
    --with-adjtimex
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname-$pkgver/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  sed -i 's/\*/0.0.0.0/' "$pkgdir/etc/ntpd.conf"

  install -d -m700 "$pkgdir/var/lib/ntp"
  install -Dm644 "$srcdir/openntpd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/openntpd.conf"
  install -Dm644 "$srcdir/openntpd.service" "$pkgdir/usr/lib/systemd/system/openntpd.service"

  install -dm755 "$pkgdir/usr/lib/systemd/ntp-units.d"
  echo "$pkgname.service" > "$pkgdir/usr/lib/systemd/ntp-units.d/$pkgname.list"
}

# vim:set ts=2 sw=2 et:
