# $Id: PKGBUILD 120880 2014-10-17 14:32:13Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alexsandr Pavlov <kidoz@mail.ru>

pkgname=ruby-mime-types
pkgver=2.4.2
pkgrel=1
pkgdesc='MIME Content-Type database'
arch=('any')
url='http://mime-types.rubyforge.org'
license=('GPL2' 'MIT' 'PerlArtistic')
depends=('ruby')
options=('!emptydirs')
source=("http://rubygems.org/downloads/mime-types-${pkgver}.gem")
noextract=("mime-types-${pkgver}.gem")
sha256sums=('dd2abe5850102ad0033ff43d3772e92a337495e7fc3b3d142b704f5d045502fb')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} mime-types-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/ruby-mime-types
  ln -s ${_gemdir}/gems/mime-types-${pkgver}/Licence.rdoc "${pkgdir}"/usr/share/licenses/ruby-mime-types/
  ln -s ${_gemdir}/gems/mime-types-${pkgver}/docs/COPYING.txt "${pkgdir}"/usr/share/licenses/ruby-mime-types/
}

# vim: ts=2 sw=2 et:
