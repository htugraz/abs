# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-libarchive-c
pkgname=('python-libarchive-c' 'python2-libarchive-c')
pkgver=2.2
pkgrel=1
pkgdesc='Python interface to libarchive'
url='https://github.com/Changaco/python-libarchive-c'
arch=('any')
license=('LGPL2.1')
makedepends=('python-setuptools' 'python2-setuptools' 'libarchive')
checkdepends=('python-pytest-cov' 'python2-pytest-cov')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/Changaco/python-libarchive-c/archive/${pkgver}.tar.gz)
sha512sums=('c39057e893f3f3b48e823d4451298b101e1421944897d56a22d9c2f2d35cbd94ee8b6fc2fbd8542f83fb7cb62265b7d1afcc214e249d64938512295dd3cf0235')

check() {
  cd ${pkgbase}-${pkgver}
  py.test
  py.test2
}

package_python-libarchive-c() {
  depends=('python' 'libarchive')

  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

package_python2-libarchive-c() {
  depends=('python2' 'libarchive')

  cd ${pkgbase}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
