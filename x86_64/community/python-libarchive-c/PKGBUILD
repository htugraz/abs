# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-libarchive-c
pkgname=('python-libarchive-c' 'python2-libarchive-c')
pkgver=2.1
pkgrel=1
pkgdesc='Python interface to libarchive'
url='https://github.com/Changaco/python-libarchive-c'
arch=('any')
license=('LGPL2.1')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'libarchive')
checkdepends=('python-pytest-cov' 'python2-pytest-cov')
source=(${pkgbase}::"git+https://github.com/Changaco/python-libarchive-c#tag=${pkgver}")
sha512sums=('SKIP')

check() {
  cd ${pkgbase}
  py.test
  py.test2
}

package_python-libarchive-c() {
  depends=('python' 'libarchive')

  cd ${pkgbase}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

package_python2-libarchive-c() {
  depends=('python2' 'libarchive')

  cd ${pkgbase}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
