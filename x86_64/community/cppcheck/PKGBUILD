# $Id: PKGBUILD 121337 2014-10-24 08:40:18Z jelle $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=cppcheck
pkgver=1.67
pkgrel=1
pkgdesc="A tool for static C/C++ code analysis"
arch=('i686' 'x86_64')
url="http://cppcheck.wiki.sourceforge.net/"
license=('GPL')
depends=('qt5-base' 'python-pygments')
makedepends=('docbook-xsl' 'qt5-tools' 'python')
source=(https://github.com/danmar/cppcheck/archive/${pkgver}.tar.gz)
sha1sums=('14b886e5cac631cec11a3f8efbdeaed15ddcc7d3')

build() {
   cd "${pkgname}-${pkgver}"
   # Unicode fix
   LANG='en_US.UTF-8' make SRCDIR=build CFGDIR=/usr/share/cppcheck/cfg
   make DB2MAN=/usr/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl man

   cd gui
   qmake HAVE_RULES=yes
   make SRCDIR=build CFGDIR=/usr/share/cppcheck/cfg HAVE_RULES=yes
}

check() {
   cd "${pkgname}-${pkgver}"

   LANG='en_US.UTF-8' make CFGDIR=./cfg  HAVE_RULES=yes test
}

package() {
   cd "${pkgname}-${pkgver}"
   LANG='en_US.UTF-8' make DESTDIR=${pkgdir} SRCDIR=build HAVE_RULES=yes install 

   install -D -p -m 644 cppcheck.1 "${pkgdir}"/usr/share/man/man1/cppcheck.1

   install -m755 gui/cppcheck-gui "${pkgdir}"/usr/bin

   install -d "${pkgdir}"/usr/share/cppcheck/cfg
   install -D ./cfg/* -t "${pkgdir}"/usr/share/cppcheck/cfg
}
