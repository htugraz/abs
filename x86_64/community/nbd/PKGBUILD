# $Id: PKGBUILD 146575 2015-11-11 11:49:07Z eworm $
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gerhard Brauer <gerbra@archlinux.de>

pkgname=nbd
pkgver=3.12.1
pkgrel=1
pkgdesc="tools for network block devices, allowing you to use remote block devices over TCP/IP"
arch=('i686' 'x86_64')
url="http://nbd.sourceforge.net"
license=('GPL')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz
	config nbd.service)
backup=('etc/nbd-server/config')
depends=('glib2')
install=nbd.install
md5sums=('055eb1c5a95cc6976256dcc84ae63abb'
         '2d05d426b8c2708d5f8a0d028fcbae05'
         '5d1acc143018117174f79eff15afa038')

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --enable-syslog --sbindir=/usr/bin

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m644 "${srcdir}"/nbd.service "${pkgdir}"/usr/lib/systemd/system/nbd.service

  install -D -m644 doc/README "${pkgdir}"/usr/share/doc/nbd/README
}
