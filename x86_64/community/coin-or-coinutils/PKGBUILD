# $Id: PKGBUILD 128876 2015-03-07 23:05:23Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tikysal <tikysal@gmail.com>
# Derived from Daniel Ehlers'

pkgname=coin-or-coinutils
pkgver=2.10.4
pkgrel=1
pkgdesc="COIN-OR collection of utility classes"
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/CoinUtils"
license=('EPL')
depends=('zlib' 'bzip2' 'glpk' 'lapack')
makedepends=('gcc-fortran')
groups=('coin-or')
source=("http://www.coin-or.org/download/source/CoinUtils/CoinUtils-${pkgver}.tgz")
sha1sums=('cc71d2b3cbe73548f87e14a6ab959076f9d2a30c')

build() {
  cd CoinUtils-$pkgver

  ./configure --prefix=/usr --with-blas-lib='-lblas' --with-lapack-lib='-llapack' --with-glpk-lib='-lglpk' --enable-dependency-linking
  make
}

check() {
  cd CoinUtils-$pkgver
  make test
}

package() {
  cd CoinUtils-$pkgver
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir" install
}
