# $Id: PKGBUILD 145144 2015-10-28 01:55:29Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_pypiname=dnslib
pkgbase=python-$_pypiname
pkgname=(python-$_pypiname python2-$_pypiname)
pkgver=0.9.5
pkgrel=1
pkgdesc="Simple library to encode/decode DNS wire-format packets"
arch=('any')
url=("https://bitbucket.org/paulc/dnslib")
license=('custom:BSD')
makedepends=('python' 'python2')
source=("https://pypi.python.org/packages/source/d/$_pypiname/$_pypiname-${pkgver}.tar.gz")
md5sums=('ab26665b468a74b01d0882bc6b67f12a')

prepare() {
  cp -a ${_pypiname}-${pkgver} python2-${_pypiname}-${pkgver}
}

package_python-dnslib() {
  depends=("python")

  cd ${_pypiname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
}

package_python2-dnslib() {
  depends=("python2")

  cd python2-${_pypiname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
