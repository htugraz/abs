# $Id: PKGBUILD 147816 2015-11-26 05:34:42Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-nautilus-properties
pkgver=3.14.2
pkgrel=2
pkgdesc="Provide file property dialog for Deepin desktop environment"
arch=('i686' 'x86_64')
license=('GPL')
depends=('nautilus' 'libnotify')
makedepends=('intltool' 'gobject-introspection' 'libnotify' 'git')
url="https://gitcafe.com/Deepin/deepin-nautilus-properties"
options=('!emptydirs')
source=("git+https://github.com/linuxdeepin/deepin-nautilus-properties.git")
sha256sums=('SKIP')

build() {
  cd deepin-nautilus-properties

  # regenerate autotools files
  libtoolize && aclocal && autoheader && automake --add-missing && autoconf

  ./configure --prefix=/usr \
              --libexecdir=/usr/lib/nautilus \
              --disable-nst-extension \
              --disable-update-mimedb \
              --disable-packagekit \
              --disable-introspection \
              --disable-tracker
  make
}

package() {
  cd deepin-nautilus-properties/src
  install -dm755 "$pkgdir/usr/bin"
  libtool --mode=install /usr/bin/install -c deepin-nautilus-properties "$pkgdir/usr/bin"
}
