# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=xtrabackup
pkgver=2.1.7
pkgrel=2
pkgdesc='Non-blocking backup tool for MySQL'
arch=('i686' 'x86_64')
url='http://www.percona.com/software/percona-xtrabackup/'
license=('GPL')
depends=('libaio' 'libgcrypt' 'openssl')
makedepends=('cmake' 'python-sphinx' 'zlib')
source=(http://percona.com/redir/downloads/XtraBackup/XtraBackup-$pkgver/source/percona-xtrabackup-$pkgver.tar.gz)
md5sums=('cce4ad2e6e1481a98cd403563fd989dc')

build() {
  cd percona-xtrabackup-$pkgver
  utils/build.sh innodb55

  cd doc
  make man
}

package() {
  cd percona-xtrabackup-$pkgver

  install -D innobackupex "$pkgdir"/usr/bin/innobackupex
  install -D src/xtrabackup_innodb55 "$pkgdir"/usr/bin/xtrabackup_55
  install -D src/xbcrypt "$pkgdir"/usr/bin/xbcrypt
  install -D src/xbstream "$pkgdir"/usr/bin/xbstream

  ln -s /usr/bin/xtrabackup_55 "$pkgdir"/usr/bin/xtrabackup

  cd doc/build/man
  for file in *.1; do
    install -Dm0644 $file "$pkgdir"/usr/share/man/man1/$file
  done
}
