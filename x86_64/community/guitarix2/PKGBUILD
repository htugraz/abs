# $Id: PKGBUILD 148425 2015-12-05 06:27:26Z foutrelis $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=guitarix2
pkgver=0.34.0
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('i686' 'x86_64')
url="http://guitarix.sourceforge.net/"
license=('GPL')
depends=('gtkmm' 'liblrdf' 'lilv' 'bluez-libs' 'boost-libs' 'webkitgtk2'
         'zita-convolver' 'zita-resampler' 'desktop-file-utils')
makedepends=('python' 'python2' 'boost' 'eigen' 'gperf' 'intltool' 'lv2')
optdepends=('meterbridge: sound meters')
provides=('guitarix' 'gx_head')
conflicts=('guitarix' 'gx_head')
replaces=('guitarix' 'gx_head')
install="$pkgname.install"
source=("http://download.sourceforge.net/project/guitarix/guitarix/$pkgname-$pkgver.tar.bz2")
md5sums=('0ac5f18ee738d535b1551ea68ac2fce9')

build() {
  cd guitarix-$pkgver
  python2 waf configure --prefix=/usr \
                        --no-desktop-update \
                        --no-ldconfig \
                        --no-faust \
                        --shared-lib \
                        --lib-dev
  python2 waf build
}

package() {
  cd guitarix-$pkgver
  python2 waf install --destdir="$pkgdir"
}
