# $Id: PKGBUILD 100198 2013-11-01 21:12:34Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Gregory Haynes <greg@greghaynes.net>

pkgname=libinfinity
pkgver=0.5.4
pkgrel=2
pkgdesc="An implementation of the Infininote protocol written in GObject-based C"
arch=('i686' 'x86_64')
url="http://gobby.0x539.de"
license=('GPL')
depends=('gnutls>=1.7.2' 'gsasl>=0.2.21' 'glib2>=2.16' 'libxml2>=2.0' 'gtk2')
makedepends=('gtk-doc' 'pkgconfig')
optdepends=('avahi: zeroconf support'
            'gtk2: gtk support')
source=("http://releases.0x539.de/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('1e83ec8b3ed3159d0c883c056b153429')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --localstatedir=/var
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf $pkgdir/var
}
