# $Id: PKGBUILD 115724 2014-07-14 15:13:08Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=parcellite
pkgver=1.1.8
pkgrel=1
pkgdesc="Lightweight GTK+ clipboard manager"
arch=('i686' 'x86_64')
url="http://parcellite.sourceforge.net/"
license=('GPL3')
depends=('gtk2')
makedepends=('intltool')
source=(http://downloads.sourceforge.net/parcellite/parcellite-$pkgver.tar.gz
	fix-crash.patch)
md5sums=('473d4d9e937c7de3502d21abac891cd3'
         'f2cc1986e573d49c92ea3d81efd5d574')

prepare() {
  cd "$srcdir/parcellite-$pkgver"
  patch -Np1 <$srcdir/fix-crash.patch
}

build() {
  cd "$srcdir/parcellite-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/parcellite-$pkgver"
  make DESTDIR="$pkgdir" install
}
