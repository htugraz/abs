# $Id: PKGBUILD 118052 2014-08-29 11:58:46Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mpie <michael.kyne-phillips1@ntlworld.com>

pkgname=parrot
pkgver=6.7.0
#_rel=stable
_rel=devel
pkgrel=1
pkgdesc="Standalone VM that can execute bytecode compiled dynamic languages"
arch=('x86_64' 'i686')
url="http://www.parrotcode.org/"
license=('GPL')
depends=('icu' 'openssl' 'libffi')
makedepends=('perl-json')
optdepends=('freeglut')
options=('!makeflags')
source=(ftp://ftp.parrot.org/pub/parrot/releases/$_rel/$pkgver/$pkgname-$pkgver.tar.bz2)
md5sums=('6ad1759b203239b1a0f6be0534ef777f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  perl Configure.pl --prefix=/usr \
    --parrot_is_shared \
    --disable-rpath \
	--mandir=/usr/share/man \
    --optimize
  export LD_LIBRARY_PATH="${LD_LIBRARY_PATH}:$(pwd)/blib/lib"
  make all parrot_utils docs html
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-dev
  sed -i "s#$srcdir#/usr/src#" \
    $pkgdir/usr/lib/parrot/$pkgver/tools/lib/Parrot/Config/Generated.pm
}
