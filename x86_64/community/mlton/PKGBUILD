# $Id: PKGBUILD 120612 2014-10-13 14:26:12Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: tochiro@no.spam.mail.berlios.de
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Contributor: Brian De Wolf <arch@bldewolf.com>

pkgname=mlton
pkgver=20130715
pkgrel=5
pkgdesc='Whole-program, optimizing Standard ML compiler'
arch=('x86_64' 'i686')
url='http://mlton.org/'
license=('BSD' 'MIT' 'LGPL')
depends=('bash' 'gmp')
makedepends=('setconf')
options=('staticlibs')

if [[ $CARCH = x86_64 ]]; then
  source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.src.tgz" \
          "http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver-1.amd64-linux.tgz")
  sha256sums=('215857ad11d44f8d94c27f75e74017aa44b2c9703304bcec9e38c20433143d6c'
              'f6f912009b0946b73af88ff5f3e713b6be63b871696e2697ccf1564b56e7d03b')
else
  source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.src.tgz" \
          "http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver-1.x86-linux.tgz")
  sha256sums=('215857ad11d44f8d94c27f75e74017aa44b2c9703304bcec9e38c20433143d6c'
              '92d89627cf760c56d0e9727f328b006341bf5be283fc7d616a6276c26d95b2cf')
fi

build() {
  setconf usr/bin/mlton lib="$srcdir/usr/lib/mlton"
  PATH="$PATH:$srcdir/usr/bin" make -C "$pkgname-$pkgver" -j1 all-no-docs
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" -j1 install-no-docs

  # Copy over the handful of licenses and the README as explanation
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  cp "$pkgname-$pkgver/doc/license/"* "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
