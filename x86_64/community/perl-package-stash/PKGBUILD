# $Id: PKGBUILD 122476 2014-11-12 22:11:58Z eric $
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-package-stash
pkgver=0.37
pkgrel=1
pkgdesc="Routines for manipulating stashes"
arch=('any')
url="http://search.cpan.org/dist/Package-Stash"
license=('PerlArtistic' 'GPL')
depends=('perl-dist-checkconflicts' 'perl-package-stash-xs'
         'perl-package-deprecationmanager' 'perl-module-runtime')
checkdepends=('perl-test-fatal' 'perl-test-requires' 'perl-module-implementation')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/D/DO/DOY/Package-Stash-$pkgver.tar.gz)
sha1sums=('43fc4e362ac1a45eb27fe779806cbd96c891e2dc')

build() {
  cd Package-Stash-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Package-Stash-$pkgver
  make test
}

package() {
  cd Package-Stash-$pkgver
  make DESTDIR="$pkgdir" install
}
