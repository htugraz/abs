# $Id: PKGBUILD 147293 2015-11-21 09:11:39Z stativ $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=intellij-idea-libs
pkgver=15.0.1
_pkgver=143.382.35
pkgrel=1
epoch=1
pkgdesc="Architecture dependent libraries needed by the Intellij Idea IDE"
arch=('i686' 'x86_64')
url="http://www.jetbrains.org/"
license=('Apache')
depends=('glibc')
options=(!strip)
source=(http://download.jetbrains.com/idea/ideaIC-$pkgver.tar.gz)
sha256sums=('faca173b216cb3eeff53b499b432780a12c1543a27c36c0bef62cf2e82fc7cb5')

package() {
  cd "idea-IC-$_pkgver"

  [ $CARCH == "x86_64" ] && SUFFIX=64
  install -D -m755 bin/fsnotifier${SUFFIX} "$pkgdir"/usr/share/intellijidea-ce/bin/fsnotifier${SUFFIX}
  install -D -m644 bin/libbreakgen${SUFFIX}.so "$pkgdir"/usr/lib/libbreakgen${SUFFIX}.so
  
  # libpty
  [ $CARCH == "x86_64" ] && _ARCH=x86_64 || _ARCH=x86
  install -D -m644 lib/libpty/linux/$_ARCH/libpty.so "$pkgdir"/usr/share/intellijidea-ce/lib/libpty/linux/$_ARCH/libpty.so
}

# vim:set ts=2 sw=2 et:
