# $Id: PKGBUILD 122631 2014-11-16 10:25:37Z stativ $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=intellij-idea-libs
pkgver=14.0.1
_pkgver=139.225.3
pkgrel=1
epoch=1
pkgdesc="Architecture dependent libraries needed by the Intellij Idea IDE"
arch=('i686' 'x86_64')
url="http://www.jetbrains.org/"
license=('Apache')
depends=('glibc')
options=(!strip)
source=(http://download.jetbrains.com/idea/ideaIC-$pkgver.tar.gz)
md5sums=('f7bc51abd9e6aba88f7984d2d8a90648')

package() {
  [ $CARCH == "x86_64" ] && SUFFIX=64
  install -D -m755 "idea-IC-$_pkgver"/bin/fsnotifier${SUFFIX} "$pkgdir"/usr/share/intellijidea-ce/bin/fsnotifier${SUFFIX}
  install -D -m644 "idea-IC-$_pkgver"/bin/libbreakgen${SUFFIX}.so "$pkgdir"/usr/lib/libbreakgen${SUFFIX}.so
  
  # libpty
  [ $CARCH == "x86_64" ] && _ARCH=x86_64 || _ARCH=x86
  install -D -m644 "idea-IC-$_pkgver"/lib/libpty/linux/$_ARCH/libpty.so "$pkgdir"/usr/share/intellijidea-ce/lib/libpty/linux/$_ARCH/libpty.so
}

# vim:set ts=2 sw=2 et:
