# $Id: PKGBUILD 132555 2015-04-30 08:15:19Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=libutf8proc
pkgver=1.1.6
_netsurf=3.3
pkgrel=5
pkgdesc='C library for processing UTF-8 encoded Unicode strings'
arch=('x86_64' 'i686')
url='http://source.netsurf-browser.org/libutf8proc.git/'
license=('custom')
replaces=('utf8proc')
makedepends=('netsurf-buildsystem')
depends=('glibc')
source=("http://download.netsurf-browser.org/netsurf/releases/source-full/netsurf-all-$_netsurf.tar.gz")
sha256sums=('5d7e31e8be6230792bf682640ec16a75d7d67c3845521a7e4c304ae89a88ff1c')

build() {
  make -C "netsurf-all-$_netsurf/$pkgname" \
    PREFIX=/usr LIBDIR=lib COMPONENT_TYPE="lib-shared" 
}

package() {
  cd "netsurf-all-$_netsurf/$pkgname"

  make PREFIX=/usr LIBDIR=lib COMPONENT_TYPE="lib-shared" \
    DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
