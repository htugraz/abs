# $Id: PKGBUILD 114640 2014-07-02 12:45:06Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=wkhtmltopdf
pkgver=0.12.1
pkgrel=1
pkgdesc="Command line tools to render HTML into PDF and various image formats"
arch=('i686' 'x86_64')
url="http://wkhtmltopdf.org/"
license=('GPL3')
depends=('qtwebkit')
optdepends=('xorg-server: wkhtmltopdf needs X or Xvfb to operate')
source=($pkgname-$pkgver.tar.gz::https://github.com/wkhtmltopdf/wkhtmltopdf/archive/$pkgver.tar.gz)
sha256sums=('c830f26dc48c2f2ad1912599b1ed92efe3aaab3ec67b3fb69195abf58dc711c0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  qmake-qt4 wkhtmltopdf.pro
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/usr" install

  # Generate and install man page
  install -d "$pkgdir/usr/share/man/man1"
  LD_LIBRARY_PATH=bin ./bin/wkhtmltopdf --manpage \
    >"$pkgdir/usr/share/man/man1/wkhtmltopdf.1"
  LD_LIBRARY_PATH=bin ./bin/wkhtmltoimage --manpage \
    >"$pkgdir/usr/share/man/man1/wkhtmltoimage.1"
}

# vim:set ts=2 sw=2 et:
