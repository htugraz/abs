# $Id: PKGBUILD 152944 2015-12-11 12:15:30Z fyan $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=regex-posix
pkgname=haskell-regex-posix
pkgver=0.95.2
pkgrel=13
pkgdesc='The posix regex backend for regex-base'
url="http://hackage.haskell.org/package/$_hkgname"
license=('custom:BSD3')
arch=('x86_64' 'i686')
depends=('ghc=7.10.3' 'haskell-regex-base')
source=("http://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
install="$pkgname.install"
options=('staticlibs')
sha256sums=('56019921cd4a4c9682b81ec614236fea816ba8ed8785a1640cd66d8b24fc703e')

build() {
  cd "$_hkgname-$pkgver"

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared --prefix=/usr \
    --docdir="/usr/share/doc/$pkgname" --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$_hkgname-$pkgver"

  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -d -m755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s "/usr/share/doc/$pkgname/html" "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
