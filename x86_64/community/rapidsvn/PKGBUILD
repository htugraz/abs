# $Id: PKGBUILD 151904 2015-12-10 04:08:18Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: AqD <aquila.deus@gmail.com>
# Contributor: TheMadsen <arch@themadsens.dk>

pkgname=rapidsvn
pkgver=0.12.1
pkgrel=7
pkgdesc="A cross-platform GUI front-end for the Subversion revision system written in C++ using the wxWidgets framework."
url='http://rapidsvn.tigris.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('subversion' 'wxgtk2.8')
makedepends=('python2' 'automake' 'libtool')
source=(http://www.rapidsvn.org/download/release/$pkgver/rapidsvn-$pkgver.tar.gz
	$pkgname.desktop)
md5sums=('8de3ca6da3ef6a27d59f959d1bbcd96b'
         '349e6d9729f88868902611bb1172f134')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./autogen.sh

  sed -i 's#src/tests/svncpp/Makefile##' configure
  [ "$NOEXTRACT" == 1 ] || ./configure --prefix=/usr \
    --with-apu-config="`which apu-1-config`" \
    --with-apr-config="`which apr-1-config`" \
    --with-wx-config="`which wx-config-2.8`"

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' tools/*.py
  sed -i 's|python|python2|' src/locale/Makefile

  make -k
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  install -Dm644 src/res/bitmaps/rapidsvn_32x32.xpm $pkgdir/usr/share/pixmaps/rapidsvn_32x32.xpm
  install -Dm644 ../$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
