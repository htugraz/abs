# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-power-manager
pkgver=1.8.0
pkgrel=3
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system for Mate Desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'libcanberra' 'libgnome-keyring' 'libnotify'
         'libunique' 'mate-panel' 'upower')
makedepends=('docbook2x' 'docbook-xml' 'mate-common' 'perl-xml-parser'
             'xmlto' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/mate-desktop/mate-power-manager/commit/8cb168b752f4130e88daefa400bb9bf07cf18227.diff"
        "https://github.com/seife/mate-power-manager/commit/2b3cf01f84204dd5d8204d519e2167b41cda6bc0.diff"
        "https://github.com/seife/mate-power-manager/commit/220a4e0a64aca0579f50e6e57d4eca848b3ac57f.diff"
        "https://github.com/seife/mate-power-manager/commit/d59f4b8bd38e1628af3a992ae8e96b8e069ab738.diff")
sha1sums=('5cde22f2d6498294a76e3bf9005b4f73bbdd0db8'
          'f1acff9fa6140c56cdb39531fc02e886f5a7437d'
          '8c8d724ac99604aad6eabaddd5012a997dbadc90'
          '884af06e9344954b4237263cd910acb2f34ff443'
          'd2950548f353b71ffa8ea6680af6d211a72a5670')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    #Work around a problem where the .sgml fail to parse.
    sed -e 's:@HAVE_DOCBOOK2MAN_TRUE@.*::' -i man/Makefile.in
    # [PATCH] RHBZ #964678 : Patch that provides DBUS interface to control
    #  - https://github.com/mate-desktop/mate-power-manager/pull/60
    patch -Np1 -i "${srcdir}/8cb168b752f4130e88daefa400bb9bf07cf18227.diff"
    # Add upower 0.99 support
    patch -Np1 -i "${srcdir}/2b3cf01f84204dd5d8204d519e2167b41cda6bc0.diff"
    patch -Np1 -i "${srcdir}/220a4e0a64aca0579f50e6e57d4eca848b3ac57f.diff"
    patch -Np1 -i "${srcdir}/d59f4b8bd38e1628af3a992ae8e96b8e069ab738.diff"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --enable-applets \
        --enable-unique
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
