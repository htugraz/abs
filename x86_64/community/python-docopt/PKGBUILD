# $Id: PKGBUILD 116968 2014-08-07 09:43:45Z fyan $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-docopt
_pkgname=docopt
pkgname=(python-docopt python2-docopt)
pkgver=0.6.2
pkgrel=1
pkgdesc="Pythonic option parser, that will make you smile"
arch=(any)
url="https://github.com/docopt/docopt"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.tar.gz" LICENSE-MIT)
sha256sums=('49b3a825280bd66b3aa83585ef59c4a8c82f2c8a522dbe754a8bc8d08c85c491'
            '3d5df78f592ff2433c3c6ce4984091b7f497059ddb8c6b08a061ba48e37b67f2')

prepare(){
    cp -dpr --no-preserve=owner $_pkgname-$pkgver $_pkgname-$pkgver-2
}

build() {
	cd "$srcdir/$_pkgname-$pkgver"
	python setup.py build
	cd "$srcdir/$_pkgname-$pkgver-2"
	python2 setup.py build
}

package_python2-docopt() {
	cd "$srcdir/$_pkgname-$pkgver-2"
	python2 setup.py install --skip-build -O1 --root="$pkgdir"
    depends=('python2')
	install -Dm0644 "$srcdir/LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-docopt() {
	cd "$srcdir/$_pkgname-$pkgver"
    depends=('python')
	python setup.py install --skip-build -O1 --root="$pkgdir"
	install -Dm0644 "$srcdir/LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

