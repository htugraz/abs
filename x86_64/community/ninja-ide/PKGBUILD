# $Id: PKGBUILD 96085 2013-08-20 06:32:25Z lcarlier $
# Maintainer: Angel Velasquez <angvp@archlinux.org>  
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: helq <linuxero789@gmail.com>
# Contributor: Jesus Jerez <jhuss@archlinux.org.ve>
# Contributor: DobleD <dobled@dobled.info>

pkgname=ninja-ide
pkgver=2.3
pkgrel=2
pkgdesc="Cross-platform IDE focused on Python application development"
arch=('any')
url="http://ninja-ide.org/"
license=('GPL3')
depends=('python2-setuptools' 'python2-pyqt' 'python2-pyinotify')
source=(https://github.com/ninja-ide/ninja-ide/archive/v$pkgver.zip
        ninja-ide.desktop)
install=$pkgname.install
sha256sums=('489fd2aede8fc16a1216a245e095f00865e0b91e8b0369f023041984f1b62a06'
            '6cabe8ba0c1589feb4919f9de6e4b8bc0a692f5cb8c630f1e9c661aa19f22fdb')

package() {
  cd $pkgname-$pkgver

  python2 setup.py install --root="$pkgdir" -O1

  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 ninja_ide/img/icon.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
