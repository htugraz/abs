# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=python2-caja
_pkgname=python-caja
pkgver=1.8.0
pkgrel=2
pkgdesc="Python binding for Caja, to allow Caja property page and menu item extensions to be written in Python."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja' 'python2' 'python2-gobject')
makedepends=('mate-common')
replaces=('python-caja')
provides=('python-caja')
source=("http://pub.mate-desktop.org/releases/1.8/${_pkgname}-${pkgver}.tar.xz"
        "use-pkg-config.diff")
sha1sums=('63cf08e3f711be8eb44c423322962a18f822031e'
          '32c348669868414d98c3a4be0a083b419256200d')

prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    # Use pkg-config for python detection and variables
    #  - https://github.com/mate-desktop/python-caja/issues/17
    patch -Np1 -i "${srcdir}/use-pkg-config.diff"
    autoreconf -fi
}

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
