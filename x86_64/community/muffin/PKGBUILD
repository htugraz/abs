# $Id: PKGBUILD 120195 2014-10-06 02:49:18Z bgyorgy $
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=2.2.6
pkgrel=2
pkgdesc="Cinnamon window manager based on Mutter"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('clutter' 'gobject-introspection' 'cinnamon-desktop' 'libcanberra' 'libsm'
         'startup-notification' 'zenity' 'dconf')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-common') 
options=('!emptydirs')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/muffin/archive/$pkgver.tar.gz")
sha256sums=('5617504f5f8ecaf53268787728e6b7bf5da1514d9bc538dd0c047d467cca9ccf')

prepare() {
  cd ${pkgname}-${pkgver}
}

build() {
  cd ${pkgname}-${pkgver}

  PYTHON=python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/muffin \
  				 			 --localstatedir=/var --disable-static --disable-schemas-compile \
                 --enable-compile-warnings=minimum
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir/" install

  # Remove unused stuff
  make -C src DESTDIR="$pkgdir" uninstall-binPROGRAMS uninstall-desktopfilesDATA
  make -C src/tools DESTDIR="$pkgdir" uninstall
  make -C src/compositor/plugins DESTDIR="$pkgdir" uninstall
  make -C doc/man DESTDIR="$pkgdir" uninstall
}

# vim:set ts=2 sw=2 et:
