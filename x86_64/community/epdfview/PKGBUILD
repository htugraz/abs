# $Id: PKGBUILD 105183 2014-02-02 12:40:09Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer:  Kyle Keen <keenerd@gmail.com>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Thayer Williams <thayer@archlinux.org>

pkgname=epdfview
pkgver=0.1.8
pkgrel=5
pkgdesc='Lightweight PDF document viewer'
url='http://freecode.com/projects/epdfview'
arch=('i686' 'x86_64')
license=('GPL')
depends=('poppler-glib' 'desktop-file-utils' 'hicolor-icon-theme' 'gtk2')
makedepends=('pkgconfig')
install=epdfview.install
source=(ftp://ftp.slackware.com/.1/blfs/conglomeration/epdfview/$pkgname-$pkgver.tar.bz2
        epdfview-0.1.8-swap-the-blue-and-red-channel.patch
        epdfview-0.1.8-glib2-headers.patch
        epdfview-0.1.8-modern-cups.patch)
md5sums=('e50285b01612169b2594fea375f53ae4'
         '7f9ea101a41f5b4e999fd024f423d41f'
         '2fffa9c7cd4c5f0744803591c2f162a3'
         '5c2cf5612d2a7dfe6cf005b94aeb5ada')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/icon_epdfview-48/epdfview/' data/epdfview.desktop

  patch -p1 -i ../epdfview-0.1.8-swap-the-blue-and-red-channel.patch
  patch -p1 -i ../epdfview-0.1.8-glib2-headers.patch # FS#30116
  patch -p1 -i ../epdfview-0.1.8-modern-cups.patch # FS#32511
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  for size in 24 32 48; do
    install -Dm644 data/icon_$pkgname-$size.png \
      "$pkgdir"/usr/share/icons/hicolor/${size}x${size}/apps/$pkgname.png
  done
}
