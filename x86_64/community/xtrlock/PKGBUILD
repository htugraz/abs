# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=xtrlock
pkgver=2.7
pkgrel=2
pkgdesc='Minimal X display lock program'
url='https://packages.debian.org/sid/xtrlock'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('libx11')
source=(${pkgname}-${pkgver}.tar.gz::http://ftp.debian.org/debian/pool/main/x/xtrlock/xtrlock_${pkgver}.tar.gz)
sha512sums=('44693dfa7b8b109b19c4f61902ac5c181af1d8fb6044115517391211411239a506108e6fd5b998978030f997f9094e9df18e5d40499c6b3b83a42cf9dd1b7f92')

build() {
  cd ${pkgname}-${pkgver}
  make -f Makefile.noimake CFLAGS="${CFLAGS} -DSHADOW_PWD" LDLIBS="-lX11 -lcrypt" xtrlock
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 4755 xtrlock "${pkgdir}/usr/bin/xtrlock"
  install -Dm 644 xtrlock.man "${pkgdir}/usr/share/man/man1/xtrlock.1"
}

# vim: ts=2 sw=2 et:
