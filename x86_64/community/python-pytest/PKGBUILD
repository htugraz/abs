# $Id: PKGBUILD 102657 2013-12-17 10:09:40Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest
pkgname=('python-pytest' 'python2-pytest')
pkgver=2.5.1
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py')
source=("http://pypi.python.org/packages/source/p/pytest/pytest-$pkgver.tar.gz")

prepare() {
  cp -r pytest-${pkgver} python2-pytest-${pkgver}
}

package_python-pytest() {
  depends=('python' 'python-py>=1.4.17')

  cd pytest-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pytest() {
  depends=('python2' 'python2-py>=1.4.17')

  cd python2-pytest-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/py.test" "${pkgdir}/usr/bin/py.test2"
}

sha512sums=('fae353268610c52dcf8a19313f063299d2e96e7c3a554d3a6a4385c1b894058e2723d22cf1fc7573e2de8d8b197c0724619044c0263b302f80f5227300d23ceb')
