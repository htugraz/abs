# $Id: PKGBUILD 124449 2014-12-23 22:21:21Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tom K <tomk@runbox.com>

pkgname=perl-ldap
pkgver=0.64
pkgrel=1
pkgdesc="Interface to LDAP Protocol (RFC1777)"
arch=('any')
url="http://search.cpan.org/dist/perl-ldap"
depends=('perl-convert-asn1' 'perl-xml-sax-writer')
license=("GPL")
source=(http://search.cpan.org/CPAN/authors/id/M/MA/MARSCHAP/perl-ldap-$pkgver.tar.gz)
md5sums=('52da55c38bf937c0d4ccff5f182aefae')

build() {
  cd  $srcdir/perl-ldap-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  $srcdir/perl-ldap-$pkgver
  make install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
