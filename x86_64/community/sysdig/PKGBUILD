# $Id: PKGBUILD 140592 2015-09-17 20:54:25Z foutrelis $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig
pkgver=0.1.104
pkgrel=2
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('i686' 'x86_64')
url="http://www.sysdig.org/"
license=('GPL2')
depends=('dkms' 'jsoncpp' 'luajit')
makedepends=('cmake')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('05a7ed95a1b73053cae0a77de199e84d7c9804014166527e3b211ed637c3fbab')
install=$pkgname.install

build() {
  cd "$srcdir"/$pkgname-$pkgver
  rm -rf build
  mkdir build
  cd build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DDIR_ETC=/etc \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_LUAJIT=OFF \
        -DUSE_BUNDLED_JSONCPP=OFF \
        -DUSE_BUNDLED_ZLIB=OFF \
        -DUSE_BUNDLED_NCURSES=OFF \
        -DBUILD_DRIVER=OFF
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  cd build
  make install DESTDIR="$pkgdir"
}
