# $Id: PKGBUILD 135546 2015-06-18 08:01:17Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig
pkgver=0.1.101
pkgrel=1
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('i686' 'x86_64')
url="http://www.sysdig.org/"
license=('GPL2')
depends=('dkms' 'jsoncpp' 'luajit')
makedepends=('cmake')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('6995e39be565514901b5cb587689ee2efbf8359293e4e597362382cccf0e9db6')
install=$pkgname.install

build() {
  cd "$srcdir"/$pkgname-$pkgver
  rm -rf build
  mkdir build
  cd build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DDIR_ETC=/etc \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_LUAJIT=OFF \
        -DUSE_BUNDLED_JSONCPP=OFF \
        -DUSE_BUNDLED_ZLIB=OFF \
        -DUSE_BUNDLED_NCURSES=OFF \
        -DBUILD_DRIVER=OFF
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  cd build
  make install DESTDIR="$pkgdir"
}
