# $Id: PKGBUILD 137120 2015-07-19 16:58:38Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>
# Contributor: David Scholl <djscholl@gmail.com> 
# Contributor: David Pretty <david.pretty@gmail.com> 

pkgbase=python-rpy2
pkgname=(python2-rpy2 python-rpy2)
pkgver=2.6.1
pkgrel=1
pkgdesc="A very simple, yet robust, Python interface to the R Programming Language."
arch=(i686 x86_64)
url="http://rpy.sourceforge.net/"
license=(MPL GPL LGPL)
makedepends=(python-setuptools python2-setuptools python-numpy python2-numpy r)
source=("http://pypi.python.org/packages/source/r/rpy2/rpy2-$pkgver.tar.gz")
md5sums=('2febe7862a9a4fa0a43fc3f6851823e6')

prepare() {
  cd rpy2-$pkgver
  sed -i "s:Rlapack:lapack:" setup.py
  sed -i "s:os.path.join(RHOME.strip(), 'include'):'/usr/include/R':" setup.py
  sed -i "s:os.path.join(RHOME.strip(), 'include'):'/usr/include/R':" setup.py
}

package_python2-rpy2() {
  depends=(python2-numpy r python2-singledispatch)
  cd rpy2-$pkgver

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i "$pkgdir"/usr/lib/python2.7/site-packages/rpy2/tests.py
}

package_python-rpy2() {
  depends=(python-numpy r)
  cd rpy2-$pkgver
  
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

