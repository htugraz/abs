# $Id: PKGBUILD 210040 2014-04-09 11:07:05Z jgc $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libgee
pkgver=0.14.0
pkgrel=1
pkgdesc="GObject collection library"
url="http://live.gnome.org/Libgee"
license=(LGPL2.1)
arch=(i686 x86_64)
depends=(glib2)
makedepends=(gobject-introspection vala)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('08e466d3f214c9466860b5a82629de0de9eb89b1de7bd918fe154e569b5834cd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  # generates a bazillion traps - make sure systemd's core_pattern
  # is deactivated, or you'll DoS the journal and the system
  #make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
