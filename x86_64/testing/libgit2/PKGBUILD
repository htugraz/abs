# $Id: PKGBUILD 117009 2014-08-07 22:35:20Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2
pkgver=0.21.1
pkgrel=2
epoch=1
pkgdesc='A linkable library for Git'
arch=('i686' 'x86_64')
url='https://libgit2.github.com/'
depends=(zlib openssl libssh2)
makedepends=(cmake python)
license=('GPL2')
source=("https://github.com/libgit2/libgit2/archive/v${pkgver}.tar.gz")
md5sums=('cbf3422d54dd6f55f09855a6eb749f41')

build() {
  cd $pkgname-$pkgver
  export LANG=en_US.UTF-8
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DTHREADSAFE=ON
  make
}

check() {
  cd $pkgname-$pkgver
  make test
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
