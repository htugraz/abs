# $Id: PKGBUILD 226489 2014-11-19 17:35:20Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-korundum
pkgver=4.14.3
pkgrel=1
pkgdesc="Ruby bindings for libraries created by the KDE community"
url="https://projects.kde.org/projects/kde/kdebindings/ruby/korundum"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-qtruby' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost'
             'kdepimlibs' 'kdegraphics-okular' 'kdesdk-kate')
optdepends=('kdegraphics-okular: okular bindings'
            'kdepimlibs: akonadi bindings')
conflicts=('kdebindings-ruby')
source=("http://download.kde.org/stable/${pkgver}/src/korundum-${pkgver}.tar.xz")
sha1sums=('5f76bca9ae7d6da7f5297bfb9f7e24457603053a')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../korundum-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCUSTOM_RUBY_SITE_LIB_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]') \
    -DCUSTOM_RUBY_SITE_ARCH_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorarchdir"]') \
    -DWITH_Nepomuk=OFF \
    -DWITH_Soprano=OFF

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
