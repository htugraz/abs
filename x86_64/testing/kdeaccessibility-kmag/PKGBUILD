# $Id: PKGBUILD 214474 2014-06-09 03:06:22Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kmag
pkgver=4.13.2
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Screen Magnifier'
url='http://kde.org/applications/utilities/kmag/'
license=('GPL' 'FDL')
groups=('kde' 'kdeaccessibility')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kmag-${pkgver}.tar.xz")
sha1sums=('ed0e541647bf516f07a4d391a2dceb59e5f6c939')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kmag-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
