# $Id: PKGBUILD 254663 2015-12-08 20:48:48Z foutrelis $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-desktop-applets
_pkgname=ktp-desktop-applets
pkgver=15.08.3
pkgrel=2
pkgdesc='The KDE-Telepathy Plasma desktop applets'
arch=('i686' 'x86_64')
url='http://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
groups=('kde-applications' 'kdenetwork' 'telepathy-kde')
depends=('telepathy-kde-common-internals' 'telepathy-kde-accounts-kcm')
makedepends=('extra-cmake-modules' 'plasma-framework')
source=("http://download.kde.org/stable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz")
md5sums=('557a7a0cf69e9d1858297a21d6c101c1')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
