# $Id: PKGBUILD 226866 2014-11-23 22:56:11Z seblu $
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=grep
pkgver=2.21
pkgrel=1
pkgdesc='A string search utility'
arch=('i686' 'x86_64')
license=('GPL3')
url='http://www.gnu.org/software/grep/grep.html'
groups=('base' 'base-devel')
depends=('glibc' 'pcre')
makedepends=('texinfo')
install=$pkgname.install
source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('43c48064d6409862b8a850db83c8038a'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --without-included-regex
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
