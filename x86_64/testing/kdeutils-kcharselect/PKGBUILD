# $Id: PKGBUILD 230708 2015-02-04 08:51:09Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kcharselect
pkgver=14.12.2
pkgrel=1
pkgdesc='Character Selector'
url="http://kde.org/applications/utilities/kcharselect/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kcharselect-${pkgver}.tar.xz")
sha1sums=('bbcffd733c63c9177b1c45c4bda89e974c571489')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kcharselect-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
