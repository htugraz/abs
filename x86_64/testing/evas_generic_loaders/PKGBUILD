# $Id: PKGBUILD 211875 2014-04-28 15:17:27Z andyrtr $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=evas_generic_loaders
pkgver=1.9.0
pkgrel=2
pkgdesc="Additional generic loaders for Evas"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('GPL2')
depends=('librsvg' 'poppler' 'libraw' 'libspectre' 'efl' 'gstreamer0.10-base')
options=('!emptydirs')
source=(http://download.enlightenment.org/rel/libs/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha1sums=('9741e7aed8adb86f21563bccde40a0e39c64c250')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="$CFLAGS -fvisibility=hidden"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/evas \
    --enable-poppler \
    --enable-spectre \
    --enable-libraw \
    --enable-svg \
    --enable-gstreamer
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

