# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gfbgraph
pkgver=0.2.2
pkgrel=1
pkgdesc="GLib/GObject wrapper for the Facebook Graph API"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('rest' 'json-glib' 'gobject-introspection' 'gtk-doc')
depends=('gnome-online-accounts')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('66c7b1c951863565c179d0b4b5207f27b3b36f80afed9f6a9acfc5fc3ae775d4')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-silent-rules \
    --disable-static \
    --enable-gtk-doc \
    --enable-introspection

  # Omit unused direct shared library dependencies.
  sed -i -e 's! -shared ! -Wl,--as-needed\0!g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir"/usr/share
  mv "$pkgdir"/usr/doc "$pkgdir"/usr/share/doc
}
