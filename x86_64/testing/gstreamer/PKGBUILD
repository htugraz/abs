# $Id: PKGBUILD 217515 2014-07-20 23:25:34Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer
pkgver=1.4.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'gobject-introspection')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz)
sha256sums=('23c39fdc2b24f889b07cab0449825384fef7592a121e180729fd9025ec45c695')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --enable-gtk-doc --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd "${srcdir}/gstreamer-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
