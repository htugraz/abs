# $Id: PKGBUILD 202137 2013-12-18 17:40:23Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kget
pkgver=4.12.0
pkgrel=1
pkgdesc='Download Manager'
url='http://kde.org/applications/internet/kget/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-workspace' 'kdebase-lib' 'libktorrent' 'libmms' 'gpgme' 'nepomuk-widgets')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kget-${pkgver}.tar.xz")
sha1sums=('5d327b26c68f0aaa7a088992de10136b1786aba9')

build() {
  mkdir build
  cd build
  cmake ../kget-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
