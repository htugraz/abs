# $Id: PKGBUILD 243363 2015-08-12 11:56:19Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo
pkgver=5.13.0
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('xapian-core' 'kfilemetadata' 'kidletime' 'kdelibs4support' 'lmdb')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('baloo-frameworks')
replaces=('baloo-frameworks')
provides=('baloo-frameworks')
install="${pkgname}.install"
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
md5sums=('67bceaf70f9da493161c0eb94a214099')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
