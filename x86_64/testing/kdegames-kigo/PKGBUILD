# $Id: PKGBUILD 202091 2013-12-18 17:35:01Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kigo
pkgver=4.12.0
pkgrel=1
pkgdesc="An open-source implementation of the popular Go game"
url="http://kde.org/applications/games/kigo/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames' 'gnugo')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kigo-${pkgver}.tar.xz")
sha1sums=('d4a7f9fed23710237f18e1879d9128821e4f3f4a')

build() {
  mkdir build
  cd build
  cmake ../kigo-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
