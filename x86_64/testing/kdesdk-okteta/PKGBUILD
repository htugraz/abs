# $Id: PKGBUILD 222458 2014-09-20 16:29:15Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-okteta
pkgver=4.14.1
pkgrel=1
pkgdesc='Hex Editor'
url='http://kde.org/applications/utilities/okteta/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/okteta-${pkgver}.tar.xz")
sha1sums=('11311414de4e51ff68c9d6e4e12d6f9102663ef6')

build() {
  mkdir build
  cd build
  cmake ../okteta-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
