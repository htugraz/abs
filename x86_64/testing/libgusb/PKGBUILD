# $Id: PKGBUILD 242972 2015-08-03 16:11:51Z anatolik $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=libgusb
pkgver=0.2.5
pkgrel=1
pkgdesc="GLib wrapper around libusb1"
arch=('i686' 'x86_64')
url="https://github.com/hughsie/libgusb"
license=('LGPL2.1')
depends=('udev' 'libusbx' 'glib2')
makedepends=('gobject-introspection' 'python2')
source=(http://people.freedesktop.org/~hughsient/releases/$pkgname-$pkgver.tar.xz)
sha256sums=('a3ad56773548695292f49b62df1eb7fb1e87e1e0c9d3982e80f65f47095fa492')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
