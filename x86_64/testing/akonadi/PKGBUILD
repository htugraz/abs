# $Id: PKGBUILD 241407 2015-06-30 03:35:20Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=akonadi
pkgver=1.13.0
pkgrel=6
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('i686' 'x86_64')
url='http://community.kde.org/KDE_PIM/Akonadi'
license=('LGPL')
depends=('shared-mime-info' 'boost-libs' 'qt4' 'libxslt')
makedepends=('cmake' 'automoc4' 'boost' 'postgresql' 'mariadb')
checkdepends=('xorg-server-xvfb')
optdepends=('postgresql: PostgreSQL backend'
            'mariadb: MariaDB backend')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/src/${pkgname}-${pkgver}.tar.bz2"
        dont-leak-old-external-payload-files.patch)
md5sums=('84eb2e471bd6bdfe54a2a2f1d858c07d'
         '9711e300dde3775a38637daa3c20befe')

prepare() {
  mkdir build

  cd $pkgname-$pkgver
  # https://bugs.kde.org/show_bug.cgi?id=341884
  patch -p1 -i ../dont-leak-old-external-payload-files.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_QSQLITE_IN_QT_PREFIX=TRUE \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DDATABASE_BACKEND=SQLITE \
    -DWITH_SOPRANO=OFF
  make
}

check() {
  # 17 akonadi-dbconfigtest failed as we changed the default backend
  # 24~30 failed due to qt cannot find the sqldriver which is not installed yet
  cd build
  xvfb-run -a dbus-launch --exit-with-session make test || warning "Tests failed"
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
