# $Id: PKGBUILD 219885 2014-08-16 20:07:15Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kalgebra
pkgver=4.14.0
pkgrel=1
pkgdesc="Graph Calculator"
url="http://kde.org/applications/education/kalgebra/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'kdeedu-analitza')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kalgebra-${pkgver}.tar.xz")
sha1sums=('5a621f4e707247e04af4e9c2711238ead39ca631')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kalgebra-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
