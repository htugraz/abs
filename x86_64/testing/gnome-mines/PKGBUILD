# $Id: PKGBUILD 223109 2014-09-26 10:56:09Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-mines
pkgver=3.14.0
pkgrel=1
pkgdesc="Clear hidden mines from a minefield"
arch=('i686' 'x86_64')
license=('GPL')
depends=('hicolor-icon-theme' 'gtk3' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection' 'python2')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=gnome-mines.install
url="https://live.gnome.org/Mines"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('c74c890913ded15e5ee895e8e59bd2d0467111f3dae01347bd4db5c50d25e274')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
