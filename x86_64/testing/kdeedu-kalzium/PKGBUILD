# $Id: PKGBUILD 232611 2015-03-04 10:56:31Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kalzium
pkgver=14.12.3
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="http://kde.org/applications/education/kalzium/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'avogadro' 'ocaml' 'chemical-mime-data')
makedepends=('cmake' 'automoc4' 'mesa' 'eigen' 'eigen2')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kalzium-${pkgver}.tar.xz")
sha1sums=('819751a1f99b42cbaf69d3eb22260dde5f6379b9')

build() {
  mkdir build
  cd build
  cmake ../kalzium-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
