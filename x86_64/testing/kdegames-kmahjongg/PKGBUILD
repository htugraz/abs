# $Id: PKGBUILD 206793 2014-03-05 10:19:11Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kmahjongg
pkgver=4.12.3
pkgrel=1
pkgdesc="A tile matching game for one or two players"
url="http://kde.org/applications/games/kmahjongg/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkmahjongg' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kmahjongg-${pkgver}.tar.xz")
sha1sums=('ab945442999a04af9428c071f7d8213b5a1c1642')

build() {
  mkdir build
  cd build
  cmake ../kmahjongg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
