# $Id: PKGBUILD 220630 2014-08-24 15:11:52Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=ca-certificates-cacert
pkgver=20140824
pkgrel=1
pkgdesc="CAcert.org root certificates"
arch=(any)
url="https://www.cacert.org/index.php?id=3"
license=('custom:RDL')
depends=(ca-certificates-utils)
install=ca-certificates-cacert.install
source=(CAcert.org_root.crt::http://www.cacert.org/certs/root.crt
        CAcert.org_class3.crt::http://www.cacert.org/certs/class3.crt
        LICENSE)
sha256sums=('c0e0773a79dceb622ef6410577c19c1e177fb2eb9c623a49340de3c9f1de2560'
            'f5badaa5da1cc05b110a9492455a2c2790d00c7175dcf3a7bcb5441af71bf84f'
            '2d9007a467e72d437ecfa13cc605c371e98a469f1aac4c890be74f953559833c')

build() {
  printf "cacert/%s\n" *.crt > cacert.conf
}

package() {
  local _certdir="$pkgdir/usr/share/ca-certificates/cacert"
  install -d "$_certdir"
  install -t "$_certdir" -m644 *.crt

  install -Dm644 cacert.conf "$pkgdir/etc/ca-certificates/conf.d/cacert.conf"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
