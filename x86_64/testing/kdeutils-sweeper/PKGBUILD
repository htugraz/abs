# $Id: PKGBUILD 232846 2015-03-04 17:03:21Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-sweeper
pkgver=14.12.3
pkgrel=1
pkgdesc='System Cleaner'
url='http://kde.org/applications/utilities/sweeper'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
source=("http://download.kde.org/stable/applications/${pkgver}/src/sweeper-${pkgver}.tar.xz")
sha1sums=('81966a624574cb75880df9a3e5bce4710920aa97')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../sweeper-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
