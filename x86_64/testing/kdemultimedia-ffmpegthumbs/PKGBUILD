# $Id: PKGBUILD 214643 2014-06-09 03:16:57Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-ffmpegthumbs
pkgver=4.13.2
pkgrel=1
pkgdesc='FFmpeg-based thumbnail creator for video files'
url='https://projects.kde.org/projects/kde/kdemultimedia/ffmpegthumbs'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
depends=('kdelibs' 'ffmpeg')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/ffmpegthumbs-${pkgver}.tar.xz")
sha1sums=('f70468a2af6228ce2430ea182cb3e61bb0834d1e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../ffmpegthumbs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
