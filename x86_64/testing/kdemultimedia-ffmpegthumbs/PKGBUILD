# $Id: PKGBUILD 226643 2014-11-19 18:44:55Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-ffmpegthumbs
pkgver=4.14.3
pkgrel=1
pkgdesc='FFmpeg-based thumbnail creator for video files'
url='https://projects.kde.org/projects/kde/kdemultimedia/ffmpegthumbs'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
depends=('kdelibs' 'ffmpeg')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/ffmpegthumbs-${pkgver}.tar.xz")
sha1sums=('2c49d2afabb0dfaca0863cfcab1aa22ad24e0151')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../ffmpegthumbs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
