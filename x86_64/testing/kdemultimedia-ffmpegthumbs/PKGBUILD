# $Id: PKGBUILD 246184 2015-09-14 21:43:18Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-ffmpegthumbs
pkgver=15.08.1
pkgrel=1
pkgdesc='FFmpeg-based thumbnail creator for video files'
url='https://projects.kde.org/projects/kde/kdemultimedia/ffmpegthumbs'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdemultimedia')
depends=('kdelibs' 'ffmpeg')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/ffmpegthumbs-${pkgver}.tar.xz")
sha1sums=('5ccbcc942c2b7df35afcd6c2b130658bd7617f02')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../ffmpegthumbs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
