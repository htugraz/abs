# $Id: PKGBUILD 232753 2015-03-04 15:37:39Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-ffmpegthumbs
pkgver=14.12.3
pkgrel=1
pkgdesc='FFmpeg-based thumbnail creator for video files'
url='https://projects.kde.org/projects/kde/kdemultimedia/ffmpegthumbs'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
depends=('kdelibs' 'ffmpeg')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/ffmpegthumbs-${pkgver}.tar.xz")
sha1sums=('8342c64e4be578c9194d44843a9e89feaaeaaedd')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../ffmpegthumbs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
