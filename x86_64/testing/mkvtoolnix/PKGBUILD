# $Id: PKGBUILD 221012 2014-09-05 13:58:06Z foutrelis $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: xduugu <xduugu@gmx.com>

pkgbase=mkvtoolnix
pkgname=('mkvtoolnix-cli' 'mkvtoolnix-gtk')
pkgver=7.1.0
pkgrel=4
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
makedepends=('libmatroska' 'flac' 'libvorbis' 'file' 'wxgtk'
             'boost-libs' 'lzo' 'xdg-utils' 'boost' 'ruby')
source=("http://www.bunkus.org/videotools/${pkgbase}/sources/${pkgbase}-${pkgver}.tar.xz"
        "0001-fix-compilation-with-Boost-1.56.0.patch")
md5sums=('a374511b0743a700548622d23cd5616c'
         '1fa23e00ad329435fda249e52df91755')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  patch -p1 -i "${srcdir}/0001-fix-compilation-with-Boost-1.56.0.patch"

  # Disable automagic curl dep used for online update checking
  sed -i -e '/curl/d' configure.in
  export CURL_CFLAGS="" CURL_LIBS=""

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=3"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=3"

  # Sets number of threads for a parallel build
  export DRAKETHREADS=4

  autoreconf -vfi
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./configure --prefix=/usr \
              --with-boost-libdir=/usr/lib \
              --disable-gui
  ./drake

  mv src/mkvinfo{,-cli}

  ./configure --prefix=/usr \
              --with-boost-libdir=/usr/lib
  ./drake
}

package_mkvtoolnix-cli() {
  pkgdesc="Set of tools to create, edit and inspect Matroska files - CLI version"
  depends=('libmatroska' 'expat' 'flac' 'libvorbis' 'file' 'boost-libs' 'lzo')
  provides=('mkvtoolnix')
  conflicts=('mkvtoolnix')
  replaces=('mkvtoolnix')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./drake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/bin/mkvinfo
  install -Dm755 src/mkvinfo-cli "${pkgdir}"/usr/bin/mkvinfo

  rm -rf "${pkgdir}"/usr/bin/mmg
  rm -rf "${pkgdir}"/usr/share/man/de/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/ja/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/nl/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/zh_CN/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/uk/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/applications
  rm -rf "${pkgdir}"/usr/share/icons
  rm -rf "${pkgdir}"/usr/share/mime
}

package_mkvtoolnix-gtk() {
  pkgdesc="Set of tools to create, edit and inspect Matroska files - wxGTK GUI"
  depends=("mkvtoolnix-cli=${pkgver}" 'wxgtk' 'xdg-utils' 'desktop-file-utils')
  install=mkvtoolnix.install

  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./drake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/bin/mkv*
  install -Dm755 src/mkvinfo "${pkgdir}"/usr/bin/mkvinfo-gtk
  sed -ri 's/^(Exec=mkvinfo)/\1-gtk/' "${pkgdir}"/usr/share/applications/mkvinfo.desktop

  rm -rf "${pkgdir}"/usr/share/doc
  rm -rf "${pkgdir}"/usr/share/locale
  rm -rf "${pkgdir}"/usr/share/man/de/man1/mkv*
  rm -rf "${pkgdir}"/usr/share/man/ja/man1/mkv*
  rm -rf "${pkgdir}"/usr/share/man/man1/mkv*
  rm -rf "${pkgdir}"/usr/share/man/nl/man1/mkv*
  rm -rf "${pkgdir}"/usr/share/man/zh_CN/man1/mkv*
  rm -rf "${pkgdir}"/usr/share/man/uk/man1/mkv*
}
