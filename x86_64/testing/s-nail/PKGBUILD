# $Id: PKGBUILD 204885 2014-01-30 17:24:29Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s@arch@gmail.com>
# Contributor: Andreas Wagner <Andreas.Wagner@em.uni-frankfurt.de>

pkgname=s-nail
pkgver=14.5.2
pkgrel=2
pkgdesc='Mail processing system with a command syntax reminiscent of ed'
url='http://sdaoden.users.sourceforge.net/code.html#s-nail'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('openssl')
optdepends=('smtp-forwarder: for sending mail')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver//./_}.tar.xz"
        'sort.patch')
sha1sums=('26ad43f5f41b429d5f13a3ce73a3dff75325950c'
          '909da731e590d1d2877ed38bed667440a02d1259')

groups=('base')
backup=('etc/mail.rc')
replaces=('mailx' 'mailx-heirloom' 'heirloom-mailx')
provides=('mailx' 'mailx-heirloom' 'heirloom-mailx')
conflicts=('mailx' 'mailx-heirloom' 'heirloom-mailx')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../sort.patch # http://sourceforge.net/mailarchive/message.php?msg_id=31906477
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make \
		PREFIX=/usr \
		SYSCONFDIR=/etc \
		MANDIR=/usr/share/man \
		MAILSPOOL=/var/spool/mail \
		SID= NAIL=mail \
		config

	make build
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" packager-install
	ln -sf mail "${pkgdir}"/usr/bin/mailx
	ln -sf mail.1.gz "${pkgdir}"/usr/share/man/man1/mailx.1.gz
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
