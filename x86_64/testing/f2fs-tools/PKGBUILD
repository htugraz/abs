# $Id: PKGBUILD 255741 2015-12-10 10:19:41Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=f2fs-tools
pkgver=1.5.0
pkgrel=1
pkgdesc="Tools for Flash-Friendly File System (F2FS)"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/f2fs-tools/"
depends=('util-linux')
license=('GPL')
source=("http://git.kernel.org/cgit/linux/kernel/git/jaegeuk/f2fs-tools.git/snapshot/$pkgname-$pkgver.tar.gz")
md5sums=('a98553e82ff77db7443a7f2f0b796d48')

build() {
	cd "${pkgname}-${pkgver}"

	autoreconf -fi
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}/" sbindir=/usr/bin install
}
