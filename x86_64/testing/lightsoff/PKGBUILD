# $Id: PKGBUILD 224330 2014-10-14 13:56:39Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lightsoff
pkgver=3.14.1
pkgrel=1
pkgdesc="Turn off all the lights"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'clutter-gtk' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection' 'python2')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=lightsoff.install
url="https://live.gnome.org/Lightsoff"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('5ca1bd65e4159caa4f8032a0cab3e545995496c12e172d5a6fa58a87ed312b61')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
