# $Id: PKGBUILD 229213 2015-01-16 04:45:31Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-knetwalk
pkgver=14.12.1
pkgrel=1
pkgdesc="Connect all the terminals to the server, in as few turns as possible"
url="http://kde.org/applications/games/knetwalk/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/knetwalk-${pkgver}.tar.xz")
sha1sums=('b33404d6ef77e75c547fd457eb67bbbe648881c3')

build() {
  mkdir build
  cd build
  cmake ../knetwalk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
