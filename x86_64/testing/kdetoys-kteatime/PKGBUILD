# $Id: PKGBUILD 205389 2014-02-04 14:07:34Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdetoys-kteatime
pkgver=4.12.2
pkgrel=1
pkgdesc='Tea Cooker'
url='http://kde.org/applications/games/kteatime/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdetoys')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kteatime-${pkgver}.tar.xz")
sha1sums=('703caf28cf5d724de18fc576fa406caf1175549b')

build() {
  mkdir build
  cd build
  cmake ../kteatime-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
