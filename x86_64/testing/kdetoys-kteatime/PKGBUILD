# $Id: PKGBUILD 210779 2014-04-16 14:15:36Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdetoys-kteatime
pkgver=4.13.0
pkgrel=1
pkgdesc='Tea Cooker'
url='http://kde.org/applications/games/kteatime/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdetoys')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kteatime-${pkgver}.tar.xz")
sha1sums=('5eee19c871809b57539b74f780cb30135924ed10')

build() {
  mkdir build
  cd build
  cmake ../kteatime-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
