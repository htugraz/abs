# $Id: PKGBUILD 201888 2013-12-18 17:28:46Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-ksystemlog
pkgver=4.12.0
pkgrel=1
pkgdesc='System log viewer tool'
url='http://kde.org/applications/system/ksystemlog/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/ksystemlog-${pkgver}.tar.xz"
        'syslog-path.patch')
sha1sums=('0e7b62342acf505d384328de2528cb22120ef639'
          '20095ce6e0f3e5b6800a7c6e52de6fddba62c031')

prepare() {
  cd ksystemlog-${pkgver}
  patch -p2 -i "${srcdir}"/syslog-path.patch
}

build() {
  mkdir build
  cd build
  cmake ../ksystemlog-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
