# $Id: PKGBUILD 217461 2014-07-18 14:54:45Z lcarlier $
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-sisusb
pkgver=0.9.6
pkgrel=5
pkgdesc="X.org SiS USB video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=18')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<18' 'X-ABI-VIDEODRV_VERSION>=19')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2 git-fixes.patch)
sha256sums=('73dbef43c56a4ce1445c27ebac2ddc062c643c32ca6e2a4d095aea2185b9e046'
            '9b64f4a9160c1055d3e01e1b603840986163a7880dbbb5c12f3a7d4c66739134')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/git-fixes.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
