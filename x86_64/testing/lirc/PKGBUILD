# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=lirc
_pkgver=0.9.2
[[ $_pkgver =~ [a-z]$ ]] && pkgver="${_pkgver:0:-1}.${_pkgver: -1}" || pkgver="$_pkgver"
pkgrel=1
epoch=1
pkgdesc="Linux Infrared Remote Control utils"
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
depends=('alsa-lib' 'libx11' 'libftdi-compat' 'libirman')
makedepends=('help2man' 'alsa-lib' 'libx11' 'libftdi-compat' 'libirman' 'python')
optdepends=('python: for lirc-setup, irdb-get and pronto2lirc')
provides=('lirc-utils')
conflicts=('lirc-utils')
replaces=('lirc-utils')
backup=('etc/lirc/lirc_options.conf' 'etc/lirc/lircd.conf' 'etc/lirc/lircmd.conf')
install=lirc.install
source=("http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}.tar.bz2"
        0001-doc-Add-Doxyfile-to-tarball.patch
        0002-lirc.pc-Fix-bad-variable-reference-ordering-86.patch
        0004-lirc-lsplugins-Bugfix.patch
        0005-lirc_options.conf-Add-effective-user-to-template.patch
        0006-doc-Clean-up-irsend-1-manpage.patch
        0009-lib-lirc_log-Add-priority-labels-info-error.-to-sysl.patch
        0010-lirc-setup-Fix-remote-config-file-display-problems.patch
        0012-lib-Bad-bugfix-for-finding-.lircrc.patch
        0013-tools-Make-make_rel_symlink.py-use-python3.patch
        lirc.logrotate
        lirc.tmpfiles)
md5sums=('3afc84e79c0839823cc20e7a710dd06d'
         '1e1a68f08c2ecb2b766973e4f1721bae'
         'c04b2ed646dbaaec2fa9f4e1ad2cc802'
         '2e60e1b3c19b0898d1d5f69e76891a60'
         '9fe1e650858ca91e3a0fe4d63ee4b946'
         '8b5c11ab325d4bef998c41b5b1160513'
         '96475d718dbeb336a3282a5e9f9fdadd'
         '308c3e1fd769b38caa6fd548f54f2440'
         '497c4bb4ac8fc6febac94abe1922595c'
         'e68b73d848302ee0f76791f60dbd3581'
         '3deb02604b37811d41816e9b4385fcc3'
         'febf25c154a7d36f01159e84f26c2d9a')

prepare() {
  cd "${srcdir}/lirc-${_pkgver}"

  patch -p1 -i ../0001-doc-Add-Doxyfile-to-tarball.patch
  patch -p1 -i ../0002-lirc.pc-Fix-bad-variable-reference-ordering-86.patch
  patch -p1 -i ../0004-lirc-lsplugins-Bugfix.patch
  patch -p1 -i ../0005-lirc_options.conf-Add-effective-user-to-template.patch
  patch -p1 -i ../0006-doc-Clean-up-irsend-1-manpage.patch
  patch -p1 -i ../0009-lib-lirc_log-Add-priority-labels-info-error.-to-sysl.patch
  patch -p1 -i ../0010-lirc-setup-Fix-remote-config-file-display-problems.patch
  patch -p1 -i ../0012-lib-Bad-bugfix-for-finding-.lircrc.patch
  patch -p1 -i ../0013-tools-Make-make_rel_symlink.py-use-python3.patch

  autoreconf
}

build() {
  cd "${srcdir}/lirc-${_pkgver}"

  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var \
	  --with-transmitter --enable-sandboxed 
  make
}

package() {
  cd "${srcdir}/lirc-${_pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/lirc.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lirc.conf
  install -Dm644 "${srcdir}"/lirc.logrotate "${pkgdir}"/etc/logrotate.d/lirc

  rmdir "${pkgdir}"/var/{run/lirc/,run/,}
}
