# $Id: PKGBUILD 226567 2014-11-19 18:17:34Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kiriki
pkgver=4.14.3
pkgrel=1
pkgdesc="An addictive and fun dice game"
url="http://kde.org/applications/games/kiriki/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kiriki-${pkgver}.tar.xz")
sha1sums=('929b878227e45f58dba10091ebd2a092ce0bcf6d')

build() {
  mkdir build
  cd build
  cmake ../kiriki-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
