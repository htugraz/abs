# $Id: PKGBUILD 252130 2015-12-02 21:11:02Z heftig $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgbase=dbus
pkgname=('dbus' 'libdbus')
pkgver=1.10.6
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
makedepends=('libx11' 'systemd' 'xmlto' 'docbook-xsl')
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz{,.asc})
md5sums=('26d0cf3a1c9782cb0e342101f0450440'
         'SKIP')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90'  # Simon McVittie <simon.mcvittie@collabora.co.uk>
              '3C8672A0F49637FE064AC30F52A43A1E4B77B059') # Simon McVittie <simon.mcvittie@collabora.co.uk>

prepare() {
  cd dbus-$pkgver
}

build() {
  cd dbus-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=dbus \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --enable-systemd --enable-user-session
  make
}

package_dbus(){
  depends=('libdbus' 'expat')
  optdepends=('libx11: dbus-launch support')
  provides=('dbus-core')
  conflicts=('dbus-core')
  replaces=('dbus-core')

  cd dbus-$pkgver

  # Disable installation of libdbus
  sed -i -e 's/^SUBDIRS = dbus/SUBDIRS =/' Makefile

  make DESTDIR="$pkgdir" install

  rm -r "${pkgdir}/var/run"
  rm -r "${pkgdir}/usr/lib/pkgconfig"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}

package_libdbus(){
  pkgdesc="DBus library"
  depends=('libsystemd')

  cd dbus-$pkgver
  make DESTDIR="$pkgdir" -C dbus install
  make DESTDIR="$pkgdir" install-data-am
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/libdbus/COPYING"
}
