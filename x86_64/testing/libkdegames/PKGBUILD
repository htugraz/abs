# $Id: PKGBUILD 210658 2014-04-16 13:58:21Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdegames
pkgver=4.13.0
pkgrel=1
pkgdesc="Common code and data for many KDE games"
url="https://projects.kde.org/projects/kde/kdegames/libkdegames"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'openal')
makedepends=('cmake' 'automoc4')
replaces=('kdegames-libkdegames')
conflicts=('kdegames-libkdegames')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('28b546ff09d7191a4381bff1ce5084ced3d286ae')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
