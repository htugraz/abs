# $Id: PKGBUILD 229075 2015-01-15 16:01:43Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdegames
pkgver=14.12.1
pkgrel=1
pkgdesc="Common code and data for many KDE games"
url="https://projects.kde.org/projects/kde/kdegames/libkdegames"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'openal')
makedepends=('cmake' 'automoc4')
replaces=('kdegames-libkdegames')
conflicts=('kdegames-libkdegames')
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('1ba6f6e0800b75d955b354a31fb71e5ce02d6e3e')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
