# $Id: PKGBUILD 254065 2015-12-08 20:30:37Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=blinken
pkgver=15.08.3
pkgrel=2
pkgdesc="Memory Enhancement Game"
url="http://kde.org/applications/education/blinken/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kxmlgui' 'phonon-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
install=${pkgname}.install
replaces=('kdeedu-blinken')
conflicts=('kdeedu-blinken')
source=("http://download.kde.org/stable/applications/${pkgver}/src/blinken-${pkgver}.tar.xz")
sha1sums=('7639f694c05b86b39cc27ec3ef56284804cfe184')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../blinken-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
