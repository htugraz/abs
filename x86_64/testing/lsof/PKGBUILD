# $Id: PKGBUILD 226349 2014-11-17 16:31:10Z anatolik $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=lsof
pkgver=4.88
pkgrel=2
pkgdesc="Lists open files for running Unix processes"
arch=('i686' 'x86_64')
url="http://people.freebsd.org/~abe/"
license=('custom')
depends=('glibc')
source=(ftp://sunsite.ualberta.ca/pub/Mirror/lsof/lsof_$pkgver.tar.bz2
        ftp://sunsite.ualberta.ca/pub/Mirror/lsof/patches/4.88/linux.dnode.c.patch
        license.txt)
sha1sums=('09db6d2cd96bc6832d9b767084b9c67cf5cf52bb'
          '9a067d0e2c1dc83f6967c2ecb8fa5086860d92e4'
          'db6b6d90ce895e4053f91ad25c7761fbf9a37dd6')

prepare() {
	cd lsof_$pkgver
	tar xf lsof_${pkgver}_src.tar
	cd lsof_${pkgver}_src
	sed -i 's|/\* #define\tHASSECURITY\t1 \*/|#define\tHASSECURITY\t1|' dialects/linux/machine.h

	patch -p1 < "$srcdir"/linux.dnode.c.patch
}

build() {
	cd lsof_$pkgver/lsof_${pkgver}_src
	./Configure -n linux
	make 
}

check() {
	cd lsof_$pkgver/lsof_${pkgver}_src/tests
	sed 's/END=0/exit 0/' -i CkTestDB
	make all
}

package() {
	cd lsof_$pkgver/lsof_${pkgver}_src

	install -Dm0755 lsof "$pkgdir"/usr/bin/lsof 
	install -Dm0644 lsof.8 "$pkgdir"/usr/share/man/man8/lsof.8 
   
	install -D -m0644 "$srcdir"/license.txt \
	     "$pkgdir"/usr/share/licenses/lsof/LICENSE 
}
