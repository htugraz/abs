# $Id: PKGBUILD 227586 2014-12-13 15:52:58Z seblu $
# Maintainer: SÃ©bastien Luttringer

# Contacted by mail, author use svn commit as release version.
# He only push tested release code into svn and support using revision as version

pkgname=lz4
pkgver=125
pkgrel=1
pkgdesc='Very fast lossless compression algorithm'
arch=('i686' 'x86_64')
url='https://code.google.com/p/lz4/'
license=('GPL2')
makedepends=('svn')
checkdepends=('diffutils')
depends=('glibc')
source=("$pkgname::svn+http://lz4.googlecode.com/svn/trunk/#revision=$pkgver"
        '01-no-sudo.patch')
md5sums=('SKIP'
         '316915aff147579235092f3081447802')

prepare() {
  patch -d $pkgname -p1 < 01-no-sudo.patch
}

build() {
  cd $pkgname
  make
}

check() {
  $pkgname/programs/lz4 /etc/passwd passwd.lz4
  $pkgname/programs/lz4 -d passwd.lz4 passwd
  diff -q /etc/passwd passwd
  rm passwd
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
