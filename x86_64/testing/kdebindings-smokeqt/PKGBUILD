# $Id: PKGBUILD 212447 2014-05-14 19:52:09Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-smokeqt
pkgver=4.13.1
pkgrel=1
pkgdesc="Language independent library for Qt bindings"
url="https://projects.kde.org/projects/kde/kdebindings/smoke/smokeqt"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokegen' 'qimageblitz' 'qscintilla')
makedepends=('cmake' 'automoc4' 'mesa')
conflicts=('kdebindings-smoke')
source=("http://download.kde.org/stable/${pkgver}/src/smokeqt-${pkgver}.tar.xz")
sha1sums=('7b3504927b83f6360b354f2cb47944df6f9189d3')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../smokeqt-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Qwt5=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
