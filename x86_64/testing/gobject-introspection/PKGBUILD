# $Id: PKGBUILD 248170 2015-10-01 16:46:39Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gobject-introspection
pkgname=('gobject-introspection' 'gobject-introspection-runtime')
pkgver=1.46.0
pkgrel=1
url="https://live.gnome.org/GObjectIntrospection"
arch=('x86_64' 'i686')
license=('LGPL' 'GPL')
makedepends=('cairo' 'python2' 'python2-mako')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('6658bd3c2b8813eb3e2511ee153238d09ace9d309e4574af27443d87423e4233')

prepare() {
  cd $pkgbase-$pkgver
  sed -i '1s|#!/usr/bin/env python$|&2|' giscanner/*.py
}

build() {
  cd $pkgbase-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static --enable-doctool
  make
}

package_gobject-introspection-runtime() {
  pkgdesc="Introspection system for GObject-based libraries - runtime files"
  depends=('glib2')
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-libLTLIBRARIES install-typelibsDATA
}

package_gobject-introspection() {
  pkgdesc="Introspection system for GObject-based libraries"
  depends=("gobject-introspection-runtime=$pkgver" 'python2-mako' 'python2')
  options=('!emptydirs')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" uninstall-libLTLIBRARIES uninstall-typelibsDATA
}
