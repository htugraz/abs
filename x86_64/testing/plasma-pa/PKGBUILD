# $Id: PKGBUILD 245573 2015-09-08 15:51:31Z fyan $
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-pa
pkgver=5.4.1
pkgrel=1
pkgdesc='Plasma applet for audio volume management using PulseAudio'
arch=(i686 x86_64)
url='https://projects.kde.org/plasma-pa'
license=(LGPL)
depends=(plasma-workspace pulseaudio)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('6793e0a36f3237d1f1581874b4297da8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
