# $Id: PKGBUILD 254509 2015-12-08 20:43:59Z foutrelis $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=1.0.7
pkgrel=2
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs' 'geoip' 'python2')
makedepends=('boost')
options=('!emptydirs')
source=(https://github.com/arvidn/libtorrent/releases/download/libtorrent-${pkgver//./_}/$pkgname-$pkgver.tar.gz)
sha1sums=('db272a0c09a1fad721fe88e6e3942b9160bd0c61')

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 \
  ./configure \
    --prefix=/usr \
    --enable-python-binding \
    --with-libgeoip=system \
    --with-libiconv
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
