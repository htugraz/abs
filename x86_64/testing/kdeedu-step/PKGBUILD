# $Id: PKGBUILD 232647 2015-03-04 14:50:05Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-step
pkgver=14.12.3
pkgrel=1
pkgdesc="Interactive Physical Simulator"
url="http://kde.org/applications/education/step/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libqalculate' 'gsl')
makedepends=('cmake' 'automoc4' 'eigen2' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/step-${pkgver}.tar.xz")
sha1sums=('97c718fd9c0aef46c6e6d00fc894347920ebce3b')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../step-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
