# $Id: PKGBUILD 229181 2015-01-16 04:06:33Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kblackbox
pkgver=14.12.1
pkgrel=1
pkgdesc="A game of hide and seek played on a grid of boxes"
url="http://kde.org/applications/games/kblackbox/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kblackbox-${pkgver}.tar.xz")
sha1sums=('420f7baf5aca45f68486433ade21b33ea1c555db')

build() {
  mkdir build
  cd build
  cmake ../kblackbox-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
