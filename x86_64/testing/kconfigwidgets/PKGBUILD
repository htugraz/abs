# $Id: PKGBUILD 238629 2015-05-08 06:44:33Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kconfigwidgets
pkgver=5.10.0
pkgrel=1
pkgdesc='Widgets for KConfig'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kconfigwidgets'
license=('LGPL')
depends=('kauth' 'kcodecs' 'kconfig' 'kguiaddons' 'ki18n' 'kwidgetsaddons')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('1be31a33a566bb6c7aeb1f61f45da4a9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
