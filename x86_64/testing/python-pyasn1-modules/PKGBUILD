# $Id: PKGBUILD 242921 2015-08-02 06:44:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyasn1-modules
pkgname=('python-pyasn1-modules' 'python2-pyasn1-modules')
pkgver=0.0.7
pkgrel=1
pkgdesc="A collection of ASN.1-based protocols modules"
arch=('any')
license=('BSD')
url="http://pypi.python.org/pypi/pyasn1-modules"
makedepends=('python-pyasn1' 'python2-pyasn1' 'python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/p/pyasn1-modules/pyasn1-modules-${pkgver}.tar.gz")
md5sums=('9e92623a86709a77d43d406612ad7389')

prepare() {
   cp -a pyasn1-modules-${pkgver}{,-python2}
}

build() {
   # Build python 3 module
   cd pyasn1-modules-${pkgver}
   python3 setup.py build
 
   # Build python 2 module
   cd ../pyasn1-modules-${pkgver}-python2
   python2 setup.py build
}
 
package_python-pyasn1-modules() {
   depends=('python' 'python-pyopenssl' 'python-pyasn1')
 
   cd pyasn1-modules-${pkgver}
   python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
 
package_python2-pyasn1-modules() {
   depends=('python2' 'python2-pyopenssl' 'python2-pyasn1')
 
   cd pyasn1-modules-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

