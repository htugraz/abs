# $Id: PKGBUILD 222716 2014-09-22 21:09:25Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=p11-kit
pkgver=0.20.7
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules"
arch=(i686 x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=('glibc' 'libtasn1' 'libffi')
source=($url/releases/$pkgname-$pkgver.tar.gz{,.sig})
md5sums=('6648cad01a3080b685b8b3bf7372c91a'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-module-path=/usr/lib/pkcs11 \
    --with-trust-paths=/etc/ssl/certs/ca-certificates.crt
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd  $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
