# $Id: PKGBUILD 202072 2013-12-18 17:32:34Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kturtle
pkgver=4.12.0
pkgrel=1
pkgdesc="Educational Programming Environment"
url="http://kde.org/applications/education/kturtle/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kturtle-${pkgver}.tar.xz")
sha1sums=('2623bf1647cc26706edcd4e0ed4b4fca0dfb3fb2')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kturtle-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
