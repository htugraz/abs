# $Id: PKGBUILD 221263 2014-09-09 09:34:54Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=3.13.90
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
depends=('dconf' 'gtk3' 'hicolor-icon-theme')
makedepends=('gtk-doc' 'intltool' 'gobject-introspection' 'itstool' 'docbook-xsl' 'desktop-file-utils' 'python2' 'appdata-tools')
options=('!emptydirs')
groups=(gnome)
install=gucharmap.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('b7f8f8ba46ea95f10c2e5f64eeb9d1269947f45a46bfbb1756d14c8b24d37b22')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile  --enable-introspection
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
