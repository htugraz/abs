# $Id: PKGBUILD 206404 2014-02-25 12:25:36Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.2.11
pkgrel=2
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(LGPL)
depends=(grilo sqlite libgcrypt)
makedepends=(gupnp-av libgdata gmime libtracker-sparql libdmapsharing
             json-glib intltool avahi itstool)
optdepends=('gupnp-av: uPnP plugin'
            'libgdata: Youtube plugin'
            'gnome-online-accounts: Flickr plugin'
            'gmime: Podcasts plugin'
            'libtracker-sparql: Tracker plugin'
            'libdmapsharing: DMAP sharing plugin'
            'json-glib: TMDb plugin'
            'avahi: Freebox plugin')
options=('!emptydirs')
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('a2ac4ad28f3ead53d00d99653b2fb2d19cd5d9cfc33a16e7e82477223d089944')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --enable-shoutcast --disable-pocket
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
