# $Id: PKGBUILD 221065 2014-09-07 07:44:57Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gtksourceview3
_pkgbasename=gtksourceview
pkgver=3.13.90
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libxml2')
makedepends=('intltool' 'gobject-introspection' 'glade' 'vala' 'python2')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgbasename/${pkgver:0:4}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('0ec66cfcbb1c8e42d972fea4d9c6f1b47290ecce19eb975453949306a787d0fa')

build() {
  cd "$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-glade-catalog
  make
}

package() {
  cd "$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
}
