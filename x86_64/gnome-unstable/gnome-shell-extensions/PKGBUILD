# $Id: PKGBUILD 222762 2014-09-23 07:58:41Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-shell-extensions
pkgver=3.14.0
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
arch=(any)
url="http://live.gnome.org/GnomeShell/Extensions"
license=(GPL2)
depends=(gnome-shell libgtop)
makedepends=(intltool)
optdepends=('nautilus: gnome-classic session')
install=gnome-shell-extensions.install
groups=('gnome')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('cbca583409ef13cbcfa9552bfe436df10541b8af12e6f24e55e1ba7aa31a948d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/gnome-session --enable-extensions=all
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
