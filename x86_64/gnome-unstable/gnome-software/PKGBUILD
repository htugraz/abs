# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>

pkgname=gnome-software
pkgver=3.14.0
pkgrel=1
pkgdesc="GNOME Software Tools"
arch=(i686 x86_64)
license=('GPL2')
url="https://wiki.gnome.org/Apps/Software/"
depends=(gtk3 appstream-glib libsoup gsettings-desktop-schemas gnome-desktop packagekit desktop-file-utils)
makedepends=(intltool)
options=('!libtool' '!emptydirs')
install=gnome-software.install
groups=('gnome-extra')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('646cf4ed3274c0a1f60b2ddd1a967f608e96e355a4436e6d6ad00915339e2fa8')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  # Remove fedora-only plugins
  rm "${pkgdir}/usr/lib/gs-plugins-6/"libgs_plugin_fedora_tagger*
}
