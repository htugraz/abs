# $Id: PKGBUILD 234082 2015-03-18 10:18:48Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=bijiben
pkgver=3.15.92
pkgrel=1
pkgdesc="Write out notes, every detail matters"
arch=(i686 x86_64)
url="https://live.gnome.org/Bijiben"
license=(GPL)
depends=(tracker zeitgeist webkitgtk hicolor-icon-theme desktop-file-utils gnome-online-accounts evolution-data-server)
makedepends=(intltool itstool evolution)
options=(!emptydirs)
groups=(gnome-extra)
install=bijiben.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('7f1812771748491e4082540eed7535fe79cacac47d8caf37afd2e9b2b3d07215')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-update-mimedb
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
