# $Id: PKGBUILD 208644 2014-03-24 19:16:25Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calculator
pkgver=3.12.0
pkgrel=1
pkgdesc="GNOME Scientific calculator"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'dconf' 'gtksourceview3')
makedepends=('intltool' 'yelp-tools' 'vala')
groups=('gnome')
replaces=('gcalctool')
conflicts=('gcalctool')
options=(!emptydirs)
url="http://www.gnome.org"
install=gnome-calculator.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('22882c770251203be85f728d73b0d3946728774fd437cf460cd6d958fc4b4dc5')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile \
      --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
