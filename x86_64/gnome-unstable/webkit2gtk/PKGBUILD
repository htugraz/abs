# $Id: PKGBUILD 222958 2014-09-24 21:36:33Z heftig $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=webkit2gtk
pkgver=2.6.0
pkgrel=1
pkgdesc="GTK+ Web content engine library"
arch=(i686 x86_64)
url="http://webkitgtk.org/"
license=(custom)
depends=(libxt libxslt sqlite libsoup enchant libgl geoclue2 gst-plugins-base-libs
         libsecret libwebp harfbuzz-icu gtk3)
makedepends=(gtk3 gtk2 gperf gobject-introspection python2 mesa ruby gtk-doc cmake python)
optdepends=('gtk2: Netscape plugin support'
            'gst-plugins-base: free media decoding'
            'gst-plugins-good: media decoding'
            'gst-libav: nonfree media decoding')
options=(!emptydirs)
source=(http://webkitgtk.org/releases/webkitgtk-${pkgver}.tar.xz
        fix-pretty-quotes.patch)
sha256sums=('beb702b3d57d5323b6eecc7f517033777454ea59f1d897d2d96090756aadf4f3'
            '56316228bbbf0b7ebcbe210a35120f4e72cb9c1b680dd82cc2bde0f4549245e6')

prepare() {
  mkdir build

  cd webkitgtk-$pkgver
  patch -Np0 -i ../fix-pretty-quotes.patch
  rm -r Source/ThirdParty/gtest/
  rm -r Source/ThirdParty/qunit/
}

build() {
  cd build
  cmake -DPORT=GTK -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_INSTALL_DIR=/usr/lib -DLIBEXEC_INSTALL_DIR=/usr/lib/webkit2gtk-4.0 \
        ../webkitgtk-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/webkit2gtk"
  cd "$srcdir/webkitgtk-$pkgver/Source"
  for f in $(find -name 'COPYING*' -or -name 'LICENSE*'); do
    echo $f >> "$pkgdir/usr/share/licenses/webkit2gtk/LICENSE"
    cat $f >> "$pkgdir/usr/share/licenses/webkit2gtk/LICENSE"
    echo "" >> "$pkgdir/usr/share/licenses/webkit2gtk/LICENSE"
  done
}
