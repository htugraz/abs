# $Id: PKGBUILD 117391 2014-08-14 14:56:40Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas

pkgname=mongodb
pkgver=2.6.3
pkgrel=2
pkgdesc='A high-performance, open source, schema-free document-oriented database'
arch=('i686' 'x86_64')
url='http://www.mongodb.org'
license=('AGPL3')
depends=('pcre' 'snappy' 'openssl' 'gperftools' 'libsasl' 'boost-libs') # 'libyaml' 'yaml-cpp' 'v8')
makedepends=('scons' 'readline' 'ncurses' 'libpcap' 'boost' 'clang')
checkdepends=('python2-pymongo')
optdepends=('libpcap: needed for mongosniff')
backup=('etc/mongodb.conf')
install=mongodb.install
source=("http://downloads.mongodb.org/src/mongodb-src-r${pkgver}.tar.gz"
        'mongodb.conf' 'mongodb.service')

build() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons all \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --use-sasl-client \
    --use-system-boost \
    --ssl --cc=clang --cxx=clang++
    # --use-system-v8
    # --use-system-yaml
}

check() {
  # Tests not passing
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons smokeAll \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --use-sasl-client \
    --use-system-boost \
    --ssl --cc=clang --cxx=clang++ \
    --smokedbprefix="$srcdir" || warning "Tests failed"
}

package() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons install \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --use-sasl-client \
    --use-system-boost \
    --ssl --cc=clang --cxx=clang++ \
    --prefix="$pkgdir/usr"
    # --use-system-yaml
    # --use-system-v8

  install -Dm644 "$srcdir/mongodb.conf" "$pkgdir/etc/mongodb.conf"
  install -Dm644 "$srcdir/mongodb.service" "$pkgdir/usr/lib/systemd/system/mongodb.service"
  install -dm700 "$pkgdir/var/lib/mongodb"
  install -dm755 "$pkgdir/var/log/mongodb"
}

sha512sums=('c73ff95c949380c9b8a8e2c851d53731b08dcd6b93088c0d67fce1580a898d6742bebab3a142d5316353f00fbe12ea32c4651f04b66b85b205589414b7f6811a'
            '05dead727d3ea5fe8af1a3c3888693f6b3e2b8cb7f197a5d793352e10d2c524e96c9a5c55ad2e88c1114643a9612ec0b26a2574b48a5260a9b51ec8941461f1c'
            '177251404b2e818ae2b546fe8b13cb76e348c99e85c7bef22a04b0f07b600fd515a309ede50214f4198594388a6d2b31f46e945b9dae84aabb4dfa13b1123bb9')
