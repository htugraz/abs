# $Id: PKGBUILD 149111 2015-12-06 19:31:53Z bpiotrowski $
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=termite
pkgver=10
pkgrel=2
pkgdesc="A simple VTE-based terminal"
arch=('i686' 'x86_64')
url="https://github.com/thestinger/termite/"
license=('LGPL')
depends=('vte3-ng')
makedepends=('git')
backup=(etc/xdg/termite/config)
source=("git://github.com/thestinger/termite#tag=v$pkgver")
md5sums=('SKIP')

build() {
  cd $pkgname
  git submodule update --init
  make
}

package() {
  cd $pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 config "$pkgdir/etc/xdg/termite/config"
}
