# $Id: PKGBUILD 134733 2015-06-03 22:53:21Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tom K <tomk@runbox.com>

pkgname=perl-gtk2-trayicon
pkgver=0.06
pkgrel=12
pkgdesc="Perl interface to the EggTrayIcon library"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Gtk2-TrayIcon/"
depends=('gtk2-perl')
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends')
license=("GPL")
options=('!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/gtk2-perl/Gtk2-TrayIcon-${pkgver}.tar.gz")
md5sums=('522c328f14681a25d76eeaf317e05049')

build() {
  cd  Gtk2-TrayIcon-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  Gtk2-TrayIcon-${pkgver}
  make install DESTDIR=${pkgdir}
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
