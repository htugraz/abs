# $Id: PKGBUILD 117192 2014-08-11 18:17:11Z jelle $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Scott Horowitz <stonecrest@gmail.com>

pkgname=python2-tagpy
pkgver=2013.1
pkgrel=2
pkgdesc='Python bindings for TagLib'
arch=('x86_64' 'i686')
url='http://pypi.python.org/pypi/tagpy'
license=('MIT')
depends=('python2' 'taglib' 'boost-libs')
makedepends=('python2-setuptools' 'boost')
replaces=('tagpy')
conflicts=('tagpy')
source=("http://pypi.python.org/packages/source/t/tagpy/tagpy-$pkgver.tar.gz")
sha256sums=('80481c78ce34878e7a1f3231acd781a3bf1b0b569c70ce0e8d63af58061152f4')

prepare() {
  cd "tagpy-$pkgver"

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py') 
}

build() {
  cd "tagpy-$pkgver"

  ./configure.py \
    --taglib-lib-dir=/usr/lib/ \
    --taglib-inc-dir=/usr/include/taglib/ \
    --boost-inc-dir=/usr/include/boost/ \
    --boost-lib-dir=/usr/lib/ \
    --boost-python-libname=boost_python
  python2 setup.py build
}

check(){
  cd "tagpy-$pkgver"

  python2 setup.py test
}

package(){
  cd "tagpy-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
