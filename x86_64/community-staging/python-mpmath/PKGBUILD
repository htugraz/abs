# $Id: PKGBUILD 141118 2015-09-20 02:14:01Z fyan $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Rafael G. Martins <rafael@rafaelmartins.com>
# Contributor: Iván Pulido <ijpulidos@riseup.net>

pkgbase=python-mpmath
pkgname=('python2-mpmath' 'python-mpmath')
pkgver=0.19
pkgrel=2
pkgdesc='Python library for arbitrary-precision floating-point arithmetic.'
arch=('any')
url='http://mpmath.org/'
license=('BSD')
makedepends=('python' 'python2')
source=("http://mpmath.org/files/mpmath-${pkgver}.tar.gz")
md5sums=('af5cc956b2673b33a25c3e57299bae7b')

prepare(){
  cp -R mpmath-$pkgver mpmath2-$pkgver
}

build() {
  cd mpmath2-$pkgver  
  python2 setup.py build

  cd ../mpmath-$pkgver
  python setup.py build
}

package_python2-mpmath() {
  depends=('python2-gmpy2')
  cd mpmath2-${pkgver}

  find -name '*.py' | xargs sed -e 's|#!/usr/bin/python|#!/usr/bin/python2|' -i

  python2 setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}


package_python-mpmath() {
  depends=('python-gmpy2')
  cd mpmath-${pkgver}  

  python setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE
}


