# $Id: PKGBUILD 101579 2013-11-28 07:53:15Z cinelli $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Vasco Costa <vasco.costa@meiodigital.com>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=pokerth
_realname=PokerTH
pkgver=1.0.1
pkgrel=3
pkgdesc="Poker game written in C++/Qt4"
arch=('i686' 'x86_64')
url="http://www.pokerth.net/"
license=('GPL' 'custom')
depends=('curl' 'boost-libs' 'gsasl' 'gnutls' 'protobuf'
         'qt4' 'sdl_mixer' 'libircclient' 'tinyxml')
makedepends=('boost')
source=("http://downloads.sourceforge.net/sourceforge/pokerth/$_realname-$pkgver-src.tar.bz2")
md5sums=('c6dcf0ed68ab50af91371348ffad5d00')

build() {
  cd "$srcdir/$_realname-$pkgver-src"
  
  qmake-qt4 "$pkgname.pro"
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver-src"

  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 "docs/pokerth.1" "$pkgdir/usr/share/man/man1/pokerth.1"
  install -Dm644 "data/data-copyright.txt" "$pkgdir/usr/share/licenses/pokerth/data-copyright.txt"
}
