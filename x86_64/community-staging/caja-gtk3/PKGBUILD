# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=caja
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=2
pkgdesc="File manager for the MATE desktop (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'exempi' 'gtk3' 'gvfs' 'libexif'
         'libunique3' 'libxt' 'mate-desktop-gtk3>=1.10' 'mate-icon-theme'
         'startup-notification')
makedepends=('gobject-introspection' 'mate-common' 'pangox-compat')
optdepends=('gstreamer: automatic media playback when mouseover'
            'gvfs-smb: To connect to Samba/Windows shares')
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('a8d9c4a479a2fea7bd2eda707e0fd4025f85f229')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --with-gtk=3.0 \
        --enable-unique \
        --enable-introspection \
        --disable-static \
        --disable-update-mimedb
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
