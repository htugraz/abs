# $Id: PKGBUILD 149089 2015-12-06 19:19:37Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=opencc
pkgname=(opencc opencc-doc)
pkgver=1.0.3
pkgrel=2
url="http://code.google.com/p/opencc/"
arch=('i686' 'x86_64')
license=('Apache')
makedepends=('cmake' 'doxygen' 'python')
source=("https://github.com/BYVoid/OpenCC/archive/ver.${pkgver}.tar.gz")
sha512sums=('0257ff15cf9c57f248ec236a7ddde1f29e223d0e26411628f0321be92098f01977e19b0af271627660d07c63f7fa6804bc31fe5cbf576398cbc783d31e23966a')

build() {
  cd OpenCC-ver.${pkgver}
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_DOCUMENTATION:BOOL=ON
  make
}

package_opencc() {
  pkgdesc="Library for Open Chinese Convert"
  depends=('gcc-libs')

  cd OpenCC-ver.${pkgver}
  make DESTDIR="${pkgdir}" install
  
  # Remove docs - install in splitted package
  rm -r "$pkgdir/usr/share/opencc/doc"
}

package_opencc-doc() {
  pkgdesc="Documentation for Library for Open Chinese Convert"

  cd OpenCC-ver.${pkgver}/doc
  make DESTDIR="${pkgdir}" install
}

