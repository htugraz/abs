# $Id: PKGBUILD 149970 2015-12-07 15:30:13Z fyan $
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Storyteller <spiralsorrow@hotmail.com>

pkgname=lmms
pkgver=1.1.3
pkgrel=2
pkgdesc='The Linux MultiMedia Studio.'
arch=('i686' 'x86_64')
url='http://lmms.sourceforge.net/'
license=('GPL')
depends=('sdl' 'glib2' 'fluidsynth' 'libpng' 'libvorbis' 'libxft' 'libxinerama' 'qt4' 'sdl_sound'
         'libsamplerate' 'shared-mime-info' 'fltk')
optdepends=('wine: VST support (experimental)'
            'fftw: SpectrumAnalyzer plugin'
            'stk: STK instruments plugins'
            'pulseaudio: PulseAudio output')
makedepends=('cmake' 'ladspa' 'libxft' 'freetype2' 'fftw' 'stk' 'wine')
makedepends_x86_64=('gcc-multilib')
provides=('lmms-extras')
conflicts=('lmms-extras')
options=('!makeflags')
install=lmms.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/LMMS/${pkgname}/archive/v${pkgver}.tar.gz"
        "make-werrror-optional.patch")
md5sums=('88d9e66d240b711c37315e3c9da644a1'
         '2c6e51b665c34fa61dc8b23e129404a9')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake

  patch -Np1 -i ../make-werrror-optional.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -USE=vst
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
