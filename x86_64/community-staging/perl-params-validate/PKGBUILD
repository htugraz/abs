# $Id: PKGBUILD 134635 2015-06-02 18:33:05Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-params-validate
pkgver=1.18
pkgrel=2
pkgdesc="Validate sub params against a spec"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Params-Validate"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-module-implementation')
makedepends=('perl-module-build')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/D/DR/DROLSKY/Params-Validate-${pkgver}.tar.gz")
md5sums=('2d6c94dd441cd96f98844aa19ebdbcb2')

build() {
  cd  Params-Validate-${pkgver}
  perl ./Build.PL --installdirs=vendor
  ./Build
}
package() {
  cd  Params-Validate-${pkgver}
  ./Build install --destdir=${pkgdir}
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
