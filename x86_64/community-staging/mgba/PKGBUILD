# $Id: PKGBUILD 133236 2015-05-11 16:40:22Z jsteel $
# Maintainer:  Jonathan Steel <jsteel at archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgbase=mgba
pkgname=('libmgba' 'mgba-sdl' 'mgba-qt')
pkgver=0.2.0
pkgrel=2
arch=('i686' 'x86_64')
url='https://endrift.com/mgba/'
license=('custom:MPL2')
makedepends=('cmake' 'qt5-multimedia' 'sdl2' 'zlib' 'libpng' 'libzip' 'libedit'
             'ffmpeg' 'imagemagick' 'desktop-file-utils')
source=($pkgbase-$pkgver.tar.gz::https://github.com/mgba-emu/mgba/archive/$pkgver.tar.gz
        mgba.desktop)
sha1sums=('96b92c016d4ceffc651ca7a99067782a81656ca7'
          '0f6f43ed45b702f6571c254d4717fbc1fe15e845')

prepare() {
  [[ ! -d build ]] && mkdir build || rm -rf build

  cd mgba-$pkgver/res
  convert mgba-1024.png -resize 256x256 mgba-256.png
}

build() {
  cd build
  cmake "$srcdir"/mgba-$pkgver -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_libmgba() {
  pkgdesc='Shared library of mGBA'
  depends=('zlib' 'libpng' 'libzip' 'libedit' 'ffmpeg' 'imagemagick')

  cmake -DCOMPONENT=libmgba mgba-$pkgver -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
    -P build/cmake_install.cmake
  install -Dm644 mgba-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_mgba-sdl() {
  pkgdesc='A Nintendo Gameboy Advance Emulator focusing on both speed and accuracy'
  depends=('libmgba' 'sdl2')

  cmake -DCOMPONENT=mgba-sdl mgba-$pkgver -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
    -P build/cmake_install.cmake

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s /usr/share/licenses/libmgba/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_mgba-qt() {
  pkgdesc='A Nintendo Gameboy Advance Emulator focusing on both speed and accuracy. Qt5 UI.'
  depends=('libmgba' 'qt5-multimedia' 'sdl2')

  cmake -DCOMPONENT=mgba-qt mgba-$pkgver -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
    -P build/cmake_install.cmake

  desktop-file-install mgba.desktop --dir "$pkgdir"/usr/share/applications/
  install -Dm644 mgba-$pkgver/res/mgba-256.png "$pkgdir"/usr/share/pixmaps/mgba.png

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s /usr/share/licenses/libmgba/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
