# $Id: PKGBUILD 134653 2015-06-03 02:40:08Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-mouse
pkgver=2.4.2
pkgrel=2
pkgdesc="Moose minus the antlers"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Mouse"
license=('PerlArtistic' 'GPL')
depends=('perl')
makedepends=('perl-test-exception' 'perl-test-fatal' 'perl-module-build'
             'perl-module-build-xsutil' 'perl-test-requires'
             'perl-test-leaktrace' 'perl-test-output' 'perl-test-deep'
             'perl-io-string')
source=(http://search.cpan.org/CPAN/authors/id/G/GF/GFUJI/Mouse-$pkgver.tar.gz)
md5sums=('7abd3ab36026425dd035aecc53aa60b7')

build() {
  cd Mouse-$pkgver
  perl Build.PL --installdirs=vendor
  ./Build
}

check() {
  cd Mouse-$pkgver
  ./Build test
}

package() {
  cd Mouse-$pkgver
  ./Build install --destdir="${pkgdir}"
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
