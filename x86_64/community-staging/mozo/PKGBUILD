# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9
_pkgbase=mozo
pkgname=${_pkgbase}
pkgver=${_ver}.90
pkgrel=1
pkgdesc="MATE menu editing tool (GTK2 version)"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('mate-menus>=1.10' 'pygtk' 'python2-gobject')
makedepends=('git' 'mate-common')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
source=("${_pkgbase}"::"git+https://github.com/mate-desktop/${_pkgbase}.git")
sha1sums=('SKIP')
install=${_pkgbase}.install

prepare() {
    cd "${srcdir}/${_pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${_pkgbase}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${_pkgbase}"
    make DESTDIR="${pkgdir}" install
}
