# $Id: PKGBUILD 107473 2014-03-17 13:17:30Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pyproj
pkgname=('python-pyproj' 'python2-pyproj')
pkgver=1.9.3
pkgrel=3
pkgdesc="Python interfaces to PROJ.4 library"
arch=('i686' 'x86_64')
url="http://code.google.com/p/pyproj"
license=('custom')
source=(http://pyproj.googlecode.com/files/pyproj-${pkgver}.tar.gz)
makedepends=('python' 'python2')
sha1sums=('3f290241e113ea63d0487a48e7e054cc1c3e19f8')

build() {
   cd "${srcdir}"

   cp -a pyproj-${pkgver}{,-python2}

   # Build python 3 module
   cd pyproj-${pkgver}
   python3 setup.py build

   # Build python 2 module
   cd ../pyproj-${pkgver}-python2
   python2 setup.py build
}

package_python-pyproj() {
   depends=('proj' 'python>=3.4')

   cd "${srcdir}"/pyproj-${pkgver}
   python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

   install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
   install -m644 LICENSE_proj4 "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyproj() {
   depends=('proj' 'python2>=2.7')
   replaces=('pyproj')

   cd "${srcdir}"/pyproj-${pkgver}-python2
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

   install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
   install -m644 LICENSE_proj4 "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
