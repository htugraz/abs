# $Id: PKGBUILD 106746 2014-03-07 08:11:41Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-iso8601
pkgname=(python-iso8601 python2-iso8601)
_libname=iso8601
pkgver=0.1.10
pkgrel=3
pkgdesc="Simple module to parse ISO 8601 dates"
arch=(any)
url="http://pypi.python.org/pypi/iso8601"
license=('MIT')
source=(http://pypi.python.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz)
makedepends=(python-setuptools python2-setuptools)

build() {
	cd "$srcdir/$_libname-$pkgver"

	rm -rf ../buildpy3; mkdir ../buildpy3
	python setup.py build -b ../buildpy3

	rm -rf ../buildpy2; mkdir ../buildpy2
	python2 setup.py build -b ../buildpy2
}

package_python-iso8601() {
	depends=(python)

	cd "$srcdir/$_libname-$pkgver"
	rm -rf build; ln -s ../buildpy3 build
	python setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-iso8601() {
	depends=(python2)

	cd "$srcdir/$_libname-$pkgver"
	#cd "$srcdir/sybren-python-rsa-8f50f506b4a2"
	rm -rf build; ln -s ../buildpy2 build
	python2 setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha256sums=('e712ff3a18604833f5073e836aad795b21170b19bbef70947c441ed89d0ac0e1')
