# $Id: PKGBUILD 141450 2015-09-22 07:09:26Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Farhad Shahbazi <farhad@enthusiasm.cc>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-bottle
pkgname=("python-bottle" "python2-bottle")
pkgver=0.12.8
pkgrel=2
pkgdesc="A fast and simple micro-framework for small web-applications"
arch=(any)
url="http://bottlepy.org"
license=('MIT')
options=(!emptydirs)
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-mako' 'python2-mako' 'python-jinja' 'python2-jinja'
              'python-cherrypy' 'python2-cherrypy' 'python-twisted'
              'python2-twisted' 'python-tornado' 'python2-tornado'
              'python2-paste' 'python2-gevent')
source=("git+https://github.com/defnull/bottle.git#tag=${pkgver}")
sha512sums=('SKIP')

prepare() {
  cp -a bottle{,-py2}
  sed -i "1s/python/python2/" bottle-py2/bottle.py
}

check() {
  cd "$srcdir/bottle"
  # https://github.com/bottlepy/bottle/issues/791
  python test/testall.py || warning 'Tests failed'

  cd "$srcdir/bottle-py2"
  python2 test/testall.py
}

package_python-bottle() {
  depends=('python3')

  cd bottle
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-bottle() {
  depends=('python2')

  cd bottle-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # deal with conflict with python-bottle's /usr/bin/bottle.py
  mv "$pkgdir/usr/bin/bottle.py"{,2}
}
