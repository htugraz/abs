# $Id: PKGBUILD 73684 2012-07-14 05:00:28Z dwallace $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Byron Clark <byron@theclarkfamily.name>
pkgname=udiskie
pkgver=1.3.0
pkgrel=2
pkgdesc="Removable disk automounter using udisks"
arch=('any')
url="https://pypi.python.org/pypi/udiskie"
license=('MIT')
depends=('udisks2' 'python-gobject' 'python-setuptools' 'python-yaml' 'python-docopt' 'gettext' 'libnotify')
makedepends=('asciidoc')
optdepends=('zenity: for luks decryption'
            'pygtk: for tray icon'
            'udisks: optional for use with old backend'
            'notification-daemon: for notifications')
options=(!emptydirs)
source=("https://pypi.python.org/packages/source/${pkgname:0:1}/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('e61f1c18e909ff8b4c83d8e0d21dc1f7')

package() {
  cd "$srcdir/$pkgname-$pkgver"
  export LC_ALL=en_US.utf8

  python setup.py install --root="$pkgdir" --optimize=1

  make -C doc
  install -m 0644 -D "doc/${pkgname}.8" "$pkgdir/usr/share/man/man8/${pkgname}.8"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

}

# vim:set ts=2 sw=2 et:
