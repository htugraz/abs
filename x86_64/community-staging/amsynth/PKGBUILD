# $Id: PKGBUILD 149931 2015-12-07 14:57:28Z allan $
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=amsynth
pkgver=1.6.2
pkgrel=2
pkgdesc="Analogue Modeling SYNTHesizer"
arch=('i686' 'x86_64')
url="http://amsynth.github.io/"
license=('GPL')
groups=('dssi-plugins' 'lv2-plugins')
depends=('gtkmm' 'jack' 'liblo' 'desktop-file-utils')
makedepends=('dssi' 'ladspa')
install="$pkgname.install"
source=("https://github.com/amsynth/amsynth/releases/download/release-$pkgver/amsynth-$pkgver.tar.gz")
md5sums=('254c393675adb7ff2d9e1ee1dc0ba1b1')

build() {
  cd $pkgname-$pkgver

  # enable required C++11 support
  export CXXFLAGS+=' -std=c++11'

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
