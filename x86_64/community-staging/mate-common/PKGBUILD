# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-common
pkgver=1.8.0
pkgrel=2
pkgdesc="Common development macros for MATE"
arch=('any')
license=('GPL')
depends=('autoconf' 'automake' 'gettext' 'gtk-doc' 'intltool' 'libtool' 'pkg-config')
url="http://mate-desktop.org"
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz"
        'add-automake-115.diff')
sha1sums=('41156798ed4cc55d39bb85eaeffa244b224fa3f6'
          '851b95dbc5151ac5e6dfcf05eaede2911fa575bc')

prepare() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    patch -Np1 -i "${srcdir}/add-automake-115.diff"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
