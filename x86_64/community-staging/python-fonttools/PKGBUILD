# $Id: PKGBUILD 141593 2015-09-25 10:12:38Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Firmicus <firmicus āt gmx dōt net>
# Contributor: flying sheep <flying-sheep@web.de>

pkgbase=python-fonttools
pkgname=('python2-fonttools' 'python-fonttools')
pkgver=3.0
pkgrel=1
pkgdesc="Converts OpenType and TrueType fonts to and from XML"
url="https://github.com/behdad/fonttools"
license=("BSD")
arch=('any')
makedepends=("python2-setuptools" "python-numpy" "python2-numpy")
source=($pkgbase-$pkgver.tar.gz::https://github.com/behdad/fonttools/archive/${pkgver}.tar.gz)
md5sums=('e5de6f32a3db235a5adce4f456e8826b')

prepare() {
  cd "$srcdir"
  cp -a fonttools-${pkgver} fonttools-py2-${pkgver}
  cd fonttools-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"

  cd "$srcdir"/fonttools-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/fonttools-${pkgver}
  python setup.py build
}

package_python2-fonttools() {
  depends=("python2-numpy")

  cd "$srcdir"/fonttools-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  # fix conflicts with python-fonttools
  mv "$pkgdir"/usr/bin/pyftinspect{,2}
  mv "$pkgdir"/usr/bin/pyftmerge{,2}
  mv "$pkgdir"/usr/bin/pyftsubset{,2}
  mv "$pkgdir"/usr/bin/ttx{,2}
  mv "$pkgdir"/usr/share/man/man1/ttx{,2}.1

  install -D -m755 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  chmod oga+r "$pkgdir"/usr/share/man/man1/ttx2.1
}

package_python-fonttools() {
  depends=("python-numpy")

  cd "$srcdir"/fonttools-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m755 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  chmod oga+r "$pkgdir"/usr/share/man/man1/ttx.1
}
