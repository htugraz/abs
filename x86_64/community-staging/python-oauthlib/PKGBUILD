# $Id: PKGBUILD 140898 2015-09-19 15:17:46Z fyan $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-oauthlib
pkgname=(python-oauthlib python2-oauthlib)
pkgver=1.0.3
pkgrel=2
_libname=oauthlib
pkgdesc="A generic, spec-compliant, thorough implementation of the OAuth request-signing logic"
arch=(any)
url="https://github.com/idan/oauthlib"
license=('BSD')
source=(http://pypi.python.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz)
makedepends=(python-setuptools python2-setuptools)

build() {
	cd "$srcdir/$_libname-$pkgver"

	rm -rf ../buildpy3; mkdir ../buildpy3
	python setup.py build -b ../buildpy3

	rm -rf ../buildpy2; mkdir ../buildpy2
	python2 setup.py build -b ../buildpy2
}

package_python-oauthlib() {
	depends=(python-cryptography)

	cd "$srcdir/$_libname-$pkgver"
	rm -rf build; ln -s ../buildpy3 build
	python setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	chmod -R a+r "$pkgdir/usr/lib"
}

package_python2-oauthlib() {
	depends=(python2-cryptography)

	cd "$srcdir/$_libname-$pkgver"
	rm -rf build; ln -s ../buildpy2 build
	python2 setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	chmod -R a+r "$pkgdir/usr/lib"
}

sha256sums=('ef4bfe4663ca3b97a995860c0173b967ebd98033d02f38c9e1b2cbb6c191d9ad')
