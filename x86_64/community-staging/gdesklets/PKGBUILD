# $Id: PKGBUILD 111552 2014-05-19 22:30:02Z anatolik $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: nifan <carlos@nifan.tk>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gdesklets
pkgver=0.36.3
pkgrel=5
pkgdesc="System for bringing mini programs (desklets) onto your desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/gdesklets"
license=('GPL') 
depends=('libgtop' 'librsvg' 'pygtk' 'desktop-file-utils' 'gnome-icon-theme' 'xdg-utils')
makedepends=('gnome-python' 'intltool')
install=$pkgname.install
source=(https://launchpad.net/gdesklets/0.3x/release-of-$pkgver/+download/$pkgname-$pkgver.tar.bz2
        gdesklets-core-0.36.3-.in-files.patch
        fix-vfs.patch
        make-gconf-optional.patch)
md5sums=('60c0c4e7301b3e653702b6fe5bf7e698'
         '27839bbefc2f6664ff31cff2d48dfeed'
         '55ac07d16a571f29033495307c4d0f0c'
         '45e81dd0fb20157bb840d006c3a4e2ea')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Build fix
  patch -Np0 -i "$srcdir/gdesklets-core-0.36.3-.in-files.patch"

  # Runtime fix without gnome-vfs
  patch -Np1 -i "$srcdir/fix-vfs.patch"

  # Make gconf dependency optional
  patch -Np1 -i "$srcdir/make-gconf-optional.patch"

  # Python2 fix
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  sed -i 's/python -c/$PYTHON -c/' configure.ac
  sed -i 's/python/python2/' shell/plugins/PackageInstaller/__init__.py
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
