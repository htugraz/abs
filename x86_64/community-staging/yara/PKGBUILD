# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: bitwave <aur [aT] oomlu [d0T] de>
# Contributor: fnord0 <fnord0 AT riseup DOT net>

pkgbase=yara
pkgname=('yara' 'python-yara' 'python2-yara')
pkgver=3.4.0
pkgrel=3
pkgdesc="Tool aimed at helping malware researchers to identify and classify malware samples"
url="https://plusvic.github.io/yara/"
arch=('i686' 'x86_64')
license=('Apache')
makedepends=('openssl' 'file' 'python-setuptools' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/plusvic/${pkgbase}/archive/v${pkgver}.tar.gz)
sha512sums=('27286107882c70de6f2c227d5307fbaa97437f8bbb221d4f0fcb17c4935f65ef01e772c18a14eb814727ca1854f5e3f9f219771706d9f4a748ae9433f475d669')

build() {
  cd ${pkgbase}-${pkgver}
  autoreconf --force --install
  ./configure --prefix=/usr --with-crypto --enable-magic
  make
}

package_yara() {
  depends=('openssl' 'file')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
  cp -r docs "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python-yara() {
  depends=('python' 'yara')
  cd ${pkgbase}-${pkgver}/${pkgbase}-python
  python setup.py install --root="${pkgdir}" -O1
  install -Dm 644 ../README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
  ln -s /usr/share/doc/yara/docs "${pkgdir}/usr/share/doc/${pkgname}/docs"


}

package_python2-yara() {
  depends=('python2' 'yara')
  cd ${pkgbase}-${pkgver}/${pkgbase}-python
  python2 setup.py install --root="${pkgdir}" -O1
  install -Dm 644 ../README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
  ln -s /usr/share/doc/yara/docs "${pkgdir}/usr/share/doc/${pkgname}/docs"
}

# vim: ts=2 sw=2 et:
