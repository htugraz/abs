# $Id: PKGBUILD 102681 2013-12-17 13:22:17Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>

pkgname=gwenhywfar
pkgver=4.3.3
_dnrel=67
pkgrel=4
pkgdesc="OS abstraction functions for various projects"
arch=(i686 x86_64)
url="http://www.aquamaniac.de"
license=("LGPL")
depends=('openssl' 'gnutls' 'libgcrypt')
makedepends=('gtk2' 'qt4')
optdepends=('gtk2' 'qt4')
source=("gwenhywfar-$pkgver.tar.gz::http://www2.aquamaniac.de/sites/download/download.php?package=01&release=${_dnrel}&file=01&dummy=gwenhywfar-$pkgver.tar.gz")
md5sums=('c5b9d52a25960f84f9264a5a5dd484d1')

build() {
  pacman -Q gwenhywfar && return 1
  cd $srcdir/gwenhywfar-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --enable-ssl
  sed -i 's|gnutls_transport_set_lowat||g' src/sio/syncio_tls.c
  make
}

package() {
  cd $srcdir/gwenhywfar-$pkgver
  make DESTDIR=$pkgdir install
}
