# $Id: PKGBUILD 124924 2014-12-30 15:49:36Z anatolik $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Emiliano Vavassori <syntaxerrormmm@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz@mail.ru>

pkgname=ruby-mail
pkgver=2.6.3
pkgrel=2
pkgdesc='A really Ruby Mail handler'
arch=('any')
url='http://github.com/mikel/mail'
license=('MIT')
depends=('ruby-mime-types' 'ruby-treetop')
options=('!emptydirs')
source=("http://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('d7fee1ec4e4ea9bb38b77de5baf53c17004133efcdff030bd1de5e3620306fd9')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} ${pkgname#*-}-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  ln -s ${_gemdir}/gems/${pkgname#*-}-${pkgver}/MIT-LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
