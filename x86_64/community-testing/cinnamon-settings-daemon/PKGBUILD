# $Id: PKGBUILD 103385 2014-01-05 03:23:19Z fyan $
# Maintainer: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=2.0.8
pkgrel=2
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('cinnamon-desktop' 'libibus' 'libcanberra-pulse' 'librsvg' 'nss' 'pulseaudio-alsa' 'upower' 'libnotify')
makedepends=('intltool' 'docbook-xsl' 'gnome-common')
options=('!emptydirs')
install=cinnamon-settings-daemon.install
url="https://github.com/linuxmint/cinnamon-settings-daemon"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-settings-daemon/archive/${pkgver}.tar.gz"
        keyboard.patch
        automount-plugin.patch)
sha256sums=('6889f021acab93d146c78fe8aa540a83a12260232e1ea5f697037a7c94c877fd'
            '3c05b978cb414c8fc1530f211bab58627a64ac9c156630c9d486f5926a85109c'
            '4fc2b3a354b53d41d92d7f95d175a6a18382330353a0579d29c2d33b9400a837')

prepare() {
  cd $pkgname-$pkgver

  # Fix crasher
  patch -Np1 -i ../keyboard.patch

  # Turn automount helper into a plugin
  patch -Np1 -i ../automount-plugin.patch
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static --enable-systemd

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
