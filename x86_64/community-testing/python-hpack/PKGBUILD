# $Id: PKGBUILD 141869 2015-09-28 10:47:37Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hpack
pkgname=(python-hpack python2-hpack)
_pkgname=hpack
pkgver=1.1.0
pkgrel=2
pkgdesc="Pure-Python HPACK header compression"
arch=('any')
url="http://hyper.rtfd.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver::https://github.com/Lukasa/hpack/archive/v${pkgver}.tar.gz")
sha512sums=('2a9618b838dc8e0360e297ac0c65584c02dc0f036a49c833aac34b6950789acda145e180dd3a80b1e02e38be12ce0f829db60fa67ee306d4e8ae7a7fff8d500d')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build

  cd ../$_pkgname-$pkgver-py2
  python2 setup.py build
}

check() {
  cd $_pkgname-$pkgver
  py.test

  cd ../$_pkgname-$pkgver-py2
  py.test2
}

package_python-hpack() {
  depends=('python')

  cd $_pkgname-$pkgver
  python setup.py install -O1 --root "${pkgdir}"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-hpack() {
  depends=('python2')

  cd $_pkgname-$pkgver-py2
  python2 setup.py install -O1 --root "${pkgdir}"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

