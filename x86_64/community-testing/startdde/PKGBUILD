# $Id: PKGBUILD 151183 2015-12-08 21:28:44Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=2.92.1.20151208
_commit=4fc6ec103ba6422ff5babc8faa582a8913ddfab3
#_golibver=0.4.2
_golibrev=902e89b94c9d657f523bcdabad399dc789dcf86e
#_deepinapiver=2.92.1
_deepinapirev=fb83410f533eec79579ac11386520db3a733c550
#_dbusfactoryver=2.92.1
_deepinfactoryrev=3c2b44c085415de28f2f82825aa36012eeb31042
pkgrel=4
pkgdesc="starter of deepin desktop environment"
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('gcc-go' 'deepin-daemon' 'deepin-wm-switcher')
makedepends=('cmake' 'coffee-script' 'deepin-dbus-generator' 'python2' 'webkitgtk' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/startdde.git#commit=$_commit"
        #"git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver"
        "git+https://github.com/linuxdeepin/go-lib.git#commit=$_golibrev"
        #"git+https://github.com/linuxdeepin/dde-api.git#tag=$_deepinapiver"
        "git+https://github.com/linuxdeepin/dde-api.git#commit=$_deepinapirev"
        #"git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://github.com/linuxdeepin/dbus-factory.git#commit=$_deepinfactoryrev")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  sed -i '/GOLDFLAGS +=/ s/)$/ libcanberra)/' startdde/Makefile

  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/

  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/startdde"
  make USE_GCCGO=1
}

package() {
  cd startdde
  make DESTDIR="${pkgdir}" install
}
