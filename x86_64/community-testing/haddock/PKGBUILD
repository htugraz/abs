# $Id: PKGBUILD 140154 2015-09-15 01:57:12Z foutrelis $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=haddock
pkgver=2.16.1
pkgrel=2
pkgdesc='Tool for generating documentation for Haskell libraries'
url='http://hackage.haskell.org/package/haddock/'
license=('custom:BSD3')
arch=('x86_64' 'i686')
makedepends=('alex' 'happy')
depends=('ghc=7.10.1' 'haskell-ghc-paths' 'haskell-haddock-api')
install="$pkgname.install"
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('0a0fa6abfecd1f3094b66f99e91a9aed72ffd11d3038a1e0ae6fec1db849cba9')
sha256sums=('46ecd130cb5ad2b5c7452c843f9b75e976f1416d1cf17e6436d65c2c0bdbd6d6')

build() {
  cd "$pkgname-$pkgver"

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/"$pkgname" \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$pkgname-$pkgver"

#  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -Dm744 unregister.sh \
    "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s "/usr/share/doc/$pkgname/html" \
    "$pkgdir/usr/share/doc/ghc/html/libraries/$pkgname"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
  mv "$pkgdir/usr/bin/haddock" "$pkgdir/usr/bin/haddock-cabal"
}

# vim:set ts=2 sw=2 et:
