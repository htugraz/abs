# $Id: PKGBUILD 113022 2014-06-11 13:38:49Z jgc $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx [dot] de>

pkgname=metapixel
pkgver=1.0.2
pkgrel=7
pkgdesc='A program for generating photomosaics.'
arch=('i686' 'x86_64')
url='http://www.complang.tuwien.ac.at/schani/metapixel/'
license=('GPL')
depends=('giflib' 'libjpeg' 'libpng' 'perl' 'glib2')
makedepends=('git' 'docbook-xsl')
source=("git://github.com/schani/metapixel.git#commit=4ad7be9e82"
        "metapixel-giflib-5.1.0-compat.patch")
md5sums=('SKIP'
         'b959c6d1b14064aee3a73ae568a82555')

prepare() {
  cd "${srcdir}/${pkgname}"

  git submodule init
  git submodule update

  cd rwimg
  patch -p1 -i ../../metapixel-giflib-5.1.0-compat.patch
}

build() {
  cd "${srcdir}/${pkgname}"

  # Build librwimg first, make dependencies seem to be bugged.
  make librwimg
  make MANPAGE_XSL="/usr/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl"
}

package() {
  cd "${srcdir}/${pkgname}"
  
  install -dm0755 "${pkgdir}/usr/share/man/man1"

  make PREFIX="${pkgdir}/usr" MANDIR="${pkgdir}/usr/share/man/man1" install
}
