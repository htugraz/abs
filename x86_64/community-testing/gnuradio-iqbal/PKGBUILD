# $Id: PKGBUILD 118366 2014-09-05 13:58:56Z foutrelis $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=gnuradio-iqbal
_pkgname=gr-iqbal
pkgver=0.37.2
pkgrel=3
pkgdesc="Gnuradio I/Q balancing"
arch=('i686' 'x86_64')
url="http://cgit.osmocom.org/gr-iqbal/"
license=('GPL')
depends=('gnuradio' 'fftw')
replaces=('gr-iqbal')
makedepends=('git' 'cmake' 'boost' 'swig')
source=("git://git.osmocom.org/gr-iqbal/#tag=v$pkgver")
md5sums=('SKIP')

build() {
  cd "$srcdir/$_pkgname"
  git submodule init
  git submodule update
  sed -i 's/LIBOSMODSP_LIBRARIES ""/LIBOSMODSP_LIBRARIES ${FFTW3F_LIBRARIES}/' CMakeLists.txt 
  mkdir build
  cd build
  cmake \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE=$(which python2) \
    -DPYTHON_INCLUDE_DIR=$(echo /usr/include/python2*) \
    -DPYTHON_LIBRARY=$(echo /usr/lib/libpython2.*.so) \
    -DCMAKE_LIBRARY_PATH=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$_pkgname/build/"
  make DESTDIR="${pkgdir}" install
}
