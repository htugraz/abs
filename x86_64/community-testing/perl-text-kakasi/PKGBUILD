# $Id: PKGBUILD 135388 2015-06-15 08:53:38Z bluewind $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-text-kakasi
pkgver=2.04
pkgrel=12
pkgdesc="Perl frontend to kakasi"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Text-Kakasi"
license=('GPL' 'PerlArtistic')
depends=('perl' 'kakasi')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/D/DA/DANKOGAI/Text-Kakasi-${pkgver}.tar.gz")
md5sums=('5a9e381cb93edfd707124a63c60f96b1')

build() {
  cd  Text-Kakasi-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd  Text-Kakasi-${pkgver}

  make install DESTDIR=${pkgdir}

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
