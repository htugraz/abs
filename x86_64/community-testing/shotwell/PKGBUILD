# $Id: PKGBUILD 135627 2015-06-19 21:42:54Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
epoch=1
pkgver=0.22.0
_basever=$(echo $pkgver | cut -f1-2 -d.)
pkgrel=2
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('libgee' 'webkitgtk' 'libgexiv2' 'libraw' 'rest'
	 'desktop-file-utils' 'dconf' 'hicolor-icon-theme' 'json-glib'
	 'gst-plugins-base' 'libgphoto2' 'libgudev')
makedepends=('intltool' 'vala>=0.17.2' 'gnome-doc-utils')
install=shotwell.install
source=("https://download.gnome.org/sources/shotwell/${_basever}/shotwell-$pkgver.tar.xz")
md5sums=('18421a67a1b859caf8542777a42b9ce3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --libexec=/usr/lib/shotwell \
	--disable-schemas-compile \
	--disable-desktop-update \
	--disable-icon-update
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
