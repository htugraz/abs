# $Id: PKGBUILD 140187 2015-09-15 01:57:55Z foutrelis $
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

_hkgname=regex-base
pkgname=haskell-regex-base
pkgver=0.93.2
pkgrel=22
pkgdesc='Interface API for regex-posix,pcre,parsec,tdfa,dfa'
url='http://hackage.haskell.org/cgi-bin/hackage-scripts/package/regex-base'
license=('custom:BSD3')
arch=('x86_64' 'i686')
depends=('ghc=7.10.1' 'haskell-mtl')
source=("http://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
install="$pkgname.install"
options=('staticlibs')
sha256sums=('20dc5713a16f3d5e2e6d056b4beb9cfdc4368cd09fd56f47414c847705243278')

build() {
  cd "$_hkgname-$pkgver"

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared --prefix=/usr \
    --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$_hkgname-$pkgver"

  install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
  install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
  runhaskell Setup copy --destdir=${pkgdir}
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
