# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgname=libnftnl
pkgver=1.0.0
pkgrel=1
pkgdesc='Netfilter library providing interface to the nf_tables subsystem'
arch=('i686' 'x86_64')
url='http://netfilter.org/projects/libnftnl/'
license=('GPL2')
depends=('libmnl')
source=("http://netfilter.org/projects/libnftnl/files/libnftnl-1.0.0.tar.bz2"{,.sig}
        '01-fix-nftables-rename.patch')
sha1sums=('02464d6511f22392c20ca003248d85741ecbc865'
          'SKIP'
          'b406eb6b71b25d2abe559a5643421d42117d9724')

prepare() {
   patch -p 1 -d $pkgname-$pkgver < '01-fix-nftables-rename.patch'
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
