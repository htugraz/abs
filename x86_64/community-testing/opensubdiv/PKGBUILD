# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=opensubdiv
pkgver=3.0.2
pkgrel=3
pkgdesc="An Open-Source subdivision surface library"
arch=(i686 x86_64)
url="http://graphics.pixar.com/opensubdiv"
license=('apache')
depends=(zlib ptex intel-tbb)
makedepends=(cmake doxygen glfw glew python2 python-pygments python-docutils opencl-headers)
#makedepends_x86_64=(cuda) Broken currently :/
source=("https://github.com/PixarAnimationStudios/OpenSubdiv/archive/v${pkgver//./_}.tar.gz")
md5sums=('acee9de1cd966ad3b4e7297858cff6d6')

prepare() {
    cd "OpenSubdiv-${pkgver//./_}"

    find . -name \*.py | xargs sed -i "s/env python/env python2/g"
}

build() {
    cd "OpenSubdiv-${pkgver//./_}"

    rm -rf build
    mkdir build && cd build

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
    cd "OpenSubdiv-${pkgver//./_}"/build

    make DESTDIR="$pkgdir/" install

    rm -rf ${pkgdir}/usr/bin
}
