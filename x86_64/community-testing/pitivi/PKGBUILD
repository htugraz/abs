# $Id: PKGBUILD 141797 2015-09-28 10:45:08Z fyan $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Gabor Nyekhelyi (n0gabor) <n0gabor@vipmail.hu>

pkgname=pitivi
pkgver=0.94
pkgrel=2
pkgdesc="Editor for audio/video projects using the GStreamer framework"
arch=('i686' 'x86_64')
url="http://www.pitivi.org/"
license=('LGPL')
depends=('clutter-gtk' 'gnonlin' 'gst-editing-services' 'gst-plugins-good' 'gst-python'
         'libnotify' 'python-gobject' 'python-numpy' 'python-cairo'
         'gnome-icon-theme' 'desktop-file-utils' 'clutter-gst')
makedepends=('intltool' 'itstool')
optdepends=('frei0r-plugins: additional video effects, clip transformation feature'
            'gst-libav: additional multimedia codecs'
            'gst-plugins-bad: additional multimedia codecs'
            'gst-plugins-ugly: additional multimedia codecs'
            'gnome-desktop: file thumbnails provided by GNOME'\''s thumbnailers')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz)
md5sums=('1617825f763959a674ab6bb66f70e670')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
