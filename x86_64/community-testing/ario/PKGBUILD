# $Id: PKGBUILD 103055 2013-12-25 21:35:21Z andyrtr $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=ario
pkgver=1.5.1
pkgrel=3
pkgdesc="A GTK client for MPD (Music player daemon) inspired by Rhythmbox but much lighter and faster"
arch=('i686' 'x86_64')
url="http://ario-player.sourceforge.net/"
license=('GPL')
depends=('avahi' 'curl' 'dbus-glib' 'gnutls' 'hicolor-icon-theme' 'libglade' 'libmpdclient' 'libnotify' 'libsoup' 'libunique' 'taglib' 'xdg-utils')
makedepends=('intltool' 'perl-xml-parser')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/sourceforge/$pkgname-player/$pkgname-$pkgver.tar.gz
        glib.patch)
sha256sums=('0831281ab8634eb92424b61230eee035822f7f92d8b0526da7316c898239c91e'
            '16481cc5fe3fb52ee36bae63522e319193285542fe0c8ab4281ecc5794a82184')

prepare() {
  cd "${srcdir}/$pkgname-$pkgver"
  patch -p1 -i ../glib.patch
}

build() {
  cd "${srcdir}/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
 }

package () {
  cd "${srcdir}/$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
