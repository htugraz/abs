# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-data-dumper
pkgver=2.154
pkgrel=2
pkgdesc="stringified perl data structures, suitable for both printing and eval"
arch=(i686 x86_64)
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/Data-Dumper/"
depends=('perl')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/S/SM/SMUELLER/Data-Dumper-$pkgver.tar.gz)
md5sums=('577b4d4e53d7609457d36d674b6169a7')

build() {
  cd $srcdir/Data-Dumper-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd $srcdir/Data-Dumper-${pkgver}
  make install DESTDIR=$pkgdir
  find $pkgdir -name perllocal.pod -delete
  find $pkgdir -name .packlist -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
