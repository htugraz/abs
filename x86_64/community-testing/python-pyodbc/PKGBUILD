# $Id: PKGBUILD 150913 2015-12-08 19:45:26Z foutrelis $
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Robert Knauer <robert@privatdemail.net>

pkgbase=python-pyodbc
pkgname=(python-pyodbc python2-pyodbc)
_srcname=pyodbc
pkgver=3.0.10
pkgrel=2
pkgdesc='ODBC module for Python'
arch=('i686' 'x86_64')
url='https://github.com/mkleehammer/pyodbc'
license=('MIT')
makedepends=('python' 'python2' 'unixodbc')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/mkleehammer/pyodbc/archive/${pkgver}.tar.gz")
sha256sums=('942bab2ce0e245f7c6a929f262fe6f79e9940f2785d71cb04359585f425d29f9')

prepare() {
	echo "Version: ${pkgver}.0" > "${srcdir}/${_srcname}-${pkgver}/PKG-INFO"
}

package_python-pyodbc() {
	pkgdesc='ODBC module for Python 3.x'
	depends=('python' 'unixodbc')

	cd "${srcdir}/${_srcname}-${pkgver}"

	python setup.py install --root="${pkgdir}/" --optimize=1
}

package_python2-pyodbc() {
	pkgdesc='ODBC module for Python 2.x'
	depends=('python2' 'unixodbc')

	cd "${srcdir}/${_srcname}-${pkgver}"

	python2 setup.py install --root="${pkgdir}/" --optimize=1
}

