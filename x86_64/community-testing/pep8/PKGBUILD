# $Id: PKGBUILD 108311 2014-03-26 15:36:46Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgbase=pep8
pkgname=("$pkgbase-python2" "$pkgbase-python3")
pkgver=1.5.0
pkgrel=2
pkgdesc="Python style guide checker"
arch=('any')
url="http://github.com/jcrocholl/pep8/"
license=('custom:Expat')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/p/$pkgbase/$pkgbase-${pkgver}.tar.gz"
        LICENSE)
sha256sums=('d3b18ca2e25827a480145e481e511a52cd067ee9b98be57575a82bc073340c23'
            '4a18840b7c132952f3f3d42c355289018ea263cbe75b25db31c9feb1b61e9b40')

prepare() {
  cp -r $pkgbase-$pkgver{,-python2}

  sed -i '1s/env python$/python2/' $pkgbase-$pkgver-python2/pep8.py
}

package_pep8-python2() {
  depends=('python2' 'python2-setuptools')
  provides=('pep8')
  conflicts=('pep8')
  replaces=('pep8')
  
  cd $pkgbase-$pkgver-python2
  python2 setup.py install --prefix=/usr --root="$pkgdir/"
  mv "$pkgdir/usr/bin/pep8" "$pkgdir/usr/bin/pep8-python2"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pep8-python3() {
  depends=('python' 'python-setuptools')
  
  cd $pkgbase-$pkgver
  python3 setup.py install --prefix=/usr --root="$pkgdir/"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
