# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: naelstrof <naelstrof@gmail.com>

pkgname=maim
pkgver=3.4.46
pkgrel=2
pkgdesc='Utility to take a screenshot using imlib2'
arch=('i686' 'x86_64')
url='https://github.com/naelstrof/maim'
license=('GPL3')
depends=('freetype2' 'imlib2' 'libx11' 'libxext' 'libxfixes' 'libxrandr')
makedepends=('cmake')
optdepends=('slop: region selection support')
source=("${pkgname}-${pkgver}.tar.gz"::"$url/archive/v${pkgver}.tar.gz")
md5sums=('a0d512567b4b8eee6af912be312ec40e')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
