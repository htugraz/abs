# $Id: PKGBUILD 73557 2012-07-09 19:15:21Z arodseth $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marcel Huber <marcelhuberfoo at gmail dott com>
# Contributor: Illarion Kovalchuk <illarion.kovalchuk at gmail dot com>

pkgname=jenkins
pkgver=1.544
pkgrel=1
pkgdesc='Extendable continuous integration server (latest)'
arch=('any')
url='https://jenkins-ci.org/changelog-stable#stable'
license=('MIT')
depends=('java-runtime' 'ttf-dejavu' 'libcups')
provides=('jenkins-ci')
conflicts=('jenkins-ci')
replaces=('jenkins-ci')
backup=('etc/conf.d/jenkins')
install='jenkins.install'
noextract=('jenkins.war')
source=("http://mirrors.jenkins-ci.org/war/$pkgver/jenkins.war"
        'jenkins.conf'
        'jenkins.service'
        'jenkins.tmpfiles.d'
        'LICENSE')

package() {
  install -Dm444 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm444 jenkins.war "$pkgdir/usr/share/java/jenkins/jenkins.war"
  install -Dm644 jenkins.service \
    "$pkgdir/usr/lib/systemd/system/jenkins.service"
  install -Dm644 jenkins.tmpfiles.d "$pkgdir/usr/lib/tmpfiles.d/jenkins.conf"
  install -Dm644 jenkins.conf "$pkgdir/etc/conf.d/jenkins"
}

# vim:set ts=2 sw=2 et:

sha256sums=('a841635f07cd145fc9396abfc41c8810ec0d86b3846b05b9c85b7048ac1648fa'
            '13b6e06de1dedff96bdb8e43f6830bbd954dc58df9d4ed5583693d2a6f8427f4'
            '23b748ae4c418bd1b98e33dd6bb55ad0d578803aecd26176998b759689b82e73'
            '0ccff16308b01b02f2699ea64a73b8dce1e1990ee1c656aa8d6119dee510262e'
            'd9f107920982cb61d807e349b4eaf190b2d593047e55d3f3ad286c7afe06cf55')
