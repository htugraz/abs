# $Id: PKGBUILD 151268 2015-12-09 14:12:07Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=15.10.4
_commit=9f8f8a39d819872621f014bd5a90bf2b5c3c3f79
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
depends=('flattr-icon-theme')
makedepends=('git' 'inkscape')
source=("git+https://github.com/linuxdeepin/deepin-icon-theme.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}"
  sed 's\flattr\Flattr\' -i deepin/index.theme
  sed 's\Faenza\Adwaita\' -i deepin/index.theme

  mkdir -p build
}

build() {
  cd "${pkgname}"
  python2 tools/convert.py deepin build
}

package() {
  cd "${pkgname}"
  install -d "${pkgdir}"/usr/share/icons
  cp -r build/deepin "${pkgdir}"/usr/share/icons/Deepin

  make DESTDIR="$pkgdir" install-cursors
}
