# $Id
# Maintainer : BlackIkeEagle <ike DOT devolder AT gmail DOT com>

pkgbase=packagekit-qt
pkgname=(
	"${pkgbase}4"
	"${pkgbase}"
)
pkgver=0.9.2
pkgrel=2
arch=('i686' 'x86_64')
license=('LGPLv2')
url="http://www.packagekit.org/"
makedepends=(
"packagekit" 'qt4' 'cmake' 'qt5-base'
)
source=(
	"http://www.freedesktop.org/software/PackageKit/releases/PackageKit-Qt-$pkgver.tar.xz"
	'0001-Remove-daemon-proxy-conection-to-changed.patch'
	'0002-Add-operator-to-bitfield.patch'
	'0003-Remove-the-metatype-registration-for-Transaction-typ.patch'
	'0004-Remove-RoleUpgradeSystem-as-it-s-not-supported-in-0..patch'
	'0005-Properly-export-cmake-targets.patch'
	'0006-Don-t-assume-the-target-is-packagekitqt5.patch'
	'0007-Fix-compilation-due-to-not-set-cmake-variable.patch'
	'0008-Fix-QPK-include-dir-in-cmake-module.patch'
	'0009-Disable-namespacing-and-classes-names-on-the-interfa.patch'
)
sha256sums=(
	'd5a20d2a477c7b77feaabb7d1eb10d00e2168f81ae4b08a34398bbeded12f4ce'
	'15133ec8115cdd4c26276c1ac819b11d59238daf38e88673f7c448e07a20df0c'
	'15d0481dd1e5b993750d4db05c55cb1086eec577cce27b95b234ae1b962094f9'
	'810967071dfeb7e0d6ec4d75b5ffee5bc2a5ccd707b6c6ad28ac97f7e1b1997f'
	'b0e81c73c72b61f68f374f03895a6deb2ee0cfffad3ad99badae5ace1a89be89'
	'93962313bfe61313803afb1262aafdf58fb273263c37ddc9f72d8041ebd8e7fc'
	'227697bd204d3f0ec7def0d8d78bb9a7a13de787b23f68f2ab1aa8926111935a'
	'582fc2e70817103756b2f62413cddb8cf56daa6a101d12971beeef1a4c895f05'
	'896d9c83722e12c03e10c60f2291f881aa807ab6e5ed88c8e8b3aae6ea6deea6'
	'47c9d5e8184a865fb4ffdc3bee5618b3b5997931f81607f93e762f192791a64d'
)

prepare() {
	cd "$srcdir/PackageKit-Qt-$pkgver"

	patch -p1 -i "$srcdir/0001-Remove-daemon-proxy-conection-to-changed.patch"
	patch -p1 -i "$srcdir/0002-Add-operator-to-bitfield.patch"
	patch -p1 -i "$srcdir/0003-Remove-the-metatype-registration-for-Transaction-typ.patch"
	patch -p1 -i "$srcdir/0004-Remove-RoleUpgradeSystem-as-it-s-not-supported-in-0..patch"
	patch -p1 -i "$srcdir/0005-Properly-export-cmake-targets.patch"
	patch -p1 -i "$srcdir/0006-Don-t-assume-the-target-is-packagekitqt5.patch"
	patch -p1 -i "$srcdir/0007-Fix-compilation-due-to-not-set-cmake-variable.patch"
	patch -p1 -i "$srcdir/0008-Fix-QPK-include-dir-in-cmake-module.patch"
	patch -p1 -i "$srcdir/0009-Disable-namespacing-and-classes-names-on-the-interfa.patch"

	mkdir "qt4"
	mkdir "qt5"
}

build() {
	cd "$srcdir/PackageKit-Qt-$pkgver/qt4"
	cmake -DCMAKE_INSTALL_LIBDIR:PATH=lib ..
	make

	cd "$srcdir/PackageKit-Qt-$pkgver/qt5"
	cmake -DCMAKE_INSTALL_LIBDIR:PATH=lib -DUSE_QT5=1 ..
	make
}

package_packagekit-qt4() {
	pkgdesc="QT4 bindings for PackageKit"
	depends=('packagekit' 'qt4')

	cd "$srcdir/PackageKit-Qt-$pkgver/qt4"
	make DESTDIR="$pkgdir" install
}

package_packagekit-qt() {
	pkgdesc="QT5 bindings for PackageKit"
	depends=('packagekit' 'qt5-base')

	cd "$srcdir/PackageKit-Qt-$pkgver/qt5"
	make DESTDIR="$pkgdir" install
}
