# $Id: PKGBUILD 143213 2015-10-07 02:59:11Z foutrelis $
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

_hkgname=extensible-exceptions
pkgname=haskell-${_hkgname}
pkgver=0.1.1.4
pkgrel=12
pkgdesc="extensible exceptions for both new and old versions of GHC"
url="http://hackage.haskell.org/package/extensible-exceptions"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc=7.10.2' 'sh')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=$pkgname.install
options=('staticlibs')
md5sums=('fa1cadd15c1c7aa362618d41c6e17d8b')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}

    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
