# $Id: PKGBUILD 140296 2015-09-15 03:10:34Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
# Contributor: Nicolas Storme <nicolas@atelier-web.com>

pkgname=goaccess
pkgver=0.9.4
pkgrel=2
pkgdesc="An open source real-time web log analyzer and interactive viewer"
arch=('i686' 'x86_64')
url='http://goaccess.io'
license=('GPL')
depends=('ncurses' 'geoip' 'glib2')
backup=('etc/goaccess.conf')
source=("http://tar.goaccess.io/$pkgname-$pkgver.tar.gz")
sha512sums=('068ce399c4b202d322f297847c1a5b80e3143de93496db234a6db772ad7beb7d497ccad30a1d39b72d131488543f45d0dd179106bbabdbcdef4771a87dd8acef')

build() {
  cd $pkgname-$pkgver
  ./configure --enable-geoip --enable-utf8
  make prefix=/usr sysconfdir=/etc
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" install
}

