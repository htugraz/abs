# $Id: PKGBUILD 110290 2014-04-28 04:21:06Z td123 $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=gtk2hs-buildtools
pkgver=0.12.5.2
pkgrel=2
pkgdesc="Tools to build the Gtk2Hs suite of User Interface libraries."
url="http://hackage.haskell.org/package/gtk2hs-buildtools"
license=('GPL2')
arch=('i686' 'x86_64')
options=('staticlibs')
makedepends=('ghc=7.8.2-2' 'alex' 'happy' 'haskell-random' 'haskell-hashtables')
depends=('gmp')
provides=('haskell-gtk2hs-buildtools')
source=("http://hackage.haskell.org/packages/archive/gtk2hs-buildtools/$pkgver/gtk2hs-buildtools-$pkgver.tar.gz")
md5sums=('dd39243ae579ab32236fbc17dbeec107')

build() {
    cd ${srcdir}/gtk2hs-buildtools-$pkgver
    runhaskell Setup configure --prefix=/usr --docdir=/usr/share/doc/${pkgname}
    runhaskell Setup build
}

package() {
    cd ${srcdir}/gtk2hs-buildtools-$pkgver
    runhaskell Setup copy --destdir=${pkgdir}
}
