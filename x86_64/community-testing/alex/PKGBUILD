# $Id: PKGBUILD 143196 2015-10-07 02:58:56Z foutrelis $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.1.4
pkgrel=6
pkgdesc='Lexical analyser generator for Haskell'
arch=('x86_64' 'i686')
url='http://hackage.haskell.org/package/alex'
license=('custom:BSD3')
depends=('gmp' 'libffi')
makedepends=('ghc=7.10.2' 'haskell-quickcheck' 'happy' 'libffi')
source=("http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('d64e4eccd953c62795ed566a6de5f3d150b653e05872ff697b3d9dbc961da19f')

build() {
  cd "$pkgname-$pkgver"

  runghc Setup.lhs configure --prefix=/usr --datasubdir="$pkgname"
  runghc Setup.lhs build
}

package() {
  cd "$pkgname-$pkgver"

  runghc Setup.lhs copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/alex/BSD3"
  # Doc only contains the license
  rm -rf $pkgdir/usr/share/doc
}

# vim:set ts=2 sw=2 et:
