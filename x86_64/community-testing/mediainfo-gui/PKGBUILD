# $Id: PKGBUILD 150776 2015-12-08 19:41:42Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: hydro <hydro@freenet.de>

pkgname=mediainfo-gui
pkgver=0.7.80
pkgrel=2
pkgdesc="GUI for mediainfo"
arch=('i686' 'x86_64')
url="http://mediainfo.sourceforge.net"
license=('GPL')
depends=('libmediainfo' 'wxgtk' 'libsm' 'desktop-file-utils')
makedepends=('imagemagick')
install=mediainfo-gui.install
source=(http://mediaarea.net/download/source/mediainfo/$pkgver/mediainfo_$pkgver.tar.bz2)
md5sums=('718b104084190a045262933985555fbb')

build() {
  cd MediaInfo/Project/GNU/GUI
  sh ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd MediaInfo/Project/GNU/GUI
  make DESTDIR="$pkgdir" install
  _iconsize=`identify $srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png | grep -oP ' \d+x\d+ ' | tr -d ' '`
  install -Dm 644 "$srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png" \
    "$pkgdir/usr/share/icons/hicolor/${_iconsize}/apps/mediainfo.png"
  install -m 644 "$srcdir/MediaInfo/Source/Resource/Image/MediaInfo."{svg,ico,xpm} "$pkgdir/usr/share/icons/"
  install -Dm 644 "$srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png" \
    "$pkgdir/usr/share/pixmaps/mediainfo-gui.png"
  install -Dm 644 "$srcdir/MediaInfo/Project/GNU/GUI/mediainfo-gui.desktop" \
    "$pkgdir/usr/share/applications/mediainfo-gui.desktop"
}
