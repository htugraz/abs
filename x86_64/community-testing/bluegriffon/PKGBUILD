# $Id: PKGBUILD 149407 2015-12-07 00:26:23Z muflone $
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=bluegriffon
pkgver=1.8
_mozilla_ver=fec90cbfbaad
_github_ver=5961e4cbc9ea546f9d7e4b9a7f2bb6884d66a378
pkgrel=1
pkgdesc="The next-generation Web Editor based on the rendering engine of Firefox"
arch=('i686' 'x86_64')
url="http://bluegriffon.org/"
license=('MPL' 'GPL' 'LGPL')
depends=('alsa-lib' 'desktop-file-utils' 'dbus-glib' 'gtk2' 'hunspell' 'mozilla-common'
         'nss' 'libevent' 'libvpx' 'libxt' 'python2' 'startup-notification')
makedepends=('autoconf2.13' 'libpulse' 'unzip' 'zip')
options=('!emptydirs')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/therealglazou/${pkgname}/archive/${_github_ver}.tar.gz"
        "mozilla-central-${_mozilla_ver}.tar.bz2"::"http://hg.mozilla.org/mozilla-central/archive/${_mozilla_ver}.tar.bz2"
        "bug_1194520_add_ftfntfmt.patch"::"https://hg.mozilla.org/mozilla-central/raw-rev/af40750720b5"
        "mozconfig"
        "${pkgname}.desktop")
sha256sums=('0c629f882a7b7fcdf4416871db8d522f43653b62c0e5a25168b1914af8448a11'
            '95a9868b809664bd79727119eace53b9f6e97c54a137c28b7e6dda688aade6fd'
            '3aea9a83bf304da5525f34a911712cf42f8ded1c8b6becf0a2cf8a4b4f7facd6'
            'fb8b56dcbf3daf13ac336b85183db8d48068d9f82dc74acad9fa619a165c601f'
            '882133b77840356985a81e496b4ddc443f8f33c068cfb7ef0858497820adcfff')

prepare() {
  cd "${srcdir}/mozilla-central-${_mozilla_ver}"

  # Move files to their correct location
  cp -r "${srcdir}/${pkgname}-${_github_ver}" bluegriffon
  cp "${srcdir}/mozconfig" .mozconfig

  # Apply patches
  patch -p1 -i "${srcdir}/bug_1194520_add_ftfntfmt.patch"

  # Apply BlueGriffon's upstream customizations
  patch -Np1 -i bluegriffon/config/content.patch

  # python2 fix
  find . -name \*.py -exec sed -i 's@^#.*python$@#!/usr/bin/python2@' '{}' \;
}

build() {
  cd "${srcdir}/mozilla-central-${_mozilla_ver}"

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  export CPPFLAGS+=' -O2'
  export PYTHON=python2

  # Build the package
  ./mach build
}

package() {
  cd "${srcdir}/mozilla-central-${_mozilla_ver}"

  # Install files
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}"
  cp -LR bg/dist/bin/* "${pkgdir}/usr/lib/${pkgname}"

  # Install executable file
  install -m 755 -d "${pkgdir}/usr/bin"
  ln -sf /usr/lib/${pkgname}/${pkgname} "${pkgdir}/usr/bin/${pkgname}"

  # Install icons
  cp bg/dist/branding/mozicon128.png bg/dist/branding/default128.png
  for _icon in 16 32 48 128; do
      install -Dm644 bg/dist/branding/default${_icon}.png \
        "${pkgdir}/usr/share/icons/hicolor/${_icon}x${_icon}/apps/${pkgname}.png"
  done

  # Install desktop file
  install -Dm644 ../${pkgname}.desktop \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Use system-provided dictionaries
  rm -rf "${pkgdir}/usr/lib/${pkgname}/dictionaries"
  ln -sf /usr/share/hunspell "${pkgdir}/usr/lib/${pkgname}/dictionaries"
  rm -rf "${pkgdir}/usr/lib/${pkgname}hyphenation"
  ln -sf /usr/share/hyphen "${pkgdir}/usr/lib/${pkgname}/hyphenation"
}
