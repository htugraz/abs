# $Id: PKGBUILD 150855 2015-12-08 19:43:50Z foutrelis $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_name=owncloudclient
pkgname=owncloud-client
pkgver=2.1.0
pkgrel=2
pkgdesc='ownCloud client based on mirall'
arch=(i686 x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain-qt5 qt5-webkit hicolor-icon-theme xdg-utils)
makedepends=(extra-cmake-modules python-sphinx qt5-tools doxygen kio)
optdepends=('python2-nautilus: integration with Nautilus')
install=$pkgname.install
backup=('etc/ownCloud/sync-exclude.lst')
source=("https://download.owncloud.com/desktop/stable/$_name-$pkgver.tar.xz"{,.asc})
md5sums=('6ab37353c8de73bd268aba6abb1f4020'
         'SKIP')
validpgpkeys=('F05F7DD7953A07DF36579DAA498C45EBE94E7B37')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$_name-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/$pkgname \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 
  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
