# $Id: PKGBUILD 124104 2014-12-19 13:58:10Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Ebubekir KARUL <ebubekirkarul@yandex.com>
# Contributor: s1gma,shirokuro

pkgname=scapy
pkgver=2.3.0
pkgrel=1
pkgdesc="A powerful interactive packet manipulation program written in Python"
url="http://www.secdev.org/projects/scapy/"
depends=('tcpdump' 'python2' )
optdepends=('python2-crypto: WEP decryption support'
            'python2-pyx: psdump() and pdfdump() functions'
            'python2-gnuplot: plotting support'
            'graphviz: conversations() method support'
            'sox: for VOIP support')
checkdepends=('python2-crypto' 'python2-pyx' 'python2-gnuplot' 'graphviz' 'sox')
arch=('any')
license=('GPL')
source=(https://bitbucket.org/secdev/scapy/downloads/${pkgname}-${pkgver}.zip)
sha256sums=('1e63e845ff7a86d4915122945cad2a3b5c60b6914b23713bcbdbab7b24fd47c8')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/env python$/env python2/g' -i doc/scapy/*.rst \
    run_scapy scapy/tools/check_asdis.py scapy/layers/inet6.py setup.py
  sed -e 's/exec python$/exec python2/g' -i run_scapy setup.py
  sed -e 's/exec python /exec python2 /' -i test/run_tests
}

build() {
  cd ${pkgname}-${pkgver}
  python2 setup.py build
}

check() {
  cd ${pkgname}-${pkgver}/test
  # Disabled tests as they currently hang
  # ./run_tests
}

package() {
  cd ${pkgname}-${pkgver}
  python2 setup.py install -O1 --skip-build --prefix="${pkgdir}/usr"
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/scapy/*.rst "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim: ts=2 sw=2 et:
