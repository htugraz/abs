# $Id: PKGBUILD 104617 2014-01-23 13:39:35Z bpiotrowski $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz@mail.ru>

pkgname=ruby-mime-types
pkgver=2.0
pkgrel=2
pkgdesc='MIME Content-Type database'
arch=('any')
url='http://mime-types.rubyforge.org'
license=('GPL2' 'MIT' 'PerlArtistic')
depends=('ruby')
source=("http://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('d3e53ba4162d4ee93ce75e644c7fca5503fad1c512277b9ca80ad43aa8103ae0')

prepare() {
  gem install --no-{document,user-install} --ignore-dependencies -i . ${pkgname#*-}-${pkgver}.gem
}

package() {
  cd gems/${pkgname#*-}-${pkgver}

  local _rubyver="$(ruby --version | sed 's/.* \(.*\..*\..*\)p.*/\1/')"

  install -dm 755 "${pkgdir}"/usr/lib/ruby/{gems/${_rubyver},vendor_ruby}
  mv lib "${pkgdir}"/usr/lib/ruby/vendor_ruby/${_rubyver}

# Gem compatibility
  mv ../../specifications "${pkgdir}"/usr/lib/ruby/gems/${_rubyver}/

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 {,"${pkgdir}"/usr/share/licenses/${pkgname}/}Licence.rdoc
}

# vim: ts=2 sw=2 et:
