# $Id: PKGBUILD 233775 2015-03-14 10:45:57Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kremotecontrol
pkgver=15.03.90
pkgrel=1
pkgdesc='Configure your remote controls for use with applications'
url='http://kde.org/applications/utilities/kremotecontrol/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
replaces=('kdeutils-kdelirc')
conflicts=('kdeutils-kdelirc')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kremotecontrol-${pkgver}.tar.xz")
sha1sums=('f83bc7d5b91e8f916b173b19bc9683ea66652913')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kremotecontrol-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
