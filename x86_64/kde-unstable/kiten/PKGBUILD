# $Id: PKGBUILD 235054 2015-03-27 08:34:41Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kiten
pkgver=15.03.97
pkgrel=1
pkgdesc="Japanese Reference/Study Tool"
url="http://kde.org/applications/education/kiten/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('khtml')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdeedu-kiten')
replaces=('kdeedu-kiten')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kiten-${pkgver}.tar.xz")
sha1sums=('e8011810473b6fb84b30f9d9860dba88b1574e18')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kiten-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
