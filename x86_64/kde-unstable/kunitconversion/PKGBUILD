# $Id: PKGBUILD 202355 2013-12-21 12:19:08Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kunitconversion
pkgver=r5.1e2ebda
pkgrel=1
pkgdesc='KUnitConversion'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kunitconversion'
license=('LGPL')
depends=('kconfig' 'ki18n')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/kunitconversion.git')
md5sums=('SKIP')

pkgver() {
  cd kunitconversion
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kunitconversion \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
