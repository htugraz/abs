# $Id: PKGBUILD 202310 2013-12-21 12:08:35Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesignerplugin
pkgver=r6.c3ebe3f
pkgrel=1
pkgdesc='KDesignerPlugin'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdesignerplugin'
license=('LGPL')
depends=('kio' 'kplotting')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
groups=('kf5')
source=('git://anongit.kde.org/kdesignerplugin.git')
md5sums=('SKIP')

pkgver() {
  cd kdesignerplugin
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  # There's something wrong with our kdoctools but I don't get it
  sed -i 's/add_subdirectory(docs)/#add_subdirectory(docs)/' \
    kdesignerplugin/CMakeLists.txt
}

build() {
  cd build
  cmake ../kdesignerplugin \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
