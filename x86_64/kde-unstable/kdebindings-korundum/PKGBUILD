# $Id: PKGBUILD 207400 2014-03-08 16:16:58Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-korundum
pkgver=4.12.80
pkgrel=2
pkgdesc="Ruby bindings for libraries created by the KDE community"
url="https://projects.kde.org/projects/kde/kdebindings/ruby/korundum"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-qtruby' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost'
             'kdepimlibs' 'kdegraphics-okular' 'kdesdk-kate')
optdepends=('kdegraphics-okular: okular bindings'
            'kdepimlibs: akonadi bindings')
conflicts=('kdebindings-ruby')
source=("http://download.kde.org/unstable/${pkgver}/src/korundum-${pkgver}.tar.xz"
        'no-nepomuk.patch')
sha1sums=('79a57dbfcae018491a5045ae6a25df2ab0f620b0'
          'c4834bab76316f0f7c956c0527351e9d885694fd')

prepare() {
  mkdir build

  cd korundum-${pkgver}
  patch -p1 -i "${srcdir}"/no-nepomuk.patch
}

build() {
  cd build
  cmake ../korundum-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCUSTOM_RUBY_SITE_LIB_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]') \
    -DCUSTOM_RUBY_SITE_ARCH_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorarchdir"]') \
    -DWITH_Nepomuk=OFF

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
