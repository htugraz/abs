# $Id: PKGBUILD 254874 2015-12-09 07:40:55Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kturtle
pkgver=15.11.90
pkgrel=2
pkgdesc="Educational Programming Environment"
url="http://kde.org/applications/education/kturtle/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(knewstuff hicolor-icon-theme)
makedepends=(extra-cmake-modules python kdoctools)
install=${pkgname}.install
conflicts=(kdeedu-kturtle)
replaces=(kdeedu-kturtle)
source=("http://download.kde.org/unstable/applications/$pkgver/src/kturtle-$pkgver.tar.xz")
sha1sums=('4c5261c70a5c6acd647e58b073c5fd60d979c27f')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
