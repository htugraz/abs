# $Id: PKGBUILD 254944 2015-12-09 08:04:58Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kppp
pkgver=15.11.90
pkgrel=2
pkgdesc='Internet Dial-Up Tool'
url='http://kde.org/applications/internet/kppp/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'ppp')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kppp-${pkgver}.tar.xz")
sha1sums=('3f5abd9357317dca60caebd7b356820f9a52c1ac')

build() {
  mkdir -p build
  cd build
  cmake ../kppp-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
