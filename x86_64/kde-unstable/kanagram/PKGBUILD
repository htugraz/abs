# $Id: PKGBUILD 252212 2015-12-03 20:14:53Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kanagram
pkgver=15.11.90
pkgrel=1
pkgdesc="Letter Order Game"
url="http://kde.org/applications/education/kanagram/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('libkeduvocdocument' 'kdeclarative' 'knewstuff')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-kanagram')
conflicts=('kdeedu-kanagram')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kanagram-${pkgver}.tar.xz")
sha1sums=('1380b45eb45259a952dab57c1c0510f0d29d7d3c')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kanagram-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
