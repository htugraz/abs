# $Id: PKGBUILD 202300 2013-12-21 12:05:10Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kconfigwidgets
pkgver=r5.b45250a
pkgrel=1
pkgdesc='Widgets for KConfig'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kconfigwidgets'
license=('LGPL')
depends=('kauth' 'kcodecs' 'kconfig' 'karchive' 'kdoctools' 'kguiaddons'
         'ki18n' 'kwidgetsaddons')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/kconfigwidgets.git')
md5sums=('SKIP')

pkgver() {
  cd kconfigwidgets
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  # There's something wrong with our kdoctools but I don't get it
  sed -i 's/add_subdirectory(docs)/#add_subdirectory(docs)/' \
    kconfigwidgets/CMakeLists.txt
}

build() {
  cd build
  cmake ../kconfigwidgets \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
