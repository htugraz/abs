# $Id: PKGBUILD 207271 2014-03-08 08:46:04Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-picmi
pkgver=4.12.80
pkgrel=1
pkgdesc="A nonogram logic game for KDE"
url="http://kde.org/applications/games/picmi/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
replaces=('picmi')
conflicts=('picmi')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/picmi-${pkgver}.tar.xz")
sha1sums=('d8a536a0f5f2088e5e7cc0bf346ac560d4b6f69b')

build() {
  mkdir build
  cd build
  cmake ../picmi-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
