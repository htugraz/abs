# $Id: PKGBUILD 202325 2013-12-21 12:11:57Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=khtml
pkgver=r5.5657983
pkgrel=1
pkgdesc='KHtml'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/khtml'
license=('LGPL')
depends=('phonon-qt5' 'giflib')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/khtml.git')
md5sums=('SKIP')

pkgver() {
  cd khtml
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../khtml \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
