# $Id: PKGBUILD 200171 2013-11-22 09:23:27Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-krdc
pkgver=4.11.95
pkgrel=1
pkgdesc='Remote Desktop Client'
url='http://kde.org/applications/internet/krdc/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-runtime' 'telepathy-qt')
makedepends=('cmake' 'automoc4' 'libvncserver' 'freerdp')
optdepends=('libvncserver: VNC support'
            'freerdp: RDP support'
            'kdebase-keditbookmarks: to edit bookmarks')
source=("http://download.kde.org/unstable/${pkgver}/src/krdc-${pkgver}.tar.xz")
sha1sums=('32cf060fdf53bc5b58766dea8431b1fac75faf15')

build() {
  mkdir build
  cd build
  cmake ../krdc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
