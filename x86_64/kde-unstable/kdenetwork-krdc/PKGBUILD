# $Id: PKGBUILD 199672 2013-11-15 10:14:22Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-krdc
pkgver=4.11.90
pkgrel=1
pkgdesc='Remote Desktop Client'
url='http://kde.org/applications/internet/krdc/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-runtime' 'telepathy-qt')
makedepends=('cmake' 'automoc4' 'libvncserver' 'freerdp')
optdepends=('libvncserver: VNC support'
            'freerdp: RDP support'
            'kdebase-keditbookmarks: to edit bookmarks')
source=("http://download.kde.org/unstable/${pkgver}/src/krdc-${pkgver}.tar.xz")
sha1sums=('523fc8fea05d444924ebb376a8a4225bab8e565e')

build() {
  mkdir build
  cd build
  cmake ../krdc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
