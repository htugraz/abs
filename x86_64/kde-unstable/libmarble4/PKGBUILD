# $Id: PKGBUILD 241398 2015-06-29 17:50:28Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libmarble4
pkgver=15.04.3
pkgrel=4
pkgdesc="KDE4 Marble libraries"
url="http://kde.org/applications/education/marble/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kdebase-runtime libastro)
makedepends=(cmake automoc4 gpsd libwlocate)
optdepends=('gpsd: GPS support' 'libwlocate: WLAN based geolocation')
conflicts=(kdeedu-marble)
provides=(kdeedu-marble)
source=("http://download.kde.org/stable/applications/$pkgver/src/marble-$pkgver.tar.xz")
sha1sums=('34f6bdedb6ff37f49c1e0f1f3cca219a515accb4')

prepare() {
  mkdir -p build

# rename include dir
  sed -e 's|marble/MarbleModel.h|marble4/MarbleModel.h|' -i marble-$pkgver/FindMarble.cmake
# hack: change soname to make it coinstallable with 15.08 version
  sed -e 's|GENERIC_LIB_VERSION "0.17.20"|GENERIC_LIB_VERSION "0.17"|' -e 's|GENERIC_LIB_SOVERSION "1"|GENERIC_LIB_SOVERSION "0"|' \
   -i marble-$pkgver/src/lib/astro/CMakeLists.txt
}

build() {
  cd build
  cmake ../marble-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# remove files provided by libastro and marble
  rm -r "$pkgdir"/usr/bin
  rm -r "$pkgdir"/usr/share/applications/kde4/marble-*
  rm -r "$pkgdir"/usr/include/astro
  rm -r "$pkgdir"/usr/lib/libastro.so
  rm -r "$pkgdir"/usr/share/{appdata,config.kcfg,doc,icons}
# rename include dir to avoid conflicts with marble
  mv "$pkgdir"/usr/include/marble{,4}
}

