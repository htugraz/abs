# $Id: PKGBUILD 200150 2013-11-22 09:20:36Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kcolorchooser
pkgver=4.11.95
pkgrel=1
pkgdesc="Color Chooser"
url="http://kde.org/applications/graphics/kcolorchooser/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kcolorchooser-${pkgver}.tar.xz")
sha1sums=('dd952ecdd4e4eaa59e199e65eb2c21a99bbd4756')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kcolorchooser-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
