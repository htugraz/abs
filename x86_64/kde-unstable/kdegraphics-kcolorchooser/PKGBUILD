# $Id: PKGBUILD 254921 2015-12-09 07:56:34Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kcolorchooser
pkgver=15.11.90
pkgrel=2
pkgdesc="Color Chooser"
url="http://kde.org/applications/graphics/kcolorchooser/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kcolorchooser-${pkgver}.tar.xz")
sha1sums=('04692cfabd33c972ed916a03989e0c31c5b49cc8')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kcolorchooser-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
