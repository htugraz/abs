# $Id: PKGBUILD 251537 2015-11-20 07:01:33Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=blinken
pkgver=15.11.80
pkgrel=1
pkgdesc="Memory Enhancement Game"
url="http://kde.org/applications/education/blinken/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kxmlgui' 'phonon-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
install=${pkgname}.install
replaces=('kdeedu-blinken')
conflicts=('kdeedu-blinken')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/blinken-${pkgver}.tar.xz")
sha1sums=('3070a2dcc56dc5d56058ec23de1465e3cb9d7b4f')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../blinken-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
