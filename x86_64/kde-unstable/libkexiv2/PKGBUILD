# $Id: PKGBUILD 252156 2015-12-03 19:54:35Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkexiv2
pkgver=15.11.90
pkgrel=1
pkgdesc="A library to manipulate pictures metadata"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkexiv2"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(qt5-base exiv2)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('e0224df00fa3049cd0cefd8a97cfa8fc81ef4218')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
