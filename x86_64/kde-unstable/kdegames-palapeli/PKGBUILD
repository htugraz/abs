# $Id: PKGBUILD 207880 2014-03-15 08:00:45Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-palapeli
pkgver=4.12.90
pkgrel=1
pkgdesc="A single-player jigsaw puzzle game"
url="http://kde.org/applications/games/palapeli/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames' 'qhull')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/palapeli-${pkgver}.tar.xz")
sha1sums=('d19a37f63c32f9524590996f74ac273ec429c21c')

build() {
  mkdir build
  cd build
  cmake ../palapeli-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
