# $Id: PKGBUILD 207839 2014-03-15 07:54:22Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-pairs
pkgver=4.12.90
pkgrel=1
pkgdesc="A memory and pairs game for KDE"
url="https://projects.kde.org/projects/kde/kdeedu/pairs"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/pairs-${pkgver}.tar.xz")
sha1sums=('cbef4703a207221f100e33817df396a7daac5c91')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../pairs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
