# $Id: PKGBUILD 202343 2013-12-21 12:16:37Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=knewstuff
pkgver=r5.6bfee78
pkgrel=1
pkgdesc='KNewStuff'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/knewstuff'
license=('LGPL')
depends=('attica-qt5' 'kconfigwidgets' 'kio')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/knewstuff.git')
md5sums=('SKIP')

pkgver() {
  cd knewstuff
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../knewstuff \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
