# $Id: PKGBUILD 234285 2015-03-20 20:53:29Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kmouth
pkgver=15.03.95
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Speech Synthesizer Frontend'
url='http://kde.org/applications/utilities/kmouth/'
license=('GPL' 'FDL')
groups=('kde-applications' 'kdeaccessibility')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kmouth-${pkgver}.tar.xz")
sha1sums=('5c90c292e30ae071e4f85ac3304a82822a534742')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kmouth-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
