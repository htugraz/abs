# $Id: PKGBUILD 251528 2015-11-20 07:01:21Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kmouth
pkgver=15.11.80
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Speech Synthesizer Frontend'
url='http://kde.org/applications/utilities/kmouth/'
license=('GPL' 'FDL')
groups=('kde-applications' 'kdeaccessibility')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kmouth-${pkgver}.tar.xz")
sha1sums=('478fdc11d5a5dae8603dc4bc25063ef7ab9b5e48')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kmouth-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
