# $Id: PKGBUILD 202359 2013-12-21 12:20:12Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwindowsystem
pkgver=r5.54d960f
pkgrel=1
pkgdesc='KWindowSystem'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kwindowsystem'
license=('LGPL')
depends=('qt5-x11extras' 'libxfixes')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/kwindowsystem.git')
md5sums=('SKIP')

pkgver() {
  cd kwindowsystem
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kwindowsystem \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
