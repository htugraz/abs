# $Id: PKGBUILD 200081 2013-11-22 09:08:46Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-ksystemlog
pkgver=4.11.95
pkgrel=1
pkgdesc='System log viewer tool'
url='http://kde.org/applications/system/ksystemlog/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/ksystemlog-${pkgver}.tar.xz"
        'syslog-path.patch')
sha1sums=('19d076b834f2353ba6dc09e2ce0a3aab1d32395b'
          '20095ce6e0f3e5b6800a7c6e52de6fddba62c031')

prepare() {
  cd ksystemlog-${pkgver}
  patch -p2 -i "${srcdir}"/syslog-path.patch
}

build() {
  mkdir build
  cd build
  cmake ../ksystemlog-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
