# $Id: PKGBUILD 242665 2015-07-29 07:24:14Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-ksystemlog
pkgver=15.07.80
pkgrel=1
pkgdesc='System log viewer tool'
url='http://kde.org/applications/system/ksystemlog/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeadmin')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/ksystemlog-${pkgver}.tar.xz"
        'syslog-path.patch')
sha1sums=('03b061f82f0873d176b8a17bdedafaa1b5bb11c6'
          '20095ce6e0f3e5b6800a7c6e52de6fddba62c031')

prepare() {
  cd ksystemlog-${pkgver}
  patch -p2 -i "${srcdir}"/syslog-path.patch
}

build() {
  mkdir build
  cd build
  cmake ../ksystemlog-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
