# $Id: PKGBUILD 219215 2014-08-03 13:58:47Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-kmix
pkgver=4.13.97
pkgrel=1
pkgdesc="KDE volume control program"
url='http://kde.org/applications/multimedia/kmix/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgver}/src/kmix-${pkgver}.tar.xz"
        'use-qdbus-qt4.patch')
sha1sums=('945e27040e19d764afcda310509fd0b320fe1602'
          'f89a076c5a29c51d213864e27d07baf5400ece03')

prepare() {
  mkdir build

  cd kmix-${pkgver}
  # FS#40786
  patch -p1 -i "${srcdir}"/use-qdbus-qt4.patch
}

build() {
  cd build
  cmake ../kmix-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
