# $Id: PKGBUILD 208463 2014-03-22 18:08:24Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-kmix
pkgver=4.12.95
pkgrel=1
pkgdesc="KDE volume control program"
url='http://kde.org/applications/multimedia/kmix/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgver}/src/kmix-${pkgver}.tar.xz")
sha1sums=('0364629cb5141086dd30b4c614beb41c3b3f500e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kmix-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
