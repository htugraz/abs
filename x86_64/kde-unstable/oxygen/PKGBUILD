# $Id: PKGBUILD 215229 2014-06-16 21:59:14Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=oxygen
pkgver=4.97.0
pkgrel=1
pkgdesc='KDE Oxygen style'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/oxygen'
license=('LGPL')
depends=('frameworkintegration' 'kwin')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('plasma-next')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('ae205e16cab10cc3e3b31e84b45b27c4')

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
