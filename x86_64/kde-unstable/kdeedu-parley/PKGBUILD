# $Id: PKGBUILD 207840 2014-03-15 07:54:31Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-parley
pkgver=4.12.90
pkgrel=1
pkgdesc="Vocabulary Trainer"
url="http://kde.org/applications/education/parley/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libkdeedu')
makedepends=('cmake' 'automoc4')
optdepends=('kdebindings-kross-python: scripting support')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/parley-${pkgver}.tar.xz")
sha1sums=('79540a1ae03b45c4add831e152b0ebb9984c10eb')

build() {
  mkdir build
  cd build
  cmake ../parley-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
