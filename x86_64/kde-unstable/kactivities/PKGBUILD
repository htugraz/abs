# $Id: PKGBUILD 207780 2014-03-15 07:43:26Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities
pkgver=4.12.90
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='API for using and interacting with the Activity Manager'
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('GPL' 'FDL')
depends=('baloo')
makedepends=('cmake' 'automoc4' 'mesa')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('46b9ffef33e22857ddbfe383d9bea0f88ea947fe')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_nepomuk=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
