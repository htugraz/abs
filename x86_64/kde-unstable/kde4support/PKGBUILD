# $Id: PKGBUILD 202305 2013-12-21 12:07:17Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde4support
pkgver=r10.3e248bd
pkgrel=1
pkgdesc='KDE 4 Support'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kde4support'
license=('LGPL')
depends=('kio' 'kunitconversion')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/kde4support.git')
md5sums=('SKIP')

pkgver() {
  cd kde4support
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  # There's something wrong with our kdoctools but I don't get it
  sed -i 's/add_subdirectory(docs)/#add_subdirectory(docs)/' \
    kde4support/CMakeLists.txt
}

build() {
  cd build
  cmake ../kde4support \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
