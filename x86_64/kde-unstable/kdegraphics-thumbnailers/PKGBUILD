# $Id: PKGBUILD 254931 2015-12-09 07:59:59Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-thumbnailers
pkgver=15.11.90
pkgrel=2
pkgdesc="Thumbnailers for various graphics file formats"
url="https://projects.kde.org/projects/kde/kdegraphics/kdegraphics-thumbnailers"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(libkexiv2 libkdcraw kio)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('0114e4fd2c15eb37f48f8924e5febe50d0848c88')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
