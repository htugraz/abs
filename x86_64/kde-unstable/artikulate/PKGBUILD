# $Id: PKGBUILD 250288 2015-11-09 21:13:39Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=artikulate
pkgver=15.11.80
pkgrel=2
pkgdesc="Improve your pronunciation by listening to native speakers"
url="https://projects.kde.org/projects/kde/kdeedu/artikulate"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(hicolor-icon-theme kdeclarative knewstuff qt5-gstreamer)
makedepends=(extra-cmake-modules kdoctools boost)
conflicts=(kdeedu-artikulate)
replaces=(kdeedu-artikulate)
install=$pkgname.install
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('8348a2fc3f3bb7c7b2fae8d4fcafa3c7562e1eaf')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
