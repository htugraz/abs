# $Id: PKGBUILD 200570 2013-11-28 13:56:56Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-superkaramba
pkgver=4.11.97
pkgrel=1
pkgdesc='An engine for cool desktop eyecandy'
url='http://kde.org/applications/utilities/superkaramba/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'qimageblitz')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/superkaramba-${pkgver}.tar.xz")
sha1sums=('46639ba1b0e384a9de3c984dbb4429d6fc5df35b')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../superkaramba-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
