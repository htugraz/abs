# $Id: PKGBUILD 252211 2015-12-03 20:14:28Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kalzium
pkgver=15.11.90
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="http://kde.org/applications/education/kalzium/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kdebase-runtime' 'avogadro' 'ocaml' 'chemical-mime-data')
makedepends=('cmake' 'automoc4' 'mesa' 'eigen' 'eigen2' 'facile')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kalzium-${pkgver}.tar.xz")
sha1sums=('97cb540296cd0dd4238ef3fb1098805c7f1a3628')

build() {
  mkdir -p build
  cd build
  cmake ../kalzium-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
