# $Id: PKGBUILD 200454 2013-11-28 13:36:14Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-perlkde
pkgver=4.11.97
pkgrel=1
pkgdesc="Perl bindings for the KDE libraries"
url="https://projects.kde.org/projects/kde/kdebindings/perl/perlkde"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-perlqt' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'kdepimlibs'
             'kdegraphics-okular' 'kdesdk-kate')
source=("http://download.kde.org/unstable/${pkgver}/src/perlkde-${pkgver}.tar.xz")
sha1sums=('01323ba53dab6375df686bd356d22121911639f6')

build() {
  mkdir build
  cd build
  cmake ../perlkde-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
