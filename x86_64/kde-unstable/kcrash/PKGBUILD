# $Id: PKGBUILD 202302 2013-12-21 12:06:04Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcrash
pkgver=r5.a51580b
pkgrel=1
pkgdesc='KCrash'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kcrash'
license=('LGPL')
depends=('kcoreaddons' 'kwindowsystem')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/kcrash.git')
md5sums=('SKIP')

pkgver() {
  cd kcrash
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kcrash \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
