# $Id: PKGBUILD 199599 2013-11-15 09:57:30Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kiten
pkgver=4.11.90
pkgrel=1
pkgdesc="Japanese Reference/Study Tool"
url="http://kde.org/applications/education/kiten/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kiten-${pkgver}.tar.xz")
sha1sums=('c58f77db2f8612b0f725193fd0ddb877d1599f78')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kiten-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
