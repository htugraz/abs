# $Id: PKGBUILD 207244 2014-03-08 08:42:42Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kfourinline
pkgver=4.12.80
pkgrel=1
pkgdesc="A four-in-a-row game"
url="http://kde.org/applications/games/kfourinline/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kfourinline-${pkgver}.tar.xz")
sha1sums=('2cfd4da74361186e67d88aeee3172a150f0521d9')

build() {
  mkdir build
  cd build
  cmake ../kfourinline-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
