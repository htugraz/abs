# $Id: PKGBUILD 242703 2015-07-29 07:44:35Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbreakout
pkgver=15.07.80
pkgrel=1
pkgdesc="A Breakout-like game"
url="http://kde.org/applications/games/kbreakout/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kbreakout')
replaces=('kdegames-kbreakout')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kbreakout-${pkgver}.tar.xz")
sha1sums=('8ab43e845fccb6b5574111375d74cbffa441553b')

build() {
  mkdir build
  cd build
  cmake ../kbreakout-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
