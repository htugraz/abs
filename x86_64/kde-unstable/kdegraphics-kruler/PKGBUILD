# $Id: PKGBUILD 219205 2014-08-03 13:57:41Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kruler
pkgver=4.13.97
pkgrel=1
pkgdesc="Screen Ruler"
url="http://kde.org/applications/graphics/kruler/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kruler-${pkgver}.tar.xz")
sha1sums=('8fc2aecebb7a6f2a5cebcd9a610e1da7a9661896')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kruler-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
