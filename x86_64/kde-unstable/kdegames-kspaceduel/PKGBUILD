# $Id: PKGBUILD 251588 2015-11-20 07:02:49Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kspaceduel
pkgver=15.11.80
pkgrel=1
pkgdesc="Each of two possible players controls a satellite spaceship orbiting the sun"
url="http://kde.org/applications/games/kspaceduel/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kspaceduel-${pkgver}.tar.xz")
sha1sums=('ae0b78aaa627d55ad6e4293a3d7a90bec1a6762d')

build() {
  mkdir -p build
  cd build
  cmake ../kspaceduel-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
