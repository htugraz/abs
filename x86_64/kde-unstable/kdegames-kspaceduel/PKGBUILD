# $Id: PKGBUILD 208440 2014-03-22 18:04:09Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kspaceduel
pkgver=4.12.95
pkgrel=1
pkgdesc="Each of two possible players controls a satellite spaceship orbiting the sun"
url="http://kde.org/applications/games/kspaceduel/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kspaceduel-${pkgver}.tar.xz")
sha1sums=('963f9f0426da536f27c4b97d420ddc70b98a4385')

build() {
  mkdir build
  cd build
  cmake ../kspaceduel-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
