# $Id: PKGBUILD 218125 2014-07-25 15:09:04Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-poxml
pkgver=4.13.95
pkgrel=1
pkgdesc='Translates DocBook XML files using gettext po files'
url='https://projects.kde.org/projects/kde/kdesdk/poxml'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdelibs')
makedepends=('cmake' 'automoc4' 'antlr2')
optdepends=('antlr2: required for po2xml and swappo tool')
source=("http://download.kde.org/unstable/${pkgver}/src/poxml-${pkgver}.tar.xz")
sha1sums=('1be939236b1b9a3d90fc153b995558d3bacec5c4')

build() {
  mkdir build
  cd build
  cmake ../poxml-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
