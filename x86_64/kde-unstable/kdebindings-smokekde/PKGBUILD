# $Id: PKGBUILD 208959 2014-03-29 08:47:36Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-smokekde
pkgver=4.12.97
pkgrel=1
pkgdesc="Language independent library for KDE bindings"
url="https://projects.kde.org/projects/kde/kdebindings/smoke/smokekde"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost'
             'kdepimlibs' 'kdegraphics-okular' 'kdesdk-kate'
             'mesa')
optdepends=('kdesdk-kate: Kate bindings'
            'kdegraphics-okular: Okular bindings'
            'kdepimlibs: akonadi bindings')
conflicts=('kdebindings-smoke')
source=("http://download.kde.org/unstable/${pkgver}/src/smokekde-${pkgver}.tar.xz")
sha1sums=('f65aba8c3ff8745f48eecfb492f69de26cb30752')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../smokekde-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Nepomuk=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
