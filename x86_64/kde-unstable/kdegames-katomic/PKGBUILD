# $Id: PKGBUILD 207238 2014-03-08 08:41:57Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-katomic
pkgver=4.12.80
pkgrel=1
pkgdesc="A fun and educational game built around molecular geometry"
url="http://kde.org/applications/games/katomic/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/katomic-${pkgver}.tar.xz")
sha1sums=('8f256b55894d79b3399a1d28176c8f19b125a31e')

build() {
  mkdir build
  cd build
  cmake ../katomic-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
