# $Id: PKGBUILD 209019 2014-03-29 09:07:28Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-katomic
pkgver=4.12.97
pkgrel=1
pkgdesc="A fun and educational game built around molecular geometry"
url="http://kde.org/applications/games/katomic/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/katomic-${pkgver}.tar.xz")
sha1sums=('c03fa235c8901c9197549f1b725ba0ad56ad647f')

build() {
  mkdir build
  cd build
  cmake ../katomic-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
