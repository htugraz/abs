# $Id: PKGBUILD 202341 2013-12-21 12:16:15Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kjsembed
pkgver=r5.89e1bdb
pkgrel=1
pkgdesc='KJSEmbed'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kjsembed'
license=('LGPL')
depends=('ki18n' 'qt5-svg')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
groups=('kf5')
source=('git://anongit.kde.org/kjsembed.git')
md5sums=('SKIP')

pkgver() {
  cd kjsembed
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  # There's something wrong with our kdoctools but I don't get it
  sed -i 's/add_subdirectory(docs)/#add_subdirectory(docs)/' \
    kjsembed/CMakeLists.txt
}

build() {
  cd build
  cmake ../kjsembed \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
