# $Id: PKGBUILD 219228 2014-08-03 14:00:46Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-dev-utils
pkgver=4.13.97
pkgrel=1
pkgdesc='Small utilities for developers using KDE/Qt libs/frameworks'
url='https://projects.kde.org/projects/kde/kdesdk/kde-dev-utils'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
conflicts=('kdesdk-kmtrace' 'kdesdk-kpartloader' 'kdesdk-kprofilemethod'
           'kdesdk-kstartperf' 'kdesdk-kuiviewer')
replaces=('kdesdk-kmtrace' 'kdesdk-kpartloader' 'kdesdk-kprofilemethod'
          'kdesdk-kstartperf' 'kdesdk-kuiviewer')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kde-dev-utils-${pkgver}.tar.xz")
sha1sums=('6c464da95807320129eacf80760a7c18665f39d2')

build() {
  mkdir build
  cd build
  cmake ../kde-dev-utils-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
