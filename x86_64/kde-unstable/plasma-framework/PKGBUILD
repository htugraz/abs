# $Id
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=plasma2tp.r3.ga3a9dff
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/libs/plasma-framework'
license=('LGPL')
depends=('qt5-quick1' 'kidletime' 'kitemmodels'
         'kwidgetsaddons' 'kcodecs' 'karchive' 'solid' 'threadweaver'
         'kglobalaccel' 'kauth' 'kwallet-framework' 'kguiaddons'
         'kconfigwidgets' 'kitemviews' 'knotifications' 'kiconthemes'
         'kcompletion' 'kjobwidgets' 'sonnet' 'ktextwidgets' 'kxmlgui'
         'kbookmarks' 'kdeclarative' 'kunitconversion' 'kparts' 'kross'
         'kdnssd-framework' 'kde4support' 'kdesignerplugin' 'kinit'
         'kactivities-frameworks')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
source=('git://anongit.kde.org/plasma-framework.git#tag=plasma2tp')
md5sums=('SKIP')

pkgver() {
  cd plasma-framework
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-framework \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
