# $Id: PKGBUILD 212232 2014-05-11 10:07:08Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcodecs
pkgver=4.99.0
pkgrel=1
pkgdesc='Plugins allowing Qt applications to access further types of images'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kcodecs'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('1e0175175d937290fa217ae13be6e642')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
