# $Id: PKGBUILD 208419 2014-03-22 17:59:57Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kdiamond
pkgver=4.12.95
pkgrel=1
pkgdesc="A single player puzzle game"
url="http://kde.org/applications/games/kdiamond/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kdiamond-${pkgver}.tar.xz")
sha1sums=('d2deeb5c7b2f2fdf7a80c3b5209aab90ca42587d')

build() {
  mkdir build
  cd build
  cmake ../kdiamond-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
