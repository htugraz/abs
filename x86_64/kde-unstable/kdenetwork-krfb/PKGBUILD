# $Id: PKGBUILD 209076 2014-03-29 09:19:20Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-krfb
pkgver=4.12.97
pkgrel=1
pkgdesc='Desktop Sharing'
url='http://www.kde.org/applications/system/krfb'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
# note on libxdamage:
#   not detected by namcap because libgl depends on it
#   but nvidia providing libgl does not depend on libxdamage
depends=('kdebase-runtime' 'libvncserver' 'libxdamage' 'telepathy-qt')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/krfb-${pkgver}.tar.xz")
sha1sums=('604de353898fd2fbd9114811d7d6479b7c3c2412')

build() {
  mkdir build
  cd build
  cmake ../krfb-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
