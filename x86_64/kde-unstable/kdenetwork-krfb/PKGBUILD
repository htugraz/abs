# $Id: PKGBUILD 219223 2014-08-03 13:59:42Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-krfb
pkgver=4.13.97
pkgrel=1
pkgdesc='Desktop Sharing'
url='http://www.kde.org/applications/system/krfb'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
# note on libxdamage:
#   not detected by namcap because libgl depends on it
#   but nvidia providing libgl does not depend on libxdamage
depends=('kdebase-runtime' 'libvncserver' 'libxdamage' 'telepathy-qt')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/krfb-${pkgver}.tar.xz")
sha1sums=('916628d6030e45906113f1d7cbc993fd11f4f5c9')

build() {
  mkdir build
  cd build
  cmake ../krfb-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
