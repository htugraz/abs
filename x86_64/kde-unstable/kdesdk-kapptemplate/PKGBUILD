# $Id: PKGBUILD 209085 2014-03-29 09:21:59Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-kapptemplate
pkgver=4.12.97
pkgrel=1
pkgdesc='KDE Template Generator'
url='http://kde.org/applications/development/kapptemplate/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kapptemplate-${pkgver}.tar.xz")
sha1sums=('ac7dee326ab3013d714ba5e851820411906f3ecc')

build() {
  mkdir build
  cd build
  cmake ../kapptemplate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
