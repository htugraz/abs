# $Id: PKGBUILD 202344 2013-12-21 12:16:52Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=knotifications
pkgver=r5.91ee78e
pkgrel=1
pkgdesc='KNotifications'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/knotifications'
license=('LGPL')
depends=('kwindowsystem')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/knotifications.git')
md5sums=('SKIP')

pkgver() {
  cd knotifications
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../knotifications \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
