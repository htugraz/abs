# $Id: PKGBUILD 217917 2014-07-25 14:54:11Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-kuser
pkgver=4.13.95
pkgrel=1
pkgdesc='User Manager'
url='http://kde.org/applications/system/kuser/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
depends=('kdebase-runtime' 'kdepimlibs')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kuser-${pkgver}.tar.xz")
sha1sums=('e5377e75d9015e5b7ce477f8c01fe7298aebde37')

build() {
  mkdir build
  cd build
  cmake ../kuser-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
