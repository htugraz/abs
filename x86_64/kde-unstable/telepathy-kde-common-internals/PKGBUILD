# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-common-internals
_pkgname=ktp-common-internals
pkgver=15.03.97
pkgrel=1
pkgdesc='Common components for KDE-Telepathy'
arch=(i686 x86_64)
url='http://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=('kde-applications' 'kdenetwork')
depends=(knotifyconfig ktexteditor kpeople telepathy-logger-qt kaccounts-integration libotr hicolor-icon-theme telepathy-mission-control)
makedepends=(extra-cmake-modules kdoctools doxygen python)
source=("http://download.kde.org/unstable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz")
md5sums=('30bd1ecd693e552f15f57e2654c32995')
install=$pkgname.install

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
