# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-contact-list
_pkgname=ktp-contact-list
pkgver=15.03.95
pkgrel=1
pkgdesc='KDE Telepathy contact list application'
arch=('i686' 'x86_64')
url='http://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
groups=('kde-applications' 'kdenetwork')
depends=('telepathy-kde-common-internals')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz")
install=$pkgname.install
md5sums=('4c2ee98323bcc9975d6cae52e171f2e9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
