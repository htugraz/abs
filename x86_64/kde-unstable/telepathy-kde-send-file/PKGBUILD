# $Id: PKGBUILD 242771 2015-07-29 08:04:56Z arojas $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-send-file
_pkgname=ktp-send-file
pkgver=15.07.80
pkgrel=1
pkgdesc='A File manager plugin to launch a file transfer job with a specified contact'
arch=('i686' 'x86_64')
url='http://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
groups=('kde-applications' 'kdenetwork' 'telepathy-kde')
depends=('telepathy-kde-common-internals')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz")
install=$pkgname.install
md5sums=('2862a4b407cbc32b648fac3c04351ed4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# Support KDE4 dolphin - TODO: Remove in 15.08
  mkdir -p "$pkgdir"/usr/share/kde4/services/ServiceMenus
  ln -s /usr/share/kservices5/ServiceMenus/ktp-send-file.desktop "$pkgdir"/usr/share/kde4/services/ServiceMenus
}
