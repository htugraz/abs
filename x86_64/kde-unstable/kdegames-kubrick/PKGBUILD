# $Id: PKGBUILD 251592 2015-11-20 07:02:55Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kubrick
pkgver=15.11.80
pkgrel=1
pkgdesc="Based on the famous Rubik's Cube"
url="http://kde.org/applications/games/kubrick/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4' 'glu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kubrick-${pkgver}.tar.xz")
sha1sums=('2f11dc1dd863dac24cf1bd09a531f3a5e3d18b5a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kubrick-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
