# $Id: PKGBUILD 207232 2014-03-08 08:41:12Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-step
pkgver=4.12.80
pkgrel=1
pkgdesc="Interactive Physical Simulator"
url="http://kde.org/applications/education/step/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libqalculate' 'gsl')
makedepends=('cmake' 'automoc4' 'eigen2' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/step-${pkgver}.tar.xz")
sha1sums=('4e56f6da443831512ccbc2a5238d1c047a58e980')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../step-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
