# $Id: PKGBUILD 200058 2013-11-22 09:05:35Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-perlqt
pkgver=4.11.95
pkgrel=1
pkgdesc="Perl bindings for the Qt libraries"
url="https://projects.kde.org/projects/kde/kdebindings/perl/perlqt"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt' 'perl-list-moreutils')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen')
source=("http://download.kde.org/unstable/${pkgver}/src/perlqt-${pkgver}.tar.xz")
sha1sums=('5ee8f5fa131ca6fa4ff8a7e4a7364f4e825dbc69')

build() {
  mkdir build
  cd build
  cmake ../perlqt-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
