# $Id: PKGBUILD 243683 2015-08-14 09:55:29Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons
pkgver=5.3.95
pkgrel=2
pkgdesc='All kind of addons to improve your Plasma experience'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kdeplasma-addons'
license=('LGPL')
depends=('krunner' 'kdelibs4support' 'knewstuff' 'kross' 'purpose')
makedepends=('extra-cmake-modules' 'kdoctools' 'libibus' 'scim')
optdepends=('ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support')
install="${pkgname}.install"
conflicts=('kdebase-workspace' 'kdeplasma-addons-frameworks')
replaces=('kdeplasma-addons-frameworks')
groups=('plasma')
source=("http://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('ab7ac39596e05755e890e3dca3c8fa5b')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
