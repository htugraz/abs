# $Id: PKGBUILD 234354 2015-03-20 22:38:12Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kgamma
pkgver=15.03.95
pkgrel=1
pkgdesc="A monitor calibration tool"
url="http://kde.org/applications/graphics/kgamma/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
# note on libxxf86vm:
#       not detected by namcap because libgl depends on it
#       but nvidia providing libgl does not depend on libxxf86vm
depends=('kdebase-runtime' 'libxxf86vm')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kgamma-${pkgver}.tar.xz")
sha1sums=('a15d07095d286bce27d4bc17db91305342571b12')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kgamma-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
