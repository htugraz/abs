# $Id: PKGBUILD 207289 2014-03-08 08:48:29Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-mplayerthumbs
pkgver=4.12.80
pkgrel=1
pkgdesc="MPlayer based thumbnail generator for video files"
url='https://projects.kde.org/projects/kde/kdemultimedia/mplayerthumbs'
arch=('i686' 'x86_64')
groups=('kde' 'kdemultimedia')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'mplayer')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/mplayerthumbs-${pkgver}.tar.xz")
sha1sums=('9417f279f7d72bf7ada5443522b9ee176e08af94')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../mplayerthumbs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
