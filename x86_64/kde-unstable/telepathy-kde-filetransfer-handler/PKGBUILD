# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-filetransfer-handler
_pkgname=ktp-filetransfer-handler
pkgver=15.03.95
pkgrel=1
pkgdesc='KDE Telepathy file transfer handler'
arch=('i686' 'x86_64')
url='http://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
groups=('kde-applications' 'kdenetwork')
depends=('telepathy-kde-common-internals')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz")
md5sums=('b6a1b1d78e05e28941c2fc5538397750')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
