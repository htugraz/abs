# $Id: PKGBUILD 199663 2013-11-15 10:12:25Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-juk
pkgver=4.11.90
pkgrel=1
pkgdesc='A jukebox, tagger and music collection manager'
url='http://kde.org/applications/multimedia/juk/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
depends=('kdebase-runtime' 'taglib')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgver}/src/juk-${pkgver}.tar.xz")
sha1sums=('a24568194db660179f4925e26b2b84f93dcbb8e3')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../juk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_TunePimp=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
