# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcalutils
pkgver=15.07.80
pkgrel=1
pkgdesc="The KDE calendar utility library"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kcalcore kidentitymanagement)
makedepends=(extra-cmake-modules python boost)
source=("http://download.kde.org/unstable/applications/15.07.80/src/$pkgname-$pkgver.tar.xz")
md5sums=('e173a147affdf3402c38f77949e9b094')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
