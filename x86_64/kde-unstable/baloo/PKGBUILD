# $Id: PKGBUILD 208948 2014-03-29 08:42:08Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo
pkgver=4.12.97
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('kdepimlibs' 'xapian-core' 'kfilemetadata')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('3f6f65a99f1a7f190b8ace1a9852c4e03014fd30')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
