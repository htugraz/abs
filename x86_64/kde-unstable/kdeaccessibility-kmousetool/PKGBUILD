# $Id: PKGBUILD 207811 2014-03-15 07:49:03Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kmousetool
pkgver=4.12.90
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Clicks the mouse for you, reducing the effects of RSI'
url='http://kde.org/applications/utilities/kmousetool/'
license=('GPL' 'FDL')
groups=('kde' 'kdeaccessibility')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kmousetool-${pkgver}.tar.xz")
sha1sums=('ff74fa1662fd9dd235207ec9cb6b30ade3e7f73b')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kmousetool-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
