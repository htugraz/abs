# $Id: PKGBUILD 199629 2013-11-15 10:04:06Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kmahjongg
pkgver=4.11.90
pkgrel=1
pkgdesc="A tile matching game for one or two players"
url="http://kde.org/applications/games/kmahjongg/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkmahjongg' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kmahjongg-${pkgver}.tar.xz")
sha1sums=('42584448b6b9827062992a29084469697a49b345')

build() {
  mkdir build
  cd build
  cmake ../kmahjongg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
