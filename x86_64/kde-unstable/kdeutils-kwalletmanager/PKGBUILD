# $Id: PKGBUILD 219253 2014-08-03 14:03:25Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kwalletmanager
pkgver=4.13.97
pkgrel=1
pkgdesc='Wallet Management Tool'
url='http://kde.org/applications/system/kwalletmanager/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
conflicts=('kdeutils-kwallet')
replaces=('kdeutils-kwallet')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kwalletmanager-${pkgver}.tar.xz")
sha1sums=('d88dbce2961d4c7844f85883f7422d873bfb2c8c')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kwalletmanager-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
