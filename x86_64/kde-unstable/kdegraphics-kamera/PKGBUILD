# $Id: PKGBUILD 200515 2013-11-28 13:46:48Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kamera
pkgver=4.11.97
pkgrel=1
pkgdesc="Configure Kamera"
url="http://kde.org/applications/graphics/kamera/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime' 'libgphoto2')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/kamera-${pkgver}.tar.xz")
sha1sums=('3d434c8d1b210e8c747803ab3403e367c7956606')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kamera-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
