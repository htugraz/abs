# $Id: PKGBUILD 106881 2014-03-09 12:49:46Z bpiotrowski $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: J0k3r <moebius282@gmail.com>

pkgname=lib32-sdl2
pkgver=2.0.2
pkgrel=1
pkgdesc='A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2)'
arch=('x86_64')
url='http://www.libsdl.org'
license=('MIT')
depends=('lib32-libgl' 'lib32-libx11' 'lib32-libxext' 'lib32-libxrender' "${pkgname#*-}")
makedepends=('lib32-alsa-lib' 'lib32-mesa' 'lib32-libpulse' 'lib32-libxinerama' 'lib32-libxrandr')
optdepends=('lib32-alsa-lib: ALSA audio driver'
            'lib32-libpulse: PulseAudio audio driver')
source=("${url}/release/SDL2-${pkgver}.tar.gz")
sha256sums=('590157f85a8a8b52723848169a74eec6ca07ab4a75cec15beedde648f980e850')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ../SDL2-${pkgver}/configure --prefix='/usr' --libdir='/usr/lib32' --enable-{alsa,pulseaudio-shared,sdl-dlopen} --disable-{arts,esd,nas,rpath}
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
