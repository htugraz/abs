# $Id: PKGBUILD 127123 2015-02-04 16:22:47Z foutrelis $
# Maintainer: Florian Pritz <flo@xinu.at>

_pkgbasename=glew
pkgname=lib32-$_pkgbasename
pkgver=1.12.0
pkgrel=1
pkgdesc="A cross-platform C/C++ extension loading library (32 bit)"
arch=('x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('lib32-libxmu' 'lib32-libxi' 'lib32-glu' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
source=(http://downloads.sourceforge.net/${_pkgbasename}/${_pkgbasename}-${pkgver}.tgz)
sha1sums=('070dfb61dbb7cd0915517decf467264756469a94')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i 's|CC = cc|CC = gcc -m32|' config/Makefile.linux
  sed -i 's|LD = cc|LD = gcc -m32|' config/Makefile.linux
  sed -i 's|lib64|lib32|' config/Makefile.linux
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make GLEW_DEST="${pkgdir}/usr" install
  chmod 0755 "${pkgdir}/usr/lib32/libGLEW.so.${pkgver}"

  rm -rf "${pkgdir}"/usr/{include,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
