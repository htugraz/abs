# $Id: PKGBUILD 112439 2014-06-02 06:14:06Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jan Alexander Steffens <jan.steffens@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>

pkgname=lib32-libxkbcommon
pkgver=0.4.2
pkgrel=1
pkgdesc='Keyboard handling library using XKB data'
arch=('x86_64')
url='http://xkbcommon.org/'
license=('custom')
depends=('lib32-glibc' "${pkgname#*-}")
makedepends=('lib32-libxcb' 'xorg-util-macros')
source=("http://xkbcommon.org/download/${pkgname#*-}-${pkgver}.tar.xz")
sha256sums=('a0fc71b07eeddba4af62bd709e24cec219778bb6871384aa850b2f5798a48957')

build() {
  cd ${pkgname#*-}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --disable-{docs,static,x11}
  make
}

package() {
  cd ${pkgname#*-}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
