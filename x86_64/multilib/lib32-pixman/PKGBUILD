# $Id: PKGBUILD 142819 2015-10-02 19:17:51Z bluewind $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
_pkgbasename=pixman
pkgname=lib32-$_pkgbasename
pkgver=0.32.8
pkgrel=1
pkgdesc="Pixman library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib')
source=(http://xorg.freedesktop.org/releases/individual/lib/${_pkgbasename}-${pkgver}.tar.bz2)
sha1sums=('5c57045622265b877c9bf02d531973eadf942140')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
