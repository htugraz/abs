# $Id: PKGBUILD 97594 2013-09-25 01:09:07Z eric $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

_pkgbasename=fakeroot
pkgname=lib32-${_pkgbasename}
pkgver=1.20
pkgrel=1
pkgdesc="Gives a fake root environment, useful for building packages as a non-privileged user (32-bit)"
arch=('x86_64')
license=('GPL')
url="http://packages.debian.org/fakeroot"
groups=('multilib-devel')
install=fakeroot.install
depends=(lib32-glibc $_pkgbasename)
options=('!libtool')
source=(http://ftp.debian.org/debian/pool/main/f/${_pkgbasename}/${_pkgbasename}_${pkgver}.orig.tar.bz2)
md5sums=('9777a81d4d1878422447a1d0030c1f9f')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32/libfakeroot \
              --disable-static --with-ipc=sysv
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR=${pkgdir} install
  rm -rf "$pkgdir"/usr/{bin,share}

  install -dm755 ${pkgdir}/etc/ld.so.conf.d/
  echo /usr/lib32/libfakeroot >"$pkgdir/etc/ld.so.conf.d/$pkgname.conf"
}
