# $Id: PKGBUILD 142904 2015-10-04 00:41:49Z heftig $
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=valgrind-multilib
pkgver=3.11.0
pkgrel=1
pkgdesc='A tool to help find memory-management problems in programs for multilib'
arch=(x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('lib32-glibc>=2.22' 'lib32-glibc<2.23' 'perl')
makedepends=(gdb openmpi gcc-multilib)
optdepends=('openmpi: MPI support')
# valgrind does not like stack protector flags
options=(!emptydirs !buildflags)
source=(http://valgrind.org/downloads/valgrind-$pkgver.tar.bz2)
sha1sums=('340757e91d9e83591158fe8bb985c6b11bc53de5')

provides=("valgrind=$pkgver-$pkgrel")
conflicts=('valgrind')

build() {
  cd valgrind-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd valgrind-$pkgver
  make DESTDIR="$pkgdir" install
}
