# $Id: PKGBUILD 127639 2015-02-13 01:27:57Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgbasename=speexdsp
pkgname=lib32-$_pkgbasename
pkgver=1.2rc3
pkgrel=1
pkgdesc="DSP library derived from Speex (32-bit)"
arch=(x86_64)
url="http://www.speex.org/"
license=(BSD)
depends=(lib32-glibc $_pkgbasename)
makedepends=(gcc-multilib)
source=(http://downloads.us.xiph.org/releases/speex/$_pkgbasename-$pkgver.tar.gz)
sha256sums=('4ae688600039f5d224bdf2e222d2fbde65608447e4c2f681585e4dca6df692f1')

build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static \
    --libdir=/usr/lib32
  make
}

check() {
  cd $_pkgbasename-$pkgver
  make -k check
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{include,share}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
