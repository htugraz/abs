# $Id: PKGBUILD 95009 2013-08-03 09:04:04Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgbasename=mesa-demos
pkgname=lib32-$_pkgbasename
pkgver=8.1.0
pkgrel=3
arch=(x86_64)
pkgdesc="Mesa demos and tools (32-bit)"
url="http://mesa3d.sourceforge.net"
license=('custom')
depends=('lib32-libgl' 'lib32-glew' 'mesa-demos')
makedepends=(gcc-multilib 'lib32-mesa')
source=(ftp://ftp.freedesktop.org/pub/mesa/demos/${pkgver}/mesa-demos-${pkgver}.tar.bz2)
md5sums=('9df33ba69a26bbfbc7c8148602e59542')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/mesa-demos-${pkgver}"
  ./configure --prefix=/usr 

  make
}

package() {
  depends=('lib32-glew' mesa-demos)

  cd "${srcdir}/mesa-demos-${pkgver}"
  make DESTDIR="${pkgdir}" install

  for i in "$pkgdir/usr/bin/"*; do
    mv "$i" "$i"32
  done

  rm -r "$pkgdir/usr/share/mesa-demos"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
