# $Id: PKGBUILD 104749 2014-01-25 17:40:10Z lcarlier $

_pkgbasename=libvorbis
pkgname=lib32-$_pkgbasename
pkgver=1.3.4
pkgrel=1
pkgdesc="Vorbis codec library (32-bit)"
arch=('x86_64')
license=('custom')
url="http://www.xiph.org/ogg/vorbis/"
depends=('lib32-libogg' $_pkgbasename)
makedepends=(gcc-multilib)
source=(http://downloads.xiph.org/releases/vorbis/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('8851c593a52d1ef9c526d95174873852')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  #-march=i686 optimizes too much, strip it out
  CFLAGS=${CFLAGS/-march=$CARCH} ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
