# $Id: PKGBUILD 72029 2012-06-06 11:58:40Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=lib32-libphobos-ldc
pkgver=20130604
pkgrel=1
pkgdesc="libphobos for ldc (32-bit)"
arch=('x86_64')
url="http://www.dsource.org/projects/ldc"
license=('BSD')
depends=('llvm' 'libconfig')
makedepends=('git' 'cmake')
source=("git://github.com/ldc-developers/ldc.git")
md5sums=("SKIP")

build() {
  cd "$srcdir"/ldc
  git submodule update --init --recursive

  mkdir build && cd build
  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DINCLUDE_INSTALL_DIR=/usr/include/d/std-ldc \
      -DMULTILIB=ON \
      ..
  make
}

package() {
  cd "$srcdir"/ldc

  cd build
  make DESTDIR=$pkgdir install
  cd ..

  # We don't want anything but the 32-bit libs
  rm -rf $pkgdir/usr/{share,lib,include,bin} $pkgdir/etc

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
