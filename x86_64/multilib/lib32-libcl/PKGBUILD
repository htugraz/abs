# Maintainer: Sven-Hendrik Haase <sh@lutzhaae.com>

pkgname=lib32-libcl
pkgver=1.1
_pkgver=304.37
pkgrel=1
pkgdesc="OpenCL library and ICD loader from NVIDIA (32-bit)"
arch=('x86_64')
url="http://www.nvidia.com/"
license=('custom')
options=('!strip')
_arch='x86'
_pkg="NVIDIA-Linux-${_arch}-${_pkgver}"
source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${_pkgver}/${_pkg}.run")
md5sums=('ee3e97109201ab954c96860e28ada611')

build() {
    cd $srcdir
    sh ${_pkg}.run --extract-only
}

package() {
    cd $srcdir/${_pkg}
    install -D -m755 libOpenCL.so.1.0.0 $pkgdir/usr/lib32/libOpenCL.so.1.0.0
    ln -s /usr/lib32/libOpenCL.so.1.0.0 $pkgdir/usr/lib32/libOpenCL.so.1
    ln -s /usr/lib32/libOpenCL.so.1 $pkgdir/usr/lib32/libOpenCL.so
    install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
