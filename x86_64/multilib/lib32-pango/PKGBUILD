# $Id: PKGBUILD 144200 2015-10-16 03:32:20Z fyan $
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
# Maintainer: Biru Ionut <ionut@archlinux.ro>

_pkgbasename=pango
pkgname=lib32-$_pkgbasename
pkgver=1.38.1
pkgrel=1
pkgdesc="A library for layout and rendering of text (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-libthai' 'lib32-libgl' 'lib32-cairo' 'lib32-libxft'
         $_pkgbasename)
makedepends=('gcc-multilib' 'lib32-mesa-libgl')
options=('!emptydirs')
install=pango.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver:0:4}/${_pkgbasename}-${pkgver}.tar.xz)
url="http://www.pango.org/"
sha256sums=('1320569f6c6d75d6b66172b2d28e59c56ee864ee9df202b76799c4506a214eb7')

prepare() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  sed -i 's#"pango.modules"#"pango.modules-32"#' pango/modules.c
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
      --localstatedir=/var --with-included-modules=basic-fc \
      --with-dynamic-modules=arabic-fc,arabic-lang,basic-fc,basic-win32,basic-x,basic-atsui,hangul-fc,hebrew-fc,indic-fc,indic-lang,khmer-fc,syriac-fc,tibetan-fc \
      --disable-introspection
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir"/etc
  rm -rf "$pkgdir"/usr/{bin/pango-view,share,include}
}
