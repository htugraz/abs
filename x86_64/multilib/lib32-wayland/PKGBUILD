# $Id: PKGBUILD 119985 2014-10-01 20:33:03Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Sébastien Luttringer <sеblu@аrchlіnux․org>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=lib32-wayland
pkgver=1.6.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('lib32-libffi' 'lib32-expat' 'wayland')
source=("http://wayland.freedesktop.org/releases/wayland-${pkgver}.tar.xz")
sha256sums=('a7d5102dcf53d08c059d24bc62de491d7cd482070abeb6737a20d0d86ba6fc7f')

build() {
  cd wayland-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' \
              --disable-{documentation,static}
  make
}

package() {
  cd wayland-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s wayland "${pkgdir}"/usr/share/licenses/lib32-wayland
}

# vim: ts=2 sw=2 et:
