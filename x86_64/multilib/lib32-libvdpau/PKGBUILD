# $Id: PKGBUILD 107104 2014-03-12 14:25:29Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libvdpau
pkgver=0.7
pkgrel=1
pkgdesc='Nvidia VDPAU library'
arch=('x86_64')
url='http://cgit.freedesktop.org/~aplattner/libvdpau'
license=('custom')
depends=('gcc-libs-multilib' 'lib32-libxext' "${pkgname#*-}")
conflicts=('lib32-nvidia-utils<190.42-2')
makedepends=('dri2proto')
source=("http://people.freedesktop.org/~aplattner/vdpau/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('24dc08467ce8c59d6cfbf0d34d2dd1f86b4ff62e90777e0a8f513c5c9de9bce0')

build() {
  cd ${pkgname#*-}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --sysconfdir='/etc'
  make
}

package() {
  cd ${pkgname#*-}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/include}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
