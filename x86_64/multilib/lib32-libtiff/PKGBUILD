# $Id: PKGBUILD 91807 2013-05-28 11:25:27Z lcarlier $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

_pkgbasename=libtiff
pkgname=lib32-$_pkgbasename
pkgver=4.0.3
pkgrel=2
pkgdesc="Library for manipulation of TIFF images (32-bit)"
arch=('x86_64')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('lib32-gcc-libs' 'lib32-libjpeg' 'lib32-zlib' 'lib32-xz' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz
        tiff-3.9.7-CVE-2012-4447.patch                                                                                                                                                                                                       
        tiff-3.9.7-CVE-2012-4564.patch                                                                                                                                                                                                       
        tiff-4.0.3-CVE-2013-1960.patch                                                                                                                                                                                                       
        tiff-4.0.3-CVE-2013-1961.patch                                                                                                                                                                                                       
        tiff-4.0.3-libjpeg-turbo.patch                                                                                                                                                                                                       
        tiff-4.0.3-tiff2pdf-colors.patch)                                                                                                                                                                                                    
sha1sums=('652e97b78f1444237a82cbcfe014310e776eb6f0'                                                                                                                                                                                         
          '41be661638282dae0d07bd2788414cb6650f8981'                                                                                                                                                                                         
          '6cb3d480908132335c05c769b5a51f951413725d'                                                                                                                                                                                         
          '5903355afdd0bb27ea3746339e2196720f9fac9d'                                                                                                                                                                                         
          '29a91870cca5d4cd9ca1c464f8074088eddc3fb8'                                                                                                                                                                                         
          '02d57835df50d3f84587571ec52b36f5af838de2'                                                                                                                                                                                         
          '23443ad0bc130d70860b6cc6d19b69584ae7a6cc')

prepare() {
  cd "${srcdir}/tiff-${pkgver}"

  patch -p0 -i "${srcdir}/tiff-4.0.3-tiff2pdf-colors.patch"
  patch -p1 -i "${srcdir}/tiff-3.9.7-CVE-2012-4447.patch"
  patch -p1 -i "${srcdir}/tiff-3.9.7-CVE-2012-4564.patch"
  patch -p1 -i "${srcdir}/tiff-4.0.3-CVE-2013-1960.patch"
  patch -p1 -i "${srcdir}/tiff-4.0.3-CVE-2013-1961.patch"
  patch -p1 -i "${srcdir}/tiff-4.0.3-libjpeg-turbo.patch"
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/tiff-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib32
  make
}

#check() {
# cd "${srcdir}/tiff-${pkgver}"
# make check
#}

package() {
  cd "${srcdir}/tiff-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
