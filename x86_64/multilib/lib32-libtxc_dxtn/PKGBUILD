# $Id: PKGBUILD 94421 2013-07-24 09:32:21Z lcarlier $
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=libtxc_dxtn
pkgname=lib32-$_pkgbasename
pkgver=1.0.1
pkgrel=5
arch=(x86_64)
pkgdesc="S3 Texture Compression (S3TC) library for Mesa (32-bit)"
url="http://dri.freedesktop.org/wiki/S3TC"
license=(custom:BSD)
depends=(lib32-glibc)
makedepends=(gcc-multilib lib32-mesa)
options=(!libtool)
source=(http://people.freedesktop.org/~cbrill/$_pkgbasename/$_pkgbasename-$pkgver.tar.bz2)
md5sums=('7105107f07ac49753f4b61ba9d0c79c5')

build() {
  cd "$_pkgbasename-$pkgver"

  CC="gcc -m32" ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/include"

  # License
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '5,22{s|^ \* \?||;p}' txc_dxtn.h \
    > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
