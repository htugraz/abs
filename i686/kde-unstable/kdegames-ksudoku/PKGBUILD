# $Id: PKGBUILD 200508 2013-11-28 13:45:37Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksudoku
pkgver=4.11.97
pkgrel=1
pkgdesc="A logic-based symbol placement puzzle"
url="http://kde.org/applications/games/ksudoku/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/ksudoku-${pkgver}.tar.xz")
sha1sums=('72bb02da82fac846fdc7f4fcfab66e048e8e8798')

build() {
  mkdir build
  cd build
  cmake ../ksudoku-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
