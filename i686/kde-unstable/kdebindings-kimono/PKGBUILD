# $Id: PKGBUILD 208988 2014-03-29 08:53:17Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-kimono
pkgver=4.12.97
pkgrel=1
pkgdesc=".NET/Mono bindings for the KDE libraries"
url="https://projects.kde.org/projects/kde/kdebindings/csharp/kimono"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-qyoto' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost' 'kdepimlibs')
optdepends=('kdepimlibs: akonadi bindings')
conflicts=('kdebindings-csharp')
source=("http://download.kde.org/unstable/${pkgver}/src/kimono-${pkgver}.tar.xz")
sha1sums=('7ecd8dde9aec94ddcc3e909e06bc59cfd8cda98e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kimono-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Soprano=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
