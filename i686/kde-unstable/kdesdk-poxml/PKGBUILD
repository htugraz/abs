# $Id: PKGBUILD 207920 2014-03-15 08:07:57Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-poxml
pkgver=4.12.90
pkgrel=1
pkgdesc='Translates DocBook XML files using gettext po files'
url='https://projects.kde.org/projects/kde/kdesdk/poxml'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdelibs')
makedepends=('cmake' 'automoc4' 'antlr2')
optdepends=('antlr2: required for po2xml and swappo tool')
source=("http://download.kde.org/unstable/${pkgver}/src/poxml-${pkgver}.tar.xz")
sha1sums=('873e9119da09de4c225204351c627a6de9c8d95a')

build() {
  mkdir build
  cd build
  cmake ../poxml-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
