# $Id: PKGBUILD 243584 2015-08-14 05:26:13Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-zeroconf-ioslave
pkgver=15.07.90
pkgrel=1
pkgdesc='Network Monitor for DNS-SD services (Zeroconf)'
url='https://projects.kde.org/projects/kde/kdenetwork/zeroconf-ioslave'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
conflicts=('kdenetwork-kdnssd')
replaces=('kdenetwork-kdnssd')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/zeroconf-ioslave-${pkgver}.tar.xz")
sha1sums=('d1afc3cdc77ddad3efee4318863f5e10b16a5b0c')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../zeroconf-ioslave-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
