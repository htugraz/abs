# $Id: PKGBUILD 243591 2015-08-14 05:28:58Z arojas $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-desktop-applets
_pkgname=ktp-desktop-applets
pkgver=15.07.90
pkgrel=1
pkgdesc='The KDE-Telepathy Plasma desktop applets'
arch=('i686' 'x86_64')
url='http://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
groups=('kde-applications' 'kdenetwork' 'telepathy-kde')
depends=('telepathy-kde-common-internals' 'telepathy-kde-accounts-kcm')
makedepends=('extra-cmake-modules' 'plasma-framework')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz")
md5sums=('84b224a912c3df4f68e87d0c78b0b3ca')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
