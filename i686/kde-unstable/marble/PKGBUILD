# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase=marble
pkgname=(marble libastro)
pkgver=15.07.80
pkgrel=2
pkgdesc="Desktop Globe"
arch=(i686 x86_64)
url="https://www.kde.org/applications/system/marble"
license=(GPL)
makedepends=(extra-cmake-modules gpsd quazip-qt5 libwlocate phonon-qt5 qt5-webkit krunner kparts knewstuff qt5-tools)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('1fe7279c7a9a6bc6695c3c016621e719')

prepare() {
  mkdir -p build

  # fix installation designer plugins path
  sed 's|LIB_SUFFIX}/plugins|LIB_SUFFIX}/qt/plugins|g' -i $pkgbase-$pkgver/CMakeLists.txt

  # only build the KDE app
  sed -e '/mobile/d' \
      -e '/qt/d'  \
      -e '/Quick/d' \
      -e '/touch/d' \
      -i $pkgname-$pkgver/src/apps/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DBUILD_MARBLE_EXAMPLES=OFF \
    -DBUILD_MARBLE_TOOLS=ON \
    -DBUILD_MARBLE_TESTS=OFF \
    -DMOBILE=OFF
  make
}

package_libastro() {
  pkgdesc='Marble astronomy library'
  depends=(gcc-libs)
  conflicts=('kdeedu-marble<15.04.3-3' marble-qt)

  cd build/src/lib/astro
  make DESTDIR="$pkgdir" install
}

package_marble() {
  depends=(libastro qt5-webkit kparts knewstuff hicolor-icon-theme)
  optdepends=('gpsd: position information via gpsd'
            'quazip-qt5: reading and displaying .kmz files'
            'libwlocate: Position information based on neighboring WLAN networks'
            'phonon-qt5: That enables the use of audio and video content'
            'krunner: Krunner plugin')
  conflicts=('kdeedu-marble<15.04.3-3' marble-qt)
  replaces=(kdeedu-marble 'marble-qt<15.07')
  groups=(kde-applications kdeedu)
  install=marble.install

  cd build
  make DESTDIR="$pkgdir" install

# provided by libastro
  rm -r "$pkgdir"/usr/include/astro
  rm "$pkgdir"/usr/lib/libastro.*
}
