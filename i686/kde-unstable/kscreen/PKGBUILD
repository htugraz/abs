# $Id: PKGBUILD 236520 2015-04-14 07:53:19Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kscreen
pkgver=5.2.95
pkgrel=1
pkgdesc="KDE's screen management software"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kscreen'
license=('LGPL')
depends=('kxmlgui' 'libkscreen' 'qt5-graphicaleffects')
makedepends=('extra-cmake-modules' 'python')
conflicts=('kscreen-frameworks')
replaces=('kscreen-frameworks')
groups=('plasma')
install=$pkgname.install
source=("http://download.kde.org/unstable/plasma/${pkgver}/kscreen-$pkgver.tar.xz")
md5sums=('3d9069dd7c1fccef2535b60c12df9191')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kscreen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
