# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=dolphin
pkgver=15.07.80
pkgrel=2
pkgdesc="File Manager"
arch=(i686 x86_64)
url="http://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras ktexteditor kactivities-frameworks)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=('kdebase-dolphin<15.04.3-3')
replaces=(kdebase-dolphin)
groups=(kde-applications kdebase)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('3de90082afaf1e635347f5f08267d713')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
