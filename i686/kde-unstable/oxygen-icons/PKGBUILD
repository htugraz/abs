# $Id: PKGBUILD 235007 2015-03-27 07:59:51Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=oxygen-icons
pkgname=('oxygen-icons'
         'oxygen-icons-svg')
pkgver=15.03.97
pkgrel=1
pkgdesc="The Oxygen Icon Theme"
arch=('any')
url='http://www.oxygen-icons.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('cd56520844d48e09ef2bea91b3b4163c8c5ca7f7')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_oxygen-icons() {
  cd build
  make DESTDIR="$pkgdir" install
}

package_oxygen-icons-svg() {
  pkgdesc="The Oxygen Icon Theme (Scalable Vector Graphics)"

  cd ${pkgbase}-${pkgver}
  find scalable -type f ! -name '*.sh' -exec \
    install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
