# $Id: PKGBUILD 200483 2013-11-28 13:41:15Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kbounce
pkgver=4.11.97
pkgrel=1
pkgdesc="A single player arcade game with the elements of puzzle"
url="http://kde.org/applications/games/kbounce/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kbounce-${pkgver}.tar.xz")
sha1sums=('64011612f55e64f841724e7e6b5a93bf38e1c3bb')

build() {
  mkdir build
  cd build
  cmake ../kbounce-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
