# $Id: PKGBUILD 207869 2014-03-15 07:59:02Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kpatience
pkgver=4.12.90
pkgrel=1
pkgdesc="Offers a selection of solitaire card games"
url="http://kde.org/applications/games/kpat/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kpat-${pkgver}.tar.xz")
sha1sums=('95f1fe1b7b9b8c6c95f9a2094c79cdcdd9cdbd08')

build() {
  mkdir build
  cd build
  cmake ../kpat-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
