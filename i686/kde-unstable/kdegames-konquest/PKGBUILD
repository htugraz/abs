# $Id: PKGBUILD 252253 2015-12-03 20:30:53Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-konquest
pkgver=15.11.90
pkgrel=1
pkgdesc="The KDE version of Gnu-Lactic"
url="http://kde.org/applications/games/konquest/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/konquest-${pkgver}.tar.xz")
sha1sums=('70a102ca9e92daaf886d0bd9639cd6a582f81340')

build() {
  mkdir -p build
  cd build
  cmake ../konquest-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
