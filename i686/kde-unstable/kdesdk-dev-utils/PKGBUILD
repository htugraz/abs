# $Id: PKGBUILD 254962 2015-12-09 08:11:45Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-dev-utils
pkgver=15.11.90
pkgrel=2
pkgdesc='Small utilities for developers using KDE/Qt libs/frameworks'
url='https://projects.kde.org/projects/kde/kdesdk/kde-dev-utils'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
conflicts=('kdesdk-kmtrace' 'kdesdk-kpartloader' 'kdesdk-kprofilemethod'
           'kdesdk-kstartperf' 'kdesdk-kuiviewer')
replaces=('kdesdk-kmtrace' 'kdesdk-kpartloader' 'kdesdk-kprofilemethod'
          'kdesdk-kstartperf' 'kdesdk-kuiviewer')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kde-dev-utils-${pkgver}.tar.xz")
sha1sums=('2a05cdf6e0fb693f80917e2cb076c39986d3fb4b')

build() {
  mkdir -p build
  cd build
  cmake ../kde-dev-utils-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
