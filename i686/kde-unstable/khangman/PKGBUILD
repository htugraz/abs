# $Id: PKGBUILD 242681 2015-07-29 07:36:18Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khangman
pkgver=15.07.80
pkgrel=1
pkgdesc="Hangman Game"
url="http://kde.org/applications/education/khangman/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('libkeduvocdocument' 'knewstuff' 'kdeclarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-khangman')
conflicts=('kdeedu-khangman')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/khangman-${pkgver}.tar.xz")
sha1sums=('996a1e1ae3fb1ddc83a215f500bcd80801d87879')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../khangman-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
