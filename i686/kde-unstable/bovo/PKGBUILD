# $Id: PKGBUILD 243513 2015-08-14 04:54:48Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=bovo
pkgver=15.07.90
pkgrel=1
pkgdesc="A Gomoku like game for two players"
url="http://kde.org/applications/games/bovo/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-bovo')
replaces=('kdegames-bovo')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/bovo-${pkgver}.tar.xz")
sha1sums=('ed2f802820112bab3725beb3d5093112463ae27f')

build() {
  mkdir build
  cd build
  cmake ../bovo-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
