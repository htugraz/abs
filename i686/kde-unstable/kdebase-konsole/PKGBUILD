# $Id: PKGBUILD 207817 2014-03-15 07:50:38Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebase-konsole
pkgver=4.12.90
pkgrel=1
arch=('i686' 'x86_64')
url='http://kde.org/applications/system/konsole/'
pkgdesc="Terminal"
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
depends=('kdebase-runtime' 'kdebase-lib')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/konsole-${pkgver}.tar.xz")
sha1sums=('0e9a8c040802b87d8986d56b1fab75c61cb7983e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../konsole-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
