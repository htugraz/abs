# $Id: PKGBUILD 242578 2015-07-28 17:20:17Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=libakonadi-qt4
pkgname=(libakonadi-qt4 akonadi-qt4-dbus)
pkgver=1.13.0
pkgrel=2
pkgdesc="Qt4 akonadi libraries"
arch=('i686' 'x86_64')
url='http://community.kde.org/KDE_PIM/Akonadi'
license=('LGPL')
depends=('boost-libs' 'qt4' 'libxslt')
makedepends=('cmake' 'automoc4' 'boost' 'postgresql' 'mariadb')
conflicts=('akonadi<2')
install="${pkgname}.install"
source=("http://download.kde.org/stable/akonadi/src/akonadi-${pkgver}.tar.bz2"
        dont-leak-old-external-payload-files.patch)
md5sums=('84eb2e471bd6bdfe54a2a2f1d858c07d'
         '9711e300dde3775a38637daa3c20befe')

prepare() {
  mkdir -p build

  cd akonadi-$pkgver
  # https://bugs.kde.org/show_bug.cgi?id=341884
  patch -p1 -i ../dont-leak-old-external-payload-files.patch
}

build() {
  cd build
  cmake ../akonadi-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_QSQLITE_IN_QT_PREFIX=TRUE \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DDATABASE_BACKEND=SQLITE \
    -DWITH_SOPRANO=OFF
  make
}

package_libakonadi-qt4() {
  cd build
  make DESTDIR="${pkgdir}" install

# conflicts with akonadi
  rm -r "$pkgdir"/usr/{bin,share}
}

package_akonadi-qt4-dbus() {
  depends=()
  conflicts=(akonadi)

  mkdir -p "$pkgdir"/usr/share/dbus-1/interfaces
  install -m644 "$srcdir"/akonadi-$pkgver/interfaces/*.xml "$pkgdir"/usr/share/dbus-1/interfaces/
}
