# $Id: PKGBUILD 234400 2015-03-21 06:25:24Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=lokalize
pkgver=15.03.95
pkgrel=1
pkgdesc='Computer-Aided Translation System'
url='http://kde.org/applications/development/lokalize/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kross')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdesdk-lokalize')
replaces=('kdesdk-lokalize')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/lokalize-${pkgver}.tar.xz")
sha1sums=('fcc60fbe3186e54e8ee27272bac2a58e994cc8b5')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../lokalize-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/share/lokalize/scripts/odf/xliffmerge.py
}
