# $Id: PKGBUILD 251735 2015-11-20 21:42:04Z arojas $
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kwallet-pam
pkgver=5.4.95
pkgrel=1
pkgdesc='KWallet PAM integration'
arch=(i686 x86_64)
url='https://projects.kde.org/kwallet-pam'
license=(LGPL)
depends=(pam libgcrypt socat)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=("http://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('fd277bf51830ecc8a42eaff3bf07daa0')

prepare() {
  mkdir -p build{,4}
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=usr/lib
  make

  cd ../build4
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=usr/lib \
    -DKWALLET4=1
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  cd ../build4
  make DESTDIR="$pkgdir" install
}
