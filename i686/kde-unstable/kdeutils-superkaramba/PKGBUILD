# $Id: PKGBUILD 243626 2015-08-14 05:48:17Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-superkaramba
pkgver=15.07.90
pkgrel=1
pkgdesc='An engine for cool desktop eyecandy'
url='http://kde.org/applications/utilities/superkaramba/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'qimageblitz')
makedepends=('cmake' 'automoc4')
groups=('kde-applications' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/superkaramba-${pkgver}.tar.xz")
sha1sums=('d7bce85f06dae23319c592224509182175036831')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../superkaramba-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
