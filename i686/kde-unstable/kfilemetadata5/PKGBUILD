# $Id: PKGBUILD 219501 2014-08-08 15:51:50Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfilemetadata5
pkgver=5.0.0
pkgrel=1
pkgdesc="A library for extracting file metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kfilemetadata'
license=('LGPL')
depends=('kservice' 'karchive' 'exiv2' 'poppler-qt5' 'taglib' 'ffmpeg' 'ebook-tools')
makedepends=('extra-cmake-modules')
conflicts=('kfilemetadata-frameworks')
replaces=('kfilemetadata-frameworks')
source=("http://download.kde.org/stable/plasma/$pkgver/kfilemetadata-$pkgver.tar.xz")
md5sums=('ce69e768ec2324051ad0ca1d2b248a75')

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kfilemetadata-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_PREFIX_PATH=/usr/lib/cmake \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
