# $Id: PKGBUILD 207280 2014-03-08 08:47:16Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-strigi-analyzer
pkgver=4.12.80
pkgrel=1
pkgdesc="Strigi analyzers for various graphics file formats"
url="https://projects.kde.org/projects/kde/kdegraphics/kdegraphics-strigi-analyzer"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('6cd01802fcdbefa16d8b4ec10c2e820db65aeb28')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
