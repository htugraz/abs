# $Id: PKGBUILD 219153 2014-08-03 13:52:11Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kig
pkgver=4.13.97
pkgrel=1
pkgdesc="Interactive Geometry"
url="http://kde.org/applications/education/kig/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'boost-libs')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kig-${pkgver}.tar.xz")
sha1sums=('00d140e5ffe477c150ef54379bcd815b148f4a36')

build() {
  mkdir build
  cd build
  cmake ../kig-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # Use the python2 executable
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/bin/pykig.py
}
