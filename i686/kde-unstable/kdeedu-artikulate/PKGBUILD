# $Id: PKGBUILD 233654 2015-03-14 10:08:01Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-artikulate
pkgver=15.03.90
pkgrel=1
pkgdesc="Improve your pronunciation by listening to native speakers"
url="https://projects.kde.org/projects/kde/kdeedu/artikulate"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'qt-gstreamer' 'kqtquickcharts')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/artikulate-${pkgver}.tar.xz")
sha1sums=('9fe2440677c468240b532525db2e41a10147eaa7')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../artikulate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
