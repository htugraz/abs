# $Id: PKGBUILD 234291 2015-03-20 21:08:08Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-artikulate
pkgver=15.03.95
pkgrel=1
pkgdesc="Improve your pronunciation by listening to native speakers"
url="https://projects.kde.org/projects/kde/kdeedu/artikulate"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kdebase-runtime' 'qt-gstreamer' 'kqtquickcharts')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/artikulate-${pkgver}.tar.xz")
sha1sums=('8f760b276a7fc6b63d9eab02a225fa75d9e2b20a')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../artikulate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
