# $Id: PKGBUILD 207212 2014-03-08 08:38:30Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-artikulate
pkgver=4.12.80
pkgrel=1
pkgdesc="Improve your pronunciation by listening to native speakers"
url="https://projects.kde.org/projects/kde/kdeedu/artikulate"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'qt-gstreamer')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/artikulate-${pkgver}.tar.xz")
sha1sums=('1a1ee8e33f521c49900c00dfd67dd4578c3cfe02')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../artikulate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
