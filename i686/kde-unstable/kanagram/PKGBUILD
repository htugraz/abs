# $Id: PKGBUILD 243497 2015-08-14 04:35:26Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kanagram
pkgver=15.07.90
pkgrel=1
pkgdesc="Letter Order Game"
url="http://kde.org/applications/education/kanagram/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('libkeduvocdocument' 'kdeclarative' 'knewstuff' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-kanagram')
conflicts=('kdeedu-kanagram')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kanagram-${pkgver}.tar.xz")
sha1sums=('72a76de51f87fc68b2d90d6f50bc9784ce187584')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kanagram-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
