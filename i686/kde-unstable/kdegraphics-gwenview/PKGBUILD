# $Id: PKGBUILD 209053 2014-03-29 09:15:16Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-gwenview
pkgver=4.12.97
pkgrel=1
pkgdesc="A fast and easy to use image viewer for KDE"
url="http://kde.org/applications/graphics/gwenview/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-lib' 'kdebase-runtime' 'libkipi' 'baloo' 'libkdcraw')
makedepends=('cmake' 'automoc4' 'mesa')
optdepends=('kipi-plugins: extra plugins to share photos')
install=$pkgname.install
source=("http://download.kde.org/unstable/${pkgver}/src/gwenview-${pkgver}.tar.xz")
sha1sums=('2ce50ec5891ff74d206336fa572622cb6cd9aba7')

build() {
  mkdir build
  cd build
  cmake ../gwenview-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
