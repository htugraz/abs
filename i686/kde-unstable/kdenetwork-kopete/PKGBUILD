# $Id: PKGBUILD 233735 2015-03-14 10:34:39Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kopete
pkgver=15.03.90
pkgrel=1
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-runtime' 'kdepimlibs' 'qca-ossl' 'libotr' 'qimageblitz' 'libgadu' 'mediastreamer' 'jsoncpp')
makedepends=('cmake' 'automoc4' 'boost' 'kdepim-libkdepim')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kopete-${pkgver}.tar.xz")
sha1sums=('6fb9cafbc38137a6f44354a7110fbfea07462331')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kopete-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DWITH_Libmsn=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
