# $Id: PKGBUILD 243579 2015-08-14 05:21:47Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kopete
pkgver=15.07.90
pkgrel=1
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'kdepimlibs4' 'qca-ossl' 'libotr' 'qimageblitz' 'jsoncpp')
makedepends=('cmake' 'automoc4' 'boost' 'mediastreamer' 'libgadu')
optdepends=('mediastreamer: jingle support' 'libgadu: Gadu-Gadu protocol')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kopete-${pkgver}.tar.xz")
sha1sums=('2d214936aae3aacc2199add00b444eccc23ec684')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kopete-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DWITH_Libmsn=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
