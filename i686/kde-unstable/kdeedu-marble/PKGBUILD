# $Id: PKGBUILD 207186 2014-03-08 08:34:24Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-marble
pkgver=4.12.80
pkgrel=1
pkgdesc="Desktop Globe"
url="http://kde.org/applications/education/marble/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'gpsd')
optdepends=('gpsd: GPS support'
            'quazip: reading and displaying KMZ files')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/marble-${pkgver}.tar.xz")
sha1sums=('74642ef8287db37557065b2c9839399e26934aa5')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../marble-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
