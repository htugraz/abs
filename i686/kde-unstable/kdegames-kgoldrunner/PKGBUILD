# $Id: PKGBUILD 209026 2014-03-29 09:09:09Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kgoldrunner
pkgver=4.12.97
pkgrel=1
pkgdesc="A game of action and puzzle solving"
url="http://kde.org/applications/games/kgoldrunner/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kgoldrunner-${pkgver}.tar.xz")
sha1sums=('a8dc62e5022faa59cffb24a7e6fad68f0937df23')

build() {
  mkdir build
  cd build
  cmake ../kgoldrunner-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
