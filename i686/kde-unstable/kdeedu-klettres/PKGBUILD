# $Id: PKGBUILD 208399 2014-03-22 17:56:28Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-klettres
pkgver=4.12.95
pkgrel=1
pkgdesc="Learn The Alphabet"
url="http://kde.org/applications/education/klettres/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/klettres-${pkgver}.tar.xz")
sha1sums=('1140c5d071e3e0d359333caf30432067cb454d66')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../klettres-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
