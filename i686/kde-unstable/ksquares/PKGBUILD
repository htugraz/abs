# $Id: PKGBUILD 254912 2015-12-09 07:53:33Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ksquares
pkgver=15.11.90
pkgrel=2
pkgdesc="A modeled after the well known pen and paper based game of Dots and Boxes"
url="http://kde.org/applications/games/ksquares/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig' 'hicolor-icon-theme') 
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-ksquares')
replaces=('kdegames-ksquares')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/ksquares-${pkgver}.tar.xz")
sha1sums=('901a6ff43e81867f688e9854f61a0f5696882d84')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ksquares-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
