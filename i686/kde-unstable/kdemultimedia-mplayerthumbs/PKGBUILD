# $Id: PKGBUILD 234369 2015-03-20 22:50:32Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-mplayerthumbs
pkgver=15.03.95
pkgrel=1
pkgdesc="MPlayer based thumbnail generator for video files"
url='https://projects.kde.org/projects/kde/kdemultimedia/mplayerthumbs'
arch=('i686' 'x86_64')
groups=('kde-applications' 'kdemultimedia')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'mplayer')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/mplayerthumbs-${pkgver}.tar.xz")
sha1sums=('0d2219501feecc018468527a844080491ca4e9b2')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../mplayerthumbs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
