# $Id: PKGBUILD 243601 2015-08-14 05:32:16Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kapptemplate
pkgver=15.07.90
pkgrel=1
pkgdesc='KDE Template Generator'
url='http://kde.org/applications/development/kapptemplate/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kio')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdesdk-kapptemplate')
conflicts=('kdesdk-kapptemplate')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kapptemplate-${pkgver}.tar.xz")
sha1sums=('1691e4d07c31bc16da94a17f9e415a20e9e963b7')

build() {
  mkdir build
  cd build
  cmake ../kapptemplate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
