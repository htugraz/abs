# $Id: PKGBUILD 236560 2015-04-14 19:30:45Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-nm
pkgver=5.2.95
pkgrel=2
pkgdesc='Plasma applet written in QML for managing network connections'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-nm'
license=('GPL2')
depends=('plasma-framework' 'modemmanager-qt' 'networkmanager-qt' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools' 'openconnect' 'mobile-broadband-provider-info'
             'python' 'kdelibs4support')
optdepends=('mobile-broadband-provider-info: Database of mobile broadband service providers'
            'openconnect: Cisco AnyConnect VPN plugin')
groups=('plasma')
source=("http://download.kde.org/unstable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('27997aec60ec54d902999501183fa1a6')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
