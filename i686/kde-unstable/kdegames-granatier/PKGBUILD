# $Id: PKGBUILD 199612 2013-11-15 09:59:43Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-granatier
pkgver=4.11.90
pkgrel=1
pkgdesc="A clone of the classic Bomberman game"
url="http://kde.org/applications/games/granatier/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/granatier-${pkgver}.tar.xz")
sha1sums=('79ed41b47755c84d88be0f2facceffc3fb59cf84')

build() {
  mkdir build
  cd build
  cmake ../granatier-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
