# $Id: PKGBUILD 241398 2015-06-29 17:50:28Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libmarble4
pkgver=15.04.3
pkgrel=1
pkgdesc="KDE4 Marble libraries"
url="http://kde.org/applications/education/marble/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kdebase-runtime libastro)
makedepends=(cmake automoc4 gpsd libwlocate)
optdepends=('gpsd: GPS support' 'libwlocate: WLAN based geolocation')
conflicts=(kdeedu-marble)
provides=(kdeedu-marble)
source=("http://download.kde.org/stable/applications/$pkgver/src/marble-$pkgver.tar.xz")
sha1sums=('9c8c4f307bb633cf7c21fdf5fad82995d2dfa50a')

prepare() {
  mkdir -p build

# rename include dir
  sed -e 's|marble/MarbleModel.h|marble4/MarbleModel.h|' -i marble-$pkgver/FindMarble.cmake
}

build() {
  cd build
  cmake ../marble-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# remove files provided by libastro and marble
  rm -r "$pkgdir"/usr/bin
  rm -r "$pkgdir"/usr/share/applications/kde4/marble-*
  rm -r "$pkgdir"/usr/include/astro
  rm -r "$pkgdir"/usr/lib/libastro.*
  rm -r "$pkgdir"/usr/share/{appdata,config.kcfg,doc,icons}
# rename include dir to avoid conflicts with marble
  mv "$pkgdir"/usr/include/marble{,4}
}

