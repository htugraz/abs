# $Id: PKGBUILD 241398 2015-06-29 17:50:28Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libmarble4
pkgver=15.07.80
pkgrel=1
pkgdesc="KDE4 Marble libraries"
url="http://kde.org/applications/education/marble/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(qtwebkit libastro marble-data)
makedepends=(cmake automoc4 gpsd libwlocate)
optdepends=('gpsd: GPS support' 'libwlocate: WLAN based geolocation')
conflicts=(kdeedu-marble)
provides=(kdeedu-marble)
source=("http://download.kde.org/unstable/applications/$pkgver/src/marble-$pkgver.tar.xz" 'marble-nowebkit-1508-1.diff')
sha1sums=('8f61030eec2b2c9702150d799e2337867b9743b2'
          'ebe19afe48153ad9409ac783c42e44c6bb567b6b')

prepare() {
  mkdir -p build

  cd marble-$pkgver
  patch -p1 -i ../marble-nowebkit-1508-1.diff
  cd ..
 
# rename include dir 
  sed -e 's|marble/MarbleModel.h|marble4/MarbleModel.h|' -i marble-$pkgver/FindMarble.cmake.in
}

build() {
  cd build
  cmake ../marble-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_MARBLE_TESTS=OFF \
    -DBUILD_MARBLE_APPS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMARBLE_PLUGIN_PATH=/usr/lib/marble4/plugins
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# move cmake file
  install -Dm644 "$pkgdir"/usr/share/marble/cmake/FindMarble.cmake "$pkgdir"/usr/share/apps/cmake/modules/FindMarble.cmake

# remove files provided by libastro and marble-data
  rm -r "$pkgdir"/usr/include/astro
  rm -r "$pkgdir"/usr/lib/libastro.*
  rm -r "$pkgdir"/usr/share/{appdata,applications,icons,marble}

# rename include dir to avoid conflicts with marble
  mv "$pkgdir"/usr/include/marble{,4}
}
