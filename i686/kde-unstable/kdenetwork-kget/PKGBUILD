# $Id: PKGBUILD 207338 2014-03-08 09:45:38Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kget
pkgver=4.12.80
pkgrel=2
pkgdesc='Download Manager'
url='http://kde.org/applications/internet/kget/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-workspace' 'kdebase-lib' 'libktorrent' 'libmms')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kget-${pkgver}.tar.xz")
sha1sums=('0c0fc23c7b643b7c7d60c388e4de6b6133bc88de')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kget-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF \
    -DWITH_NepomukWidgets=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
