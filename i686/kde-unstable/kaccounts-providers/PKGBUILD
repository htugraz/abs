# $Id: PKGBUILD 243478 2015-08-13 23:05:47Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kaccounts-providers
pkgver=15.07.90
pkgrel=1
pkgdesc='Small system to administer web accounts for the sites and services across the KDE desktop, including: Google, Facebook, Owncloud, IMAP, 
Jabber and others'
arch=('any')
url='https://projects.kde.org/projects/playground/base/kde-accounts/kaccounts-providers'
license=('GPL')
groups=('kde-applications' 'kdenetwork')
depends=('libaccounts-glib' 'kaccounts-integration')
makedepends=('extra-cmake-modules' 'intltool')
install=$pkgname.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/$pkgname-${pkgver}.tar.xz")
md5sums=('3e904f76164f51f9735c19be7d07f1b4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
