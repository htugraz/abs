# $Id: PKGBUILD 251725 2015-11-20 21:38:51Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons
pkgver=5.4.95
pkgrel=1
pkgdesc='All kind of addons to improve your Plasma experience'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kdeplasma-addons'
license=('LGPL')
depends=('plasma-workspace')
makedepends=('extra-cmake-modules' 'kdoctools' 'libibus' 'scim' 'kross' 'purpose')
optdepends=('ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support'
            'kross: comic applet'
            'purpose: Quickshare applet'
            'quota-tools: disk quota applet')
install="${pkgname}.install"
conflicts=('kdebase-workspace' 'kdeplasma-addons-frameworks')
replaces=('kdeplasma-addons-frameworks')
groups=('plasma')
source=("http://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('00f58719c62edf4e8da1e9c35508f046')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
