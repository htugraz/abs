# $Id: PKGBUILD 199659 2013-11-15 10:11:43Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-thumbnailers
pkgver=4.11.90
pkgrel=1
pkgdesc="Thumbnailers for various graphics file formats"
url="https://projects.kde.org/projects/kde/kdegraphics/kdegraphics-thumbnailers"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkexiv2' 'libkdcraw')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('3553ab2d1da69de5d33c205d583a6dabbd774218')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
