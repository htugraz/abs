# $Id: PKGBUILD 233677 2015-03-14 10:17:12Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=granatier
pkgver=15.03.90
pkgrel=1
pkgdesc="A clone of the classic Bomberman game"
url="http://kde.org/applications/games/granatier/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('libkdegames')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-granatier')
replaces=('kdegames-granatier')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/granatier-${pkgver}.tar.xz")
sha1sums=('ef5b1d59322590c8b096ec609a452cd9cca6a028')

build() {
  mkdir build
  cd build
  cmake ../granatier-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
