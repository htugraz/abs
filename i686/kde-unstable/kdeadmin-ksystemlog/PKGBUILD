# $Id: PKGBUILD 199582 2013-11-15 09:54:04Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-ksystemlog
pkgver=4.11.90
pkgrel=1
pkgdesc='System log viewer tool'
url='http://kde.org/applications/system/ksystemlog/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/ksystemlog-${pkgver}.tar.xz"
        'syslog-path.patch')
sha1sums=('763f622af8c689353eaa6993780e721e4f13cf4b'
          '20095ce6e0f3e5b6800a7c6e52de6fddba62c031')

prepare() {
  cd ksystemlog-${pkgver}
  patch -p2 -i "${srcdir}"/syslog-path.patch
}

build() {
  mkdir build
  cd build
  cmake ../ksystemlog-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
