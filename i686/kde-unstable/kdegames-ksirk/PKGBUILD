# $Id: PKGBUILD 200138 2013-11-22 09:18:59Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksirk
pkgver=4.11.95
pkgrel=1
pkgdesc="A computerized version of a well known strategy game"
url="http://kde.org/applications/games/ksirk/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/ksirk-${pkgver}.tar.xz")
sha1sums=('56e77d6886eb3657aa8fbdb5f28adaa40edd7057')

build() {
  mkdir build
  cd build
  cmake ../ksirk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
