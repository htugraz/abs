# $Id: PKGBUILD 234330 2015-03-20 22:02:17Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=klines
pkgver=15.03.95
pkgrel=1
pkgdesc="A simple but highly addictive, one player game"
url="http://kde.org/applications/games/klines/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-klines')
replaces=('kdegames-klines')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/klines-${pkgver}.tar.xz")
sha1sums=('03ebfa270d166de8c43cc8ca1d375fbcafba84d6')

build() {
  mkdir build
  cd build
  cmake ../klines-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
