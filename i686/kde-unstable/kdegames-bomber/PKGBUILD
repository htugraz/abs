# $Id: PKGBUILD 208409 2014-03-22 17:58:19Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-bomber
pkgver=4.12.95
pkgrel=1
pkgdesc="A single player arcade game"
url="http://kde.org/applications/games/bomber/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/bomber-${pkgver}.tar.xz")
sha1sums=('a20652ea6c20a731d4f169f2873ffaed0007e6a6')

build() {
  mkdir build
  cd build
  cmake ../bomber-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
