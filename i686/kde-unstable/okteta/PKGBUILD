# $Id: PKGBUILD 243605 2015-08-14 05:33:57Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okteta
pkgver=15.07.90
pkgrel=1
pkgdesc='Hex Editor'
url='http://kde.org/applications/utilities/okteta/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kcmutils' 'kparts' 'knewstuff' 'qca-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'qt5-tools')
replaces=('kdesdk-okteta')
conflicts=('kdesdk-okteta<4.14.3-2')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/okteta-${pkgver}.tar.xz")
sha1sums=('6c5bf2216f7f99a94e1e6e5c34f06e97943a821b')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../okteta-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
