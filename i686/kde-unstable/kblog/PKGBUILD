# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kblog
pkgver=15.07.80
pkgrel=1
pkgdesc="A blogging library for KDE"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kxmlrpcclient kde-syndication kcalcore)
makedepends=(extra-cmake-modules-git python)
source=("http://download.kde.org/unstable/applications/15.07.80/src/$pkgname-$pkgver.tar.xz")
md5sums=('cd67e480c8810d3387fc540f617142f4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
