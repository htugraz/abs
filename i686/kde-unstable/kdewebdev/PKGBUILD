# $Id: PKGBUILD 235175 2015-03-27 09:17:49Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdewebdev
pkgname=('kdewebdev-kfilereplace'
         'kdewebdev-kimagemapeditor'
         'kdewebdev-klinkstatus'
         'kdewebdev-kommander')
pkgver=15.03.97
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdewebdev')
makedepends=('cmake' 'automoc4' 'ruby' 'tidyhtml' 'kdepimlibs' 'boost')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('ce8749a68d389b26ae231f0ac2f1c3c300380ac3')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdewebdev-kfilereplace() {
	pkgdesc='Search & Replace Tool'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kfilereplace/"
	install='kdewebdev.install'
	cd $srcdir/build/kfilereplace
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kfilereplace
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kimagemapeditor() {
	pkgdesc='HTML Image Map Editor'
	depends=('kdebase-runtime')
    url="http://www.kde.org/applications/development/kimagemapeditor/"
	install='kdewebdev-kimagemapeditor.install'
	cd $srcdir/build/kimagemapeditor
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kimagemapeditor
	make DESTDIR=$pkgdir install
}

package_kdewebdev-klinkstatus() {
	pkgdesc='Link Checker'
	depends=('kdebase-runtime' 'kdepimlibs' 'tidyhtml')
    url="http://www.kde.org/applications/development/klinkstatus/"
	install='kdewebdev.install'
	cd $srcdir/build/klinkstatus
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/klinkstatus
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kommander() {
	pkgdesc='Executor for Kommander dialogs'
	depends=('kdebase-runtime')
        url="http://www.kde.org/applications/development/kommander/"
	cd $srcdir/build/kommander
	make DESTDIR=$pkgdir install
}
