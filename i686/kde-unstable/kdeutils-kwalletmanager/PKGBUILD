# $Id: PKGBUILD 233777 2015-03-14 10:46:30Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kwalletmanager
pkgver=15.03.90
pkgrel=1
pkgdesc='Wallet Management Tool'
url='http://kde.org/applications/system/kwalletmanager/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'kdepimlibs')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
conflicts=('kdeutils-kwallet')
replaces=('kdeutils-kwallet')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kwalletmanager-${pkgver}.tar.xz")
sha1sums=('3290221f5759d86dded8669a390e472a727c7a83')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kwalletmanager-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
