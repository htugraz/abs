# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmailtransport
pkgver=15.07.80
pkgrel=1
pkgdesc="Mail Transport Service"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kdepimlibs)
makedepends=(extra-cmake-modules python)
source=("http://download.kde.org/unstable/applications/15.07.80/src/$pkgname-$pkgver.tar.xz")
md5sums=('e4fe3f251a435db46ab43f2a7dc352e3')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
