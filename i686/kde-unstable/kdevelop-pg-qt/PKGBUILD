# $Id: PKGBUILD 206624 2014-03-03 18:01:46Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-pg-qt
pkgver=1.90.91
pkgrel=1
pkgdesc="KDevelop Parser Generator, a LL(1) parser generator used by KDevelop language plugins"
arch=(i686 x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/unstable/$pkgname/src/$pkgname-$pkgver.tar.xz")
sha1sums=('34cf30fdf3dde61412d4b147f869020615c77df5')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
