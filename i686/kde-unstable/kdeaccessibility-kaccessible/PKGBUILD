# $Id: PKGBUILD 252196 2015-12-03 20:09:01Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kaccessible
pkgver=15.11.90
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Provides accessibility services like focus tracking and a screenreader'
url='https://projects.kde.org/projects/kde/kdeaccessibility/kaccessible'
license=('GPL' 'FDL')
groups=('kde-applications' 'kdeaccessibility')
depends=('kdelibs' 'speech-dispatcher')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kaccessible-${pkgver}.tar.xz")
sha1sums=('bf33795360cc4e8835df4c4d951797bc8fa1dc52')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kaccessible-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
