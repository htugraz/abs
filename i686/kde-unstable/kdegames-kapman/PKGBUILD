# $Id: PKGBUILD 208413 2014-03-22 17:58:53Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kapman
pkgver=4.12.95
pkgrel=1
pkgdesc="A clone of the well known game Pac-Man"
url="http://kde.org/applications/games/kapman/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kapman-${pkgver}.tar.xz")
sha1sums=('cb566892c32af6dd06da658b751833515ed9686c')

build() {
  mkdir build
  cd build
  cmake ../kapman-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
