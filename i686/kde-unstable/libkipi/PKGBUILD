# $Id: PKGBUILD 252158 2015-12-03 19:55:12Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkipi
pkgver=15.11.90
pkgrel=1
pkgdesc="An interface to use kipi-plugins from a KDE application"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkipi"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kxmlgui hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
install=$pkgname.install
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('4b282afded150bd7c11026113b8df9870dee01d1')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
