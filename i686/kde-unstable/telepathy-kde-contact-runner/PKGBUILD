# $Id: PKGBUILD 207370 2014-03-08 11:39:56Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-contact-runner
_pkgname=ktp-contact-runner
pkgver=0.7.80
pkgrel=1
pkgdesc="KRunner plugin for KDE Telepathy"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-common-internals')
makedepends=('cmake' 'automoc4')
groups=('kde-telepathy')
source=("http://download.kde.org/stable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('19106f74918bc25e7c7a4eca29614ecd2c417b86')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
