# $Id: PKGBUILD 200555 2013-11-28 13:54:18Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-umbrello
pkgver=4.11.97
pkgrel=1
pkgdesc='UML Modeller'
url='http://kde.org/applications/development/umbrello/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'boost')
optdepends=('ruby')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/umbrello-${pkgver}.tar.xz")
sha1sums=('e624e4d3dcceabbe0252a6225eb244f17827e8b2')

build() {
  mkdir build
  cd build
  cmake ../umbrello-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
