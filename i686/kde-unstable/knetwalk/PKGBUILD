# $Id: PKGBUILD 243534 2015-08-14 05:03:27Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knetwalk
pkgver=15.07.90
pkgrel=1
pkgdesc="Connect all the terminals to the server, in as few turns as possible"
url="http://kde.org/applications/games/knetwalk/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-knetwalk')
replaces=('kdegames-knetwalk')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/knetwalk-${pkgver}.tar.xz")
sha1sums=('dab6a04a5a81d8d4be8a0841dd1e34ee00be73b3')

build() {
  mkdir build
  cd build
  cmake ../knetwalk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
