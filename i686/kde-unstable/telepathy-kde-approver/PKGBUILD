# $Id: PKGBUILD 207364 2014-03-08 11:39:22Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-approver
_pkgname=ktp-approver
pkgver=0.7.80
pkgrel=1
pkgdesc="KDE Channel Approver for Telepathy"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-common-internals' 'telepathy-kde-contact-list')
makedepends=('cmake' 'automoc4')
groups=('kde-telepathy')
source=("http://download.kde.org/stable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('9f63863a55c1a36921234d6f3658dad4ed4a6555')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
