# $Id: PKGBUILD 242505 2015-07-25 21:40:19Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=putty
pkgver=0.65
pkgrel=1
pkgdesc="A terminal integrated SSH/Telnet client"
arch=('i686' 'x86_64')
url="http://www.chiark.greenend.org.uk/~sgtatham/putty/"
license=('MIT')
depends=('gtk2')
source=(http://the.earth.li/~sgtatham/putty/${pkgver}/${pkgname}-${pkgver}.tar.gz
        putty-${pkgver}.tar.gz.sig::http://the.earth.li/~sgtatham/putty/latest/putty-${pkgver}.tar.gz.DSA)
sha1sums=('3449d2aad4c1f444d61f77bd3edef737a91f5be0'
          '3124e4b6e4c2f2bf0716c110bd8d28835344fd25')
validpgpkeys=('00B1100938E698006518F0ABFECD6F3F08B0A90B')

build() {
  cd ${pkgname}-${pkgver}/unix
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}/unix
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
