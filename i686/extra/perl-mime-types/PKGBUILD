# $Id: PKGBUILD 221731 2014-09-15 00:03:19Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-mime-types
pkgver=2.09
pkgrel=1
pkgdesc="Perl/CPAN Module MIME::Types : Information and processing MIME types"
arch=('any')
url="http://search.cpan.org/dist/MIME-Types/"
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/M/MA/MARKOV/MIME-Types-${pkgver}.tar.gz")
md5sums=('99952df4f4a1e2e31eb80d98b6197774')

build() {
  cd MIME-Types-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd MIME-Types-${pkgver}
  make test
}

package() {
  cd MIME-Types-${pkgver}
  make DESTDIR="${pkgdir}" install
}
