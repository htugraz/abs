# $Id: PKGBUILD 230085 2015-01-27 12:19:18Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=kmenuedit
pkgver=5.2.0
pkgrel=1
pkgdesc='KDE menu editor'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kmenuedit'
license=('LGPL')
depends=('khotkeys')
groups=('plasma')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdebase-workspace')
install="${pkgname}.install"
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('d856aeec5f16dcdf2c0698f8167df9a4')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
