# $Id: PKGBUILD 211402 2014-04-18 18:02:17Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kajongg
pkgver=4.13.0
pkgrel=1
pkgdesc="The ancient Chinese board game for 4 players"
url="http://kde.org/applications/games/kajongg/"
arch=('any')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'twisted' 'kdebindings-python2' 'libkmahjongg')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kajongg-${pkgver}.tar.xz")
sha1sums=('4930b73f2301c602fbad97f0f157a84243c1ea1e')

prepare() {
  mkdir build

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    kajongg-${pkgver}/src/{deferredutil,kajongg,kajonggserver,server}.py
}

build() {
  cd build
  cmake ../kajongg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
