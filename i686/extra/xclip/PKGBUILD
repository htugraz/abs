# $Id: PKGBUILD 149046 2012-02-05 15:20:59Z bisson $
# Contributor: damir <damir@archlinux.org>
# Committer: Manolis Tzanidakis <manolis@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xclip
pkgver=0.12
pkgrel=3
pkgdesc='Command line interface to the X11 clipboard'
url='http://sourceforge.net/projects/xclip'
license=('GPL')
depends=('libxmu')
arch=('i686' 'x86_64')
source=("http://downloads.sourceforge.net/sourceforge/xclip/${pkgname}-${pkgver}.tar.gz")
sha1sums=('aed2cff18b9aecfe3377ad064c6a67518bbec211')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
