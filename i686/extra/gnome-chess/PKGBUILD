# $Id: PKGBUILD 225961 2014-11-10 23:21:06Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-chess
pkgver=3.14.1
pkgrel=1
pkgdesc="Play the classic two-player boardgame of chess"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'gtk3' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection' 'python2')
optdepends=('gnuchess: Play against computer')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=gnome-chess.install
url="https://wiki.gnome.org/Apps/Chess"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('052336b72c19c92792e79980448b32b567d32514133bd05f42be737b433f81a4')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
