# $Id: PKGBUILD 210452 2014-04-14 20:06:24Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-chess
pkgver=3.12.1
pkgrel=1
pkgdesc="Play the classic two-player boardgame of chess"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'gtk3' 'librsvg' 'glu')
makedepends=('intltool' 'itstool' 'gobject-introspection')
optdepends=('gnuchess: Play against computer')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=gnome-chess.install
url="https://live.gnome.org/Chess"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('9f0052ffb3deef5108c94558c1bb18ab4b2c8ca6e4adb7c75faaac0ce6e8ead8')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
