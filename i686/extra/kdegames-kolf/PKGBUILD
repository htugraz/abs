# $Id: PKGBUILD 198906 2013-11-05 20:04:01Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kolf
pkgver=4.11.3
pkgrel=1
pkgdesc="A miniature golf game with 2d top-down view"
url="http://kde.org/applications/games/kolf/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kolf-${pkgver}.tar.xz")
sha1sums=('5302fa38428e790d288687f6cfb6975e4b3ea6c6')

build() {
  mkdir build
  cd build
  cmake ../kolf-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
