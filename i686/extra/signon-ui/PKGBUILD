# $Id: PKGBUILD 237806 2015-04-20 19:39:53Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-ui
pkgver=0.17+15.04.20150410
pkgrel=1
pkgdesc='UI component responsible for handling the user interactions which can happen during the login process of an online account'
arch=(i686 x86_64)
url="https://launchpad.net/online-accounts-signon-ui"
license=(GPL)
depends=(libaccounts-qt5 qt5-webkit signon libnotify)
source=("http://archive.ubuntu.com/ubuntu/pool/main/s/$pkgname/${pkgname}_$pkgver.orig.tar.gz")
md5sums=('94d0675d1d06956910dcc850ec70914b')

prepare() {
  cd $pkgname-$pkgver

  # Do not install tests
  echo 'INSTALLS =' >>tests/unit/tst_inactivity_timer.pro
  echo 'INSTALLS =' >>tests/unit/tst_signon_ui.pro
}

build() {
  cd $pkgname-$pkgver

  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver

  make INSTALL_ROOT="$pkgdir" install
}

