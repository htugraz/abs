# $Id: PKGBUILD 211843 2014-04-28 05:52:08Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-ktouch
pkgver=4.13.0
pkgrel=2
pkgdesc="Touch Typing Tutor"
url="http://kde.org/applications/education/ktouch/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'kqtquickcharts')
makedepends=('cmake' 'automoc4' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/ktouch-${pkgver}.tar.xz")
sha1sums=('a737598452c62f18a6471d714581a7cb16871e4f')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../ktouch-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
