# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=15.08.1
pkgrel=1
pkgdesc='UML modeller'
arch=(i686 x86_64)
url='http://kde.org/applications/development/umbrello'
license=(GPL)
depends=(hicolor-icon-theme ktexteditor kinit)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdesdk-umbrello)
replaces=(kdesdk-umbrello)
groups=(kde-applications kdesdk)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz" appstream.patch)
install=$pkgname.install
md5sums=('002910788a647072deb7314a22c6ba44'
         'db568533f3722bb5629c98031debe16f')

prepare() {
  mkdir -p build

# Add appstream metadata https://bugs.kde.org/show_bug.cgi?id=352607
  cd $pkgname-$pkgver
  touch umbrello/org.kde.umbrello.appdata.xml
  patch -p1 -i ../appstream.patch
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_KF5=ON \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

