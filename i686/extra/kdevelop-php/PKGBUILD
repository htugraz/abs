# $Id: PKGBUILD 220703 2014-08-27 16:23:30Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-php
pkgver=1.7.0
_pkgver=4.7.0
pkgrel=1
pkgdesc="PHP language and documentation plugin for KDevelop"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop')
makedepends=('cmake' 'automoc4' 'kdevelop-pg-qt')
replaces=('kdevelop-php-docs')
conflicts=('kdevelop-php-docs')
source=(http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-{,docs-}${pkgver}.tar.xz)
sha1sums=('e3a41f7700af56840fd60219e083b761180b8b8c'
          '487b60f591929e7a8292ef8f1c555a0243f1176a')

prepare() {
  mkdir build
  mkdir build-docs
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
  cd ../

  # kdevelop-php-docs
  cd build-docs
  cmake ../${pkgname}-docs-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  cd ../

  # kdevelop-php-docs
  cd build-docs
  make DESTDIR="${pkgdir}" install
}
