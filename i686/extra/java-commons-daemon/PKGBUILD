# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=('java-commons-daemon' 'java-jsvc')
pkgbase=java-commons-daemon
pkgver=1.0.15
pkgrel=2
arch=('i686' 'x86_64')
url='http://commons.apache.org/daemon/'
license=('APACHE')
_libname=commons-daemon
source=(http://archive.apache.org/dist/commons/daemon/source/${_libname}-${pkgver}-src.tar.gz)
sha256sums=('11ee57d2c6b281589c1497b2f88180553f06d5ac1560ce1841e05f89f0342432')
makedepends=('apache-ant' 'docbook2x' 'docbook-xsl')

build() {
  # commons-daemon.jar
  cd "${srcdir}/${_libname}-${pkgver}-src"
  . /etc/profile.d/jdk.sh
  ant

  # jsvc
  cd "${srcdir}/${_libname}-${pkgver}-src/src/native/unix"
  ./configure
  make
  docbook2man man/jsvc.1.xml
}

package_java-commons-daemon() {
  pkgdesc='Alternative invocation mechanism for unix-daemon-like java code'
  depends=('java-runtime')

  cd "${srcdir}/${_libname}-${pkgver}-src"

  install -Dm755 dist/${_libname}-${pkgver}.jar \
                 ${pkgdir}/usr/share/java/${_libname}-${pkgver}.jar
  ln -s ${_libname}-${pkgver}.jar \
        ${pkgdir}/usr/share/java/${_libname}.jar
}

package_java-jsvc() {
  pkgdesc='Apache application for making Java applications run on UNIX more easily'
  depends=("java-${_libname}" 'libcap')

  cd "${srcdir}/${_libname}-${pkgver}-src/src/native/unix"

  install -Dm755 jsvc ${pkgdir}/usr/bin/jsvc
  install -Dm644 JSVC.1 ${pkgdir}/usr/share/man/man1/JSVC.1
}

