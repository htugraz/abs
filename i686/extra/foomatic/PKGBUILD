# $Id: PKGBUILD 200705 2013-12-02 17:54:26Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
 
pkgbase="foomatic"
pkgname=('foomatic-db' 'foomatic-db-engine' 'foomatic-db-nonfree')
arch=('i686' 'x86_64') # needs to be changed  in the subpackages when makepkg will support it
_snapdate=20131201
_enginever=4.0.9
pkgver=${_enginever}_${_snapdate}
pkgrel=1
epoch=2
makedepends=('cups' 'perl' 'libxml2' 'enscript' 'perl' 'net-snmp' 'bash')
_url1=http://www.openprinting.org/download/foomatic/
_url2=ftp://ftp.archlinux.org/other/packages/foomatic/
source=(${_url1}/$pkgbase-db-engine-${_enginever}.tar.gz			#										20130311 = 4.0.9
        ${_url2}/$pkgbase-db-4.0-${_snapdate}.tar.gz				# switch after build url1 -> url2		20131128
        ${_url2}/$pkgbase-db-nonfree-${_snapdate}.tar.gz) 			# switch after build url1 -> url2		20130925
url="http://www.linuxprinting.org/foomatic.html"
options=('!emptydirs')
md5sums=('5b33c1adb21e7b6f3652e9a18d0e6f4e'
         '1ea8175a49bb3c0578629d70c6698cf8'
         '57f98ad75c8e071e3944d97ef9388d37')

# check http://bzr.linuxfoundation.org/loggerhead/openprinting/foomatic/
# for latest commits and developement

package_foomatic-db() {

 pkgdesc="Foomatic - The collected knowledge about printers, drivers, and driver options in XML files, used by foomatic-db-engine to generate PPD files."
# arch=('any')
 license=('GPL' 'custom')
 depends=('perl' 'libxml2')
 replaces=('foomatic-db-ppd' 'foomatic-db-hpijs')
 conflicts=('foomatic-db-ppd' 'foomatic-db-hpijs')
 provides=('foomatic-db-hpijs')

   cd ${srcdir}/${pkgname}-${_snapdate}
   ./configure --prefix=/usr
   make DESTDIR=${pkgdir} install
   install -v -Dm644 ${srcdir}/${pkgname}-${_snapdate}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

package_foomatic-db-engine() {

 pkgdesc="Foomatic - Foomatic's database engine generates PPD files from the data in Foomatic's XML database. It also contains scripts to directly generate print queues and handle jobs."
# arch=('i686' 'x86_64')
 license=('GPL')
 depends=('perl' 'libxml2' "cups-filters>=1.0.42" 'bash')

   export LIB_CUPS="/usr/lib/cups"
   cd ${srcdir}/foomatic-db-engine-${_enginever}
    ./configure --prefix=/usr \
      --sbindir=/usr/bin
   make
   eval `perl -V:archname`
   make DESTDIR=${pkgdir} \
        INSTALLARCHLIB=/usr/lib/perl5/vendor_perl/ \
        INSTALLSITELIB=/usr/lib/perl5/vendor_perl/ \
        INSTALLSITEARCH=/usr/lib/perl5/vendor_perl/ install
   /usr/bin/find ${pkgdir} -name '.packlist' -delete
   # fix permissions
   chmod 755 ${pkgdir}/usr/lib{,/perl5,/perl5/vendor_perl}
}

package_foomatic-db-nonfree() {

 pkgdesc="Foomatic - database extension consisting of manufacturer-supplied PPD files released under non-free licenses"
# arch=('any')
 license=('custom')
 depends=('perl' 'libxml2' "foomatic-db-engine" 'foomatic-db')

   cd ${srcdir}/foomatic-db-nonfree-${_snapdate}
   ./configure --prefix=/usr
   make DESTDIR=${pkgdir} install
   install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
