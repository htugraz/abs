# $Id: PKGBUILD 255396 2015-12-10 04:54:01Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kollision
pkgver=15.08.3
pkgrel=2
pkgdesc="A simple ball dodging game"
url="http://kde.org/applications/games/kollision/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
install=${pkgname}.install
conflicts=('kdegames-kollision')
replaces=('kdegames-kollision')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kollision-${pkgver}.tar.xz")
sha1sums=('4ffedcc3d6267ee77992db252014838392e24b7b')

build() {
  mkdir -p build
  cd build
  cmake ../kollision-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
