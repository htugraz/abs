# $Id: PKGBUILD 208837 2014-03-27 14:43:23Z fyan $
# Maintainer: Angel Velasquez <angvp@archlinux.org> 

pkgbase=python-astroid
pkgname=('python2-astroid' 'python-astroid')
pkgver=1.0.1
pkgrel=3
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="http://www.logilab.org/project/logilab-common"
license=('LGPL')
makedepends=('python2' 'python')
source=(https://pypi.python.org/packages/source/a/astroid/astroid-${pkgver}.tar.gz
        python-3.4.patch)
sha1sums=('4b0080576174cc0a3519a6c6e9025a8ee853956a'
          '8d4f2f76f5b0c90ad508c5fded85d37bbbe9e15a')

prepare() {
  cp -a astroid-${pkgver}{,-py3}

  cd astroid-${pkgver}-py3
  # FS#39585
  patch -p1 -i "$srcdir/python-3.4.patch"
}

build() {
  cd "${srcdir}"

  cd astroid-${pkgver}
  python2 setup.py build

  cd ../astroid-${pkgver}-py3
  python3 setup.py build
}

package_python2-astroid() {
  depends=('python2' 'python2-logilab-common')
  replaces=('python2-logilab-astng')
  conflicts=('python2-logilab-astng')
  cd "${srcdir}"/astroid-${pkgver}

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  # fix permissions ...
  find "${pkgdir}" -type f -exec chmod +r {} \;
}

package_python-astroid() {
  depends=('python' 'python-logilab-common')
  replaces=('python-logilab-astng')
  conflicts=('python-logilab-astng')
  cd "${srcdir}"/astroid-${pkgver}-py3

  python3 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  # fix permissions ...
  find "${pkgdir}" -type f -exec chmod +r {} \;
}
