# $Id: PKGBUILD 237261 2015-04-17 02:03:39Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=libgsf
pkgver=1.14.33
pkgrel=1
arch=(i686 x86_64)
pkgdesc="The GNOME Structured File Library is a utility library for reading and writing structured file formats"
url="http://www.gnome.org/"
license=('GPL' 'LGPL')
depends=('libxml2' 'gdk-pixbuf2' 'bzip2')
makedepends=('intltool' 'gobject-introspection' 'python2')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/1.14/$pkgname-$pkgver.tar.xz)
sha256sums=('82dd38e0c1f497704bf3b43682fca2768886058f004b14e9b5d103596f8c6e6b')

build() {
  cd "$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --enable-introspection
  make
}

package_libgsf() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
