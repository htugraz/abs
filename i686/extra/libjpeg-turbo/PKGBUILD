# $Id: PKGBUILD 226114 2014-11-12 15:32:05Z foutrelis $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libjpeg-turbo
pkgver=1.3.1
pkgrel=2
pkgdesc='JPEG image codec with accelerated baseline compression and decompression'
url='http://libjpeg-turbo.virtualgl.org/'
license=('GPL' 'custom')
arch=('i686' 'x86_64')
makedepends=('nasm')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'libjpeg-turbo-1.3.1-jstdhuff.patch')
sha1sums=('5fa19252e5ca992cfa40446a0210ceff55fbe468'
          'f459890257df430dc4e2d8bd3060173a1be7516e')

provides=('libjpeg=8.0.2' 'turbojpeg')
conflicts=('libjpeg' 'turbojpeg')
replaces=('libjpeg' 'turbojpeg')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	# Backport fix for motion JPEG video frames that are encoded without the
	# default Huffman tables. Fixes HD webcam input in Chromium (FS#42747).
	# http://sourceforge.net/p/libjpeg-turbo/code/1104/
	patch -Np0 -i ../libjpeg-turbo-1.3.1-jstdhuff.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make \
		DESTDIR="${pkgdir}" \
		docdir="/usr/share/doc/${pkgname}" \
		exampledir="/usr/share/doc/${pkgname}" \
		install
	
	install -d "${pkgdir}/usr/share/licenses/libjpeg-turbo"
	ln -t "${pkgdir}/usr/share/licenses/libjpeg-turbo" -s ../../doc/libjpeg-turbo/README{,-turbo.txt}
	install -m644 jpegint.h "${pkgdir}/usr/include" # required by other software
}
