# $Id: PKGBUILD 210371 2014-04-14 18:38:21Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sound-recorder
pkgver=3.12.1
pkgrel=1
pkgdesc="A utility to make simple audio recording from your GNOME desktop"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Design/Apps/SoundRecorder"
license=(GPL2)
depends=(gtk3 gjs gst-plugins-good gst-plugins-base)
makedepends=(intltool gobject-introspection)
install=gnome-sound-recorder.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('12e94ac910b6259ca5e22216b14769167d668e3bc8ff240f57c1d0a2713d045e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
