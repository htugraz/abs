# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kalarmcal
pkgver=15.08.2
pkgrel=1
pkgdesc="The KAlarm client library"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kidentitymanagement kholidays kcalcore akonadi-client)
makedepends=(extra-cmake-modules kdoctools python boost)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('fecc7ecf238225a1e2fa20f357c76653')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
