# $Id: PKGBUILD 196739 2013-10-17 14:02:42Z andyrtr $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=brltty
pkgver=4.5
pkgrel=5
pkgdesc="Braille display driver for Linux/Unix"
arch=(i686 x86_64)
url="http://mielke.cc/brltty"
license=(GPL LGPL)
depends=(libxaw gpm icu tcl cython)
makedepends=(bluez-libs at-spi2-core)
optdepends=('bluez-libs: bluetooth support'
	    'at-spi2-core: X11/GNOME Apps accessibility'
	    'atk: ATK bridge for X11/GNOME accessibility')
backup=(etc/brltty.conf)
options=('!emptydirs')
install=brltty.install
source=(http://mielke.cc/$pkgname/releases/$pkgname-$pkgver.tar.gz
        brltty.service)

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --mandir=/usr/share/man \
    --with-tables-directory=/usr/share/brltty \
    --with-screen-driver=lx \
    --enable-gpm \
    --disable-java-bindings \
    --disable-static

  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 Documents/brltty.conf "$pkgdir/etc/brltty.conf"
  install -Dm644 ../brltty.service "$pkgdir/usr/lib/systemd/system/brltty.service"
}
md5sums=('7b52fa7746fed41ed344a1f75ce55951'
         '0cad54bb5470122535f5e3a11d5ca123')
