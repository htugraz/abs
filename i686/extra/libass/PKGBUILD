# $Id: PKGBUILD 211910 2014-04-29 15:53:17Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>

pkgname=libass
pkgver=0.11.2
pkgrel=1
pkgdesc="A portable library for SSA/ASS subtitles rendering"
arch=('i686' 'x86_64')
url="https://github.com/libass/libass/releases"
license=('BSD')
depends=('fribidi' 'fontconfig' 'enca' 'harfbuzz')
makedepends=('pkg-config' 'yasm')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('701b761934de0eff7d45f58d7d13eaf6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-enca \
              --enable-harfbuzz \
              --enable-fontconfig
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
