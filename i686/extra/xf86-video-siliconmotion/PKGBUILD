# $Id: PKGBUILD 218309 2014-07-29 10:12:33Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xf86-video-siliconmotion
pkgver=1.7.7
pkgrel=5
pkgdesc="X.org siliconmotion video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=18')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<18' 'X-ABI-VIDEODRV_VERSION>=19')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        0001-Remove-miInitializeBackingStore.patch)
sha256sums=('87b8b59d43945d4fc8012860c0bd9aed42c4684a943355c607b8eb8d6710c3aa'
            '257cc6ccf2ed565daaacaf87cb2f1195b4c2f6bb95fefe5e89766f88393e6647')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/0001-Remove-miInitializeBackingStore.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
