# $Id: PKGBUILD 238509 2015-05-05 15:55:33Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=1.0.2
pkgrel=1
pkgdesc="NetworkManager VPN plugin for pptp "
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
_pppver=2.4.7
depends=("networkmanager>=${pkgver}" 'pptpclient' 'gtk3' 'libgnome-keyring' "ppp=$_pppver")
makedepends=('intltool' 'network-manager-applet')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
install=networkmanager-pptp.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-pptp/${pkgver::3}/NetworkManager-pptp-${pkgver}.tar.xz)
sha256sums=('37cb7c6064d1a65ea9b67bb9d31947c2434db3cfb55a2911481fa4e48e332017')

build() {
  cd NetworkManager-pptp-${pkgver}
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/networkmanager \
        --disable-static \
        --enable-more-warnings=yes \
        --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver
  make
}

package() {
  cd NetworkManager-pptp-${pkgver}
  make DESTDIR="${pkgdir}" install
}
