# $Id: PKGBUILD 225014 2014-10-20 08:19:56Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-kscd
pkgver=4.14.2
pkgrel=1
pkgdesc="KDE CD player"
url='http://kde.org/applications/multimedia/kscd/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
depends=('kdebase-runtime' 'libmusicbrainz3')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/kscd-${pkgver}.tar.xz")
sha1sums=('73dec60ec1dfe626254bd2ab4fba472df62ae4f3')

build() {
  mkdir build
  cd build
  cmake ../kscd-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
