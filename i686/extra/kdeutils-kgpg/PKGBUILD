# $Id: PKGBUILD 198976 2013-11-05 20:25:37Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kgpg
pkgver=4.11.3
pkgrel=1
pkgdesc='A GnuPG frontend'
url='http://kde.org/applications/utilities/kgpg/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'kdepimlibs' 'kde-agent')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kgpg-${pkgver}.tar.xz")
sha1sums=('8b2172abb9ee002d1ac65d77363f29ece238aeb4')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kgpg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
