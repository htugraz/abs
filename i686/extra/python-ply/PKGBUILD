# $Id: PKGBUILD 238051 2015-04-27 03:30:56Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marcin "avalan" Falkiewicz <avalatron@gmail.com>
# Contributor: C Anthony Risinger <anthony@xtfx.me>

pkgbase=python-ply
pkgname=(python-ply python2-ply)
pkgver=3.6
pkgrel=1
pkgdesc='Implementation of lex and yacc parsing tools'
arch=('any')
url='http://www.dabeaz.com/ply/'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${url}ply-$pkgver.tar.gz")
sha256sums=('61367b9eb2f4b819f69ea116750305270f1df8859992c9e356d6a851f25a4b47')

prepare() {
  cp -a ${pkgbase#python-}-$pkgver{,-py2}
}

package_python-ply() {
  depends=('python')

  cd "${pkgbase#python-}-$pkgver"
  python setup.py install --root="$pkgdir"
}

package_python2-ply() {
  depends=('python2')

  cd "${pkgbase#python-}-$pkgver-py2"
  python2 setup.py install --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
