# $Id: PKGBUILD 209175 2014-03-29 22:48:23Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.40.2
pkgrel=1
epoch=1
pkgdesc="A SVG viewing library"
arch=(i686 x86_64)
license=('LGPL')
depends=('gdk-pixbuf2' 'pango' 'libcroco')
makedepends=('intltool' 'gtk3' 'gobject-introspection' 'vala')
optdepends=('gtk3: for rsvg-view-3')
options=('!emptydirs')
url="https://live.gnome.org/LibRsvg"
install=librsvg.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('48049b643294636df7de1a4b997414d699666f5dc44776945c218a257d2a291c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --enable-vala
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
