# $Id: PKGBUILD 229829 2015-01-24 10:42:59Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-lskat
pkgver=14.12.1
pkgrel=1
pkgdesc="Lieutenant Skat is a fun and engaging card game for two players"
url="http://kde.org/applications/games/lskat/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/lskat-${pkgver}.tar.xz")
sha1sums=('05f87ecd10a6c28736382b5f5767ca7d19fbf93d')

build() {
  mkdir build
  cd build
  cmake ../lskat-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
