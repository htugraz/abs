# $Id: PKGBUILD 248297 2015-10-03 10:45:34Z fyan $
# Maintainer:

pkgbase=pysmbc
pkgname=('python2-pysmbc' 'python-pysmbc')
pkgver=1.0.15.5
pkgrel=1
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/pysmbc/"
license=('GPL')
makedepends=('python2' 'python' 'smbclient')
#source=(http://pypi.python.org/packages/source/p/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2{,.asc})
#md5sums=('7e1d0b5438bf9c2660a60094d7eaabb3'
#         'SKIP')
# validpgpkeys=('228521116D3438163C2EA5B916BED1016069BE55')  # Tsukasa Hamano
# Key revoked as of 1.0.15.4
source=(http://pypi.python.org/packages/source/p/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2)
md5sums=('58cd8c60ca83ebf502e8d3b393b9127d')

prepare() {
  cp -r ${pkgbase}-${pkgver} ${pkgbase}-${pkgver}-python2
  sed -i 's|/usr/bin/python|/usr/bin/python2|' ${pkgbase}-${pkgver}-python2/smbc/xattr.py
}

build(){
  cd ${pkgbase}-${pkgver}
  python setup.py build
  cd "${srcdir}"/${pkgbase}-${pkgver}-python2
  python2 setup.py build
}

package_python-pysmbc() {
  pkgdesc="Python 3 bindings for libsmbclient"
  depends=('python' 'smbclient')

  cd ${pkgbase}-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pysmbc() {
  pkgdesc="Python 2 bindings for libsmbclient"
  depends=('python2' 'smbclient')

  cd ${pkgbase}-${pkgver}-python2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
