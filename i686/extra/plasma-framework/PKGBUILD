# $Id: PKGBUILD 223385 2014-09-27 07:30:33Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.2.0
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/libs/plasma-framework'
license=('LGPL')
depends=('qt5-quickcontrols' 'kdeclarative' 'kactivities-frameworks')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('0b1eb7dd65b63a32f51ca29365dfd4f7')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
