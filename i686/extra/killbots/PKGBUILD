# $Id: PKGBUILD 244601 2015-08-23 08:54:55Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=killbots
pkgver=15.08.0
pkgrel=1
pkgdesc="A simple game of evading killer robots"
url="http://kde.org/applications/games/killbots/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-killbots')
replaces=('kdegames-killbots')
source=("http://download.kde.org/stable/applications/${pkgver}/src/killbots-${pkgver}.tar.xz")
sha1sums=('47faaec809234564787d851b4228b888e326971d')

build() {
  mkdir build
  cd build
  cmake ../killbots-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
