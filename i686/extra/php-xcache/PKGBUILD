# $Id: PKGBUILD 196293 2013-10-11 06:26:09Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-xcache
pkgver=3.1.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A PHP opcode cacher'
url='http://xcache.lighttpd.net/'
depends=('php')
license=('custom')
source=("http://xcache.lighttpd.net/pub/Releases/${pkgver}/xcache-${pkgver}.tar.bz2"
        'xcache.ini')
backup=('etc/php/conf.d/xcache.ini')
md5sums=('e5816d47d52be200b959bf69a673ff74'
         '8fd9ce537ada9463c4b0c042243158c0')

build() {
	cd $srcdir/xcache-$pkgver
	phpize
	./configure --prefix=/usr
	make
}

# check() {
# 	cd $srcdir/xcache-$pkgver
# 	printf "n\n" | make test
# 	echo
# }

package() {
	cd $srcdir/xcache-$pkgver
	make INSTALL_ROOT=$pkgdir install

	install -Dm644 $srcdir/xcache.ini $pkgdir/etc/php/conf.d/xcache.ini

	install -dm755 $pkgdir/usr/share/php-xcache/admin
	cp -r htdocs/* $pkgdir/usr/share/php-xcache/admin/

	install -Dm644 COPYING $pkgdir/usr/share/licenses/php-xcache/COPYING
}
