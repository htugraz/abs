# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Kristof JOZSA <kristof.jozsa@gmail.com>
pkgname=visualvm
pkgver=1.3.8
_shortver=${pkgver//\./}
pkgrel=1
pkgdesc='Visual tool integrating several commandline JDK tools and lightweight profiling capabilities'
arch=('i686' 'x86_64')
url='http://visualvm.java.net/'
license=('custom:GPL')
depends=('java-environment')
source=("https://java.net/projects/${pkgname}/downloads/download/release${_shortver}/${pkgname}_${_shortver}.zip"
        'visualvm.desktop'
        'icon.png')
sha256sums=('05fa81e756552a42d1150d7c32ab2186f6f736244c95022b341deaefc077d899'
            'e820807e8d78446cf156a3947d97856e24865bb0d8c957e9ce2fed309c737441'
            '452fbd85c968ec7176c5894bc4106b7e25310314d44278d807510675b6a5c864')

package() {
  cd "${srcdir}/${pkgname}_${_shortver}"

  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  cp -R bin platform profiler visualvm "${pkgdir}/usr/share/${pkgname}"

  install -d "${pkgdir}/etc/${pkgname}"
  cp -R etc/* "${pkgdir}/etc/${pkgname}"
  ln -s /etc/${pkgname} "${pkgdir}/usr/share/${pkgname}/etc"
    # 'visualvm' shell script cannot even set his own variable 'visualvm_jdkhome'
  sed -i \
    -e 's|#visualvm_jdkhome="/path/to/jdk"|visualvm_jdkhome="${JAVA_HOME}"|' \
    -e 's|visualvm_default_options="|visualvm_default_options="-J-Dawt.useSystemAAFontSettings=on |' \
      "${pkgdir}/etc/${pkgname}/visualvm.conf"

  rm -rf "${pkgdir}"/usr/share/${pkgname}/profiler/lib/deployed/jdk*/{hpux*,mac,solaris*,windows*,linux-arm*}
  if [ ${CARCH} == 'i686' ]; then
    rm -rf "${pkgdir}"/usr/share/${pkgname}/profiler/lib/deployed/jdk*/linux-amd64 \
           "${pkgdir}"/usr/share/${pkgname}/platform/modules/lib/{x86,amd64}
  else
    rm -rf "${pkgdir}"/usr/share/${pkgname}/profiler/lib/deployed/jdk*/linux \
           "${pkgdir}"/usr/share/${pkgname}/platform/modules/lib/{x86,i386}
  fi

  find "${pkgdir}"/usr/share/${pkgname} \( -name "*.exe" -o -name "*.dll" \) -delete

  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/${pkgname}/bin/visualvm "${pkgdir}/usr/bin/${pkgname}"

  install -Dm644 "${srcdir}/icon.png" "${pkgdir}/usr/share/${pkgname}/icon.png"
  install -Dm644 "${srcdir}/visualvm.desktop" "${pkgdir}/usr/share/applications/visualvm.desktop"

  install -D LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
