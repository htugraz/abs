# $Id: PKGBUILD 198775 2013-11-04 03:53:08Z eric $
# Maintainer: 

pkgname=perl-file-mimeinfo
pkgver=0.21
pkgrel=1
pkgdesc='Determine file type, includes mimeopen and mimetype'
arch=('any')
url="http://search.cpan.org/dist/File-MimeInfo/"
license=('GPL' 'PerlArtistic')
depends=('perl-file-basedir' 'perl-file-desktopentry' 'shared-mime-info')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/MI/MICHIELB/File-MimeInfo-${pkgver}.tar.gz)
sha1sums=('871bd30ab78b3533da7389c6af176e17d7737bb7')

build() {
  cd File-MimeInfo-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-MimeInfo-${pkgver}
  make test
}

package() {
  cd File-MimeInfo-${pkgver}
  make DESTDIR="${pkgdir}" install
}
