# $Id: PKGBUILD 198888 2013-11-05 19:57:11Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-katomic
pkgver=4.11.3
pkgrel=1
pkgdesc="A fun and educational game built around molecular geometry"
url="http://kde.org/applications/games/katomic/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/katomic-${pkgver}.tar.xz")
sha1sums=('9bd793a68f4ebfb468829eb89b951d6a5a36d917')

build() {
  mkdir build
  cd build
  cmake ../katomic-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
