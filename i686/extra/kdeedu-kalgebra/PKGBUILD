# $Id: PKGBUILD 200970 2013-12-06 12:23:04Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kalgebra
pkgver=4.11.4
pkgrel=1
pkgdesc="Graph Calculator"
url="http://kde.org/applications/education/kalgebra/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libkdeedu' 'kdeedu-analitza')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kalgebra-${pkgver}.tar.xz")
sha1sums=('58c3844b7c89ab353a900bbf8ca8541cad89f99b')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kalgebra-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
