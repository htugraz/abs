# $Id: PKGBUILD 208845 2014-03-27 14:43:30Z fyan $
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=python-logilab-common
pkgname=('python2-logilab-common' 'python-logilab-common')
pkgver=0.61.0
pkgrel=2
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="http://www.logilab.org/project/logilab-common"
license=('LGPL')
makedepends=('python2' 'python')
source=(http://download.logilab.org/pub/common/logilab-common-${pkgver}.tar.gz)
sha1sums=('700c87f45133a8a20e1ac815f197cc1a5a408e27')

build() {
  cd "${srcdir}"

  cp -a logilab-common-${pkgver}{,-py3}

  cd logilab-common-${pkgver}
  python2 setup.py build

  cd ../logilab-common-${pkgver}-py3
  python3 setup.py build
}

package_python2-logilab-common() {
  depends=('python2')
  cd "${srcdir}"/logilab-common-${pkgver}

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  mv "${pkgdir}"/usr/bin/pytest{,2}

  # fix permissions ...
  find "${pkgdir}" -type f -exec chmod +r {} \;
}

package_python-logilab-common() {
  depends=('python')
  cd "${srcdir}"/logilab-common-${pkgver}-py3

  python3 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  # fix permissions ...
  find "${pkgdir}" -type f -exec chmod +r {} \;
}
