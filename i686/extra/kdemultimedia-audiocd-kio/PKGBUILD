# $Id: PKGBUILD 214908 2014-06-10 17:55:05Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-audiocd-kio
pkgver=4.13.2
pkgrel=1
pkgdesc="Kioslave for accessing audio CDs"
url='https://projects.kde.org/projects/kde/kdemultimedia/audiocd-kio'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkcddb' 'libkcompactdisc' 'lame' 'cdparanoia')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdemultimedia')
replaces=('kdemultimedia-kioslave')
conflicts=('kdemultimedia-kioslave')
source=("http://download.kde.org/stable/${pkgver}/src/audiocd-kio-${pkgver}.tar.xz")
sha1sums=('491be8cd79847184c7d17c06e5f383d1be96b175')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../audiocd-kio-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
