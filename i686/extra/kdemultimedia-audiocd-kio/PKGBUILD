# $Id: PKGBUILD 198934 2013-11-05 20:09:53Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-audiocd-kio
pkgver=4.11.3
pkgrel=1
pkgdesc="Kioslave for accessing audio CDs"
url='https://projects.kde.org/projects/kde/kdemultimedia/audiocd-kio'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkcddb' 'libkcompactdisc' 'lame' 'cdparanoia')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdemultimedia')
replaces=('kdemultimedia-kioslave')
conflicts=('kdemultimedia-kioslave')
source=("http://download.kde.org/stable/${pkgver}/src/audiocd-kio-${pkgver}.tar.xz")
sha1sums=('9b23a28843b4d23fc1cbb947320a1dd66f2b0018')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../audiocd-kio-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
