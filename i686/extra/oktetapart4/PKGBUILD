# $Id: PKGBUILD 227013 2014-11-24 07:20:51Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=oktetapart4
pkgver=4.14.3
pkgrel=3
pkgdesc='Hex Editor KPart'
url='http://kde.org/applications/utilities/okteta/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
provides=('kdesdk-okteta=$pkgver-$pkgrel')
conflicts=('kdesdk-okteta<$pkgver-$pkgrel')
source=("http://download.kde.org/stable/${pkgver}/src/okteta-${pkgver}.tar.xz")
sha1sums=('479779119ef5dd5cea2b2acf1f134b355dffe74d')

build() {
  mkdir build
  cd build
  cmake ../okteta-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# provided by okteta
  rm -r "$pkgdir"/usr/{bin,share/{appdata,config.kcfg,icons,mime}}
}
