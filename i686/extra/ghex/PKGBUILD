# $Id: PKGBUILD 197985 2013-10-30 12:05:49Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=ghex
pkgver=3.10.0
pkgrel=1
pkgdesc="A simple binary editor for the Gnome desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/Ghex"
depends=('gtk3' 'hicolor-icon-theme')
makedepends=('intltool' 'itstool' 'docbook-xsl')
options=('!emptydirs')
install=ghex.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('9c31383b1d32c8942ca0327c615336f5777309674c4fc090e8cceb1576c63255')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
