# $Id: PKGBUILD 211378 2014-04-18 18:01:57Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-artikulate
pkgver=4.13.0
pkgrel=1
pkgdesc="Improve your pronunciation by listening to native speakers"
url="https://projects.kde.org/projects/kde/kdeedu/artikulate"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'qt-gstreamer' 'kqtquickcharts')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/stable/${pkgver}/src/artikulate-${pkgver}.tar.xz")
sha1sums=('7fa0cd6e9fa453a37196d4e439d0fe74f25e875e')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../artikulate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
