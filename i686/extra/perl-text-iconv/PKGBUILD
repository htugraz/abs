# $Id: PKGBUILD 186418 2013-05-26 09:25:46Z bluewind $
# Maintainer:
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-text-iconv
_realname=Text-Iconv
pkgver=1.7
pkgrel=8
pkgdesc="Interface to iconv codeset conversion"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/${_realname}/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/M/MP/MPIOTR/${_realname}-${pkgver}.tar.gz) 
md5sums=('81b26e069eaebb084e91ea3c009b67ae')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
