# $Id: PKGBUILD 201144 2013-12-06 12:36:20Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-dev-scripts
pkgver=4.11.4
pkgrel=1
pkgdesc='Scripts and setting files useful during development of KDE software'
url='https://projects.kde.org/projects/kde/kdesdk/kde-dev-scripts'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime' 'python2')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/kde-dev-scripts-${pkgver}.tar.xz")
sha1sums=('728bcf4d60fe7038e226fbe4f2ff7e93e7c516cb')

build() {
  mkdir build
  cd build
  cmake ../kde-dev-scripts-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/{zonetab2pot,kde-systemsettings-tree}.py
  sed -i 's|#! /usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/{kdelnk2desktop.py,kde_generate_export_header}
}
