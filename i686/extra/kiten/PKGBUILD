# $Id: PKGBUILD 255376 2015-12-10 04:53:21Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kiten
pkgver=15.08.3
pkgrel=2
pkgdesc="Japanese Reference/Study Tool"
url="http://kde.org/applications/education/kiten/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('khtml')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdeedu-kiten')
replaces=('kdeedu-kiten')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kiten-${pkgver}.tar.xz")
sha1sums=('c121593e593c25f33e15579b6033053feb7c8df9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kiten-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
