# $Id: PKGBUILD 215653 2014-06-30 05:45:00Z fyan $
# Maintainer:
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-eyes-plugin
pkgver=4.4.2
pkgrel=2
pkgdesc="A rolling eyes (following mouse pointer) plugin for the Xfce panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-eyes-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'hicolor-icon-theme')
makedepends=('intltool' 'perlxml')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/4.4/$pkgname-$pkgver.tar.bz2)
sha256sums=('dad102fedd0d9e6df45338a018387b2aacaf24b05eb798275be0dfc05ffa3021')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
