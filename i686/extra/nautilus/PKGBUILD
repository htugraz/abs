# $Id: PKGBUILD 224513 2014-10-16 11:58:18Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=nautilus
pkgname=(nautilus libnautilus-extension)
pkgver=3.14.0
pkgrel=2
pkgdesc="GNOME file manager"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs desktop-file-utils dconf 
         libtracker-sparql libnotify nautilus-sendto)
makedepends=(intltool gobject-introspection python python2 packagekit)
url="http://www.gnome.org"
options=('!emptydirs')
source=(http://download.gnome.org/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz
        git-fixes.patch)
sha256sums=('6f909ed9afa9abffe489b80b57888bd7bc7acea2cf48717c54362bd3b6aee0d4'
            '5b9303d8b2d779d5522c8a16f206233d46dd1fbc17409b8b9b0292be2177b843')

prepare() {
  cd $pkgbase-$pkgver
  patch -Np1 -i ../git-fixes.patch
}

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nautilus \
      --disable-update-mimedb \
      --disable-schemas-compile
  make
}

package_nautilus() {
  depends+=(libnautilus-extension)
  groups=(gnome)
  install=nautilus.install

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

### Split libnautilus-extension
  cd ..
  mkdir -p n-e/usr/{lib,share}
  mv "$pkgdir"/usr/include n-e/usr
  mv "$pkgdir"/usr/lib/{girepository-1.0,pkgconfig} n-e/usr/lib
  mv "$pkgdir"/usr/lib/libnautilus-extension.so* n-e/usr/lib
  mv "$pkgdir"/usr/share/{gir-1.0,gtk-doc} n-e/usr/share
}

package_libnautilus-extension() {
  pkgdesc="Library for extending the $pkgdesc"
  depends=(gtk3)

  mv n-e/* "$pkgdir"
}
