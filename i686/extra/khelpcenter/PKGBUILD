# $Id: PKGBUILD 246992 2015-09-20 08:32:49Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=khelpcenter
pkgver=5.4.1
pkgrel=2
pkgdesc="Application to show KDE Applications' documentation"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/khelpcenter'
license=('LGPL')
groups=('plasma')
depends=('khtml' 'kdelibs4support' 'kcmutils' 'kdoctools')
makedepends=('extra-cmake-modules')
conflicts=('khelpcenter4' 'kdebase-runtime-doc')
replaces=('khelpcenter4' 'kdebase-runtime-doc')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('99df60a8b2cc2b5c0f924a7c36cd9ad3')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
