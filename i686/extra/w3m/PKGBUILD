# $Id: PKGBUILD 251698 2015-11-20 20:35:46Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=w3m
pkgver=0.5.3.git20151119
pkgrel=1
pkgdesc='Text-based Web browser, as well as pager'
url='http://w3m.sourceforge.net/'
license=('custom')
arch=('i686' 'x86_64')
makedepends=('git' 'imlib2')
optdepends=('imlib2: for graphics support') 
depends=('openssl' 'gc' 'ncurses' 'gpm')
source=("git://anonscm.debian.org/collab-maint/w3m.git#commit=9cce622287fb58229998137d016208d327ce1141")
sha1sums=('SKIP')

build() {
	cd "${srcdir}/${pkgname}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-image=x11,fb \
		--with-imagelib=imlib2 \
		--with-termlib=ncurses \
		--disable-w3mmailer \

	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
	find doc/* | grep -v CVS | xargs -i install -m644 "{}" "${pkgdir}/usr/share/doc/${pkgname}"
	ln -s ../../doc/"${pkgname}"/README "${pkgdir}/usr/share/licenses/${pkgname}"
}
