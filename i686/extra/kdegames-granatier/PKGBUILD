# $Id: PKGBUILD 224961 2014-10-20 08:12:06Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-granatier
pkgver=4.14.2
pkgrel=1
pkgdesc="A clone of the classic Bomberman game"
url="http://kde.org/applications/games/granatier/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/granatier-${pkgver}.tar.xz")
sha1sums=('b09d1d6a78becceb4e6ca3d23bf9ba90c1db28d8')

build() {
  mkdir build
  cd build
  cmake ../granatier-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
