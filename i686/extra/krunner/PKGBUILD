# $Id: PKGBUILD 230216 2015-01-28 17:57:34Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=krunner
pkgver=5.6.0
pkgrel=2
pkgdesc='Framework for providing different actions given a string query'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/krunner'
license=('LGPL')
depends=('plasma-framework' 'threadweaver')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('drkonqi: KDE crash handler application')
groups=('kf5-aids')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=('fac2693844895d7e4b0413f08cce3003')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
