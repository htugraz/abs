# $Id: PKGBUILD 216645 2014-07-08 18:07:17Z andyrtr $
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=lpsolve
_origname=lp_solve
pkgver=5.5.2.0
_mainver=5.5
pkgrel=3
pkgdesc="a Mixed Integer Linear Programming (MILP) solver"
arch=('i686' 'x86_64')
url="http://lpsolve.sourceforge.net/"
license=('GPL')
depends=('glibc')
source=(http://downloads.sourceforge.net/project/lpsolve/lpsolve/${pkgver}/lp_solve_${pkgver}_source.tar.gz
        cflags.patch)
md5sums=('167c0fb4ab178e0b7ab50bf0a635a836'
         '7403f745d06619f59f52800b655d8751')

build() {
  cd ${_origname}_${_mainver}
  patch -Np1 -i $srcdir/cflags.patch

  # taken from Fedora spec
  cd lpsolve55
  sh -x ccc
  rm bin/ux*/liblpsolve55.a
  cd ../lp_solve
  sh -x ccc
}

package() {
  cd ${_origname}_${_mainver}

  install -dm755 ${pkgdir}/usr/{bin,lib,include/lpsolve}
  install -m755 lp_solve/bin/ux*/lp_solve ${pkgdir}/usr/bin/
  install -m755 lpsolve55/bin/ux*/liblpsolve55.so ${pkgdir}/usr/lib/
  install -m644 lp*.h ${pkgdir}/usr/include/lpsolve/
}
