# $Id: PKGBUILD 239162 2015-05-12 14:59:25Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kapptemplate
pkgver=15.04.1
pkgrel=1
pkgdesc='KDE Template Generator'
url='http://kde.org/applications/development/kapptemplate/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kio')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdesdk-kapptemplate')
conflicts=('kdesdk-kapptemplate')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kapptemplate-${pkgver}.tar.xz")
sha1sums=('7815ffc16d48b7db6043f3e9df7ae82d1997a6d1')

build() {
  mkdir build
  cd build
  cmake ../kapptemplate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
