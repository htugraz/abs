# $Id: PKGBUILD 227521 2014-12-11 16:07:43Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kunitconversion
pkgver=5.5.0
pkgrel=1
pkgdesc='Support for unit conversion'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kunitconversion'
license=('LGPL')
depends=('ki18n')
makedepends=('extra-cmake-modules' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('3a456a2d653e737faf9afea7425567fa')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
