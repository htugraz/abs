# $Id: PKGBUILD 206148 2014-02-20 08:59:56Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=mutter-wayland
pkgver=3.10.4
pkgrel=1
pkgdesc="A Wayland compositor for GNOME"
arch=(i686 x86_64)
license=(GPL)
depends=(mutter)
makedepends=(intltool gnome-doc-utils)
url="http://www.gnome.org"
options=('!emptydirs')
install=mutter-wayland.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        mutter-wayland-3.10.0.1-logfile.patch
        0001-monitor-expose-min-backlight-step.patch)
sha256sums=('2dd625a0e0429a4852ec52ca9a40890d57537d947cc6269a9d85c52987d87828'
            'b79866b6b3fa6029b35ef2b5c5d0cb92886ec2ed0173f780c4d46a5b8d9a1644'
            'f1ef173d96ac27abdcf765972d2ac434f63bf4629b8bd51c177e8963d07f5eb7')


prepare() {
  cd "$pkgname-$pkgver"

  # From Fedora: Don't try to redirect the logfile. It won't work.
  patch -Np1 -i ../mutter-wayland-3.10.0.1-logfile.patch

  # FS#37224
  patch -Np1 -i ../0001-monitor-expose-min-backlight-step.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/$pkgname \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile --enable-compile-warnings=minimum

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Already in "mutter"
  rm -r "$pkgdir"/usr/share/{man,gtk-doc,GConf,gnome-control-center}
  rm "$pkgdir/usr/share/glib-2.0/schemas/org.gnome.mutter.gschema.xml"
}
