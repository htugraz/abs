# $Id: PKGBUILD 210431 2014-04-14 20:06:07Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=evolution-ews
pkgver=3.12.1
pkgrel=1
pkgdesc="MS Exchange integration through Exchange Web Services"
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="https://wiki.gnome.org/Apps/Evolution/EWS"
depends=('evolution-data-server' 'gtkhtml4' 'libmspack')
makedepends=('intltool' 'evolution')
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('8558cc504e1c7f86e15b369f66acbcd9a041c213d22435493d6365ecbeefd1c7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/evolution --disable-static --with-krb5
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
