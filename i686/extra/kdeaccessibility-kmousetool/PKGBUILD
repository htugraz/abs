# $Id: PKGBUILD 214827 2014-06-10 17:52:49Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kmousetool
pkgver=4.13.2
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Clicks the mouse for you, reducing the effects of RSI'
url='http://kde.org/applications/utilities/kmousetool/'
license=('GPL' 'FDL')
groups=('kde' 'kdeaccessibility')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kmousetool-${pkgver}.tar.xz")
sha1sums=('045ad6f42f3904e9f5c601e8ff11e764ac560948')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kmousetool-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
