# $Id: PKGBUILD 255072 2015-12-10 04:42:48Z foutrelis $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity
pkgver=2.1.1
pkgrel=2
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacityteam.org"
license=('GPL')
depends=('libmad' 'libid3tag' 'wxgtk2.8' 'lame' 'lilv' 'soundtouch'
	 'ffmpeg' 'vamp-plugin-sdk' 'sbsms' 'portsmf' 'desktop-file-utils')
makedepends=('cmake' 'python2')
options=('!makeflags')
install=audacity.install
source=(https://github.com/audacity/audacity/archive/Audacity-${pkgver}.zip
        audacity-ffmpeg.patch)
sha1sums=('3ff019e02a8ef9d712166dd1b466c494102a859a'
          '5f1733a3802bcec7d9b54cb3ec8d7d81fc38fc61')

prepare() {
  cd audacity-Audacity-${pkgver}
  patch -p1 -i "${srcdir}/audacity-ffmpeg.patch"
}

build() {
  cd audacity-Audacity-${pkgver}
  WX_CONFIG=/usr/bin/wx-config-2.8 ./configure --prefix=/usr --with-libsamplerate
  make
}

package() {
  cd audacity-Audacity-${pkgver}
  make DESTDIR="${pkgdir}" install
}
