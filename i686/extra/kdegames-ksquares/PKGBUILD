# $Id: PKGBUILD 214890 2014-06-10 17:54:35Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksquares
pkgver=4.13.2
pkgrel=1
pkgdesc="A modeled after the well known pen and paper based game of Dots and Boxes"
url="http://kde.org/applications/games/ksquares/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/ksquares-${pkgver}.tar.xz")
sha1sums=('ee15bf56a82b464d2ed9e0545d51fc6f214fa96c')

build() {
  mkdir build
  cd build
  cmake ../ksquares-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
