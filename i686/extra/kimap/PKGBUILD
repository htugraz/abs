# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kimap
pkgver=15.08.0
pkgrel=2
pkgdesc="Job-based API for interacting with IMAP servers"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kmime kdelibs4support)
makedepends=(extra-cmake-modules python boost kdoctools)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz" fix-date-format.patch)
md5sums=('22c14eb3038281492cc51f38c4123cb8'
         'e30cd849643dcc9f67326ec192dcfe4e')

prepare() {
  mkdir -p build

# Fix sent messages not being synced to the server https://bugs.kde.org/show_bug.cgi?id=352223
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-date-format.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
