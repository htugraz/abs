# $Id: PKGBUILD 209380 2014-03-31 10:09:54Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkexiv2
pkgver=4.12.4
pkgrel=1
pkgdesc="A library to manipulate pictures metadata"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkexiv2"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('25809f70e001f4a59e5c66223c2097b098599ed6')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
