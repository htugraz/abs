# $Id: PKGBUILD 196933 2013-10-21 07:46:00Z tpowa $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=iniparser
pkgver=3.1
pkgrel=4
pkgdesc="A free stand-alone ini file parsing library written in portable ANSI C"
arch=('i686' 'x86_64')
url="http://ndevilla.free.fr/iniparser/"
license=('MIT')
depends=('glibc')
makedepends=('chrpath')
source=(http://ndevilla.free.fr/iniparser/$pkgname-$pkgver.tar.gz)
md5sums=('0aa4f995468ed390caf323f50a285bc3')

build() {
  cd $srcdir/$pkgname
  make
}

package() {
  cd $srcdir/$pkgname
  install -Dm644 src/iniparser.h $pkgdir/usr/include/iniparser.h
  install -Dm644 src/dictionary.h $pkgdir/usr/include/dictionary.h
  install -Dm644 libiniparser.a $pkgdir/usr/lib/libiniparser.a
  install -Dm644 libiniparser.so.0 $pkgdir/usr/lib/libiniparser.so.0
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/iniparser/LICENSE
  ln -sf libiniparser.so.0 $pkgdir/usr/lib/libiniparser.so
  chrpath -d $pkgdir/usr/lib/libiniparser.so
}
