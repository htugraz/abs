# $Id: PKGBUILD 192099 2013-08-05 20:52:00Z eric $
# Maintainer:

pkgname=perl-dbd-sqlite
pkgver=1.40
pkgrel=1
pkgdesc="Self-contained RDBMS in a DBI driver"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/DBD-SQLite"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi' 'sqlite')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/I/IS/ISHIGAKI/DBD-SQLite-${pkgver}.tar.gz)
md5sums=('b9876882186499583428b14cf5c0e29c')

build() {
  cd DBD-SQLite-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd DBD-SQLite-${pkgver}
  make test
}

package() {
  cd DBD-SQLite-${pkgver}
  make install DESTDIR="${pkgdir}"
}
