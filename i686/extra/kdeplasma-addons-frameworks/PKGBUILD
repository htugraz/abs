# $Id: PKGBUILD 234659 2015-03-24 20:10:46Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons-frameworks
pkgver=5.2.2
pkgrel=1
pkgdesc='All kind of addons to improve your Plasma experience'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kdeplasma-addons'
license=('LGPL')
depends=('krunner' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools' 'libibus' 'scim')
optdepends=('ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support')
install="${pkgname}.install"
conflicts=('kdebase-workspace')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/kdeplasma-addons-$pkgver.tar.xz")
md5sums=('d172b2438d751f7b39ddffb47aacbfc3')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kdeplasma-addons-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
