# $Id: PKGBUILD 226046 2014-11-11 08:27:12Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons-frameworks
pkgver=5.1.1
pkgrel=1
pkgdesc='All kind of addons to improve your Plasma experience'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kdeplasma-addons'
license=('LGPL')
depends=('krunner' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kdebase-workspace')
groups=('plasma-next')
source=("http://download.kde.org/stable/plasma/${pkgver}/kdeplasma-addons-$pkgver.tar.xz")
md5sums=('52db706c16a87d4b5100203815ec39af')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdeplasma-addons-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
