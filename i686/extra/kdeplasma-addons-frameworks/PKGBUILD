# $Id: PKGBUILD 230095 2015-01-27 12:21:03Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons-frameworks
pkgver=5.2.0
pkgrel=1
pkgdesc='All kind of addons to improve your Plasma experience'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kdeplasma-addons'
license=('LGPL')
depends=('krunner' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools' 'libibus' 'scim')
optdepends=('ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support')
install="${pkgname}.install"
conflicts=('kdebase-workspace')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/kdeplasma-addons-$pkgver.tar.xz")
md5sums=('ffc167621958ea47162c2a4d5359f381')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kdeplasma-addons-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
