# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=antlr4
pkgver=4.3
pkgrel=1
pkgdesc='Parser generator for reading, processing, executing, or translating structured text or binary files'
arch=('any')
url='http://www.antlr.org/index.html'
license=('BSD')
depends=('java-environment>=6')
makedepends=('maven' 'openjdk7-src' 'unzip')
provides=("antlr=${pkgver}")
source=(${pkgname}-${pkgver}.zip::https://github.com/${pkgname:0:-1}/${pkgname}/archive/${pkgver}.zip
        bin_antlr4
        bin_grun)
sha256sums=('a41afe391f9512ad44173def8751b276b9c115e7f0e93e7a0381f3c5dfd972d5'
            '345697e77b7191e2afe70b223f205a8a7fd18a655e6865f503a764583d9a3dcc'
            '119c85b2b6fd02538ae9a7fd53d5eab874ea56d8acc244ce174e174de21c333a')

prepare() {
  unzip -o -q /usr/lib/jvm/java-7-openjdk/src.zip -d "${srcdir}"/jdk_src
  export M2_REPO="${srcdir}"/m2_repo
  mkdir -p "${M2_REPO}"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/tool"
  mvn -Dmaven.repo.local="${M2_REPO}" \
      -DJDK_SOURCE_ROOT="${srcdir}"/jdk_src \
      -Dbootclasspath.compile=${JAVA_HOME}/jre/lib/rt.jar \
      -Duser.name='Arch Linux' \
      -Psonatype-oss-release \
      -Dmaven.javadoc.skip=true \
      -Dmaven.test.skip=true \
      clean package
}

# https://github.com/antlr/antlr4/issues/563
#check() {
#  cd "${srcdir}/${pkgname}-${pkgver}/tool"
#  mvn -Dmaven.repo.local="${M2_REPO}" \
#      -DJDK_SOURCE_ROOT="${srcdir}"/jdk_src \
#      -Dbootclasspath.testCompile=${JAVA_HOME}/jre/lib/rt.jar \
#      test
#}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -D tool/target/${pkgname}-${pkgver}-complete.jar \
             "${pkgdir}"/usr/share/java/${pkgname:0:-1}-${pkgver}-complete.jar
  ln -s antlr-${pkgver}-complete.jar "${pkgdir}"/usr/share/java/antlr-complete.jar
  install -D "${srcdir}"/bin_antlr4 "${pkgdir}"/usr/bin/antlr4
  install -D "${srcdir}"/bin_grun   "${pkgdir}"/usr/bin/grun
  install -D LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
