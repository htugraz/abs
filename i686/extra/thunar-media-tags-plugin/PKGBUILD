# $Id: PKGBUILD 198459 2013-10-30 15:00:06Z allan $
# Maintainer:
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar-media-tags-plugin
pkgver=0.2.1
pkgrel=1
pkgdesc="Adds special features for media files to the Thunar File Manager"
arch=('i686' 'x86_64')
license=('GPL')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-media-tags-plugin"
groups=('xfce4-goodies')
depends=('taglib' 'thunar')
makedepends=('intltool')
source=(http://archive.xfce.org/src/thunar-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('056d012a10403ee3e2e55b6ff2faecb796821de9ebee000315589b95d95ed253')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
