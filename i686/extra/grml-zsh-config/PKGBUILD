# $Id: PKGBUILD 241128 2015-06-20 05:36:16Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=grml-zsh-config
pkgver=0.12.1
pkgrel=1
pkgdesc="grml's zsh setup"
arch=('any')
url='http://grml.org/zsh/'
license=('GPL2')
provides=('grmlzshrc')
depends=('zsh' 'coreutils' 'inetutils' 'grep' 'sed' 'procps')
makedepends=('txt2tags')
source=("http://deb.grml.org/pool/main/g/grml-etc-core/grml-etc-core_${pkgver}.tar.gz")
sha256sums=('fb202e9e5d7b2c9f48f7bcc785c625104b6f20e0a5ac2f54bfa5b98d9a593ac8')

build() {
	cd ${srcdir}/grml-etc-core-${pkgver}/doc
	make
}

package() {
	cd ${srcdir}/grml-etc-core-${pkgver}
	install -D -m644 etc/skel/.zshrc ${pkgdir}/etc/skel/.zshrc
	install -D -m644 etc/zsh/keephack ${pkgdir}/etc/zsh/keephack
	install -D -m644 etc/zsh/zshrc ${pkgdir}/etc/zsh/zshrc

	install -D -m644 doc/grmlzshrc.5 ${pkgdir}/usr/share/man/man5/grmlzshrc.5
	ln -sf grmlzshrc.5.gz ${pkgdir}/usr/share/man/man5/grml-zsh-config.5.gz
}
