# $Id: PKGBUILD 250808 2015-11-12 10:55:39Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=vino
pkgver=3.18.1
pkgrel=1
pkgdesc="a VNC server for the GNOME desktop"
arch=(i686 x86_64)
license=(GPL)
depends=(libnotify libxtst libsm telepathy-glib gtk3 libsecret avahi desktop-file-utils gnutls)
makedepends=(intltool networkmanager)
groups=(gnome)
url="http://www.gnome.org"
options=(!emptydirs)
install=vino.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('07ec6e78bbecd4ee3fce873eb26932fdda9c7642bb09d17ac36483b996fafe5a')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vino \
      --localstatedir=/var \
      --disable-http-server --with-secret

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
