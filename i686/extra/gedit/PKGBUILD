# $Id: PKGBUILD 202235 2013-12-20 16:53:55Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=3.10.3
pkgrel=1
pkgdesc="A text editor for GNOME"
arch=(i686 x86_64)
license=(GPL)
depends=(gtksourceview3 gsettings-desktop-schemas libpeas enchant iso-codes libsm desktop-file-utils python-gobject dconf)
makedepends=(yelp-tools intltool zeitgeist)
optdepends=('zeitgeist: Zeitgeist plugin')
groups=(gnome-extra)
options=('!emptydirs')
url="http://www.gnome.org"
install=gedit.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('d0efec59622061389fcc06d23b88e2621d4d66b4bafe7d4461c1c7d48a54bc82')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
      --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib --disable-updater --disable-schemas-compile \
      --enable-python
  make
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
