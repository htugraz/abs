# $Id: PKGBUILD 241536 2015-06-30 20:30:08Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=step
pkgver=15.04.3
pkgrel=1
pkgdesc="Interactive Physical Simulator"
url="http://kde.org/applications/education/step/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('libqalculate' 'gsl' 'knewstuff' 'khtml' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'eigen' 'mesa')
install=${pkgname}.install
conflicts=('kdeedu-step')
replaces=('kdeedu-step')
source=("http://download.kde.org/stable/applications/${pkgver}/src/step-${pkgver}.tar.xz")
sha1sums=('9bed74f17aef234e576460bb0851e88636fcba72')

build() {
  mkdir build
  cd build
  cmake ../step-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
