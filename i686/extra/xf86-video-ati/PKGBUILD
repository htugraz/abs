# $Id: PKGBUILD 216170 2014-07-01 18:18:25Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xf86-video-ati
pkgver=7.4.0
epoch=1
pkgrel=2
pkgdesc="X.org ati video driver"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libdrm>=2.4.41' 'systemd' 'ati-dri' 'glamor-egl')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=15')
conflicts=('xorg-server<1.15.0' 'X-ABI-VIDEODRV_VERSION<15' 'X-ABI-VIDEODRV_VERSION>=16')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	0001-Handle-CRTC-DPMS-from-output-DPMS-hooks.patch)
sha256sums=('96230ca9859a2a3e347b7f37597b7b344e80b83f378d3a8463ab1ceb69df76d9'
            '50b3ff79874c16af449fca40e2c2b9fe3d0341216d47594ff616515174f1e84b')

prepare() {
  cd ${pkgname}-${pkgver}

  # merged upstream
  patch -Np1 -i ../0001-Handle-CRTC-DPMS-from-output-DPMS-hooks.patch
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --enable-glamor
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
