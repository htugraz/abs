# $Id: PKGBUILD 225729 2014-11-07 13:49:53Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.12.1
pkgrel=2
pkgdesc="Disk Management Utility for GNOME"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
groups=(gnome)
depends=(udisks2 gtk3 libsecret libpwquality libcanberra libdvdread libnotify desktop-file-utils parted)
makedepends=(intltool docbook-xsl gnome-settings-daemon)
options=('!emptydirs')
install=gnome-disk-utility.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('5994bfae57063d74be45736050cf166cda5b1600a599703240b641b39375718e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
