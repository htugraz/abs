# $Id: PKGBUILD 227533 2014-12-11 16:09:52Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kimageformats
pkgver=5.5.0
pkgrel=1
pkgdesc='Image format plugins for Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kimageformats'
license=('LGPL')
depends=('qt5-base' 'jasper' 'openexr')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('141a157144456a338962d4e61e9b0ca5')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
