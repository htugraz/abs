# $Id: PKGBUILD 249845 2015-10-28 22:49:11Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-control-center
pkgver=3.18.1
pkgrel=3
pkgdesc="The Control Center for GNOME"
license=(GPL)
arch=(i686 x86_64)
depends=(accountsservice cups-pk-helper gnome-bluetooth gnome-desktop
         gnome-online-accounts gnome-settings-daemon gsettings-desktop-schemas gtk3
         libgtop libnm-gtk sound-theme-freedesktop upower libpwquality
         gnome-color-manager smbclient libmm-glib libgnomekbd grilo clutter-gtk libibus
         cheese libgudev)
makedepends=(intltool docbook-xsl modemmanager gnome-common)
optdepends=('system-config-printer: Printer settings'
            'gnome-user-share: Bluetooth and WebDAV file sharing'
            'rygel: media sharing'
            'vino: screen sharing'
            'openssh: remote login')
groups=(gnome)
url="http://www.gnome.org"
install=gnome-control-center.install
options=('!emptydirs')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        sharing.patch 0001-Add-systemd-user-services.patch)
sha256sums=('9a59ec1fe03d873a75a669d933f6e37d8243252ff41850da2a9009e99b578b41'
            'edfe12d2c08a4fcc3921c98f62faf813de732c267b2c28fe48bc1c83c3e3dbf6'
            '4170e89fb07fa6bee86f7bbac6e8bcabfe148b08c8f1a65998a8bb7f2241a601')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../sharing.patch
  patch -Np1 -i ../0001-Add-systemd-user-services.patch
  ACLOCAL_FLAGS="-I libgd" NOCONFIGURE=1 srcdir=. gnome-autogen.sh
}


build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static

  #https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}
