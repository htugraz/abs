# $Id: PKGBUILD 227109 2014-11-26 01:59:43Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=flac
pkgname=('flac' 'flac-doc')
pkgver=1.3.0
pkgrel=5
arch=('i686' 'x86_64')
url="http://flac.sourceforge.net/"
license=('BSD' 'GPL')
depends=('gcc-libs' 'libogg')
makedepends=('nasm' 'doxygen')
options=('!makeflags')
source=(http://downloads.xiph.org/releases/flac/${pkgbase}-${pkgver}.tar.xz
        flac-realloc.patch flac-divby0.patch flac-CVE-2014-8962.patch flac-CVE-2014-9028.patch)
sha1sums=('a136e5748f8fb1e6c524c75000a765fc63bb7b1b'
          '22bf08219c289d747f399e351c701d0f195d3ce0'
          'ceecf0603895267eda112c1cf85e3096e3d3acde'
          'f78547bc7a085a47273ad85c3ec86a2960739944'
          '18f7a66fc0faf857e3c82b462619acf5b5a19af4')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -p1 -i "${srcdir}/flac-realloc.patch"
  patch -p1 -i "${srcdir}/flac-divby0.patch"
  patch -p1 -i "${srcdir}/flac-CVE-2014-8962.patch"
  patch -p1 -i "${srcdir}/flac-CVE-2014-9028.patch"
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --disable-sse
  make
}

check() {
  cd ${pkgbase}-${pkgver}
  make check
}

package_flac() {
  pkgdesc="Free Lossless Audio Codec"
  options=('!docs')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_flac-doc() {
  pkgdesc="Developer documentation for the Free Lossless Audio Codec"
  depends=('flac')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" -C doc install
  sed -i "s|$srcdir/${pkgbase}-${pkgver}|/usr|" "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}/FLAC.tag"
  install -D -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
