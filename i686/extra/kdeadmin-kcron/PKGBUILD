# $Id: PKGBUILD 230950 2015-02-07 09:37:35Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-kcron
pkgver=14.12.2
pkgrel=1
pkgdesc='Configure and schedule tasks'
url='http://userbase.kde.org/KCron'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kcron-${pkgver}.tar.xz")
sha1sums=('1c4218a117717dd66de4158e5dbe4395204b2b3b')

build() {
  mkdir build
  cd build
  cmake ../kcron-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
