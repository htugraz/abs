# $Id: PKGBUILD 241191 2015-06-22 21:17:32Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=libgudev
pkgver=230
pkgrel=1
pkgdesc="GObject bindings for libudev"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/libgudev"
license=('LGPL2.1')
depends=('libsystemd')
makedepends=('systemd' 'gobject-introspection' 'gtk-doc')
provides=('libgudev-1.0.so')
conflicts=('libsystemd<221')
source=(https://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('a2e77faced0c66d7498403adefcc0707105e03db71a2b2abd620025b86347c18')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-gtk-doc
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
