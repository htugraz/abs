# $Id: PKGBUILD 211397 2014-04-18 18:02:13Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-rocs
pkgver=4.13.0
pkgrel=1
pkgdesc="Rocs Graph Theory"
url="http://kde.org/applications/education/rocs/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/rocs-${pkgver}.tar.xz")
sha1sums=('ac2233d9012715a66cd357de5d555d04ead9bc1e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../rocs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
