# $Id: PKGBUILD 242596 2015-07-28 18:14:14Z schiv $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: see .contrib

pkgname=hydrogen
pkgver=0.9.6.1
pkgrel=2
pkgdesc="An advanced drum machine"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.hydrogen-music.org/"
depends=('libarchive' 'liblrdf' 'qt4' 'jack' 'libpulse')
optdepends=('rubberband')
makedepends=('cmake')
source=("https://github.com/hydrogen-music/hydrogen/archive/$pkgver.tar.gz")
install=$pkgname.install
md5sums=('527a44eec22fd31f55e6b3cf1089613d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # lash will have to wait until I bring it into [extra] --schiv
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DWANT_CPPUNIT=OFF \
          -DWANT_LRDF=ON
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # TODO: tell upstream to comply with freedesktop.org icon conventions
  sed -i 's:Icon=h2-icon:Icon=/usr/share/hydrogen/data/img/gray/h2-icon.svg:' \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
