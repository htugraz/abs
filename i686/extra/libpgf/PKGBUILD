# $Id: PKGBUILD 226229 2014-11-14 11:36:48Z ronald $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Tai Chi Minh Ralph Eastwood <tcmreastwood@gmail.com>

pkgname=libpgf
pkgver=6.14.12
pkgrel=2
pkgdesc="An implementation of the Progressive Graphics File (PGF)"
arch=('i686' 'x86_64')
url="http://www.libpgf.org"
depends=('gcc-libs')
makedepends=('dos2unix')
license=('GPL')
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}-latest/${pkgname}-src-${pkgver}.tar.gz")
sha1sums=('bba68ebc8a4f275e56a7615cd1e6a252c59ca613')

build() {
  cd "${srcdir}/${pkgname}"
  dos2unix configure.ac
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR=${pkgdir} install
}
