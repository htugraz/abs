# $Id: PKGBUILD 249286 2015-10-14 03:22:48Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-frameworks
pkgver=5.15.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Core components for the KDE's Activities"
url='https://projects.kde.org/projects/frameworks/kactivities'
license=('LGPL')
depends=('kcmutils')
makedepends=('extra-cmake-modules' 'boost' 'python')
provides=('kactivities')
conflicts=('kactivities4' 'libkactivities-frameworks')
replaces=('libkactivities-frameworks')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/kactivities-${pkgver}.tar.xz")
md5sums=('1c4bf1f81a246a8227afbd184375c8bc')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
