# $Id: PKGBUILD 221709 2014-09-14 13:26:03Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-frameworks
pkgver=5.2.0
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="The KActivity Manager Daemon"
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('LGPL')
depends=('kcmutils' 'kdeclarative')
makedepends=('extra-cmake-modules' 'boost' 'python')
provides=('kactivities')
conflicts=('kactivities4' 'libkactivities-frameworks')
replaces=('libkactivities-frameworks')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/kactivities-${pkgver}.tar.xz")
md5sums=('78264630f265bfb46d99a07a8b6d4983')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DLOCALE_INSTALL_DIR=/usr/share/locale/kf5 \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
