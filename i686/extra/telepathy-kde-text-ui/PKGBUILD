# $Id: PKGBUILD 197622 2013-10-27 14:20:52Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-text-ui
_pkgname=ktp-text-ui
pkgver=0.7.0
pkgrel=1
pkgdesc="Telepathy handler for Text Chats"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-contact-list' 'telepathy-logger-qt')
makedepends=('cmake' 'automoc4' 'boost')
groups=('kde-telepathy')
source=("http://download.kde.org/stable/kde-telepathy/${pkgver}/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('f940e7ae031d5ad897906efad3b8f37e3197bd95')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
