# $Id: PKGBUILD 255619 2015-12-10 05:05:53Z foutrelis $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=0.4.0
pkgrel=2
pkgdesc="Qt GUI for FluidSynth"
arch=('i686' 'x86_64')
url="http://qsynth.sourceforge.net/"
license=('GPL')
depends=('fluidsynth' 'qt5-x11extras' 'hicolor-icon-theme')
makedepends=('qt5-tools')
install=$pkgname.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('47f9a2784082372689db9bf220afd63f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export ac_qmake=/usr/bin/qmake-qt5 \
         ac_moc=/usr/bin/moc-qt5 \
         ac_uic=/usr/bin/uic-qt5 \
         ac_lupdate=/usr/bin/lupdate-qt5 \
         ac_lrelease=/usr/bin/lrelease-qt5 \
         QMAKE=qmake-qt5

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
