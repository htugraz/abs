# $Id: PKGBUILD 246892 2015-09-20 08:21:10Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kjumpingcube
pkgver=15.08.1
pkgrel=1
pkgdesc="A simple tactical game"
url="http://kde.org/applications/games/kjumpingcube/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kjumpingcube')
replaces=('kdegames-kjumpingcube')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kjumpingcube-${pkgver}.tar.xz")
sha1sums=('ae824b775a1741704d4acd560efe2e07d270df3a')

build() {
  mkdir -p build
  cd build
  cmake ../kjumpingcube-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
