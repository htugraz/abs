# $Id: PKGBUILD 209842 2014-04-05 18:40:31Z eric $
# Maintainer : Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sylvain HENRY <hsyl20@yahoo.fr>
# Contributor: Hervé YVIQUEL <elldekaa@gmail.com>

pkgname=hwloc
pkgver=1.9
pkgrel=1
pkgdesc="Portable Hardware Locality is a portable abstraction of hierarchical architectures"
arch=('i686' 'x86_64')
url="http://www.open-mpi.org/projects/hwloc/"
license=('BSD')
depends=('sh' 'pciutils' 'cairo' 'libxml2')
options=('!docs')
source=(http://www.open-mpi.org/software/hwloc/v${pkgver}/downloads/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('99646446502e0f9952170bf1082be63361d99b6d')

build() {
   cd ${pkgname}-${pkgver}
   ./configure --prefix=/usr
   make
}

check() {
   cd ${pkgname}-${pkgver}
   make check
}

package() {
   cd ${pkgname}-${pkgver}
   make DESTDIR="${pkgdir}" install

   install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
