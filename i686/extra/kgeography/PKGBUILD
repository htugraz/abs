# $Id: PKGBUILD 255351 2015-12-10 04:52:41Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kgeography
pkgver=15.08.3
pkgrel=2
pkgdesc="Geography Trainer"
url="http://kde.org/applications/education/kgeography/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kxmlgui')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
replaces=('kdeedu-kgeography')
conflicts=('kdeedu-kgeography')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kgeography-${pkgver}.tar.xz")
sha1sums=('211227acbd77ea08dabbf4f9c8fb221f9a2cd73f')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kgeography-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
