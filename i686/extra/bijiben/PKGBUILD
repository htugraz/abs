# $Id: PKGBUILD 249153 2015-10-13 08:07:32Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=bijiben
pkgver=3.18.1
pkgrel=1
pkgdesc="Write out notes, every detail matters"
arch=(i686 x86_64)
url="https://live.gnome.org/Bijiben"
license=(GPL)
depends=(tracker zeitgeist webkitgtk desktop-file-utils gnome-online-accounts evolution-data-server)
makedepends=(intltool itstool evolution)
options=(!emptydirs)
groups=(gnome-extra)
install=bijiben.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('07bb0872cb78a02c967bafa1cc4f46e84c7d0c2a8b8b4fa1d1f579b897b366ee')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-update-mimedb
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="$pkgdir" install
}
