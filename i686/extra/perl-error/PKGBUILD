# $Id: PKGBUILD 231354 2015-02-13 03:28:05Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-error
pkgver=0.17023
pkgrel=1
pkgdesc="Perl/CPAN Error module - Error/exception handling in an OO-ish way"
url="http://search.cpan.org/dist/Error/"
arch=('any')
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/SH/SHLOMIF/Error-${pkgver}.tar.gz)
sha1sums=('fd3772bd83067fefdc858e7c2bd607e0f94f952b')

build() {
  cd Error-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make 
}

check() {
  cd Error-${pkgver}
  make test
}

package() {
  cd Error-${pkgver}
  make DESTDIR="${pkgdir}" install
}

