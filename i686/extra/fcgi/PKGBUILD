# $Id: PKGBUILD 197174 2013-10-23 13:52:02Z pierre $
# Maintainer: tobias <tobias@justdreams.de>
# Modified by: eliott <eliott@cactuswax.net>
# Original by Benjamin Andresen, klapmuetz [at] gmail [dot] com

pkgname=fcgi
pkgver=2.4.0
pkgrel=9
depends=('gcc-libs')
pkgdesc="FASTCgi (fcgi) is a language independent, high performant extension to CGI"
arch=('i686' 'x86_64')
license=('custom')
options=('!makeflags')
url="http://www.fastcgi.com"
source=("http://www.fastcgi.com/dist/${pkgname}-${pkgver}.tar.gz"
        'gcc44-fix-include.patch')
md5sums=('d15060a813b91383a9f3c66faf84867e'
         'a8028462163755f3ce457a5c641f237b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/gcc44-fix-include.patch"
  libtoolize --force
  aclocal
  autoconf
  automake --foreign
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE.TERMS "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
