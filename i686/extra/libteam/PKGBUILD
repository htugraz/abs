# $Id: PKGBUILD 242967 2015-08-03 14:13:44Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libteam
pkgver=1.17
pkgrel=1
pkgdesc="Library for controlling team network device"
arch=(i686 x86_64)
url="http://libteam.org/"
license=(LGPL2.1)
depends=(libnl libdaemon jansson libdbus bash zeromq)
makedepends=(python python2 swig)
source=($url/files/$pkgname-$pkgver.tar.gz)
sha256sums=('7f83bb6bccecd79b0f5b803f4cd9d8e0029e39d55d11e253647956fcf2404763')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libexecdir=/usr/lib
  make

  cp -aT binding/python binding/python2

  cd binding/python
  python  ./setup.py build
  cd ../python2
  python2 ./setup.py build
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 teamd/redhat/systemd/teamd@.service \
    "$pkgdir/usr/lib/systemd/system/teamd@.service"
  install -Dm644 teamd/dbus/teamd.conf \
    "$pkgdir/etc/dbus-1/system.d/teamd.conf"

  cd binding/python
  python  ./setup.py install --root "$pkgdir" -O1
  cd ../python2
  python2 ./setup.py install --root "$pkgdir" -O1
}
