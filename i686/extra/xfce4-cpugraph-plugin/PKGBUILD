# $Id: PKGBUILD 198549 2013-10-30 15:32:39Z allan $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-cpugraph-plugin
pkgver=1.0.5
pkgrel=1
pkgdesc="CPU graph plugin for the Xfce4 panel"
arch=('i686' 'x86_64')
license=('GPL' 'custom')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-cpugraph-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/1.0/$pkgname-$pkgver.tar.bz2)
sha256sums=('85da0ec89aacfd31e0bbafcefea37cdca618d62e681c1c9da8bdd492f028f4c7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
