# $Id: PKGBUILD 238778 2015-05-09 23:22:20Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=liferea
pkgver=1.10.15
pkgrel=1
pkgdesc="A desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('webkitgtk' 'libnotify' 'json-glib' 'libpeas' 'python2-gobject' 
         'gnome-icon-theme' 'desktop-file-utils')
makedepends=('intltool' 'gobject-introspection')
optdepends=('gnome-keyring: for keyring plugin support'
            'libgnome-keyring: for keyring plugin support'
	    'gst-plugins-base: for media player plugin support'
	    'gst-plugins-good: for media player plugin support'
	    'gst-plugins-bad: for media player plugin support')
options=('!emptydirs')
install=liferea.install
source=(https://github.com/lwindolf/liferea/releases/download/v${pkgver}/liferea-${pkgver}.tar.bz2)
sha1sums=('5325d1fc06417abd4de5568fa87daf0c7f3e9c82')

build() {
  cd liferea-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd liferea-${pkgver}
  make DESTDIR="${pkgdir}" install
}
