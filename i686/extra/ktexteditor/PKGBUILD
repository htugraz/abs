# $Id: PKGBUILD 227537 2014-12-11 16:10:46Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.5.0
pkgrel=1
pkgdesc='Advanced embeddable text editor'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/ktexteditor'
license=('LGPL')
depends=('kparts' 'libgit2')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        'pkgbuild-syntax-highlight.patch')
md5sums=('e0f30e95e0c63253c4942b10cfb9b26b'
         'cc66f75c967dd7ac7c97375875772d69')

prepare() {
  mkdir -p build

  cd ${pkgname}-${pkgver}
  patch -p0 -i "${srcdir}"/pkgbuild-syntax-highlight.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
