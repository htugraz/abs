# $Id: PKGBUILD 198431 2013-10-30 14:50:00Z allan $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=smpeg
pkgver=0.4.4
pkgrel=7
pkgdesc="SDL MPEG Player Library"
arch=('i686' 'x86_64')
url="http://icculus.org/smpeg/"
license=('LGPL')
depends=('sdl')
makedepends=('gtk' 'mesa' 'glu')
optdepends=('gtk: to use gtv ' 'glu: to use glmovie')
options=('!makeflags')
source=(http://mirrors.dotsrc.org/lokigames/open-source/smpeg/${pkgname}-${pkgver}.tar.gz
        smpeg-0.4.4-gcc41.patch)
md5sums=('59c76ac704088ef5539210190c4e1fe3'
         '8b979a58307d7196655758bd3d2466c4')
sha1sums=('6d7f4449472e6270ab435b2224f3fad951c35259'
          '7d9a2ad7f6b702dfe3adcb87601d9b55022bbd1e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i $srcdir/smpeg-0.4.4-gcc41.patch
  ./configure --prefix=/usr --mandir=/usr/share/man --disable-static
  make LDFLAGS+=-lstdc++
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # fix aclocal warnings
  sed -i "s#(AM_PATH_SMPEG#([AM_PATH_SMPEG]#" ${pkgdir}/usr/share/aclocal/smpeg.m4
}
