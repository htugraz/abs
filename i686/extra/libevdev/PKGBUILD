# $Id: PKGBUILD 227351 2014-12-07 13:52:10Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libevdev
pkgver=1.3.2
pkgrel=1
pkgdesc="Wrapper library for evdev devices"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/libevdev/"
license=(custom:X11)
depends=('glibc')
makedepends=('python2' 'check' 'valgrind' 'doxygen')
#checkdepends=('kmod')
source=(http://freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('a4317de9e818093dc131cc2bb96cdfc0cebd631e4be5c5244e1e373ce1536eed'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static
  make
}
check() {
  cd $pkgname-$pkgver
  # test suite requires root access and needs to load uinput module
  # that's not possible in our chroot
  #modprobe uinput
  make check || /bin/true
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
