# $Id: PKGBUILD 214330 2014-06-06 20:59:43Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libevdev
pkgver=1.2.2
pkgrel=1
pkgdesc="Wrapper library for evdev devices"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/libevdev/"
license=(custom:X11)
depends=('glibc')
makedepends=('python2' 'check' 'valgrind' 'doxygen')
#checkdepends=('kmod')
source=(http://freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('860e9a1d5594393ff1f711cdeaf048efe354992019068408abbcfa4914ad6709')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static
  make
}
check() {
  cd $pkgname-$pkgver
  # test suite requires root access and needs to load uinput module
  # that's not possible in our chroot
  #modprobe uinput
  make check || /bin/true
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
