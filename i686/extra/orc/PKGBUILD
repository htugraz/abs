# $Id: PKGBUILD 227400 2014-12-10 14:02:42Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=orc
pkgver=0.4.22
pkgrel=2
pkgdesc="The Oild Runtime Compiler"
arch=('i686' 'x86_64')
license=('custom')
url="http://code.entropywave.com/projects/orc/"
depends=('glibc')
makedepends=('valgrind')
source=(http://gstreamer.freedesktop.org/data/src/orc/orc-${pkgver}.tar.xz)
md5sums=('eecd4788688a95aae97adf1125585cbf')

build () {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --disable-static
    make
}

check() {
    cd $pkgname-$pkgver
    make check || [[ $CARCH == i686 ]]
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
