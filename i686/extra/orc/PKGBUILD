# $Id: PKGBUILD 247179 2015-09-23 21:30:55Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=orc
pkgver=0.4.24
pkgrel=1
pkgdesc="The Oil Runtime Compiler"
arch=('i686' 'x86_64')
license=('custom')
url="http://code.entropywave.com/projects/orc/"
depends=('glibc')
makedepends=('valgrind')
source=(http://gstreamer.freedesktop.org/data/src/orc/orc-${pkgver}.tar.xz)
md5sums=('9e793ec34c0e20339659dd4bbbf62135')

build () {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --disable-static
    make
}

check() {
    cd $pkgname-$pkgver
    make check || [[ $CARCH == i686 ]]
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
