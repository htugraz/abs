# $Id: PKGBUILD 246812 2015-09-20 08:19:13Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdegames
pkgver=15.08.1
pkgrel=1
pkgdesc="Common code and data for many KDE games"
url="https://projects.kde.org/projects/kde/kdegames/libkdegames"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs4support' 'kdeclarative' 'kdnssd' 'knewstuff' 'openal')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('f86edc440ef643b7fa68c910106f3ddd9f729063')

build() {
  mkdir -p build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
