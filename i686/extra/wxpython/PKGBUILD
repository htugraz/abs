# $Id: PKGBUILD 224302 2014-10-14 03:58:07Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxpython
pkgver=3.0.1.1
_editraver=0.7.20
pkgrel=2
pkgdesc="A wxWidgets GUI toolkit for Python"
arch=('i686' 'x86_64')
license=('custom:wxWindows')
url="http://www.wxpython.org"
depends=('wxgtk' 'python2')
makedepends=('mesa' 'glu')
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2
        http://editra.googlecode.com/files/Editra-${_editraver}.tar.gz
	wxpython-numeric-77995.patch)
sha1sums=('d2c4719015d7c499a9765b1e5107fdf37a32abfb'
          'f439f4aa2cb90b8348cebb1670df9cf47ba341ab'
          '9775446944267b422e5ddaa4a60d24755ef7bc6a')

prepare() {
  cd "${srcdir}"
  find . -type f -exec sed -i 's/env python/env python2/' {} \;
  sed -i 's/sys.exit(1)//' Editra-${_editraver}/setup.py

  cd wxPython-src-${pkgver}/wxPython
  patch -p3 -i "${srcdir}/wxpython-numeric-77995.patch"
}

build() {
  cd wxPython-src-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --disable-precomp-headers \
    --with-regex=sys --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys
  cd wxPython
  python2 setup.py WXPORT=gtk2 UNICODE=1 build
}

package() {
  cd wxPython-src-${pkgver}/wxPython
  python2 setup.py WXPORT=gtk2 UNICODE=1 install --root="${pkgdir}"
  install -D -m644 ../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd "${srcdir}/Editra-${_editraver}"
  python2 setup.py install --root="${pkgdir}"
  rm -r "${pkgdir}/usr/lib/python2.7/site-packages/wx-3.0-gtk2/wx/tools/Editra"
}
