# $Id: PKGBUILD 213824 2014-05-29 16:21:41Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgname=cairo
pkgver=1.12.16
pkgrel=2
pkgdesc="Cairo vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'mesa' 'libgl' 'sh' 'lzo2')
makedepends=('mesa-libgl' 'librsvg' 'gtk2' 'poppler-glib' 'libspectre' 'gtk-doc' 'valgrind'
             # for the test suite:
             'ttf-dejavu' 'gsfonts' 'xorg-server-xvfb' ) # 'libdrm')
#optdepends=('xcb-util: for XCB backend') # really needed?
provides=('cairo-xcb')
replaces=('cairo-xcb')
source=(http://cairographics.org/releases/$pkgname-$pkgver.tar.xz
        cairo-1.12.16-lto-optional.patch)
sha1sums=('4f6e337d5d3edd7ea79d1426f575331552b003ec'
          '80883e44a57942762995aea2f136701b1fa54568')

prepare() {
  cd $pkgname-$pkgver
  # https://bugs.archlinux.org/task/40313 + https://bugs.gentoo.org/510782
  # patch to make it optional is taken from Gentoo
  patch -Np1 -i $srcdir/cairo-1.12.16-lto-optional.patch
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--disable-lto \
	--enable-tee \
	--enable-gl \
	--enable-egl \
	--enable-svg \
	--enable-ps \
	--enable-pdf \
	--enable-gobject #\
	# --enable-test-surfaces
	
	#--disable-xlib-xcb \
	# --enable-test-surfaces \ takes ages
	#--enable-drm # breaks build
	
  make
}

check() {
  cd $pkgname-$pkgver
  make -k test || /bin/true
  
  # results:
  # 1.12.8-1	# 162 Passed, 328 Failed [8 crashed, 10 expected], 26 Skipped
  # 1.12.12-2:	# 29 Passed, 464 Failed [460 crashed, 2 expected], 26 Skipped
  # 1.12.16-1:	144 Passed, 364 Failed [6 crashed, 12 expected], 27 Skipped

}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
