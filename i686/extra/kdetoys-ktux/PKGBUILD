# $Id: PKGBUILD 227020 2014-11-24 07:21:06Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdetoys-ktux
pkgver=4.14.3
pkgrel=1
pkgdesc='A Tux-in-a-spaceship screen saver'
url='https://projects.kde.org/projects/kde/kdetoys/ktux'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdetoys')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/ktux-${pkgver}.tar.xz")
sha1sums=('13157abb78fc1a42362a992be0d2c2f61f805fc5')

build() {
  mkdir build
  cd build
  cmake ../ktux-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
