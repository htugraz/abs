# $Id: PKGBUILD 221585 2014-09-13 10:06:26Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeclarative
pkgver=5.2.0
pkgrel=1
pkgdesc='KDeclarative'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdeclarative'
license=('LGPL')
depends=('kio')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('5c74a2802200f10afed93cb7efa0e752')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DLOCALE_INSTALL_DIR=/usr/share/locale/kf5 \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
