# $Id: PKGBUILD 233226 2015-03-10 07:39:04Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ktuberling
pkgver=14.12.3
pkgrel=1
pkgdesc="A simple constructor game suitable for children and adults alike"
url="http://kde.org/applications/games/ktuberling/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/ktuberling-${pkgver}.tar.xz")
sha1sums=('ee0b3cd4892efc84c0d195792429033a660cffcc')

build() {
  mkdir build
  cd build
  cmake ../ktuberling-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
