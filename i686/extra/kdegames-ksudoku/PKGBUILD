# $Id: PKGBUILD 224992 2014-10-20 08:16:35Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksudoku
pkgver=4.14.2
pkgrel=1
pkgdesc="A logic-based symbol placement puzzle"
url="http://kde.org/applications/games/ksudoku/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames' 'glu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/ksudoku-${pkgver}.tar.xz")
sha1sums=('662fe5ca4920b03fe122e075edd29eb32aa20729')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../ksudoku-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
