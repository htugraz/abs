# $Id: PKGBUILD 221245 2014-09-09 07:01:36Z andrea $
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=cfitsio
pkgver=3.370
pkgrel=2
pkgdesc="A library of C and Fortran subroutines for reading and writing data files in FITS (Flexible Image Transport System) data format"
arch=('i686' 'x86_64')
url="http://heasarc.gsfc.nasa.gov/fitsio/"
license=('custom')
depends=('glibc')
source=("ftp://heasarc.gsfc.nasa.gov/software/fitsio/c/${pkgname}${pkgver/./}.tar.gz")
sha512sums=('97c820757513ac0c59c277b39b8f8a6d1cbc1b2d37ca8ee68d645533a37d25fcdba87e676eaaf3cfa557c34591e9476326d892e0f8c42d3e1372e1d6a4406d18')

build() {
  cd ${pkgname}
  ./configure --prefix=/usr
  make shared

  # Build extra utils
  make fpack
  make funpack
}

check() {
  cd ${pkgname}
  make testprog
  LD_LIBRARY_PATH=. ./testprog > testprog.lis
  [[ -z $(diff testprog.lis testprog.out) ]] || return 1
  [[ -z $(cmp testprog.fit testprog.std) ]] || return 1
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/bin
  install -m755 {fpack,funpack} "${pkgdir}"/usr/bin/

  install -D -m644 License.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
