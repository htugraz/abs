# $Id: PKGBUILD 198922 2013-11-05 20:07:12Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-gwenview
pkgver=4.11.3
pkgrel=1
pkgdesc="A fast and easy to use image viewer for KDE"
url="http://kde.org/applications/graphics/gwenview/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-lib' 'kdebase-runtime' 'libkipi' 'lcms2')
makedepends=('cmake' 'automoc4' 'mesa')
optdepends=('kipi-plugins: extra plugins to share photos')
install=$pkgname.install
source=("http://download.kde.org/stable/${pkgver}/src/gwenview-${pkgver}.tar.xz")
sha1sums=('e4e6dcb883819fdc53e739d4d78d2b90c930f12e')

build() {
  mkdir build
  cd build
  cmake ../gwenview-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
