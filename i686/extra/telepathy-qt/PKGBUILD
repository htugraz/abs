# $Id: PKGBUILD 225109 2014-10-20 17:42:36Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=telepathy-qt
pkgname=('telepathy-qt4' 'telepathy-qt5')
pkgver=0.9.5
pkgrel=2
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/"
license=('LGPL')
makedepends=('qt4' 'telepathy-farstream' 'libxslt' 'python2' 'cmake' 'doxygen' 'qt5-base')
options=('staticlibs')
source=("http://telepathy.freedesktop.org/releases/${pkgbase}/${pkgbase}-${pkgver}.tar.gz"{,.asc})
md5sums=('22c0daa7e4f7e48e779f703c9b27b816'
         'SKIP')

prepare() {
  mkdir build{,-qt4}
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DENABLE_EXAMPLES=OFF \
    -DENABLE_TESTS=OFF
  make
  cd ..

  cd build-qt4
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DDESIRED_QT_VERSION=4 \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -DENABLE_EXAMPLES=OFF \
    -DENABLE_TESTS=OFF
  make
}

package_telepathy-qt4() {
  pkgdesc="A library for Qt4-based Telepathy clients"
  depends=('qt4' 'telepathy-farstream')
  conflicts=('telepathy-qt')
  replaces=('telepathy-qt')
  provides=('telepathy-qt')

  cd build-qt4
  make DESTDIR="$pkgdir" install
}

package_telepathy-qt5() {
  pkgdesc="A library for Qt5-based Telepathy clients"
  depends=('qt5-base' 'telepathy-farstream')

  cd build
  make DESTDIR="$pkgdir" install
}
