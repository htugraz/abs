# $Id: PKGBUILD 234657 2015-03-24 20:09:59Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=kwrited
pkgver=5.2.2
pkgrel=1
pkgdesc='Kwrited'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kwrited'
license=('LGPL')
depends=('kpty' 'knotifications')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('258d03d6b29cf8a58758c1b80f922e31')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_AS_EXECUTABLE=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
