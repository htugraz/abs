# $Id: PKGBUILD 255279 2015-12-10 04:50:13Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kubrick
pkgver=15.08.3
pkgrel=2
pkgdesc="Based on the famous Rubik's Cube"
url="http://kde.org/applications/games/kubrick/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4' 'glu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kubrick-${pkgver}.tar.xz")
sha1sums=('4c41ad099abfe7f91eda0f6a3c6888bd8bed402c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kubrick-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
