# $Id: PKGBUILD 229835 2015-01-24 10:43:14Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kolourpaint
pkgver=14.12.1
pkgrel=1
pkgdesc="Paint Program"
url="http://kde.org/applications/graphics/kolourpaint/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime' 'qimageblitz')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kolourpaint-${pkgver}.tar.xz")
sha1sums=('0ba95fb5b89ac7fa411a336394502da81af8a2b7')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kolourpaint-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
