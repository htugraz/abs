# $Id: PKGBUILD 246918 2015-09-20 08:21:59Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kolourpaint
pkgver=15.08.1
pkgrel=1
pkgdesc="Paint Program"
url="http://kde.org/applications/graphics/kolourpaint/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('kdebase-runtime' 'qimageblitz')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kolourpaint-${pkgver}.tar.xz")
sha1sums=('68f6500fda4bdc35692f9cdb056ada465319fe4f')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kolourpaint-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
