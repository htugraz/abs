# $Id: PKGBUILD 242017 2015-07-10 13:34:34Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcompletion
pkgver=5.12.0
pkgrel=1
pkgdesc='Text completion helpers and widgets'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kcompletion'
license=('LGPL')
depends=('kwidgetsaddons' 'kconfig')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('b778c5f541e35c076a5729a8e6765b63')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
