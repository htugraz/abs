# $Id: PKGBUILD 201094 2013-12-06 12:32:42Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kruler
pkgver=4.11.4
pkgrel=1
pkgdesc="Screen Ruler"
url="http://kde.org/applications/graphics/kruler/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kruler-${pkgver}.tar.xz")
sha1sums=('417d1cf1cf2ed8d9681d0d88eac0ffe664555e2b')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kruler-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
