# $Id: PKGBUILD 202451 2013-12-22 12:20:10Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=rygel
pkgver=0.20.3
pkgrel=1
pkgdesc="UPnP AV MediaServer and MediaRenderer that allows you to easily share audio, video and pictures, and control of media player on your home network"
arch=(i686 x86_64)
url="http://live.gnome.org/Rygel"
license=(LGPL)
depends=(gupnp-av gupnp-dlna libgee gtk3 libunistring libtracker-sparql hicolor-icon-theme xdg-utils)
makedepends=(vala intltool)
optdepends=('gst-plugins-base: Extra media codecs'
            'gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'            
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs'
            'tracker: Share indexed media files')
backup=(etc/rygel.conf)
install=rygel.install
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('4e7e3105a1d27d126938b7530f60bd7867121188651cee806f36694fbd7dd349')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --enable-mediathek-plugin --enable-gst-launch-plugin
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
