# $Id: PKGBUILD,v 1.10 2008/05/13 19:03:39 ronald Exp $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=wxmaxima
pkgver=14.09.0
pkgrel=1
pkgdesc="A wxWidgets GUI for the computer algebra system Maxima"
arch=('i686' 'x86_64')
url="http://wxmaxima.sourceforge.net/"
license=('GPL2')
depends=('maxima' 'libxml2' 'wxgtk2.8' 'shared-mime-info' 'desktop-file-utils')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
install=wxmaxima.install
sha1sums=('b8dad346edc820b0c3744219fa55561ad084cdda')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export WX_CONFIG_PATH=/usr/bin/wx-config-2.8
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install 

  # Fix category in .desktop file
  sed -i -e 's/Utility;X-Red-Hat-Base;X-Red-Hat-Base-Only;/Science;Math;/' wxmaxima.desktop

  # Install desktop file and icon
  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m644 wxmaxima.desktop "${pkgdir}/usr/share/applications/" 
  install -m644 data/wxmaxima.png "${pkgdir}/usr/share/pixmaps/" 
}
