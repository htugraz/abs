# $Id: PKGBUILD 229779 2015-01-24 10:41:18Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kalzium
pkgver=14.12.1
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="http://kde.org/applications/education/kalzium/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'avogadro' 'ocaml' 'chemical-mime-data')
makedepends=('cmake' 'automoc4' 'mesa' 'eigen' 'eigen2')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kalzium-${pkgver}.tar.xz")
sha1sums=('0e25175c5af160792bc8992870cbdc440aea2804')

build() {
  mkdir build
  cd build
  cmake ../kalzium-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
