# $Id: PKGBUILD 208228 2014-03-19 20:36:58Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Gour <Gour <gour@mail.inet.hr>

pkgname=libisoburn
pkgver=1.3.6.pl01
pkgrel=1
pkgdesc="frontend for libraries libburn and libisofs"
url="http://libburnia-project.org"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('libburn>=1.3.6.pl01' 'libisofs>=1.3.6' 'readline')
optdepends=('tk:      for xorriso-tcltk frontend'
            'bwidget: for xorriso-tcltk frontend')
provides=('xorriso' 'xorriso-tcltk')
install=${pkgname}.install
source=(http://files.libburnia-project.org/releases/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('51f010e022b54e38a5f0c194b78c5b3a'
         'SKIP')

build() {
  cd ${pkgname}-${pkgver/.pl01/}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver/.pl01/}
  make DESTDIR=${pkgdir} install
}
