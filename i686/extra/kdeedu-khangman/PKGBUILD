# $Id: PKGBUILD 200980 2013-12-06 12:23:48Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-khangman
pkgver=4.11.4
pkgrel=1
pkgdesc="Hangman Game"
url="http://kde.org/applications/education/khangman/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libkdeedu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/khangman-${pkgver}.tar.xz")
sha1sums=('11936477132316f9954550df03daa7446a1f7de1')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../khangman-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
