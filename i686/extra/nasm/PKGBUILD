# $Id: PKGBUILD 192097 2013-08-05 20:37:17Z eric $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=nasm
pkgver=2.10.09
pkgrel=1
pkgdesc="An 80x86 assembler designed for portability and modularity"
arch=('i686' 'x86_64')
url="http://www.nasm.us"
license=('BSD')
depends=('glibc')
install=nasm.install
source=(http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha1sums=('062dd7315b307870917e708a96b6862481cefc54')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
  make -C doc info
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALLROOT="${pkgdir}" install install_rdf
  install -dm755 "${pkgdir}"/usr/share/info
  install -m644 doc/info/* "${pkgdir}"/usr/share/info/
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/nasm/LICENSE
}
