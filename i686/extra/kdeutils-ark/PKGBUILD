# $Id: PKGBUILD 198970 2013-11-05 20:24:49Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-ark
pkgver=4.11.3
pkgrel=1
pkgdesc='Archiving Tool'
url='http://kde.org/applications/utilities/ark/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeutils')
depends=('kdebase-runtime' 'kdebase-lib' 'libarchive' 'qjson')
makedepends=('cmake' 'automoc4')
optdepends=('p7zip' 'zip' 'unzip' 'unrar')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/ark-${pkgver}.tar.xz")
sha1sums=('02488f636275729980f6d15cbab7c79cecf219c8')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../ark-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
