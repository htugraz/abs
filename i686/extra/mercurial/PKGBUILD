# $Id: PKGBUILD 221545 2014-09-13 09:53:16Z bpiotrowski $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=mercurial
pkgver=3.1.1
pkgrel=2
pkgdesc="A scalable distributed SCM tool"
arch=('i686' 'x86_64')
url="http://mercurial.selenic.com/"
license=('GPL')
depends=('python2')
optdepends=('tk: for the hgk GUI')
backup=('etc/mercurial/hgrc')
source=("http://mercurial.selenic.com/release/${pkgname}-${pkgver}.tar.gz"
        'mercurial.profile')
md5sums=('5a530bb472b3cb306b757c8f5df91358'
         '43e1d36564d4c7fbe9a091d3ea370a44')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's#env python#env python2#' mercurial/lsprof.py
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -d ${pkgdir}/usr/share/man/{man1,man5}
  install -m644 doc/hg.1 "${pkgdir}/usr/share/man/man1"
  install -m644 doc/{hgrc.5,hgignore.5} "${pkgdir}/usr/share/man/man5"
  install -m755 contrib/hgk "${pkgdir}/usr/bin"
  install -m644 -D contrib/zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_hg"
  install -m644 -D contrib/bash_completion "${pkgdir}/usr/share/bash-completion/completions/hg"
  install -d "${pkgdir}/usr/share/emacs/site-lisp"
  install -m644 contrib/{mq.el,mercurial.el} "${pkgdir}/usr/share/emacs/site-lisp"

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm644 contrib/vim/HGAnnotate.vim "${vimpath}/syntax/HGAnnotate.vim"

  # set some variables
  install -m755 -d ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/mercurial.profile "${pkgdir}/etc/profile.d/mercurial.sh"

  # install configuration file
  install -m755 -d ${pkgdir}/etc/mercurial
  install -m644 contrib/sample.hgrc "${pkgdir}/etc/mercurial/hgrc"

  # FS#38825 - Add certs config to package
  echo -e "\n[web]\ncacerts = /etc/ssl/certs/ca-certificates.crt\n" >> "${pkgdir}/etc/mercurial/hgrc"
}
