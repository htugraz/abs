# $Id: PKGBUILD 213489 2014-05-23 23:47:37Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-nibbles
pkgver=3.12.2
pkgrel=1
pkgdesc="Guide a worm around a maze"
arch=('i686' 'x86_64')
license=('GPL')
depends=('hicolor-icon-theme' 'clutter-gtk' 'libcanberra' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=gnome-nibbles.install
url="https://wiki.gnome.org/Apps/Nibbles"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('3a8a2a845664bfae9a810bfb1dd039015d1b33b7503d2320212e801e5c19d90e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
