# $Id: PKGBUILD 250380 2015-11-09 21:48:25Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kigo
pkgver=15.08.3
pkgrel=1
pkgdesc="An open-source implementation of the popular Go game"
url="http://kde.org/applications/games/kigo/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4' 'gnugo')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kigo-${pkgver}.tar.xz")
sha1sums=('16f0bc9d6732a4a4517f5590acfaebe2e7e38d3d')

build() {
  mkdir -p build
  cd build
  cmake ../kigo-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
