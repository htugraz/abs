# $Id: PKGBUILD 246887 2015-09-20 08:21:03Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfourinline
pkgver=15.08.1
pkgrel=1
pkgdesc="A four-in-a-row game"
url="http://kde.org/applications/games/kfourinline/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kfourinline')
replaces=('kdegames-kfourinline')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kfourinline-${pkgver}.tar.xz")
sha1sums=('edfcb93370b255ae3d7e9e37d3ee040717e291fc')

build() {
  mkdir -p build
  cd build
  cmake ../kfourinline-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
