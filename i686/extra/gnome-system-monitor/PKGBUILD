# $Id: PKGBUILD 210583 2014-04-15 08:27:36Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=3.12.1
pkgrel=1
pkgdesc="A system monitor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libgtop' 'gtkmm3' 'librsvg' 'gnome-icon-theme' 'systemd')
makedepends=('itstool' 'docbook-xsl' 'intltool')
options=(!emptydirs)
url="http://www.gnome.org"
groups=('gnome')
install=gnome-system-monitor.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('9abdf046248f26b3945a6aba4098ab04907416cdc07e19a7a8aeb568e2428f0f')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --enable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
