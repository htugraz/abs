# $Id: PKGBUILD 228312 2015-01-01 02:46:32Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=mutagen
pkgname=('mutagen' 'python-mutagen')
pkgver=1.27
pkgrel=1
arch=('any')
url="https://bitbucket.org/lazka/mutagen"
license=('GPL2')
makedepends=('python' 'python2')
source=(https://bitbucket.org/lazka/mutagen/downloads/${pkgbase}-${pkgver}.tar.gz{,.sig})
sha1sums=('4c9b874feb6f1e9dbf6f2c37341dad3a47135640'
          'SKIP')
validpgpkeys=('FADC2AE3503EAF61F3668BDB29CF60290C693B8F')

prepare() {
  cp -r ${pkgbase}-${pkgver} python-${pkgbase}-${pkgver}
}

check() {
  cd ${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python2 setup.py test
  cd ../python-${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python setup.py test
}

package_mutagen() {
  pkgdesc="An audio metadata tag reader and writer"
  depends=('python2')
 
  cd ${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}"
}

package_python-mutagen() {
  pkgdesc="An audio metadata tag reader and writer library for Python 3"
  depends=('python')

  cd python-${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}"
  rm -r "${pkgdir}"/usr/{bin,share}
}
