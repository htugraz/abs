# $Id: PKGBUILD 242232 2015-07-14 23:41:08Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=soundtouch
pkgver=1.9.0
pkgrel=1
pkgdesc="An audio processing library"
arch=('i686' 'x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs')
source=(http://www.surina.net/soundtouch/${pkgname}-${pkgver}.tar.gz)
sha1sums=('25c6103b7116a53570a3f7d5b291621442173906')

prepare() {
  cd ${pkgname}
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
}

build() {
  cd ${pkgname}
  [ "${CARCH}" = "x86_64" ] && CONFIGFLAG="--with-pic"
  ./bootstrap
  ./configure --prefix=/usr --enable-shared ${CONFIGFLAG}
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/soundtouch install
}
