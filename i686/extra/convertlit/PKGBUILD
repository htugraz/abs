# $Id: PKGBUILD 144473 2011-12-06 12:35:16Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: gDD (usrgdd | gmail)
# Contributor: cantabile

pkgname=convertlit
pkgver=1.8
pkgrel=6
pkgdesc="An extractor/converter for .LIT eBooks"
arch=('i686' 'x86_64')
url="http://www.convertlit.com/"
license=('GPL')
depends=('glibc')
makedepends=('libtommath')
source=("http://www.convertlit.com/clit18src.zip")
md5sums=('d691d4729485fe5d73e3f0937d8fb42e')

build() {
  cd ${srcdir}/lib
  make 

  cd ../clit18
  sed -i 's|../libtommath-0.30/|/usr/lib/|' Makefile
  make
}

package() {
  cd ${srcdir}/clit18

  install -Dm755 clit "$pkgdir/usr/bin/clit"
}
