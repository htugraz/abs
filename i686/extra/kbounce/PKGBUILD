# $Id: PKGBUILD 255227 2015-12-10 04:48:37Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbounce
pkgver=15.08.3
pkgrel=2
pkgdesc="A single player arcade game with the elements of puzzle"
url="http://kde.org/applications/games/kbounce/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kbounce')
replaces=('kdegames-kbounce')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kbounce-${pkgver}.tar.xz")
sha1sums=('bf71b12c9c6ef72c96ed576a13594cd9a2409406')

build() {
  mkdir -p build
  cd build
  cmake ../kbounce-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
