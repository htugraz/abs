# $Id: PKGBUILD 211439 2014-04-18 18:02:49Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kamera
pkgver=4.13.0
pkgrel=1
pkgdesc="Configure Kamera"
url="http://kde.org/applications/graphics/kamera/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime' 'libgphoto2')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/kamera-${pkgver}.tar.xz")
sha1sums=('37b1dd6872db79c00e93881f8efd8ab11d1f1bd2')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kamera-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
