# $Id: PKGBUILD 198925 2013-11-05 20:07:41Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kgamma
pkgver=4.11.3
pkgrel=1
pkgdesc="A monitor calibration tool"
url="http://kde.org/applications/graphics/kgamma/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
# note on libxxf86vm:
#       not detected by namcap because libgl depends on it
#       but nvidia providing libgl does not depend on libxxf86vm
depends=('kdebase-runtime' 'libxxf86vm')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/kgamma-${pkgver}.tar.xz")
sha1sums=('54dbbf355bfed3b4d94fee10c30217443570926a')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kgamma-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
