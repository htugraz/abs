# $Id: PKGBUILD 225536 2014-11-01 14:29:41Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-log-log4perl
pkgver=1.46
pkgrel=1
pkgdesc='Log4j implementation for Perl'
arch=('any')
url="https://metacpan.org/release/Log-Log4perl"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSCHILLI/Log-Log4perl-$pkgver.tar.gz")
md5sums=('d247d2327d7f32dca09cbeb51a953fc5')

build() {
  cd Log-Log4perl-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Log-Log4perl-$pkgver
  make test
}

package() {
  cd Log-Log4perl-$pkgver
  make DESTDIR="$pkgdir" install
}
