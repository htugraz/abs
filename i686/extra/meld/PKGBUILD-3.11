# $Id: PKGBUILD 204566 2014-01-22 23:15:09Z heftig $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=3.11.0
pkgrel=1
pkgdesc='Visual diff and merge tool'
url='http://meldmerge.org/'
license=('GPL')
arch=('any')
makedepends=('intltool' 'itstool')
depends=('python2-gobject' 'gtk3' 'gtksourceview3' 'hicolor-icon-theme' 'desktop-file-utils')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('3a23ef1b813bdcb3f8fd0308d46ba83f8536c90b')

# Don't forget to add the glib-compile-schemas call to install before using this PKGBUILD
install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python2 setup.py build
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

	# Missing files; required
	install -d "${pkgdir}"/usr/share/{glib-2.0/schemas,icons}
	cp -r data/icons/hicolor data/icons/HighContrast "${pkgdir}/usr/share/icons"
	cp data/org.gnome.meld.gschema.xml "${pkgdir}/usr/share/glib-2.0/schemas"
}
