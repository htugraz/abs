# $Id: PKGBUILD 213117 2014-05-19 00:27:36Z eric $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=1.8.5
pkgrel=1
pkgdesc='Visual diff and merge tool'
url='http://meldmerge.org/'
license=('GPL')
arch=('any')
makedepends=('intltool' 'gnome-doc-utils')
depends=('pygtk' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'python2-dbus: open a new tab in an already running instance'
            'python2-gconf: gnome integration')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('a1f01bc68820c7e35c374dfb1923ed20ac28e603')

install=install
options=('!emptydirs')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's/env python/env python2/' bin/meld
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PYTHON=/usr/bin/python2 prefix=/usr
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make PYTHON=/usr/bin/python2 prefix=/usr DESTDIR="${pkgdir}" install \
		update_desktop_database= update_mime_database=
}
