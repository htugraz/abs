# $Id: PKGBUILD 255592 2015-12-10 05:03:09Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=polkit-kde-agent
pkgver=5.5.0
pkgrel=2
pkgdesc='Daemon providing a polkit authentication UI for KDE'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/polkit-kde-agent-1'
license=('LGPL')
depends=('knotifications' 'kiconthemes' 'kdbusaddons' 'kcrash')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('polkit-kde-frameworks')
replaces=('polkit-kde-frameworks')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-1-$pkgver.tar.xz")
md5sums=('2fbd41c9ef9a09b93db363d7123b535a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-1-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib/polkit-kde \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
