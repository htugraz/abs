# $Id: PKGBUILD 255614 2015-12-10 05:05:28Z foutrelis $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=qjson
pkgver=0.8.1
pkgrel=5
pkgdesc="A qt-based library that maps JSON data to QVariant objects"
arch=('i686' 'x86_64')
license=('GPL')
url="http://qjson.sourceforge.net"
depends=('qt4')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/flavio/${pkgname}/archive/${pkgver}.tar.gz")
md5sums=('4eef13da988edf8f91c260a3e1baeea9')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
