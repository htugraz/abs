# $Id: PKGBUILD 199264 2013-11-08 22:07:37Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-soap-lite
pkgver=1.08
pkgrel=1
pkgdesc="Client and server side SOAP implementation"
arch=('any')
url="http://search.cpan.org/dist/SOAP-Lite"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-crypt-ssleay' 'perl-mime-lite' 'perl-libwww' 'perl-uri' \
         'perl-mime-tools' 'perl-io-socket-ssl' 'perl-xml-parser' 'perl-fcgi' \
         'perl-class-inspector' )
options=('!emptydirs')
provides=(
    perl-apache-soap
    perl-apache-xmlrpc-lite
    perl-io-sessiondata=1.20.0
    perl-io-sessionset
    perl-soap-constants
    perl-soap-lite-deserializer-xmlschema1999
    perl-soap-lite-deserializer-xmlschema2001
    perl-soap-lite-deserializer-xmlschemasoap1_1
    perl-soap-lite-deserializer-xmlschemasoap1_2
    perl-soap-lite-packager
    perl-soap-lite-utils
    perl-soap-packager=0.712.0
    perl-soap-test
    perl-soap-transport-http
    perl-soap-transport-io
    perl-soap-transport-local
    perl-soap-transport-loopback
    perl-soap-transport-mailto
    perl-soap-transport-pop3=0.712.0
    perl-soap-transport-tcp=0.712.0
    perl-uddi-lite=0.712.0
    perl-xml-parser-lite
    perl-xmlrpc-lite
    perl-xmlrpc-test
    perl-xmlrpc-transport-http
    perl-xmlrpc-transport-pop3
    perl-xmlrpc-transport-tcp
)
source=("http://search.cpan.org/CPAN/authors/id/P/PH/PHRED/SOAP-Lite-${pkgver}.tar.gz")
md5sums=('d5de2cd5a4415590cfc96d7a41fa811e')

build() {
  cd  "${srcdir}/SOAP-Lite-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "${srcdir}/SOAP-Lite-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
