# $Id: PKGBUILD 228436 2015-01-05 04:02:10Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=wcslib
pkgver=4.24
pkgrel=1
pkgdesc="A C library that implements the 'World Coordinate System' (WCS) standard in FITS"
arch=('i686' 'x86_64')
url="http://www.atnf.csiro.au/people/Mark.Calabretta/WCS/"
license=('GPL3')
depends=('cfitsio')
makedepends=('gcc-fortran')
source=("ftp://ftp.atnf.csiro.au/pub/software/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('10f44010cd4a47b63a5180a215386ab9')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --without-pgplot
  make
}

# Fails on i686
#check() {
#  cd $pkgname-$pkgver
#  make -k check
#}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # Fix wrong permissions
  chmod g=rx,-s "${pkgdir}"/usr/{bin/,/lib/{,pkgconfig},share/man/man1/}
  chmod -s "${pkgdir}"/usr/{include/wcslib-${pkgver}/,share/doc/wcslib-${pkgver}/{,html/}}
}
