# $Id: PKGBUILD 198085 2013-10-30 12:42:46Z allan $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=gtk-vnc
pkgver=0.5.2
pkgrel=2
pkgdesc="A VNC viewer widget for GTK"
arch=('i686' 'x86_64')
url="http://live.gnome.org/gtk-vnc"
license=('LGPL')
depends=('libgcrypt' 'gtk3')
optdepends=('gtk2: GTK2 widget')
makedepends=('intltool' 'pygtk' 'pygobject2-devel' 'gtk-doc' 'gtk2' 'vala' 'gobject-introspection')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('b51bda7edebb82ab142c292bd1b1493406bc8225a5c834d854bbafef9e9a471f')

build() {
  rm -rf gtk2-build gtk3-build
  cp -a $pkgname-$pkgver gtk2-build
  mv $pkgname-$pkgver gtk3-build

  ( cd gtk2-build
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --with-python --disable-static --with-gtk=2.0
    make )

  ( cd gtk3-build
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --with-python --with-examples --disable-static --with-gtk=3.0
    make )
}

package() {
  make -C gtk2-build DESTDIR="${pkgdir}" install
  make -C gtk3-build DESTDIR="${pkgdir}" install
}
