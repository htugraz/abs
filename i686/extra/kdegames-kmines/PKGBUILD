# $Id: PKGBUILD 211419 2014-04-18 18:02:33Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kmines
pkgver=4.13.0
pkgrel=1
pkgdesc="The classic Minesweeper game"
url="http://kde.org/applications/games/kmines/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kmines-${pkgver}.tar.xz")
sha1sums=('e0bbc0dd2b350d03678ab5611531a510798ba073')

build() {
  mkdir build
  cd build
  cmake ../kmines-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
