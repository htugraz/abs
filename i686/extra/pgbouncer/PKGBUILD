# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pgbouncer
pkgver=1.6
pkgrel=1
pkgdesc="A lightweight connection pooler for PostgreSQL"
arch=('i686' 'x86_64')
url="https://wiki.postgresql.org/wiki/PgBouncer"
license=('BSD')
depends=('libevent>=2.0' 'c-ares' 'postgresql')
makedepends=('asciidoc' 'xmlto')
backup=('etc/pgbouncer/pgbouncer.ini' 'etc/logrotate.d/pgbouncer')
install=$pkgname.install
source=("https://pgbouncer.github.io/downloads/$pkgname-$pkgver.tar.gz"
        pgbouncer.ini
        pgbouncer.logrotate
        pgbouncer.service
        pgbouncer.tmpfiles.conf)
sha256sums=('d0aec755afcdcff0538b513f71ba4de03041f21ed5548171df9de53099efa916'
            '4f30e4a3eb76acdd233ebc7dd099dff6976299ba958e40a8429b74112e804b05'
            '8da38746d9c9dfc2433a8cfe22fdaf517e14492672d09e3c48cd4745fc03e9bd'
            '274a3d447c151323f2d297aae881ec69be1477f16e30b0bba469afe68c2d122a'
            '476ea0400ba063e932a58f1f49ae401d65b22add521894872c09ec6985e0960d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 ../pgbouncer.ini "$pkgdir/etc/pgbouncer/pgbouncer.ini"
  install -D -m644 ../pgbouncer.logrotate "$pkgdir/etc/logrotate.d/pgbouncer"
  install -D -m644 ../pgbouncer.service "$pkgdir/usr/lib/systemd/system/pgbouncer.service"
  install -D -m644 ../pgbouncer.tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/pgbouncer.conf"
  mkdir -p "$pkgdir/var/log/pgbouncer"
}

# vim:set ts=2 sw=2 et:
