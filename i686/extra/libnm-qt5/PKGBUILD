# $Id: PKGBUILD 226039 2014-11-11 08:25:55Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=libnm-qt5
pkgver=5.1.1
pkgrel=1
pkgdesc='Qt5 wrapper for NetworkManager API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libnm-qt'
license=('LGPL')
depends=('networkmanager' 'qt5-base')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/stable/plasma/${pkgver}/libnm-qt-$pkgver.tar.xz")
md5sums=('11e04181b97796762cfd3f48eec8eabe')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libnm-qt-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
