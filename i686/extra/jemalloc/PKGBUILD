# $Id: PKGBUILD 248512 2015-10-08 15:12:34Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

pkgname=jemalloc
pkgver=4.0.3
pkgrel=1
pkgdesc='General-purpose scalable concurrent malloc implementation'
arch=('i686' 'x86_64')
license=('BSD')
url='http://www.canonware.com/jemalloc/'
depends=('glibc')
provides=('libjemalloc.so')
optdepends=('perl: for jeprof')
source=(http://www.canonware.com/download/jemalloc/$pkgname-$pkgver.tar.bz2)
md5sums=('b8eb650528d60b4772c4ec92c5fcc474')

build() {
  cd $pkgname-$pkgver
  CFLAGS="$CFLAGS -std=gnu11" ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name \*.a -type f -exec chmod 644 '{}' \;
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
