# $Id: PKGBUILD 220905 2014-09-02 18:43:40Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxgtk
pkgver=3.0.1
pkgrel=3
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base' 'libsm')
makedepends=('gstreamer0.10-base-plugins' 'gconf' 'webkitgtk2' 'glu')
optdepends=('webkitgtk2: for webview support')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2 wxgtk-freeze.patch)
sha1sums=('73e58521d6871c9f4d1e7974c6e3a81629fddcf8'
          'f9b70baea573e309c6adcbb08a742592b45b2084')

prepare() {
  cd wxWidgets-${pkgver}
  patch -p3 -i "${srcdir}/wxgtk-freeze.patch"
}

build() {
  cd wxWidgets-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --enable-webview --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
