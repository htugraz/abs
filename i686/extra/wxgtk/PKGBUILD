# $Id: PKGBUILD 184450 2013-05-06 19:40:13Z foutrelis $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxgtk
pkgver=2.8.12.1
pkgrel=5
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base' 'sdl')
makedepends=('gstreamer0.10-base-plugins' 'gconf')
#source=(http://downloads.sourceforge.net/wxwindows/wxGTK-${pkgver}.tar.bz2)
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2)
sha1sums=('05688dc03d61631750f5904273122bb40a2115f5')

build() {
  cd wx*-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx  --disable-optimize --enable-mediactrl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --with-sdl --disable-precomp-headers
  make
  make -C locale allmo
  make -C contrib/src
}

package() {
  cd wx*-${pkgver}
  make DESTDIR="${pkgdir}" install
  make -C contrib/src DESTDIR="${pkgdir}" install
  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
