# $Id: PKGBUILD 198422 2013-10-30 14:46:49Z allan $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.com>
# Contributor: gyo <nucleogeek@gmail.com>

pkgname=sdl_pango
pkgver=0.1.2
pkgrel=5
pkgdesc="Pango SDL binding"
arch=('i686' 'x86_64')
url="http://sdlpango.sourceforge.net/"
license=('LGPL')
depends=('pango' 'sdl')
source=(http://downloads.sourceforge.net/sourceforge/sdlpango/SDL_Pango-$pkgver.tar.gz
        SDL_Pango-$pkgver-API-adds.patch
        matrix_declarations.patch)
md5sums=('85bbf9bb7b1cee0538154dadd045418c'
         '5decf3d7eb724e27548af3142131a2f6'
         '1067d401f4df54c7eb84ac3fd56ab76f')

build() {
  cd $srcdir/SDL_Pango-$pkgver
  patch -Np0 -i $srcdir/SDL_Pango-$pkgver-API-adds.patch
  patch -Np1 -i $srcdir/matrix_declarations.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/SDL_Pango-$pkgver
  make DESTDIR=$pkgdir install
}
