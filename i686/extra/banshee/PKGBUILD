# $Id: PKGBUILD 141668 2011-11-02 03:17:12Z eric $
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=banshee
pkgver=2.6.1
pkgrel=2
pkgdesc="Music management and playback for GNOME"
arch=('i686' 'x86_64')
url="http://banshee.fm/"
license=('MIT')
depends=(libxxf86vm gstreamer0.10-base-plugins mono-addins dbus-sharp-glib webkitgtk2 libsoup-gnome gdata-sharp taglib-sharp gudev-sharp gkeyfile-sharp gconf-sharp libmtp libgpod mono-zeroconf desktop-file-utils hicolor-icon-theme media-player-info gstreamer0.10-bad-plugins mono-upnp)
makedepends=('intltool' 'gnome-doc-utils' 'gtk-sharp-beans' 'gnome-common')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: CD burning')
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/2.6/$pkgname-$pkgver.tar.xz
        git-fixes.patch)
sha256sums=('53b8990cd439963edbfa776b3029bd73809e413bec302e3b6cf3914c6e74fb64'
            '57bac0eff43426044385c3650a677fb546f6e3e74a59b4b44dcd000a03e97fa6')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../git-fixes.patch
}
  
build() {
  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p "$MONO_SHARED_DIR"

  cd $pkgname-$pkgver
  MCS=/usr/bin/dmcs ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-docs \
              --disable-static \
              --disable-scrollkeeper \
              --disable-schemas-install \
              --disable-boo \
              --with-vendor-build-id=ArchLinux
  make
}

package() {
  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
