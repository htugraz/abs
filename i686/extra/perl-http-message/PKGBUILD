# $Id: PKGBUILD 242378 2015-07-20 03:33:51Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Generator : pbjam 0.01

pkgname=perl-http-message
pkgver=6.10
pkgrel=1
pkgdesc="HTTP style messages"
arch=('any')
url='http://search.cpan.org/dist/HTTP-Message'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-encode-locale' 'perl-html-parser'
         'perl-http-date' 'perl-lwp-mediatypes' 'perl-uri')
checkdepends=('perl-io-html')
conflicts=('perl-libwww<6')
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/HTTP-Message-$pkgver.tar.gz")
md5sums=('b8a68ce1898691c536714eeae3f963d4')

build() {
  cd HTTP-Message-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd HTTP-Message-$pkgver
  make test
}

package() {
  cd HTTP-Message-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
