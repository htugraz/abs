# $Id: PKGBUILD 225031 2014-10-20 08:23:00Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-kcachegrind
pkgver=4.14.2
pkgrel=1
pkgdesc='Visualization of Performance Profiling Data'
url='http://kde.org/applications/development/kcachegrind/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime' 'python2')
makedepends=('cmake' 'automoc4')
optdepends=('php: PHP support')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kcachegrind-${pkgver}.tar.xz")
sha1sums=('72d6062bfaa6b1380e77f931ef35e0d503658d69')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kcachegrind-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/hotshot2calltree
}
