# $Id: PKGBUILD 188639 2013-06-17 09:41:26Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
pkgname=gphoto2
pkgver=2.5.2
pkgrel=1
pkgdesc="A digital camera download and access program."
arch=(i686 x86_64)
url="http://www.gphoto.org"
license=('GPL' 'LGPL')
depends=('libgphoto2' 'popt' 'readline')
source=(http://downloads.sourceforge.net/gphoto/${pkgname}-${pkgver}.tar.bz2)
md5sums=('7e59e7e06a58febd35820978c6e48287')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
