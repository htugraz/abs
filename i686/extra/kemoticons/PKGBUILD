# $Id: PKGBUILD 216701 2014-07-08 18:36:22Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kemoticons
pkgver=5.0.0
pkgrel=1
pkgdesc='KEmoticons'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kemoticons'
license=('LGPL')
depends=('karchive' 'kservice')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('27adb273827ff6abc8dd8d3983765bca')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
