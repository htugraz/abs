# $Id: PKGBUILD 231033 2015-02-07 09:40:37Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-filesharing
pkgver=14.12.2
pkgrel=1
pkgdesc='Properties dialog plugin to share a directory with the local network'
url='https://projects.kde.org/projects/kde/kdenetwork/kdenetwork-filesharing'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-runtime' 'smbclient')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kdenetwork-filesharing-${pkgver}.tar.xz")
sha1sums=('385915e422c1aba97649a656b84a663f9f0d637d')

build() {
  mkdir build
  cd build
  cmake ../kdenetwork-filesharing-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
