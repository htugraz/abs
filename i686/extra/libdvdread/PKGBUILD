# $Id: PKGBUILD 237889 2015-04-22 06:35:25Z bpiotrowski $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libdvdread
pkgver=5.0.3
pkgrel=1
pkgdesc="Provides a simple foundation for reading DVD video disks"
arch=(i686 x86_64)
url="http://dvdnav.mplayerhq.hu"
license=('GPL')
depends=('glibc')
makedepends=('libdvdcss' 'git')
optdepends=('libdvdcss: for decoding encrypted DVDs')
source=(git://git.videolan.org/libdvdread.git#tag=$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
