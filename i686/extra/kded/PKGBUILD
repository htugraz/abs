# $Id: PKGBUILD 228821 2015-01-09 07:51:20Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kded
pkgver=5.6.0
pkgrel=1
pkgdesc='Extensible deamon for providing system level services'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kded'
license=('LGPL')
depends=('kinit')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('e760120cfe0519634cbe10f72c874587')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
