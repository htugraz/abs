# $Id: PKGBUILD 197962 2013-10-30 11:56:55Z allan $
# Maintainer:
# Contributor: Aaron Malone <aaron@munge.net>

pkgname=freetds
pkgver=0.91
pkgrel=3
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='http://www.freetds.org'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('unixodbc')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("ftp://ftp.freetds.org/pub/freetds/stable/${pkgname}-${pkgver}.tar.bz2")
md5sums=('e88e96800c89d62307812315b235eb18')

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr --sysconfdir=/etc/freetds --mandir=/usr/share/man --enable-msdblib --with-tdsver=7.0
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
}
