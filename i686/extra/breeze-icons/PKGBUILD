# $Id: PKGBUILD 251871 2015-11-22 09:54:01Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=breeze-icons
pkgver=5.16.0
pkgrel=2
pkgdesc='Breeze icon themes'
arch=('any')
url='https://projects.kde.org/projects/kde/workspace/breeze'
license=('LGPL')
groups=('kf5')
makedepends=('extra-cmake-modules' 'qt5-base')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz" applet-icons.patch)
md5sums=('649b3b20669d052abe3ebc639ef0c705'
         'ef4b101e95bcaad33a6797e01deef984')

prepare() {
  mkdir -p build

# Install applet icons https://bugs.kde.org/show_bug.cgi?id=355676
  cd $pkgname-$pkgver
  patch -p1 -i ../applet-icons.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
