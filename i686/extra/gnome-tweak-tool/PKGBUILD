# $Id: PKGBUILD 237257 2015-04-16 20:27:33Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweak-tool
pkgver=3.16.1
pkgrel=1
pkgdesc="A tool to customize advanced GNOME 3 options."
arch=(any)
url="https://live.gnome.org/GnomeTweakTool"
license=('GPL')
depends=('gnome-settings-daemon' 'python2-gobject' 'hicolor-icon-theme')
makedepends=('intltool')
groups=('gnome-extra')
install=gnome-tweak-tool.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('5201dea4cac641de19997faccea5fc771829c5b4c2d3ca8f84ccfa8180ffea50')

build() {
  cd "$pkgname-$pkgver"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gnome-tweak-tool
}

# vim:set ts=2 sw=2 et:
