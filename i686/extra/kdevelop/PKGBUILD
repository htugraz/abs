# $Id: PKGBUILD 198618 2013-10-30 22:40:58Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevelop
pkgver=4.5.2
pkgrel=1
pkgdesc="A C/C++ development environment for KDE"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdebase-workspace' 'kdevplatform' 'kdebase-katepart')
makedepends=('cmake' 'automoc4' 'kdesdk-okteta')
optdepends=('kdesdk-okteta: hex editor integration'
            'kdebase-konsole: embedded terminal'
            'git: Git support'
            'subversion: SVN support'
            'cvs: Concurrent Versions System support'
            'gdb: GNU Debugger support'
            'qt5-doc: Qt documentation integration'
            'cmake: cmake integration')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('3bd354ca9ff8562c8099a41836ccd72456561409')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
