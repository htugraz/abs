# $Id: PKGBUILD 227318 2014-12-06 10:23:34Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevelop
pkgver=4.7.0
pkgrel=5
pkgdesc="A C/C++ development environment for KDE"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdebase-runtime' 'kdevplatform' 'kdebase-katepart' 'grantlee-qt4')
makedepends=('cmake' 'automoc4' 'kdesdk-okteta' 'kdebase-workspace')
optdepends=('kdesdk-okteta: hex editor integration'
            'kdebase-konsole: embedded terminal'
            "kdebase-workspace: GDB plugin and 'Attach to process' functionality"
            'git: Git support'
            'subversion: SVN support'
            'cvs: Concurrent Versions System support'
            'gdb: GNU Debugger support'
            'qt5-doc: Qt documentation integration'
            'cmake: cmake integration')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('6c4becf482956334975886963ba0acf158f2aa15')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
