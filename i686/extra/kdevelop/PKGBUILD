# $Id: PKGBUILD 248916 2015-10-12 10:12:51Z arojas $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevelop
pkgver=4.7.2
pkgrel=1
pkgdesc='A C/C++ development environment for KDE'
arch=('i686' 'x86_64')
url='http://www.kdevelop.org/'
license=('GPL')
depends=('kdebase-runtime' 'kdevplatform' 'katepart4')
makedepends=('cmake' 'automoc4' 'oktetapart4' 'kdebase-workspace')
optdepends=('oktetapart4: hex editor integration'
            'konsolepart4: embedded terminal'
            "kdebase-workspace: GDB plugin and 'Attach to process' functionality"
            'git: Git support'
            'subversion: SVN support'
            'cvs: Concurrent Versions System support'
            'gdb: GNU Debugger support'
            'qt5-doc: Qt documentation integration'
            'cmake: cmake integration')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('c377eff02c2fe39aef3d49122af57c97ad7e930a')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
