# $Id: PKGBUILD 250807 2015-11-12 10:53:25Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts
pkgver=3.18.1
pkgrel=1
pkgdesc="Contacts Manager for GNOME"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
groups=(gnome)
depends=('gtk3' 'folks' 'gnome-desktop' 'dconf' 'gnome-online-accounts' 'libgee' 'telepathy-glib' 'cheese' 'libchamplain')
makedepends=('intltool' 'vala' 'gobject-introspection')
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('0418d25e70e73c05f4db58ce843819ef91180a21531549a832eafeaf2700cf26')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/gnome-contacts
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
