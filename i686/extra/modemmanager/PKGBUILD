# $Id: PKGBUILD 242290 2015-07-16 19:37:17Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=modemmanager
pkgname=(modemmanager libmm-glib)
pkgver=1.4.10
pkgrel=2
pkgdesc="Mobile broadband modem management service"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/ModemManager/"
license=(GPL2 LGPL2.1)
depends=(systemd libgudev polkit ppp libqmi libmbim hicolor-icon-theme)
makedepends=(intltool gtk-doc gobject-introspection vala)
source=(http://www.freedesktop.org/software/ModemManager/ModemManager-${pkgver}.tar.xz
        bus-signals.patch)
sha256sums=('107ba0b4d0749aebb0347691a39f60891cc6004aeca8b2128d69c50557049a63'
            '2bf544ace3e79276f8e9c113fe3468506da74eb7f4ce1de5b33ea0e955dfd09e')

prepare() {
  cd ModemManager-$pkgver
  patch -Np1 -i ../bus-signals.patch
}

build() {
  cd ModemManager-$pkgver
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --with-udev-base-dir=/usr/lib/udev \
        --with-polkit=permissive \
        --enable-gtk-doc \
        --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd ModemManager-$pkgver
  make -k check
}

package_modemmanager() {
  depends+=(libmm-glib)
  optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
  options=(!emptydirs)
  install=modemmanager.install

  cd ModemManager-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C libmm-glib uninstall
  make DESTDIR="$pkgdir" -C vapi uninstall

  # Some stuff to move is left over
  mv "$pkgdir/usr/include" ..
  mv "$pkgdir/usr/lib/pkgconfig" ..
}

package_libmm-glib() {
  pkgdesc="ModemManager library"
  depends=(glib2)

  install -d "$pkgdir/usr/lib"
  mv include "$pkgdir/usr"
  mv pkgconfig "$pkgdir/usr/lib"

  cd ModemManager-$pkgver
  make DESTDIR="$pkgdir" -C libmm-glib install
  make DESTDIR="$pkgdir" -C vapi install
}
