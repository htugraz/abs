# $Id: PKGBUILD 219431 2014-08-07 12:08:57Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpty
pkgver=5.1.0
pkgrel=1
pkgdesc='KPty'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kpty'
license=('LGPL')
depends=('kcoreaddons' 'ki18n' 'libutempter')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('69958845c262b4e345d20418a5bea0b2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
