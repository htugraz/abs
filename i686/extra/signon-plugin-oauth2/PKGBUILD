# $Id: PKGBUILD 233582 2015-03-13 18:11:50Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-plugin-oauth2
_pkgname=signon-oauth2
pkgver=0.20
pkgrel=1
pkgdesc='OAuth 2 plugin for signon'
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
depends=('signon')
makedepends=('qt5-xmlpatterns')
source=("$pkgname-$pkgver.tar.gz::http://signon-plugin-oauth2.accounts-sso.googlecode.com/archive/$pkgver.tar.gz")
md5sums=('0f33239a661d11b51a0803657983043d')

prepare() {
  cd signon-plugin-oauth2.accounts-sso-$pkgver

  sed -i '/-Werror/d' common-project-config.pri

  # Do not install tests and example
  echo 'INSTALLS =' >>tests/tests.pro
  echo 'INSTALLS =' >>example/example.pro
}

build() {
  cd signon-plugin-oauth2.accounts-sso-$pkgver

  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd signon-plugin-oauth2.accounts-sso-$pkgver

  make INSTALL_ROOT="$pkgdir" install
}

