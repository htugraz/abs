# $Id: PKGBUILD 113060 2014-06-12 10:51:06Z seblu $
# Maintainer: SÃ©bastien Luttringer
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Mario Vazquez <mario_vazq@hotmail.com>

pkgname=bind
_pkgver=9.10.1
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc='The ISC BIND nameserver'
url='http://www.isc.org/software/bind/'
license=('custom:ISC')
arch=('i686' 'x86_64')
options=('!emptydirs')
depends=('openssl' 'krb5' 'libxml2' 'libcap')
provides=('dns-server')
backup=('etc/named.conf'
        'var/named/127.0.0.zone'
        'var/named/localhost.zone'
        'var/named/empty.zone')
install=$pkgname.install
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz"{,.asc}
        'tmpfiles.conf'
        'sysusers.conf'
        'named.conf'
        'named.service'
        'named.logrotate'
        'localhost.zone'
        '127.0.0.zone'
        'empty.zone')
sha1sums=('96aa28c6112c6a8c33a19efeac98c715f03b35ca'
          'SKIP'
          'c5a2bcd9b0f009ae71f3a03fbdbe012196962a11'
          '6bebf4ff8ca4482a83f4d3dbf176d9bffd89eefa'
          '71e30a3648d695911352c9d663ca216ae3a7d5d6'
          'cb2e81b4cbf9efafb3e81e3752f0154e779cc7ec'
          '3fe1f0b5c1a51dc1db9ebe5e173d18c52c97169b'
          '76a0d4cd1b913db177a5a375bebc47e5956866ec'
          '53be0f1437ebe595240d8dbdd819939582b97fb9'
          '792bde1bcc95b30e36970eeb97f5bf02ea37b37c')

prepare() {
  # remove dig to avoid conflict with dnsutils
  sed -i 's/dig//' $pkgname-$_pkgver/bin/Makefile.in

  msg2 'Getting a fresh version of root DNS'
  # no more using source array, lack of versioning.
  curl -o root.hint http://www.internic.net/zones/named.root
  [[ -s root.hint ]]
}

build() {
  cd bind-$_pkgver
  # for gcc 4.8 rebuild
  #export CFLAGS="-march=${CARCH/_/-} -mtune=generic -O2 -pipe -fstack-protector --param=ssp-buffer-size=4"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --disable-static \
    --with-openssl \
    --with-libxml2 \
    --with-libtool
  make
}

package() {
  pushd "bind-$_pkgver"
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  make DESTDIR="$pkgdir" install
  popd

  install -D -m644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  install -D -m644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  install -D -m644 named.service "$pkgdir/usr/lib/systemd/system/named.service"
  install -D -m640 -o 0 -g 40 named.conf "$pkgdir/etc/named.conf"

  install -d -m770 -o 0 -g 40 "$pkgdir/var/named"
  install    -m640 -o 0 -g 40 root.hint "$pkgdir/var/named"
  install    -m640 -o 0 -g 40 127.0.0.zone "$pkgdir/var/named"
  install    -m640 -o 0 -g 40 localhost.zone "$pkgdir/var/named"
  install    -m640 -o 0 -g 40 empty.zone "$pkgdir/var/named"
}

# vim:set ts=2 sw=2 et:
