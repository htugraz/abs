# $Id: PKGBUILD 216704 2014-07-08 18:36:48Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kitemmodels
pkgver=5.0.0
pkgrel=1
pkgdesc='KItemModels'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kitemmodels'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('c1bb781f39b2c6d232a0fb490f2aa9dc')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
