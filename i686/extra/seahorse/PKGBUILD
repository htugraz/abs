# $Id: PKGBUILD 232468 2015-03-03 09:33:39Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=3.14.1
pkgrel=1
pkgdesc="GNOME application for managing PGP keys."
arch=(i686 x86_64)
license=(GPL)
url="http://projects.gnome.org/seahorse/"
depends=(gtk3 gcr libsecret libsoup gpgme desktop-file-utils hicolor-icon-theme dconf)
makedepends=(libldap intltool yelp-tools gobject-introspection openssh libsm vala)
optdepends=('openssh: SSH support')
provides=(x11-ssh-askpass)
options=('!emptydirs')
groups=(gnome-extra)
install=seahorse.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        gpg-2.1.patch
        seahorse-ssh-askpass.sh)
sha256sums=('092a890982fa594da8a384f156b961392f69ce5d9518a9801035e6deb1f05e93'
            '491cacc029e6eac1c13b7808806cc45b49413570e73e1fa7668c28efe10427a0'
            '400ea9a6084acc93f1dbd4223b95622bbd48071b763063328efba00de378b68c')

prepare() {
  cd "$pkgname-$pkgver"
  
  # Quick fix for now
  patch -Np1 -i ../gpg-2.1.patch

  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --enable-ldap --enable-hkp \
      --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/seahorse-ssh-askpass.sh" \
    "$pkgdir/etc/profile.d/seahorse-ssh-askpass.sh"
}
