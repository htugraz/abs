# $Id: PKGBUILD 239512 2015-05-18 09:02:18Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=glib-perl
pkgver=1.307
pkgrel=1
pkgdesc="Perl wrappers for glib 2.x, including GObject"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('glib2' 'perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Glib-${pkgver}.tar.gz)
md5sums=('2b2b0d20d87c6d2db3d98c8b733275aa')

build() {
  cd Glib-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Glib-$pkgver
  make test
}

package() {
  cd Glib-$pkgver
  make DESTDIR="${pkgdir}" install
}
