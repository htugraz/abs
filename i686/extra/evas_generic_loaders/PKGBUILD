# $Id: PKGBUILD 213612 2014-05-26 19:28:18Z ronald $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=evas_generic_loaders
pkgver=1.10.0
pkgrel=1
pkgdesc="Additional generic loaders for Evas"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('GPL2')
depends=('librsvg' 'poppler' 'libraw' 'libspectre' 'efl' 'gstreamer0.10-base')
options=('!emptydirs')
source=(http://download.enlightenment.org/rel/libs/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha1sums=('3ac1faf875cadaedfa86776e127874432e0273ea')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="$CFLAGS -fvisibility=hidden"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/evas \
    --enable-poppler \
    --enable-spectre \
    --enable-libraw \
    --enable-svg \
    --enable-gstreamer
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

