# $Id: PKGBUILD 252084 2015-12-01 02:46:31Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=perl-extutils-depends
pkgver=0.405
pkgrel=1
pkgdesc="The Perl depends module"
arch=('any')
url="http://search.cpan.org/dist/ExtUtils-Depends/"
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/X/XA/XAOC/ExtUtils-Depends-${pkgver}.tar.gz")
md5sums=('caefbca2f173d0cea3f5ac26b6c08a2c')

build() {
  cd ExtUtils-Depends-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ExtUtils-Depends-${pkgver}
  make test
}

package() {
  cd ExtUtils-Depends-${pkgver}
  make DESTDIR="${pkgdir}" install
}
