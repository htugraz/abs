# $Id: PKGBUILD 245037 2015-08-29 08:50:51Z arojas $
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=user-manager
pkgver=5.4.0
pkgrel=1
pkgdesc='A simple system settings module to manage the users of your system'
arch=(i686 x86_64)
url='https://projects.kde.org/user-manager'
license=(LGPL)
depends=(accountsservice kdelibs4support libpwquality)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('8b32fbfa6acb33a38ebe3a9188e0836d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
