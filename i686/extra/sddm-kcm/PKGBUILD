# $Id: PKGBUILD 245033 2015-08-29 08:50:45Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Maintainer: Antonio Rojas

pkgname=sddm-kcm
pkgver=5.4.0
pkgrel=1
pkgdesc='KDE Config Module for SDDM'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/sddm-kcm'
license=('GPL')
depends=('sddm' 'libxcursor' 'kio')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('9483b80cb48a919328c682056c38ccba')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

