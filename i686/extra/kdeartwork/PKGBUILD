# $Id: PKGBUILD 224933 2014-10-20 08:07:11Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeartwork
pkgname=('kdeartwork-colorschemes'
         'kdeartwork-desktopthemes'
         'kdeartwork-emoticons'
         'kdeartwork-iconthemes'
         'kdeartwork-kscreensaver'
         'kdeartwork-styles'
         'kdeartwork-wallpapers'
         'kdeartwork-weatherwallpapers')
pkgver=4.14.2
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeartwork')
makedepends=('cmake' 'automoc4' 'xscreensaver' 'eigen' 'kdebase-workspace' 'libkexiv2')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('f8461c819fca8bf606e08ecb011ee7ac7e46865b')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeartwork-colorschemes() {
	pkgdesc='KDE color schemes'
	replaces=('kdeaccessibility-colorschemes')
	cd $srcdir/build/ColorSchemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-desktopthemes() {
	pkgdesc='KDE desktop themes'
	cd $srcdir/build/desktopthemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-emoticons() {
	pkgdesc='KDE emoticons'
	cd $srcdir/build/emoticons
	make DESTDIR=$pkgdir install
}

package_kdeartwork-iconthemes() {
	pkgdesc='KDE icon themes'
	replaces=('kdeaccessibility-iconthemes')
	cd $srcdir/build/IconThemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-kscreensaver() {
	pkgdesc='KDE screensaver'
	depends=('kdebase-workspace' 'libkexiv2')
	cd $srcdir/build/kscreensaver
	make DESTDIR=$pkgdir install
}

package_kdeartwork-styles() {
	pkgdesc='KDE styles'
	depends=('kdebase-workspace')
	cd $srcdir/build/styles
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kwin-styles
	make DESTDIR=$pkgdir install
}

package_kdeartwork-wallpapers() {
	pkgdesc='KDE wallpapers'
	cd $srcdir/build/wallpapers
	make DESTDIR=$pkgdir install
	cd $srcdir/build/HighResolutionWallpapers
	make DESTDIR=$pkgdir install
}

package_kdeartwork-weatherwallpapers() {
	pkgdesc='KDE weather wallpapers'
	cd $srcdir/build/WeatherWallpapers
	make DESTDIR=$pkgdir install
}
