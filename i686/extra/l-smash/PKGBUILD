# $Id: PKGBUILD 242310 2015-07-17 14:19:27Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=l-smash
pkgver=2.3.0
pkgrel=2
pkgdesc='MP4 muxer and other tools'
arch=('i686' 'x86_64')
url='https://github.com/l-smash/l-smash'
license=('custom')
depends=('glibc')
provides=('liblsmash.so')
source=("l-smash-${pkgver}.tar.gz::https://github.com/l-smash/l-smash/archive/v${pkgver}.tar.gz")
sha256sums=('1c1a487191b723a486bd3e8abd5e28e0d34f7ac1964d98fac0a9a2db962e3838')

build() {
  cd l-smash-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-shared \
    --disable-static
  make
}

package() {
  cd l-smash-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/l-smash
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/l-smash/
}

# vim: ts=2 sw=2 et:
