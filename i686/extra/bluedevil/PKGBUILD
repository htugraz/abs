# $Id: PKGBUILD 223961 2014-10-07 07:03:36Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=bluedevil
pkgver=2.0rc1
_pkgver=2.0-rc1
pkgrel=2
epoch=1
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kdebase-workspace' 'libbluedevil')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgname}/${pkgname}-${_pkgver}.tar.xz")
md5sums=('d49a3037d29cf4ba9e0a0968a0139333')

prepare() {
  mkdir build
}

build(){
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
