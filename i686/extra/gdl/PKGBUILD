# $Id: PKGBUILD 197977 2013-10-30 12:02:25Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gdl
pkgver=3.10.0
pkgrel=1
pkgdesc="GNOME Docking Library"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org/"
depends=('gtk3')
makedepends=('gtk-doc' 'intltool' 'gobject-introspection')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('4e0d6f66115ba3b0f3cd2495d8a95c6714d701f89d556bb691bd0c0a62a1c760')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
