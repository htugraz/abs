# $Id: PKGBUILD 251907 2015-11-23 18:35:55Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-vpnc
pkgver=1.0.8
pkgrel=1
pkgdesc="NetworkManager VPN plugin for vpnc"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('libnm-gtk' 'vpnc' 'hicolor-icon-theme' 'gtk3' 'libsecret')
makedepends=('intltool')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
install=networkmanager-vpnc.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-vpnc/${pkgver::3}/NetworkManager-vpnc-${pkgver}.tar.xz)
sha256sums=('5e4034d89e2ccb2845c29f212ec263102bf9819e6c923407073aab6ffb92b66e')

build() {
  cd NetworkManager-vpnc-${pkgver}
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
	--localstatedir=/var \
	--libexecdir=/usr/lib/networkmanager \
	--enable-more-warnings=yes \
	--disable-static
  make
}

package() {
  cd NetworkManager-vpnc-${pkgver}
  make DESTDIR="${pkgdir}" install
}
