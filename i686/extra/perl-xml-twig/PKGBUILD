# $Id: PKGBUILD 236289 2015-04-12 13:56:10Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-xml-twig
pkgver=3.49
pkgrel=1
pkgdesc="A module for easy processing of XML"
arch=('any')
url="http://search.cpan.org/dist/XML-Twig/"
license=('GPL' 'PerlArtistic')
depends=('perl-xml-parser' 'perl-text-iconv' 'perl')
checkdepends=('perl-libwww' 'perl-test-pod' 'perl-xml-simple' 'perl-html-tree' 'perl-xml-xpath' 'perl-tie-ixhash')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/M/MI/MIROD/XML-Twig-${pkgver}.tar.gz")
sha1sums=('4d46acdb3d81ce0b44d7d1ae9feb7c411b29e430')

build() {
  cd XML-Twig-${pkgver}
  perl Makefile.PL -y INSTALLDIRS=vendor
  make
}

check() {
  cd XML-Twig-${pkgver}
  make test || warning "Tests failed"
}

package() {
  cd XML-Twig-${pkgver}
  make DESTDIR="${pkgdir}" install
}
