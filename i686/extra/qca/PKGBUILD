# $Id: PKGBUILD 206534 2014-03-01 08:14:43Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=qca
pkgver=2.0.3
pkgrel=4
pkgdesc="Qt Cryptographic Architecture"
arch=('i686' 'x86_64')
url="http://delta.affinix.com/qca/"
license=('LGPL')
depends=('qt4' 'ca-certificates')
source=("http://delta.affinix.com/download/${pkgname}/2.0/${pkgname}-${pkgver}.tar.bz2"
        'gcc47.patch')
md5sums=('fc15bd4da22b8096c51fcfe52d2fa309'
         'dffc7367152d8888c1447253a0387a2a')

prepare() {
	cd ${pkgname}-${pkgver}
	patch -p0 -i "${srcdir}"/gcc47.patch
}

build() {
	cd ${pkgname}-${pkgver}
	./configure \
		--prefix=/usr \
		--disable-tests \
		--certstore-path=/etc/ssl/certs/ca-certificates.crt \
		--release \
		--no-separate-debug-info
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make INSTALL_ROOT="${pkgdir}" install
}
