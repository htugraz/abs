# $Id: PKGBUILD 220266 2014-08-20 09:21:14Z lfleischer $
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgbase=lirc
pkgname=('lirc' 'lirc-utils')
_pkgver=0.9.1a
[[ $_pkgver =~ [a-z]$ ]] && pkgver="${_pkgver:0:-1}.${_pkgver: -1}" || pkgver="$_pkgver"
pkgrel=3
epoch=1
_extramodules=extramodules-3.16-ARCH
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
# NOTICE: Don't forget to bump version in depends in package_lirc()!
makedepends=('help2man' 'linux>=3.16' 'linux<3.17' 'linux-headers>=3.16' 'linux-headers<3.17' 'alsa-lib' 'libx11' 'libftdi-compat' 'libirman' 'python2')
options=('!makeflags' '!strip')
source=("http://prdownloads.sourceforge.net/${pkgbase}/${pkgbase}-${_pkgver}.tar.bz2"
        wpc8769l-build-fix.patch
        lirc-0.9.1a-fix-segfaults.patch
        lirc.logrotate
        irexec.service
        lirc.tmpfiles)
md5sums=('1f1fac162ed309dd50f307e96a292957'
         '9ec33169b3407f74264062d7679dd269'
         'cd00acf480e82a0bf050032732d0d733'
         '3deb02604b37811d41816e9b4385fcc3'
         '32df3b9bc859565d6acf5f0e5b747083'
         'febf25c154a7d36f01159e84f26c2d9a')

prepare() {
  cd "${srcdir}/lirc-${_pkgver}"

  patch -Np1 -i "${srcdir}/wpc8769l-build-fix.patch"
  patch -Np1 -i "${srcdir}/lirc-0.9.1a-fix-segfaults.patch"

  sed -i '/AC_PATH_XTRA/d' configure.ac
  sed -e 's/@X_CFLAGS@//g' \
      -e 's/@X_LIBS@//g' \
      -e 's/@X_PRE_LIBS@//g' \
      -e 's/@X_EXTRA_LIBS@//g' -i Makefile.am tools/Makefile.am

  libtoolize
  autoreconf
}

build() {
  cd "${srcdir}/lirc-${_pkgver}"
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

  PYTHON=python2 ./configure --enable-sandboxed --prefix=/usr \
      --with-driver=all --with-kerneldir="/usr/lib/modules/${_kernver}/build/" \
      --with-moduledir="/usr/lib/modules/${_kernver}/kernel/drivers/misc" \
      --sbindir=/usr/bin --with-transmitter

  # Remove drivers already in kernel
  sed -e "s:lirc_dev::" -e "s:lirc_bt829::" -e "s:lirc_igorplugusb::" \
      -e "s:lirc_imon::" -e "s:lirc_parallel::" -e "s:lirc_sasem::" \
      -e "s:lirc_serial::" -e "s:lirc_sir::" -e "s:lirc_ttusbir::" \
      -e "s:lirc_atiusb::" -e "s:lirc_i2c::" \
      -i Makefile drivers/Makefile drivers/*/Makefile tools/Makefile
  make
}

package_lirc() {
  pkgdesc="Linux Infrared Remote Control kernel modules for stock arch kernel"
  depends=('lirc-utils' 'linux>=3.16' 'linux<3.17')
  replaces=('lirc+pctv')
  install=lirc.install

  cd "${srcdir}/lirc-${_pkgver}/drivers"

  make DESTDIR="${pkgdir}" moduledir="/usr/lib/modules/${_extramodules}" install
  gzip -9 "${pkgdir}/usr/lib/modules/${_extramodules}"/*.ko

  # set the kernel we've built for inside the install script
  sed -i "s/^EXTRAMODULES=.*$/EXTRAMODULES=${_extramodules}/" "${startdir}/lirc.install"
}

package_lirc-utils() {
  pkgdesc="Linux Infrared Remote Control utils"
  depends=('alsa-lib' 'libx11' 'libftdi-compat' 'libirman')
  optdepends=('python2: pronto2lirc utility')
  options=('strip')
  backup=('etc/lirc/lirc_options.conf' 'etc/lirc/lircd.conf' 'etc/lirc/lircmd.conf')
  install=lirc-utils.install

  cd "${srcdir}/lirc-${_pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/irexec.service "${pkgdir}"/usr/lib/systemd/system/irexec.service
  install -Dm644 "${srcdir}"/lirc.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lirc.conf

  cp -rp remotes "${pkgdir}/usr/share/lirc"
  chmod -R go-w "${pkgdir}/usr/share/lirc/"

  # install the logrotate config
  install -Dm644 "${srcdir}/lirc.logrotate" "${pkgdir}/etc/logrotate.d/lirc"
    
  # remove built modules
  rm -r "${pkgdir}/usr/lib/modules"
}
