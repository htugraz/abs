# $Id: PKGBUILD 215046 2014-06-11 22:00:55Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=perl-sdl
pkgver=2.544
pkgrel=1
pkgdesc="Simple DirectMedia Layer for Perl"
arch=(i686 x86_64)
license=(LGPL)
url="http://sdl.perl.org"
depends=(perl-alien-sdl perl-tie-simple
         sdl_net sdl_ttf sdl_image sdl_mixer sdl_gfx sdl_pango smpeg)
makedepends=('perl-module-build>=0.40')
options=('!emptydirs')
replaces=(sdl_perl)
conflicts=(sdl_perl)
provides=("sdl_perl=$pkgver")
# http://search.cpan.org/dist/SDL/
_author=FROGGS
source=(http://search.cpan.org/CPAN/authors/id/${_author::1}/${_author::2}/$_author/SDL-$pkgver.tar.gz)
md5sums=('ae3c7cd3ec73f96f1c10e29521b19e97')

build() {
  cd SDL-$pkgver
  # Perl selects the proper CFLAGS set for binary modules
  unset CFLAGS
  # install module in vendor directories
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd SDL-$pkgver
  perl Build test || true
}

package() {
  cd SDL-$pkgver
  perl Build install destdir="$pkgdir"
}
