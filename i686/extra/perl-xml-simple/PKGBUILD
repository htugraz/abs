# $Id: PKGBUILD 219730 2014-08-14 00:38:10Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: kleptophobiac@gmail.com

pkgname=perl-xml-simple
_realname=XML-Simple
pkgver=2.20
pkgrel=3
pkgdesc="Simple XML parser for perl"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-xml-sax-expat')
options=('!emptydirs')
source=("ftp://ftp.cpan.org/pub/CPAN/authors/id/G/GR/GRANTM/${_realname}-${pkgver}.tar.gz")
md5sums=('4d10964e123b76eca36678464daa63cd')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
