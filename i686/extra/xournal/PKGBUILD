# $Id: PKGBUILD 216247 2014-07-02 14:29:19Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xournal
pkgver=0.4.8
pkgrel=1
pkgdesc='Notetaking and sketching application'
url='http://xournal.sourceforge.net/'
license=('GPL')
arch=('i686' 'x86_64')
optdepends=('ghostscript: import PS/PDF files as bitmap backgrounds')
depends=('libgnomecanvas' 'poppler-glib' 'shared-mime-info' 'desktop-file-utils' 'hicolor-icon-theme')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('e8b15c427287928e0995cc1b3bc83c2897c689ef')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install desktop-install
}
