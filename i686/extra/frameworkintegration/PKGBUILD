# $Id: PKGBUILD 225858 2014-11-10 08:28:20Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=frameworkintegration
pkgver=5.4.0
pkgrel=1
pkgdesc='Framework providing components to allow applications to integrate with a KDE Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/frameworkintegration'
license=('LGPL')
depends=('kio' 'ttf-oxygen')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('2ac10b279a05fb5eac587dcde5b122ec')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
