# $Id: PKGBUILD 204154 2014-01-15 10:17:19Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kblocks
pkgver=4.12.1
pkgrel=1
pkgdesc="The classic falling blocks game"
url="http://kde.org/applications/games/kblocks/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kblocks-${pkgver}.tar.xz")
sha1sums=('a66ae615bf5b2c8d86814ced8e722f359d6c3aa6')

build() {
  mkdir build
  cd build
  cmake ../kblocks-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
