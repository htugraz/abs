# $Id: PKGBUILD 239247 2015-05-12 21:11:16Z heftig $
# Maintainer:  Jan de Groot <jan@archlinux.org>

pkgname=gnome-desktop
pkgver=3.16.2
pkgrel=1
epoch=1
pkgdesc="Library with common API for various GNOME modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(gsettings-desktop-schemas gtk3 libxkbfile xkeyboard-config iso-codes)
makedepends=(intltool gobject-introspection itstool)
url="http://www.gnome.org"
groups=(gnome)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('3a8f196b46eb9dbd3ba2afb8fb5fef6a8825539d449a02181311242e22227bd0')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --with-gnome-distributor="Arch Linux"
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
