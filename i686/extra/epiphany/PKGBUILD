# $Id: PKGBUILD 219318 2014-08-05 09:33:36Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.12.1
pkgrel=2
install=epiphany.install
pkgdesc="A GNOME web browser based on the WebKit rendering engine."
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup' 'libnotify' 'gsettings-desktop-schemas' 'webkitgtk' 'nss' 'iso-codes' 'dconf' 'desktop-file-utils' 'gnome-icon-theme-symbolic' 'gcr' 'gnome-desktop' 'libwnck3' 'gnome-themes-standard')
makedepends=('intltool' 'itstool' 'docbook-xml' 'startup-notification' 'gobject-introspection')
options=('!emptydirs')
groups=('gnome')
url="https://wiki.gnome.org/Apps/Web"
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        git-fixes.patch)
sha256sums=('16d9f8f10443328b2f226c2da545e75c8433f50f103af8aeb692b098d5fbbf93'
            '22b026b30e4792b3244f3fecf12bd48f928efc34097bf501d87611f77c0750bf')

prepare() {
  cd $pkgname-$pkgver

  # Patch up to f43f9af7b195de53011b2e22c91041674dc00e2e
  patch -Np1 -i ../git-fixes.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/$pkgname

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
