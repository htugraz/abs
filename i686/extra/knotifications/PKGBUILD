# $Id: PKGBUILD 228803 2015-01-09 07:48:21Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=knotifications
pkgver=5.6.0
pkgrel=1
pkgdesc='Abstraction for system notifications'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/knotifications'
license=('LGPL')
depends=('phonon-qt5' 'libdbusmenu-qt5' 'kservice' 'kiconthemes')
makedepends=('extra-cmake-modules' 'qt5-tools' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('9f398c79b7b0c3fa48b1305dc3b60cbc')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
