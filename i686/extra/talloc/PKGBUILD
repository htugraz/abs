# $Id: PKGBUILD 195486 2013-10-01 06:06:00Z bpiotrowski $
# Maintainer:
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=talloc
pkgver=2.0.8
pkgrel=2
pkgdesc="Hierarchical pool based memory allocator with destructors"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://talloc.samba.org/"
depends=('glibc')
makedepends=('python2' 'docbook-xsl')
optdepends=('python2: for python bindings')
source=("http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('7857200b58a8617ca18fe2c91296cd93')

prepare() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   sed -i -e 's#python#python2#g' buildtools/bin/waf
}

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   ./configure --prefix=/usr \
     --sysconfdir=/etc/samba \
     --localstatedir=/var \
     --enable-talloc-compat1
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install
}
