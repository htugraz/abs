# $Id: PKGBUILD 246924 2015-09-20 08:22:09Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-okular
pkgver=15.08.1
pkgrel=1
pkgdesc='Document Viewer'
arch=('i686' 'x86_64')
url="http://kde.org/applications/graphics/okular/"
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('kdebase-runtime' 'qimageblitz' 'chmlib' 'djvulibre' 'libspectre'
         'libkexiv2' 'poppler-qt4' 'libkscreen4')
makedepends=('cmake' 'automoc4' 'ebook-tools' 'kdegraphics-mobipocket')
optdepends=('ebook-tools: mobi and epub support'
            'kdegraphics-mobipocket: mobi support')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/okular-${pkgver}.tar.xz")
sha1sums=('87abdd8efc8520cddf686b220b83fe3290f8ef14')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../okular-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_ActiveApp=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
