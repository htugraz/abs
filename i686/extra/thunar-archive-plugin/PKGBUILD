# $Id: PKGBUILD 207512 2014-03-11 16:41:15Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=thunar-archive-plugin
pkgver=0.3.1
pkgrel=3
pkgdesc="Create and extract archives in Thunar"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-archive-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('thunar' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('file-roller'
            'kdeutils-ark'
            'xarchiver')
install=$pkgname.install
source=(http://archive.xfce.org/src/thunar-plugins/$pkgname/0.3/$pkgname-$pkgver.tar.bz2)
sha256sums=('9ad559b0c11308f6897ad56604e5a06dc8f369f649eb20120b2d3018ef5da54c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
