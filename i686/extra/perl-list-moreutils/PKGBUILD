# $Id: PKGBUILD 228105 2014-12-28 03:28:23Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Generator : pbjam 0.01

pkgname=perl-list-moreutils
pkgver=0.402
pkgrel=1
pkgdesc="Provide the stuff missing in List::Util"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-exporter-tiny')
url=http://search.cpan.org/dist/List-MoreUtils
source=("http://search.cpan.org/CPAN/authors/id/R/RE/REHSACK/List-MoreUtils-${pkgver}.tar.gz")
sha512sums=('d835c3b947bac692806031383deb7151b834c86d12c6b6f745c6fd1bfb0bc7be8944f380ae18708aa275217c47519c19101c87989bbbb97109aed7e67d21a934')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/List-MoreUtils-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${srcdir}/List-MoreUtils-${pkgver}"
    make test
  )
}

package() {
    cd "${srcdir}/List-MoreUtils-${pkgver}"
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
