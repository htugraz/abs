# $Id: PKGBUILD 198714 2013-11-02 08:58:18Z ioni $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Daniel Balieiro <daniel@balieiro.com>
# Contributor: Rodrigo L. M. Flores <mail@rodrigoflores.org>
pkgname=telepathy-gabble
pkgver=0.18.1
pkgrel=2
pkgdesc="A Jabber/XMPP connection manager for Telepathy"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
groups=('telepathy')
license=('LGPL2.1')
depends=('telepathy-glib' 'libsoup' 'libnice' 'sqlite')
makedepends=('libxslt' 'python2')
options=('!emptydirs')
source=(http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
install=telepathy-gabble.install
md5sums=('a5384758df829b80d47fbc8a672609ad')

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
  	--libexecdir=/usr/lib/telepathy --with-tls=openssl
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir/usr/bin/telepathy-gabble-xmpp-console"
}
