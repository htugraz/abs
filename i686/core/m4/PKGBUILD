# $Id: PKGBUILD 228367 2015-01-03 06:38:19Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=m4
pkgver=1.4.17
pkgrel=1
pkgdesc="The GNU macro processor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/m4"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'bash')
install=m4.install
source=(ftp://ftp.gnu.org/gnu/m4/$pkgname-$pkgver.tar.xz{,.sig})
md5sums=('12a3c829301a4fd6586a57d3fcf196dc'
         'SKIP')
validpgpkeys=('CFE2BE707B538E8B26757D84151308092983D606')   # Gary Vaughan

build() {
  cd ${srcdir}/$pkgname-$pkgver  
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/$pkgname-$pkgver
  make check
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make prefix=${pkgdir}/usr install
}
