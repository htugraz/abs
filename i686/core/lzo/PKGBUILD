# $Id: PKGBUILD 231229 2015-02-10 17:56:56Z anatolik $
# Contributor: Low Kian Seong <fastmail_low@speedymail.org>
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=lzo
pkgver=2.09
pkgrel=1
pkgdesc="Portable lossless data compression library"
arch=('i686' 'x86_64')
url="http://www.oberhumer.com/opensource/lzo"
license=('GPL')
depends=('glibc')
provides=("lzo2=$pkgver")
replaces=('lzo2')
conflicts=('lzo2')
source=(http://www.oberhumer.com/opensource/lzo/download/lzo-${pkgver}.tar.gz)
sha1sums=('e2a60aca818836181e7e6f8c4f2c323aca6ac057')

build() {
  cd lzo-${pkgver}
  ./configure --prefix=/usr --enable-shared
  make

  # build minilzo
  gcc $CFLAGS -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
  gcc $LDFLAGS -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 minilzo/minilzo.o
}

check() {
  cd lzo-${pkgver}
  make test # Larger test
  make check
}

package() {
  cd lzo-${pkgver}
  make DESTDIR=${pkgdir} install

  # install minilzo
  install -m 755 libminilzo.so.0 ${pkgdir}/usr/lib
  install -p -m 644 minilzo/minilzo.h ${pkgdir}/usr/include/lzo
  cd ${pkgdir}/usr/lib
  ln -s libminilzo.so.0 libminilzo.so
}
