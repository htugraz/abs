# $Id: PKGBUILD 197825 2013-10-30 11:05:08Z allan $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libpipeline
pkgver=1.2.4
pkgrel=1
pkgdesc="a C library for manipulating pipelines of subprocesses in a flexible and convenient way"
arch=('i686' 'x86_64')
url="http://libpipeline.nongnu.org/"
license=('GPL')
depends=('glibc')
source=(http://download.savannah.gnu.org/releases/libpipeline/$pkgname-$pkgver.tar.gz{,.sig})
md5sums=('a98b07f6f487fa268d1ebd99806b85ff'
         'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
