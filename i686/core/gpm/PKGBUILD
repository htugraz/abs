# $Id: PKGBUILD 246458 2015-09-17 20:59:51Z foutrelis $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gpm
pkgver=1.20.7
pkgrel=5
pkgdesc="A mouse server for the console and xterm"
arch=('i686' 'x86_64')
url="http://www.nico.schottelius.org/software/gpm/"
license=('GPL')
depends=('bash')
options=('!makeflags')
install=gpm.install
source=(http://www.nico.schottelius.org/software/gpm/archives/${pkgname}-${pkgver}.tar.lzma \
        gpm.sh gpm.service)
sha1sums=('8d9f3655c80ce7667d31ede2a100b44986480369'
          '88fe5ff10916c68a87abc8418a56eb0073f69fa9'
          'f9bde96eb9c78da9f225bcf40822647f6592cf23')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m755 ../gpm.sh "${pkgdir}/etc/profile.d/gpm.sh"
  install -D -m644 ../gpm.service "${pkgdir}/usr/lib/systemd/system/gpm.service"

# library fixes
  cd "${pkgdir}/usr/lib/"
  ln -s libgpm.so.2.* libgpm.so
  chmod 755 "${pkgdir}"/usr/lib/libgpm.so.*
}
