# $Id: PKGBUILD 193785 2013-08-31 10:51:10Z tpowa $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=reiserfsprogs
pkgver=3.6.24
pkgrel=1
pkgdesc="Reiserfs utilities"
arch=('i686' 'x86_64')
url="http://www.kernel.org/"
license=('GPL')
groups=('base')
depends=('util-linux')
source=("http://ftp.kernel.org/pub/linux/kernel/people/jeffm/reiserfsprogs/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('66787380fb418ff7d88a23e47cda7af6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
