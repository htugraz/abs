# $Id: PKGBUILD 207128 2014-03-07 09:14:44Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=libnl
pkgver=3.2.24
pkgrel=1
pkgdesc="Library for applications dealing with netlink sockets"
arch=(i686 x86_64)
url="http://www.infradead.org/~tgr/libnl/"
license=(GPL)
depends=(glibc)
backup=(etc/libnl/classid etc/libnl/pktloc)
source=("$url/files/$pkgname-$pkgver.tar.gz")
sha256sums=('fb8d6e5dc8af5b85bc6d00a71582a68a01e6a3f7d1664d4a646e289a99dd6816')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
