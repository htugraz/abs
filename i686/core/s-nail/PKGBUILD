# $Id: PKGBUILD 240739 2015-06-13 22:29:08Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s@arch@gmail.com>
# Contributor: Andreas Wagner <Andreas.Wagner@em.uni-frankfurt.de>

pkgname=s-nail
pkgver=14.8.1
pkgrel=1
pkgdesc='Mail processing system with a command syntax reminiscent of ed'
url='http://sdaoden.users.sourceforge.net/code.html#s-nail'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('openssl' 'krb5')
optdepends=('smtp-forwarder: for sending mail')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver//./_}.tar.xz")
sha1sums=('0efd05f9e079adb5852876d308b18f56c8f9100f')

groups=('base')
backup=('etc/mail.rc')
replaces=('mailx' 'mailx-heirloom' 'heirloom-mailx')
provides=('mailx' 'mailx-heirloom' 'heirloom-mailx')
conflicts=('mailx' 'mailx-heirloom' 'heirloom-mailx')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	CFLAGS+=" $CPPFLAGS"
	make \
		PREFIX=/usr \
		SYSCONFDIR=/etc \
		MANDIR=/usr/share/man \
		MAILSPOOL=/var/spool/mail \
		SID= NAIL=mail \
		WANT_AUTOCC=0 \
		config

	make build
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" packager-install
	ln -sf mail "${pkgdir}"/usr/bin/mailx
	ln -sf mail.1.gz "${pkgdir}"/usr/share/man/man1/mailx.1.gz
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
