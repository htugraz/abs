# $Id: PKGBUILD 197187 2013-10-23 17:10:51Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s@arch@gmail.com>
# Contributor: Andreas Wagner <Andreas.Wagner@em.uni-frankfurt.de>

pkgname=s-nail
pkgver=14.4.5
pkgrel=1
pkgdesc='Mail processing system with a command syntax reminiscent of ed'
url='http://sourceforge.net/projects/s-nail/'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('openssl')
optdepends=('smtp-forwarder: for sending mail')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver//./_}.tar.gz")
sha1sums=('cc972ddacdf9593739289eb0589ad0939255dfd7')

groups=('base')
backup=('etc/mail.rc')
replaces=('mailx' 'mailx-heirloom' 'heirloom-mailx')
provides=('mailx' 'mailx-heirloom' 'heirloom-mailx')
conflicts=('mailx' 'mailx-heirloom' 'heirloom-mailx')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's/s-nail/mail/g' -i cc-test.sh
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make \
		PREFIX=/usr \
		SYSCONFDIR=/etc \
		MANDIR=/usr/share/man \
		MAILSPOOL=/var/spool/mail \
		SID= NAIL=mail \
		config
	make \
		CFLAGS="${CFLAGS} ${CPPFLAGS}" \
		LDFLAGS="${LDFLAGS}" \
		build
}


check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" packager-install
	ln -sf mail "${pkgdir}"/usr/bin/mailx
	ln -sf mail.1.gz "${pkgdir}"/usr/share/man/man1/mailx.1.gz
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
