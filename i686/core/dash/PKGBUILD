# $Id: PKGBUILD 187022 2013-06-03 11:14:59Z allan $
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=dash
pkgver=0.5.7
pkgrel=4
pkgdesc="A POSIX compliant shell that aims to be as small as possible"
arch=('i686' 'x86_64')
url="http://gondor.apana.org.au/~herbert/dash/"
license=('BSD')
depends=('glibc')
install=dash.install
source=("http://gondor.apana.org.au/~herbert/dash/files/${pkgname}-${pkgver}.tar.gz")
sha1sums=('a3ebc16f2e2c7ae8adf64e5e62ae3dcb631717c6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --bindir=/usr/bin --mandir=/usr/share/man --exec-prefix=""
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # license
  install -m644 -D COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
