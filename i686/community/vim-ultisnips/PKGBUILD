# $Id: PKGBUILD 141697 2015-09-27 13:28:10Z lfleischer $
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=vim-ultisnips
pkgver=3.0
pkgrel=4
pkgdesc='TextMate-style snippets for Vim.'
arch=('any')
url='https://github.com/SirVer/ultisnips'
license=('GPL3')
depends=('vim' 'python')
groups=('vim-plugins')
replaces=('vim-snipmate')
install=vimdoc.install
makedepends=('git')
source=("${pkgname}.tar.gz"::"https://github.com/SirVer/ultisnips/archive/${pkgver}.tar.gz"
        git+https://github.com/honza/vim-snippets.git)
md5sums=('52d6df546c790e1cbe912fdeef8579ee'
         'SKIP')

package() {
  cd "${srcdir}/ultisnips-$pkgver"

  install -dm755 "$pkgdir/usr/share/vim/vimfiles/"
  cp -r -t "$pkgdir/usr/share/vim/vimfiles/" pythonx after doc ftdetect autoload\
      ftplugin plugin syntax utils

  cd "${srcdir}/vim-snippets"

  install -dm755 "$pkgdir/usr/share/vim/vimfiles/"
  cp -r -t "$pkgdir/usr/share/vim/vimfiles/" UltiSnips autoload snippets

  # compile .pyc and .pyo files (fixes FS#43121)
  find "${pkgdir}" -name '*.py' -exec python2 -mpy_compile {} +
  find "${pkgdir}" -name '*.py' -exec python2 -O -mpy_compile {} +
}
