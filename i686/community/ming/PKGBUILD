# Maintainer: Connor Behan <connor.behan@gmail.com>
# Contributor: mar77i <mysatyre at gmail dot com>

pkgname=ming
pkgver=0.4.5
pkgrel=2
pkgdesc="SWF output library"
arch=('i686' 'x86_64')
url="http://www.libming.net/"
license=('GPL' 'LGPL')
depends=('freetype2' 'libpng' 'flex')
makedepends=('bison' 'pkg-config' 'python2')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('73bcbcb837f3872ef1a936c539a2513c')

build() {
  cd "$srcdir"/$pkgname-$pkgver/
  LDFLAGS="${LDFLAGS/ -Wl,--as-needed/}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --enable-python
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/
  make DESTDIR="$pkgdir" install
}

