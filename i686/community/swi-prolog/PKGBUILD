# $Id: PKGBUILD 140591 2015-09-17 20:54:24Z foutrelis $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=7.2.3
pkgrel=2
pkgdesc='Prolog environment'
arch=('x86_64' 'i686')
url='http://www.swi-prolog.org/'
license=('GPL' 'LGPL')
depends=('gmp' 'readline' 'openssl' 'libarchive')
makedepends=('libxft' 'libjpeg' 'unixodbc' 'libxpm' 'libxinerama')
optdepends=('unixodbc: for using the odbc4pl library'
            'libjpeg: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxft: for using the pl2xpce library')
source=("http://www.swi-prolog.org/download/stable/src/swipl-$pkgver.tar.gz")
sha256sums=('43657d51b7c5887bc2d2bced50a9822b86a08a6841399b8e76ee877f51d646b5')

build() {
  cd "swipl-$pkgver"
  ./configure --prefix=/usr --with-world

  cd src
  ./configure --enable-readline --prefix=/usr

  make -C ..
}

check() {
  make -C "swipl-$pkgver" check || true
}

package() {
  make -C "swipl-$pkgver" DESTDIR="$pkgdir" install

  # Fix for FS#20873
  chmod +x "$pkgdir/usr/lib/swipl-$pkgver/library/dialect/sicstus/swipl-lfr.pl"
}

# getver: swi-prolog.org/download/stable
# vim:set ts=2 sw=2 et:
