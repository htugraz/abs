# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=2.0.0
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=(i686 x86_64)
url="https://github.com/linuxmint/cjs"
license=(GPL)
depends=(cairo gobject-introspection js185 dbus-glib)
makedepends=(gnome-common)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cjs/archive/${pkgver}.tar.gz")
sha256sums=('2fea6c491e6ce8567201f14c0e926181154d798d4811dd65da477c014bfb2a9a')

build() {
  cd $pkgname-$pkgver

  # Python2 fix
  sed -i 's|#! /usr/bin/env python|#! /usr/bin/env python2|' \
  		scripts/make-tests

  ./autogen.sh --prefix=/usr --disable-static --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname-$pkgver
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
