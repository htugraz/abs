# $Id: PKGBUILD 137688 2015-08-02 11:15:05Z arcanis $
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.0.8
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN.GB2312   "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _path=${_lang%% *}
  _locale=${_path%%.*}

  pkgname+=(eric-i18n-${_locale,,} eric-qt4-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_path}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale,,}() {
      _package ${_lang} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${2} language pack for Eric IDE"
  depends=("eric-common${3}>=${pkgver}")

  [ -z ${3} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('2713e8b0d4d909c81611cb81c40fcfca'
         'fb12367ab65ecd9ed0fa441399878c4c'
         '53edda4f215b489d40af314850e327ec'
         'b058f1d221506981a5670a32884df796'
         '5fe7616bf632c3770a0915bd5a4f1704'
         'eb97c043d7bfadab6db70efe046ad465'
         '944c94d5c096e0d0521da1c81c15ff9c'
         '0d2b8d515abf15c0a1dc69baa5068834'
         'b4967c58437e35b46ee65db88e60d927'
         '7ab7542bd3544ee36ced5fb958c23c89')
