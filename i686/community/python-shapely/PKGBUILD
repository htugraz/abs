# $Id: PKGBUILD 136938 2015-07-15 02:38:10Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Verelst <david dot verelst at gmail com>
# Contributor: richli

pkgbase=python-shapely
pkgname=(python-shapely python2-shapely)
pkgver=1.5.9
pkgrel=2
pkgdesc="Manipulation and analysis of geometric objects in the Cartesian plane"
arch=('i686' 'x86_64')
url="https://pypi.python.org/pypi/Shapely"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'geos' 'python-numpy' 'python2-numpy' 'cython' 'cython2')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://pypi.python.org/packages/source/S/Shapely/Shapely-${pkgver}.tar.gz")
md5sums=('b502824b154a49fbb8f33703d71557dd')

prepare() {
  cp -a Shapely-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/Shapely-$pkgver
  python setup.py build

  cd "$srcdir"/Shapely-$pkgver-py2
  python2 setup.py build
}

check() {
  # TODO: make the hack less dirty...

  cd "$srcdir"/Shapely-$pkgver
  mv shapely shapely.bak
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.4:$PYTHONPATH" py.test
  mv shapely.bak shapely

  cd "$srcdir"/Shapely-$pkgver-py2
  mv shapely shapely.bak
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" py.test2
  mv shapely.bak shapely
}

package_python-shapely() {
  depends=('geos' 'python')
  optdepends=('python-numpy: for shapely.vectorized submodule')

  cd "$srcdir"/Shapely-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Temporary workaround for file conflict, also the path was not following FHS
  # https://github.com/Toblerity/Shapely/issues/287
  mv "$pkgdir"/usr/{shapely,lib/$pkgname}
}
 
package_python2-shapely() {
  depends=('geos' 'python2')
  optdepends=('python2-numpy: for shapely.vectorized submodule')

  cd "$srcdir"/Shapely-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/{shapely,lib/$pkgname}
}

# vim:set ts=2 sw=2 et:
