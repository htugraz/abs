# $Id$
# Maintainer: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor:  Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=2.6.4
pkgrel=2
pkgdesc="Library with common API for various Cinnamon modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(gtk3 libxkbfile python2 glib2 libgsystem)
makedepends=(intltool gobject-introspection gnome-common python2)
url="https://github.com/linuxmint/cinnamon-desktop"
install=cinnamon-desktop.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-desktop/archive/${pkgver}.tar.gz"
        "gnome-3.14.patch")
sha256sums=('d40fc57202408476ff0bfa191958f16de07cf4c00d911bef57c3107c8e483970'
            'c4df93c49fd3f9f8bc359cbad5d5526eef80ee5c34680f8ba62c67c3dbb7eca5')

prepare() {
  cd $pkgname-$pkgver
  # Update configuration for GNOME 3.14
  patch -Np1 -i ../gnome-3.14.patch
  # Arch uses systemd
  sed -i 's|false|true|g' schemas/org.cinnamon.desktop.session.gschema.xml.in.in
  # Fix build failure 
  sed -i 's|subdir-objects||g' configure.ac
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}