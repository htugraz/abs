# $Id: PKGBUILD 135275 2015-06-13 18:28:59Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=zeromq
pkgver=4.1.1
pkgrel=1
pkgdesc="Fast messaging system built on sockets.  C and C++ bindings.  aka 0MQ, ZMQ."
arch=('i686' 'x86_64')
url="http://www.zeromq.org"
license=('LGPL')
depends=('gcc-libs' 'util-linux' 'libsodium' 'libpgm')
makedepends=('asciidoc' 'xmlto')
options=('staticlibs')
source=(http://download.zeromq.org/$pkgname-$pkgver.tar.gz
        https://raw.githubusercontent.com/zeromq/cppzmq/ac705f6047/zmq.hpp)
md5sums=('0a4b44aa085644f25c177f79dc13f253'
         'c1388ca58d417610b6c00d66c81a93f2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure prefix=/usr --with-pgm --with-libsodium \
    --with-documentation --enable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/zmq.hpp" "$pkgdir/usr/include/zmq.hpp"
}

