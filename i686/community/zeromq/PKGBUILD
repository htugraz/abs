# $Id: PKGBUILD 99117 2013-10-24 16:13:47Z mtorromeo $
# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=zeromq
pkgver=4.0.1
pkgrel=5
pkgdesc="Fast messaging system built on sockets, C and C++ bindings.  aka 0MQ, ZMQ."
arch=('i686' 'x86_64')
url="http://www.zeromq.org"
license=('LGPL')
depends=('gcc-libs' 'util-linux' 'libsodium')
makedepends=('python2')
options=('staticlibs')
source=(http://download.zeromq.org/$pkgname-$pkgver.tar.gz
        https://raw.github.com/zeromq/cppzmq/master/zmq.hpp
        LIBZMQ-569.patch)
md5sums=('e738c310d135e20fa48c5b348da18e22'
         '388ddb07fdd88b040769e8f4c9b397fa'
         '2ef4f92027ec2b88e8eec86b2cccf2e4')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i "$srcdir/LIBZMQ-569.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure prefix=/usr --with-pgm --with-libsodium
  sed -i 's/python$/&2/' foreign/openpgm/build-staging/openpgm/pgm/{Makefile,version_generator.py}
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/zmq.hpp" "$pkgdir/usr/include/zmq.hpp"
}


