# $Id: PKGBUILD 94268 2013-07-18 13:44:10Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>

pkgname=clojure
pkgver=1.5.1
pkgrel=2
pkgdesc='LISP dialect for the JVM'
url='http://www.clojure.org/'
arch=('any')
license=('CPL')
depends=('java-environment' 'sh')
optdepends=('rlwrap: friendlier shell with readline support')
install=clojure.install
source=("http://repo1.maven.org/maven2/org/$pkgname/$pkgname/$pkgver/clojure-$pkgver.zip"
        'clj.sh'
        "$pkgname.sh")
sha256sums=('fe7b32ca36c1a5451fe6fe0ea8ad25f7bc04beeb63e171e10464efd64c94f1e1'
            '0d0f9e7db77f4b5f507ee18e140c29138b7bff5c4ca9d0b963dfd657c6e3c157'
            'b3fa6257dad10fb39851e87563269b80b2473623126595d9c279bbd90e97f052')

package() {
  cd "$srcdir"

  install -Dm755 clj.sh "$pkgdir/usr/bin/clj"
  install -Dm755 clj.sh "$pkgdir/usr/bin/clojure"
  install -Dm755 "$pkgname.sh" "$pkgdir/etc/profile.d/$pkgname.sh"
  install -Dm644 "$pkgname-$pkgver/$pkgname-$pkgver.jar" \
    "$pkgdir/usr/share/$pkgname/$pkgname.jar"
}

# vim:set ts=2 sw=2 et:
