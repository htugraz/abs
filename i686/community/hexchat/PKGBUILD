# $Id: PKGBUILD 112696 2014-06-05 09:20:57Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: TingPing <tingping@tingping.se>

pkgname=hexchat
pkgver=2.10.0
pkgrel=2
pkgdesc='A popular and easy to use graphical IRC (chat) client'
arch=('i686' 'x86_64')
url='http://hexchat.github.io/'
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gtk2' 'libcanberra' 'libnotify' 'libproxy' 'pciutils')
makedepends=('intltool' 'iso-codes' 'perl' 'python')
optdepends=('enchant: Spell check'
            'perl: Perl plugin'
            'python: Python plugin')
install="${pkgname}.install"
source=("http://dl.hexchat.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a0247f1e12379154d0719d9c6861dc22817b588562653bb9d3626863d8eca916')

prepare() {
  cd ${pkgname}-${pkgver}

  sed 's/CFLAGS="$PERL_CFLAGS"/CFLAGS="$PERL_CFLAGS -fuse-ld=gold"/' -i configure
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --enable-{perl,python='python3.4',textfe}
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
