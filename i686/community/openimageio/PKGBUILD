# $Id: PKGBUILD 126109 2015-01-18 16:07:28Z svenstaro $
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=openimageio
pkgver=1.4.15
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=(i686 x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'intel-tbb')
makedepends=('cmake' 'qt4' 'python2' 'boost' 'mesa' 'git')
optdepends=('qt4: iv image viewer'
            'python2: bindings support')
source=(git+git://github.com/OpenImageIO/oiio.git#tag=Release-${pkgver})
md5sums=('SKIP')

prepare() {
  cd oiio

  curl "https://github.com/OpenImageIO/oiio/commit/e1150f22f012d8d86f14534eda14bec960e96f3e.patch"|patch -Np1
  # https://github.com/OpenImageIO/oiio/issues/987
  #git revert -n 6f0e019
}

build() {
  cd oiio

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
      -DUSE_EXTERNAL_TBB=ON \
      -DOIIO_BUILD_TESTS=OFF \
      ..
  make
}

package() {
  cd oiio/build

  make DESTDIR="$pkgdir" install

  # license
  cd ..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
