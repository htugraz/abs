# $Id: PKGBUILD 120331 2014-10-07 07:02:06Z andrea $
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Zamprogno <stefano dot zamprogno at gmail dot com>

pkgname=knemo
pkgver=0.7.6
pkgrel=5
pkgdesc="The KDE Network Monitor"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php?content=12956"
license=('GPL')
depends=('kdebase-workspace' 'wireless_tools' 'iw' 'libnl' 'gtk-update-icon-cache')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
options=('libtool')
source=(https://launchpad.net/ubuntu/+archive/primary/+files/knemo_$pkgver.orig.tar.bz2)
md5sums=('42b35d4d48698a2f514adeb2ca872408')

prepare() {
  mkdir build
}

build() {
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # remove knemo autostart
  # rm -rf ${pkgdir}/usr/share/autostart
}
