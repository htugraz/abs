# $Id: PKGBUILD 153065 2015-12-11 22:09:40Z arojas $
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Zamprogno <stefano dot zamprogno at gmail dot com>

pkgname=knemo
pkgver=0.7.7.git20151003
pkgrel=1
pkgdesc="The KDE Network Monitor"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php?content=12956"
license=('GPL')
depends=('kdelibs4support' 'libksysguard' 'knotifyconfig' 'wireless_tools' 'iw' 'gtk-update-icon-cache')
makedepends=('extra-cmake-modules' 'plasma-framework' 'kdoctools')
install=${pkgname}.install
options=('libtool')
#source=(http://kde-apps.org/CONTENT/content-files/12956-${pkgname}-${pkgver}.tar.xz)
source=("http://quickgit.kde.org/?p=knemo.git&a=snapshot&h=7efb4f62344c245e9eb6822df284f8c7f4e97f2f&fmt=tgz")
md5sums=('ab1f02cd06647278141c609fba5f61fb')

prepare() {
  mkdir build
}

build() {
  cd build

  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
