# $Id: PKGBUILD 111720 2014-05-22 20:11:46Z dwallace $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
pkgbase=python-oslo-config
pkgname=(python-oslo-config python2-oslo-config)
_pythonname=oslo.config
pkgver=1.3.0
pkgrel=4
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/1.1.0"
license=('GPL')
makedepends=('python2-setuptools' python-setuptools)
options=(!emptydirs)
source=("https://pypi.python.org/packages/source/o/$_pythonname/$_pythonname-$pkgver.tar.gz")
md5sums=('585d980bb831e6011276e872109f87de')

prepare(){
  cp -a $_pythonname-$pkgver{,-2}
  find $_pythonname-$pkgver-2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build(){
  cd "$srcdir/$_pythonname-$pkgver"
  python setup.py build
  cd "$srcdir/$_pythonname-$pkgver-2"
  python2 setup.py build
}
package_python-oslo-config() {
  cd "$srcdir/$_pythonname-$pkgver"
  depends=(python)
  python setup.py install --root="$pkgdir/" --optimize=1
}
package_python2-oslo-config() {
  cd "$srcdir/$_pythonname-$pkgver"
  depends=(python2)
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
