# $Id: PKGBUILD 127939 2015-02-18 15:21:21Z dwallace $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
pkgbase=python-oslo-config
pkgname=(python-oslo-config python2-oslo-config)
_pythonname=oslo.config
pkgver=1.6.1
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('GPL')
makedepends=('python2-setuptools' python-setuptools)
options=(!emptydirs)
source=("https://pypi.python.org/packages/source/o/$_pythonname/$_pythonname-$pkgver.tar.gz")
md5sums=('d51fd561300f46945dd24977c429280c')

prepare(){
  cp -a $_pythonname-$pkgver{,-2}
  find $_pythonname-$pkgver-2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
  sed -i '/argparse/d' "$_pythonname-$pkgver"{,-2}/requirements.txt
}

build(){
  cd "$srcdir/$_pythonname-$pkgver"
  python setup.py build
  cd "$srcdir/$_pythonname-$pkgver-2"
  python2 setup.py build
}
package_python-oslo-config() {
  cd "$srcdir/$_pythonname-$pkgver"
  depends=(python)
  python setup.py install --root="$pkgdir/" --optimize=1
}
package_python2-oslo-config() {
  cd "$srcdir/$_pythonname-$pkgver"
  depends=(python2)
  python2 setup.py install --root="$pkgdir/" --optimize=1
  mv $pkgdir/usr/bin/oslo-config-generator $pkgdir/usr/bin/oslo-config-generator2
}

# vim:set ts=2 sw=2 et:
