# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christos Nouskas nous <nous at archlinux.us>

pkgname=smokeping
pkgver=2.6.10
pkgrel=1
pkgdesc="A tool to keep track of your network latency"
arch=('any')
url="http://oss.oetiker.ch/smokeping/"
license=('GPL2')
depends=('perl-config-grammar' 'perl-digest-hmac' 'perl-libwww' 'rrdtool' 'perl-fcgi' 'perl-cgi-fast')
source=(http://oss.oetiker.ch/smokeping/pub/smokeping-${pkgver}.tar.gz
service)
sha256sums=('0121672951f47bd45575898c3e4d767e22cc692dd272d723b422515fd3cc641c'
            'd8269fb14221d679bd4eb7e7028eda47ac5bb24490ecff4ec7e949c6209688be')
install=install

build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc/smokeping --with-htdocs-dir=/srv/http/smokeping
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/smokeping.service"
  chmod 700 "$pkgdir/etc/smokeping"
}

