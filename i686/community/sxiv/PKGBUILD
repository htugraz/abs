# $Id: PKGBUILD 97145 2013-09-14 07:15:14Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Bert Muennich <muennich at informatik.hu-berlin.de>
# Contributor: Brad Fanella <bradfanella@archlinux.us>

pkgname=sxiv
pkgver=1.1.1
pkgrel=2
pkgdesc='Simple X Image Viewer'
arch=('i686' 'x86_64')
license=('GPL2')
install=sxiv.install
url='https://github.com/muennich/sxiv'
depends=('libx11' 'xproto' 'imlib2' 'giflib' 'desktop-file-utils')
source=(https://github.com/muennich/sxiv/archive/v$pkgver.tar.gz
        config.h)
sha256sums=('30569b7cb1568a7f60d102f6eb21f25c93de42243a1c8ac3c1a521d58b08281f'
            '61515a87692849c942bfc3cce8b4c0f19139b4a1e9b6375a994cc541c64aab5c')

build() {
  cd sxiv-$pkgver
  cp "$srcdir"/config.h .
  make
}

package() {
  cd sxiv-$pkgver

  make PREFIX="$pkgdir"/usr install
  install -D -m644 sxiv.desktop \
    "$pkgdir"/usr/share/applications/sxiv.desktop
}
