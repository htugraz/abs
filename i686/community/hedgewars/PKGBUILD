# $Id: PKGBUILD 97071 2013-09-12 10:11:10Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=hedgewars
pkgver=0.9.19
pkgrel=4
pkgdesc="Free Worms-like turn based strategy game"
arch=('i686' 'x86_64')
url="http://hedgewars.org"
license=('GPL' 'custom')
depends=('qt4' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'lua51' 'ffmpeg' 'glut')
makedepends=('fpc' 'cmake'
	     # haskell specific
	     'ghc'
	     'haskell-network'
	     'haskell-bytestring-show'
	     'haskell-utf8-string'
	     'haskell-dataenc'
	     'haskell-hslogger'
	     'haskell-vector'
	     'haskell-random')
source=(http://download.gna.org/hedgewars/hedgewars-src-$pkgver.tar.bz2
	hedgewars.png
	build-fix.patch
	ffmpeg2.0.patch)
md5sums=('51d84ad6ea00e9894e6119b118b4a523'
         'eeb14d50df39063549ac5eca9dbc65d1'
         'a9d7b0ba72cbeb7fc74fce459db9dd90'
         'da84d64a252579236d3ff0cc6e008e72')

prepare() {
  cd $pkgname-src-$pkgver
  patch -Rp1 <$srcdir/build-fix.patch
  patch -Np1 -i ../ffmpeg2.0.patch
}

build() {
  cd $pkgname-src-$pkgver
  cmake \
	-DCMAKE_BUILD_TYPE="Release" \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DDATA_INSTALL_DIR=/usr/share/hedgewars \
	-DWITH_SERVER=1 \
	-DSYSTEM_PHYSFS=OFF .
  make
}

package() {
  cd $pkgname-src-$pkgver

  make DESTDIR=$pkgdir install

  install -D -m644 Fonts_LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/Fonts_LICENSE.txt
  install -D -m644 $srcdir/hedgewars.png $pkgdir/usr/share/pixmaps/hedgewars.png
  install -D -m644 share/hedgewars/Data/misc/hedgewars.desktop $pkgdir/usr/share/applications/hedgewars.desktop
}
