# $Id: PKGBUILD 123256 2014-11-28 15:47:36Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=14.1
pkgrel=1
pkgdesc="Download from Usenet using .nzb files"
arch=('i686' 'x86_64')
url="http://nzbget.sourceforge.net/"
license=('GPL')
depends=('libxml2' 'python')
changelog=$pkgname.changelog
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a16b816b61f7035cc373e9b77094ca474d5b7b7f7ceff5fa8818249181db4b18')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --enable-parcheck --with-tlslib=OpenSSL
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/$pkgname
  install -m644 -t "${pkgdir}"/usr/share/$pkgname AUTHORS ChangeLog README
}
