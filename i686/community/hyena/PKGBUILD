# $Id: PKGBUILD 107243 2014-03-15 11:19:26Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Claudio Riva <firetux83@gmail.com>

pkgname=hyena
pkgver=0.5
pkgrel=4
pkgdesc="Library for creating C Sharp applications"
arch=('any')
url="https://wiki.gnome.org/Hyena"
license=('MIT')
depends=('gtk-sharp-2')
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2
        fix-build.patch)
sha256sums=('eb7154a42b6529bb9746c39272719f3168d6363ed4bad305a916ed7d90bc8de9'
            '7528a78b710c253d787c3cc8239d42f85290bad9253eb0575b87d9dd9034609b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build
  # https://bugzilla.gnome.org/show_bug.cgi?id=633333
  patch -Np1 -i "$srcdir/fix-build.patch"

  # Build for mono 4.0
  sed -i 's/MONO_2_0/MONO_4_0/' configure.ac
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  aclocal -I build/m4/shamrock
  automake --gnu
  autoconf
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING
}
