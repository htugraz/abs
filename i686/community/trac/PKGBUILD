# $Id: PKGBUILD 128724 2015-03-04 15:25:00Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Rubin Simons <rubin@xs4all.nl>

pkgname=trac
pkgver=1.0.4
pkgrel=2
pkgdesc="Trac Integrated SCM & Project Management using Apache & Subversion."
arch=('any')
url="http://trac.edgewall.org/"
license=('BSD')
depends=(python2 python2-genshi python2-babel python2-setuptools)
makedepends=(clearsilver)
install=trac.install
backup=("etc/conf.d/tracd.conf")
source=(http://ftp.edgewall.com/pub/trac/Trac-$pkgver.tar.gz
	license.txt
	tracd.service)
md5sums=('20f1f76b4d993cecab13b7b4a93547f5'
         'af9a4064fd7a5759fee0fc288f749977'
         '74087f86566113b79018a93e02fbde18')

prepare() {
  cd $srcdir/Trac-$pkgver
  # python2 fix
  for file in trac/admin/console.py trac/test.py trac/tests/functional/*.py \
              trac/web/*.py trac/wiki/default-pages/TracFastCgi \
              contrib/cgi-bin/trac.{f,}cgi; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
      sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done
}

package() {
  cd $srcdir/Trac-$pkgver
  python2 setup.py install --prefix=/usr --root=$pkgdir
  install -D -m644 $srcdir/license.txt $pkgdir/usr/share/licenses/trac/license.txt
  install -D -m0755 contrib/cgi-bin/trac.cgi $pkgdir/usr/share/trac/trac.cgi
  install -D -m0755 contrib/cgi-bin/trac.fcgi $pkgdir/usr/share/trac/trac.fcgi
  install -Dm0644 $srcdir/tracd.service $pkgdir/usr/lib/systemd/system/tracd.service
}
