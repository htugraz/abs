# $Id: PKGBUILD 100152 2013-11-01 20:24:18Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Ben <ben@benmazer.net>

pkgname=fox
pkgver=1.6.49
pkgrel=3
pkgdesc="Free Objects for X: GUI Toolkit for C++"
arch=('i686' 'x86_64')
url="http://www.fox-toolkit.org/"
license=('LGPL' 'custom')
depends=('bzip2' 'libxcursor' 'libxft' 'libxrandr' 'libxi' 'libpng' 'libtiff' 'glu')
makedepends=('mesa')
optdepends=('perl')
source=(http://ftp.fox-toolkit.org/pub/fox-$pkgver.tar.gz)
md5sums=('e120647d26a27ed37cd5fed8b75c8a3d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  export CPPFLAGS="$CPPFLAGS -I/usr/include/freetype2"
  ./configure   --prefix=/usr \
                --enable-release \
                --with-xft=yes \
                --with-opengl=yes \
                --with-xim \
                --with-xshm \
                --with-shape \
                --with-xcursor \
                --with-xrender \
                --with-xrandr \
                --with-xfixes \
                --with-xinput
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  cd ${srcdir}/${pkgname}-${pkgver}/tests
  make ControlPanel
  install -m755 ${srcdir}/${pkgname}-${pkgver}/tests/.libs/ControlPanel ${pkgdir}/usr/bin/
  install -Dm644 ../LICENSE_ADDENDUM ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
