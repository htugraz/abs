# $Id: PKGBUILD 112025 2014-05-28 18:14:20Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>

pkgname=gmtk
pkgver=1.0.9
pkgrel=1
pkgdesc='Common functions for gnome-mplayer and gecko-mediaplayer.'
arch=('i686' 'x86_64')
url='http://gmtk.googlecode.com/'
license=('GPL')
depends=('glib2' 'gtk3' 'dconf')
makedepends=('intltool' 'pkg-config')
source=("https://${pkgname}.googlecode.com/svn/packages/${pkgname}-${pkgver}.tar.gz")
md5sums=('9184658014ca7b325b569ac7417e4860')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CPPFLAGS="$CPPFLAGS $(pkg-config --cflags gtk+-3.0)"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-gtk3
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
