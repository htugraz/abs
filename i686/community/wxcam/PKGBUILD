# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=wxcam
pkgver=1.1
pkgrel=1
arch=('i686' 'x86_64')
license=(GPL)
pkgdesc="Webcam application for linux"
# version of CImg library needed to compile wxcam -
# http://cimg.sourceforge.net/
_CImg_ver=1.4.9
url="http://wxcam.sourceforge.net"
makedepends=('intltool>=0.35.0')
depends=('libglade' 'mjpegtools' 'perlxml' 'wxgtk' 'xvidcore' 'v4l-utils' 'alsa-lib')
source=(http://downloads.sourceforge.net/wxcam/${pkgname}-${pkgver}.tar.bz2
	    http://downloads.sourceforge.net/cimg/CImg-${_CImg_ver}.zip)
md5sums=('c9ea8991b6375c1709a7ce4e61fcc263'
         'a07cba03f6d66a9970e0b3fcc230bddc')

build() {
    cp $srcdir/CImg-${_CImg_ver}/CImg.h $srcdir/$pkgname-$pkgver

    cd $srcdir/$pkgname-$pkgver
    ./configure --prefix=/usr
    make
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir install

    mv $pkgdir/usr/doc $pkgdir/usr/share/
}
