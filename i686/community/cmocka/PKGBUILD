# $Id: PKGBUILD 112515 2014-06-03 01:07:06Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=cmocka
url='http://cmocka.org/'
pkgver=0.4.1
pkgrel=1
pkgdesc='an elegant unit testing framework for C with support for mock objects'
arch=(i686 x86_64)
license=(GPL)
depends=(glibc)
makedepends=(cmake)
source=(http://git.cryptomilk.org/projects/cmocka.git/snapshot/cmocka-$pkgver.tar.bz2)
sha1sums=('215378198f489033d982122e34168d9d225e2f5b')

build() {
  rm -rf build; mkdir build
  cd build
  cmake ../$pkgname-$pkgver -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DUNIT_TESTING=ON
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"
}
