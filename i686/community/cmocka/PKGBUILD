# $Id: PKGBUILD 127988 2015-02-19 16:13:05Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=cmocka
url='http://cmocka.org/'
pkgver=1.0.0
pkgrel=1
pkgdesc='an elegant unit testing framework for C with support for mock objects'
arch=(i686 x86_64)
license=(GPL)
depends=(glibc)
makedepends=(cmake)
source=(http://git.cryptomilk.org/projects/cmocka.git/snapshot/cmocka-$pkgver.zip)
sha1sums=('e4bcc4358549ca83ad792ae0d911194c800ab627')

build() {
  rm -rf build; mkdir build
  cd build
  cmake ../$pkgname-$pkgver -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DUNIT_TESTING=ON
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"
}
