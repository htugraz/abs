# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Zalewski <lcamtuf[at]coredump[dot]cx>
# Contributor: cbreaker <cbreaker[at]tlen[dot]pl>]
# Contributor: Tino Reichardt
# Contributor: Timoth√©e Ravier <tim[at]siosm[dot]fr>

pkgname=p0f
pkgver=3.08b
pkgrel=2
pkgdesc="Purely passive TCP/IP traffic fingerprinting tool"
url="http://lcamtuf.coredump.cx/p0f3/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libpcap')
source=(http://lcamtuf.coredump.cx/p0f3/releases/${pkgname}-${pkgver}.tgz)
sha512sums=('283dc76bd29350a46999fb5ef8bc6d3acc28975ca3ecf34e42348b0806b2a97aa1670f334b80ab086a6e09d1e2cdff82f387f9142f95fb42e270ebc55b04f68b')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's|/usr/local/|/usr/|g' -i build.sh
  sed -e "s|-fstack-protector-all|$CFLAGS|g" -i build.sh
  sed -e "s|-g -ggdb||g" -i build.sh
  sed -e "s|-Wl,-z,relro -pie|$LDFLAGS|g" -i build.sh
  sed -e 's|"p0f.fp"|"/etc/p0f/p0f.fp"|g' -i config.h
  sed -e "s|-g -ggdb|$CFLAGS|g" -i tools/Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
  cd tools
  make p0f-client
  make p0f-sendsyn
  make p0f-sendsyn6
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 p0f "${pkgdir}/usr/bin/p0f"
  install -Dm 644 p0f.fp "${pkgdir}/etc/p0f/p0f.fp"
  install -Dm 644 docs/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 docs/README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -m 755 tools/{p0f-client,p0f-sendsyn,p0f-sendsyn6} "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
