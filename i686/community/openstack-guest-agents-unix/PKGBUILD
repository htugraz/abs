# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
pkgname=openstack-guest-agents-unix
pkgver=1.39.0
epoch=1
pkgrel=2
pkgdesc="This is the Unix Guest Agent for Openstack"
arch=('i686' 'x86_64')
url="https://github.com/rackerlabs/openstack-guest-agents-unix"
license=('GPL')
depends=('python2-simplejson' 'python2-crypto' 'patchelf' 'python2-xenstore')
makedepends=('git')
source=("$pkgname::git://github.com/rackerlabs/${pkgname%-git}#tag=v$pkgver")
md5sums=('SKIP')

prepare(){
  cd "$srcdir/$pkgname"
  find . -name \*.py -exec sed -i '1s/python$/&2/' {} +
  git cherry-pick -n 387d4e77695598a07be9e4593c5b236b81a1aeea
  git cherry-pick -n f9a365e7a9543d407ca9d75164688faa0ff53528
}

build() {
  cd "$srcdir/$pkgname"
  sh autogen.sh
  ./configure CC=gcc --sbindir=/usr/bin
  make
}

package() {
  make -C "$srcdir/$pkgname" bintar
  tar -C "$pkgdir" -xf $srcdir/$pkgname/nova-agent-Linux-$CARCH-${pkgver}.tar.gz
  install -Dm644 $pkgname/scripts/systemd/nova-agent.service \
    $pkgdir/usr/lib/systemd/system/nova-agent.service
}

# vim:set ts=2 sw=2 et:
