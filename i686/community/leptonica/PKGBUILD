# $Id: PKGBUILD 114932 2014-07-04 16:39:54Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Christoph Drexler <chrdr at gmx dot at>

pkgname=leptonica
pkgver=1.71
pkgrel=1
pkgdesc="Software that is broadly useful for image processing and image analysis applications"
arch=('i686' 'x86_64')
url="http://www.leptonica.com/"
license=('custom')
depends=('giflib' 'libjpeg' 'libpng' 'libtiff' 'zlib' 'libwebp')
source=(http://www.leptonica.com/source/leptonica-${pkgver}.tar.gz
	giflib6.patch)
md5sums=('9840f758ff83ca0b5ec847a9fa8099b6'
         'deeea9d5dccd9a0fb4e0e4433db49086')

prepare() {
  cd ${srcdir}/leptonica-${pkgver}
  patch -p1 <$srcdir/giflib6.patch
}

build() {
  cd ${srcdir}/leptonica-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/leptonica-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D leptonica-license.txt ${pkgdir}/usr/share/licenses/leptonica/leptonica-license.txt
}
