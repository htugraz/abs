# $Id: PKGBUILD 125509 2015-01-06 03:31:31Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Techlive Zheng <techlivezheng at gmail dot com>
# Contributor: peace4all <markspost at rocketmail dot com>

pkgname=dnscrypt-proxy
pkgver=1.4.3
pkgrel=1
pkgdesc="A tool for securing communications between a client and a DNS resolver"
arch=('i686' 'x86_64')
url="http://dnscrypt.org/"
license=('custom:ISC')
depends=('libsodium')
source=(http://download.dnscrypt.org/$pkgname/$pkgname-$pkgver.tar.gz
        conf.d.file
        dnscrypt-proxy.service)
backup=(etc/conf.d/dnscrypt-proxy)
sha512sums=('3aae9f20bc06087b0860a24488bcda117010a34218d7aef3c68a0f155f8164ce06a88865a9caf1ce5c5eaa8588518ccc2c8a61a97cc39f527f7b2b14cdf8c118'
            '26e5ce3198c25ff25542dc399f2bb5467ac349dbc11c2ebd6d3ba978ffaef09607088d9401e62ba33c6f50b8b29a59f56fe97d964f55e63ee9d8ca23862c3e00'
            '9a1072f83e83e55dcdd7b80d1943f60963fa240da8a50d7491cc219797c6b85b8d79d087c0f7bb9766a4d467dec58eaae2bef31b759652545ace65a6a8afb608')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sbindir=/usr/bin
  make -j2
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir"/{usr/share/{licenses,doc}/$pkgname,etc/conf.d,usr/lib/systemd/system}
  install -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 AUTHORS NEWS README README.markdown "$pkgdir"/usr/share/doc/$pkgname
  install -m 644 "$srcdir"/conf.d.file "$pkgdir"/etc/conf.d/$pkgname
  install -m 644 "$srcdir"/dnscrypt-proxy.service "$pkgdir"/usr/lib/systemd/system
}
