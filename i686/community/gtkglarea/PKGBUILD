# Maintainer: Connor Behan <connor.behan@gmail.com>
# Contributor: Andreas B. Wagner <AndreasBWagner@pointfree.net>
# Contributor: arjan <arjan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=gtkglarea
pkgver=2.0.1
pkgrel=3
pkgdesc="GTK/GL Area libraries"
arch=('i686' 'x86_64')
url="http://mono-project.com/GtkGLArea"
license=('GPL')
depends=('gtk2' 'glu')
source=("http://ftp.gnome.org/pub/GNOME/sources/$pkgname/2.0/$pkgname-$pkgver.tar.gz")
md5sums=('2a81a86cfa80a920a5454dd00fad2e1d')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	# DSO link fix
	LDFLAGS+=" -lm" \
		./configure --prefix=/usr \
		--enable-static=no
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
