# $Id: PKGBUILD 118246 2014-09-04 08:32:01Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Nishit Joseph (reachjlight at gmail dot com)
# Contributor: Martin Corley <Martin.Corley@ed.ac.uk>

pkgbase=python-openpyxl1
pkgname=('python2-openpyxl1' 'python-openpyxl1')
pkgver=1.8.6
pkgrel=2
pkgdesc="A Python library to read/write Excel 2007 xlsx/xlsm files, 1.x branch"
arch=('any')
url="http://openpyxl.readthedocs.org/en/1.8.6"
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/source/o/openpyxl/openpyxl-${pkgver}.tar.gz"
        "LICENCE")
md5sums=('a610d32ca5460eb78df4eb83c896ba34'
         'd5feddd06c61a04aea40b7aa1707e625')

prepare() {
  cd "$srcdir"
  cp -a openpyxl-${pkgver} openpyxl-py2-${pkgver}
  cd openpyxl-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/openpyxl-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/openpyxl-${pkgver}
  python setup.py build
}

package_python2-openpyxl1() {
  depends=('python2')
  optdepends=('python2-pillow: needed to include images')
  provides=('python2-openpyxl')
  conflicts=('python2-openpyxl')

  cd "$srcdir"/openpyxl-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

package_python-openpyxl1() {
  depends=('python')
  optdepends=('python-pillow: needed to include images')
  provides=('python-openpyxl')
  conflicts=('python-openpyxl')

  cd "$srcdir"/openpyxl-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

