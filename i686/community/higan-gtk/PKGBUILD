# $Id: PKGBUILD 126332 2015-01-20 23:21:23Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=higan-gtk
pkgver=094
pkgrel=3
pkgdesc='Nintendo multi-system emulator'
arch=('i686' 'x86_64')
url='http://byuu.org/'
license=('GPL3')
depends=('libao' 'libpulse' 'libxv' 'openal' 'sdl' 'xdialog')
makedepends=('mesa')
optdepends=('beat: Delta patcher')
conflicts=('higan-qt')
source=("http://files.byuu.org/download/higan_v${pkgver}-source.tar.xz"
        'higan-makefile.patch'
        'higan')
sha256sums=('5e149df9d50c1066eb97c0d66665428d75304da782bba5a87078b87fc311151b'
            '5069d4847ec6cbeb2b1633f2538b7402c1ab27b31dd2b0d48ee7b617f361db6a'
            'c799232a0f67c4b7f40fc6ad05325a6c04e86d78e40f361d02f168a16d11d048')

prepare() {
  cd higan_v${pkgver}-source

  patch -Np1 -i ../higan-makefile.patch
}

build() {
  cd higan_v${pkgver}-source

  make phoenix='gtk' -C ananke

  for p in {accuracy,balanced,performance}; do
    make phoenix='gtk' profile="$p"
    mv out/higan{,-$p}
    make clean
  done
}

package() {
  cd higan_v${pkgver}-source

  install -dm 755 "${pkgdir}"/usr/{bin,lib,share/{applications,pixmaps,higan/Video\ Shaders}}
  install -m 755 ../higan "${pkgdir}"/usr/bin/
  install -m 644 data/higan.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 data/higan.png "${pkgdir}"/usr/share/pixmaps/
  cp -dr --no-preserve='ownership' profile/* data/cheats.bml "${pkgdir}"/usr/share/higan/
  cp -dr --no-preserve='ownership' shaders/*.shader "${pkgdir}"/usr/share/higan/Video\ Shaders/

  install -m 644 ananke/libananke.so "${pkgdir}"/usr/lib/libananke.so.1
  ln -s libananke.so.1 "${pkgdir}"/usr/lib/libananke.so

  for p in {accuracy,balanced,performance}; do
    install -m 755 out/higan-$p "${pkgdir}"/usr/bin/
  done
}

# vim: ts=2 sw=2 et:
