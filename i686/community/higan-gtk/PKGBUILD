# $Id: PKGBUILD 96210 2013-08-21 19:09:14Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=higan-gtk
pkgver=093
pkgrel=2
pkgdesc="Nintendo multi-system emulator - GTK version"
arch=('i686' 'x86_64')
url="http://code.google.com/p/higan/"
license=('GPL3')
depends=('libao' 'libpulse' 'libxv' 'openal' 'sdl' 'xdialog')
makedepends=('gendesk' 'mesa')
optdepends=('beat: Delta patcher')
conflicts=('higan-qt')
source=("http://byuu.org/higan/release/higan_v${pkgver}-source.tar.xz"
        "http://higan.googlecode.com/files/purify_v03-source.tar.xz"
        'higan')
sha256sums=('6ad526ab7ac16a5b814f84235fbcd48946310af8645c42980b0063e3270ec538'
            '2020f0b5a744f08c24085a15a6c2ec1d7c4424bba6727f244e513b95c4247e38'
            'c799232a0f67c4b7f40fc6ad05325a6c04e86d78e40f361d02f168a16d11d048')

_profiles="accuracy balanced performance"

prepare() {
  gendesk -n --pkgname "higan" --pkgdesc "${pkgdesc}" --name='higan' --exec='higan %u'
  gendesk -n --pkgname "purify" --pkgdesc "ROM Purifier - GTK version" --name='purify' --exec='purify'

  cd higan_v${pkgver}-source

  sed -i "s/flags   += -I. -O3 -fomit-frame-pointer/flags   += -I. -fomit-frame-pointer -std=gnu++11/
          s/flags += -march=native/flags += $CXXFLAGS/g" Makefile
}

build() {
  cd "${srcdir}"/higan_v${pkgver}-source

# libananke
  make compiler=g++ platform=x phoenix=gtk flags="$CXXFLAGS -I.. -fomit-frame-pointer -std=gnu++11" -C ananke

# higan
  for _profile in ${_profiles}; do
    make compiler=g++ platform=x target=ethos phoenix=gtk profile=${_profile}
    mv out/higan{,-${_profile}}
    make clean
  done

# purify
  cd "${srcdir}"/purify_v03-source/purify
  make compiler=g++ platform=x phoenix=gtk flags="$CXXFLAGS -I. -fomit-frame-pointer -std=gnu++11" link='-s -lX11 -ldl -Wl,-export-dynamic'
}

package() {
  cd "${srcdir}"/higan_v${pkgver}-source

# Common files
  install -dm 755 "${pkgdir}"/usr/{bin,lib,share/{applications,pixmaps,higan/Video\ Shaders}}
  install -m 755 {..,"${pkgdir}"/usr/bin}/higan
  install -m 644 {..,"${pkgdir}"/usr/share/applications}/higan.desktop
  install -m 644 data/higan512.png "${pkgdir}"/usr/share/pixmaps/higan.png
  cp -dr --no-preserve=ownership profile/* data/cheats.bml "${pkgdir}"/usr/share/higan/
  cp -dr --no-preserve=ownership shaders/*.shader "${pkgdir}"/usr/share/higan/Video\ Shaders/

# libananke
  install -m 644 ananke/libananke.so "${pkgdir}"/usr/lib/libananke.so.1
  ln -s libananke.so.1 "${pkgdir}"/usr/lib/libananke.so

# higan
  for _profile in ${_profiles}; do
    install -m 755 {out,"${pkgdir}"/usr/bin}/higan-${_profile}
  done

# purify
  cd "${srcdir}"/purify_v03-source/purify
  install -m 755 {,"${pkgdir}"/usr/bin/}purify
  install -m 644 {../..,"${pkgdir}"/usr/share/applications}/purify.desktop
  ln -s higan.png "${pkgdir}"/usr/share/pixmaps/purify.png

# Fix permissions
  find "${pkgdir}"/usr/share/higan/ -type d -exec chmod 755 {} +
  find "${pkgdir}"/usr/share/higan/ -type f -exec chmod 644 {} +
}

# vim: ts=2 sw=2 et:
