# $Id: PKGBUILD 111897 2014-05-26 13:51:40Z flexiondotorg $
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Ernesto Ricart <ernest2193@gmail.com>

_pkgname=Plank-Config
# Convert to lowercase
pkgname=${_pkgname,,}
pkgver=1.3
pkgrel=1
pkgdesc="A tool to configure Plank Dock"
arch=('i686' 'x86_64')
url="https://github.com/ernest21/Plank-Config/"
license=('GPL')
depends=('desktop-file-utils' 'libnotify' 'plank')
makedepends=('vala')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/ernest21/${_pkgname}/archive/v${pkgver}.tar.gz")
sha1sums=('f125b8351e4204bcb38e8d5a5291423aff92ecf6')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    chmod +x ./autogen.sh
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
