# $Id: PKGBUILD 99927 2013-10-31 02:31:10Z allan $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=newsbeuter
pkgver=2.7
pkgrel=1
pkgdesc="A RSS feed reader for the text console with special Podcast support"
arch=('i686' 'x86_64')
url="http://www.newsbeuter.org/"
license=('custom: MIT')
depends=('curl' 'json-c' 'libxml2' 'sqlite' 'stfl')
makedepends=('swig' 'gettext')
optdepends=('ruby')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://www.newsbeuter.org/downloads/$pkgname-$pkgver.tar.gz)
sha256sums=('e6720da7ac3f5ea83bc39cd17bd61ccf458b7167f5a17170ae32c7b6e8ff903a')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix=/usr
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix=/usr DESTDIR=$pkgdir install

#license
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
