# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: lilydjwg <lilydjwg@gmail.com>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Jan-Erik Meyer-Luetgens <nyan at meyer-luetgens dot de>
pkgbase=python-pyside
pkgname=(python-pyside-common python2-pyside python-pyside)
_pkgrealname=pyside
pkgver=1.2.4
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
url="http://qt-project.org/wiki/PySide"
_qtver=4.8
makedepends=('python' 'python2' "qt4>=${_qtver}" "python"{,2}"-shiboken>=$pkgver"
             'cmake' 'qtwebkit' 'phonon')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/PySide/PySide/archive/$pkgver.tar.gz")
md5sums=('c28563e1f3c3ce269a7cc634f97d271c')

build(){
	cd "$srcdir/PySide-$pkgver"
    mkdir -p build_py3
    cd build_py3
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_TESTS=OFF \
      -DQT_PHONON_INCLUDE_DIR=/usr/include/qt4/phonon -DQT_QMAKE_EXECUTABLE=qmake-qt4
    make

	cd "$srcdir/PySide-$pkgver"
    mkdir -p build_py2
    cd build_py2
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_TESTS=OFF \
      -DQT_PHONON_INCLUDE_DIR=/usr/include/qt4/phonon -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
      -DPYTHON_SUFFIX=-python2.7
    make
}

package_python-pyside-common(){
    pkgdesc="Provides LGPL Qt bindings for Python and related tools for binding generation (Common Files)"
    # cmake will use Python 3 version by default
	cd "$srcdir/PySide-$pkgver/build_py3"
    make DESTDIR="$pkgdir" install

    rm -rf "$pkgdir"/usr/lib/pkgconfig
    rm -rf "$pkgdir"/usr/lib/python* "$pkgdir"/usr/lib/libpyside.*
    rm "$pkgdir"/usr/lib/cmake/PySide-$pkgver/PySideConfig*python*.cmake
}

package_python-pyside(){
    depends=('python' "qt4>=${_qtver}" "python-shiboken>=$pkgver" "python-pyside-common=$pkgver-$pkgrel")
    pkgdesc="Provides LGPL Qt bindings for Python and related tools for binding generation (Python 3 version)"
    optdepends=('qtwebkit: for PySide.QtWebKit'
                'phonon: for PySide.phonon')

	cd "$srcdir/PySide-$pkgver/build_py3"
    make DESTDIR="$pkgdir" install

    mv "$pkgdir"/usr/lib/pkgconfig/pyside.pc \
      "$pkgdir"/usr/lib/pkgconfig/pyside-py3.pc

    rm -rf "$pkgdir"/usr/include
    rm -rf "$pkgdir"/usr/share
    rm "$pkgdir"/usr/lib/cmake/PySide-$pkgver/PySideConfig.cmake
    rm "$pkgdir"/usr/lib/cmake/PySide-$pkgver/PySideConfigVersion.cmake
}

package_python2-pyside(){
    depends=('python2' "qt4>=${_qtver}" "python2-shiboken>=$pkgver" "python-pyside-common=$pkgver-$pkgrel")
    pkgdesc="Provides LGPL Qt bindings for Python and related tools for binding generation (Python 2 version)"
    optdepends=('qtwebkit: for PySide.QtWebKit'
                'phonon: for PySide.phonon')

	cd "$srcdir/PySide-$pkgver/build_py2"
    make DESTDIR="$pkgdir" install

    mv "$pkgdir"/usr/lib/pkgconfig/pyside.pc \
      "$pkgdir"/usr/lib/pkgconfig/pyside-py2.pc

    sed -i 's#^Requires: shiboken$#Requires: shiboken-py2#' "$pkgdir"/usr/lib/pkgconfig/pyside-py2.pc

    rm -rf "$pkgdir"/usr/include
    rm -rf "$pkgdir"/usr/share
    rm "$pkgdir"/usr/lib/cmake/PySide-$pkgver/PySideConfig.cmake
    rm "$pkgdir"/usr/lib/cmake/PySide-$pkgver/PySideConfigVersion.cmake
}

