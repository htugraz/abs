# $Id: PKGBUILD 110821 2014-05-07 23:03:49Z seblu $
# Maintainer: SÃ©bastien Luttringer
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: mickael9 <mickael9 at gmail dot com>

pkgname=znc
pkgver=1.2
pkgrel=3
pkgdesc='An IRC bouncer with modules & scripts support'
url='http://en.znc.in/wiki/index.php/ZNC'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libsasl')
makedepends=('tcl' 'python' 'perl')
optdepends=('tcl: modtcl module'
            'python: modpython module'
            'perl: modperl module'
            'cyrus-sasl: saslauth module')
install=$pkgname.install
source=("http://znc.in/releases/$pkgname-$pkgver.tar.gz"
        '01-sec-correctly-handle-channel-names.patch')
md5sums=('ef18e5402a82cc3fcab5c2ac5c2e6f3b'
         'SKIP')

prepare() {
  patch -p1 -d $pkgname-$pkgver < 01-sec-correctly-handle-channel-names.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-cyrus \
    --enable-python \
    --enable-perl \
    --enable-tcl \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
