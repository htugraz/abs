# $Id: PKGBUILD 115628 2014-07-12 19:47:12Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=gnuradio-fcdproplus
_pkgname=gr-fcdproplus
pkgver=3.7.4
pkgrel=3
_tag=v3.7_hidraw
pkgdesc="GNU Radio source block for Funcube Dongle Pro +"
arch=('i686' 'x86_64')
url="https://github.com/dl1ksv/gr-fcdproplus"
license=('GPL3')
depends=('gnuradio' 'libusb' 'alsa-lib' 'boost-libs')
makedepends=('git' 'cmake' 'boost' 'swig')
replaces=('gr-fcdproplus')
source=("git://github.com/dl1ksv/gr-fcdproplus.git#tag=$_tag"
        "88-fcdproplus.rules")
md5sums=('SKIP'
         '465e12c454c6a22ebec9849181af7bdc')

build() {
  cd "$srcdir/$_pkgname"
  mkdir build
  cd build
  cmake \
    -DPYTHON_EXECUTABLE=$(which python2) \
    -DPYTHON_INCLUDE_DIR=$(echo /usr/include/python2*) \
    -DPYTHON_LIBRARY=$(echo /usr/lib/libpython2.*.so) \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$_pkgname/build/"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "$srcdir/88-fcdproplus.rules" "$pkgdir/etc/udev/rules.d/88-fcdproplus.rules"
}
