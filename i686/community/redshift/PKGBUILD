# $Id: PKGBUILD 103169 2013-12-28 17:00:30Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Geoffrey Teale <tealeg@stop-squark>
# Contributor: Mark, Huo Mian <markhuomian[at]gmail[dot]com>
# Contributor: Biginoz <biginoz a free dot fr>

pkgname=redshift
pkgver=1.8
pkgrel=1
pkgdesc='Adjusts the color temperature of your screen according to your surroundings.'
arch=('i686' 'x86_64')
url='http://jonls.dk/redshift/'
license=('GPL3')
depends=('gconf' 'geoclue' 'libxxf86vm')
optdepends=('pygtk: for gtk-redshift'
            'python2-xdg: for gtk-redshift'
            'librsvg: for gtk-redshift')
makedepends=('python2')
install='redshift.install'
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
md5sums=('a05c34bc7b8fbb4f5d546a0b5cca5d6f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/python/python2/' src/redshift-gtk/redshift-gtk
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
