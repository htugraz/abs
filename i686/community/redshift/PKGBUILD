# $Id: PKGBUILD 95564 2013-08-14 09:35:51Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Geoffrey Teale <tealeg@stop-squark>
# Contributor: Mark, Huo Mian <markhuomian[at]gmail[dot]com>
# Contributor: Biginoz <biginoz a free dot fr>

pkgname=redshift
pkgver=1.7
pkgrel=7
pkgdesc='Adjusts the color temperature of your screen according to your surroundings.'
arch=('i686' 'x86_64')
url='http://jonls.dk/redshift/'
license=('GPL3')
depends=('gconf' 'geoclue' 'libxxf86vm')
optdepends=('pygtk: for gtk-redshift'
            'python2-xdg: for gtk-redshift'
            'librsvg: for gtk-redshift')
makedepends=('python2')
install='redshift.install'
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2"
        '165_164.diff') 
md5sums=('c56512afa292b5a94b715ed4a1841d4c'
         'eaafcdc62a47d725c256cd5473f4efa1')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's/python/python2/' src/gtk-redshift/gtk-redshift

  # fix GTK UI segmentation fault (fixes FS#33412)
  patch -p0 -i ../165_164.diff
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
