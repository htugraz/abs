# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jens Adam <jra@byte.cx>

pkgname=zmap
pkgver=1.2.1
pkgrel=3
pkgdesc='Fast network scanner designed for Internet-wide network surveys'
url='https://zmap.io/'
arch=('i686' 'x86_64')
license=('Apache')
depends=('gmp' 'hiredis' 'json-c' 'libpcap')
makedepends=('byacc' 'cmake' 'gengetopt')
backup=('etc/zmap/blacklist.conf' 'etc/zmap/zmap.conf')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/zmap/zmap/archive/v${pkgver}.tar.gz)
sha512sums=('1b3d7fa41bb75c859463f3ec8bcde31778f5d91a6f561aa9638ba93be97dacdba89493574581ce52aebcf093a86b042dcce41cffe6cb0849bb76a6005b714be2')

build() {
  cd ${pkgname}-${pkgver}
  cmake -DWITH_JSON=ON -DWITH_REDIS=ON
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 src/zmap "${pkgdir}/usr/bin/zmap"
  install -Dm 644 src/zmap.1 "${pkgdir}/usr/share/man/man1/zmap.1"
  install -Dm 644 conf/* -t "${pkgdir}/etc/zmap"
  for F in AUTHORS CHANGELOG INSTALL README; do
    install -Dm 644 "${F}" "${pkgdir}/usr/share/doc/zmap/${F}"
  done
  cp -a examples "${pkgdir}/usr/share/doc/zmap"
}

# vim: ts=2 sw=2 et:
