# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=stuntrally-data
_pkgname=stuntrally
pkgver=2.6
pkgrel=1
pkgdesc="Stunt Rally game with track editor, based on VDrift (data files)"
arch=('any')
license=('GPL3')
url="http://code.google.com/p/vdrift-ogre"
makedepends=('cmake' 'boost' 'libvorbis' 'mygui' 'sdl2' 'enet' 'hicolor-icon-theme' 'libxcursor' 'bullet' 'openal')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stuntrally/stuntrally/archive/${pkgver}.tar.gz"
        "$pkgname-tracks-$pkgver.tar.gz::https://github.com/stuntrally/tracks/archive/${pkgver}.tar.gz")
sha256sums=('8cc309d27d26f78dcc596440547d68b5e41d02ef830df2d52d38611d45ebbaca'
            '4bce700516a2a178ac04cfbf7df2887ba42225802f5a3e670f770ea01345687b')

build() {
  # build the sources
  cd "${srcdir}/stuntrally-${pkgver}/"

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX="/usr" \
      -DSHARE_INSTALL="share/stuntrally"
  make

  # build the tracks
  cd "${srcdir}/tracks-${pkgver}/"

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  # install the sources
  cd "${srcdir}/stuntrally-${pkgver}/build/"
  make DESTDIR="${pkgdir}" install

  # install the tracks
  cd "${srcdir}/tracks-${pkgver}/build"
  make DESTDIR="${pkgdir}/usr/share/stuntrally/" install

  # clean up
  rm -rf "${pkgdir}/usr/share/stuntrally/tracks/build"
  rm -rf "${pkgdir}/usr/share/icons"
  rm -rf "${pkgdir}/usr/share/applications"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/bin"
}
