# $Id: PKGBUILD 146028 2015-11-05 10:54:09Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bence Balint <balintbence1337@gmail.com>

pkgbase=python-w3lib
pkgname=('python-w3lib' 'python2-w3lib')
pkgver=1.13.0
pkgrel=1
pkgdesc="Library of web-related functions"
arch=('any')
license=('BSD')
url="http://w3lib.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six' 'git')
checkdepends=('python-pytest' 'python2-pytest')
source=("git+https://github.com/scrapy/w3lib.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a w3lib{,-py2}
}

check() {
  cd "$srcdir/w3lib"
  py.test

  cd "$srcdir/w3lib-py2"
  py.test2
}

package_python-w3lib() {
  depends=('python-six')

  cd "${srcdir}/w3lib"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-w3lib() {
  depends=('python2-six')

  cd "${srcdir}/w3lib-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
