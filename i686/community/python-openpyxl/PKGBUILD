# $Id: PKGBUILD 112830 2014-06-08 20:47:19Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Martin Corley <Martin.Corley@ed.ac.uk>
# Contributor: Nishit Joseph (reachjlight at gmail dot com)

pkgbase=python-openpyxl
pkgname=('python2-openpyxl' 'python-openpyxl')
pkgver=2.0.3
pkgrel=2
pkgdesc="A Python library to read/write Excel 2007 xlsx/xlsm files"
arch=('any')
url="http://openpyxl.readthedocs.org"
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/source/o/openpyxl/openpyxl-${pkgver}.tar.gz"
        "LICENSE")
md5sums=('9f04048f3fbeb857134341b9860cca40'
         'd5feddd06c61a04aea40b7aa1707e625')

build() {
  cd "$srcdir"
  cp -a openpyxl-${pkgver} openpyxl-py2-${pkgver}

  msg "Building Python2"
  cd "$srcdir"/openpyxl-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/openpyxl-${pkgver}
  python setup.py build
}

package_python2-openpyxl() {
  depends=('python2')
  optdepends=('python2-pillow: needed for including images')

  cd "$srcdir"/openpyxl-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-openpyxl() {
  depends=('python')
  optdepends=('python-pillow: needed for including images')

  cd "$srcdir"/openpyxl-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

