# $Id: PKGBUILD 107990 2014-03-20 13:45:41Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

pkgname=proxychains-ng
pkgver=4.7
pkgrel=1
pkgdesc="A hook preloader that allows to redirect TCP traffic of existing dynamically linked programs through one or more SOCKS or HTTP proxies"
arch=('i686' 'x86_64')
url="https://github.com/rofl0r/proxychains"
license=('GPL')
provides=('proxychains')
replaces=('proxychains')
conflicts=('proxychains')
depends=('glibc')
backup=('etc/proxychains.conf')
source=("http://downloads.sourceforge.net/project/$pkgname/proxychains-$pkgver.tar.bz2")
md5sums=('5c127db02feb45860feb887949955ecf')

build() {
  cd proxychains-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd proxychains-$pkgver
  make DESTDIR="$pkgdir/" install
  make DESTDIR="$pkgdir/" install-config
  ln -s proxychains4 "$pkgdir/usr/bin/proxychains"
}

# vim:set ts=2 sw=2 et:
