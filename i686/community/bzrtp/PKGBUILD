# $Id: PKGBUILD 141463 2015-09-22 15:20:42Z kkeen $
# Maintainer:
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=bzrtp
pkgver=1.0.2
pkgrel=3
pkgdesc="Opensource implementation of ZRTP keys exchange protocol"
arch=('i686' 'x86_64')
url="http://www.linphone.org/"
license=('GPL2')
depends=('libxml2' 'mbedtls')
validpgpkeys=('9774BC1725758EB16D639F8B3ECD52DEE2F56985')
source=("http://download.savannah.gnu.org/releases/linphone/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
	mbedtls2.patch)
md5sums=('b52fa670fb319022166cb10d641da4e6'
         'SKIP'
         'a5c77d9bcfda0aed17572a32b4cab69d')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 <$srcdir/mbedtls2.patch
  autoreconf
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
