# $Id: PKGBUILD 121876 2014-11-04 18:57:01Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-devel-stacktrace
pkgver=2.00
pkgrel=1
pkgdesc='An object representing a stack trace'
arch=('any')
url="https://metacpan.org/release/Devel-StackTrace"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.6')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Devel-StackTrace-$pkgver.tar.gz)
sha256sums=('1debe7273099a60e1386e0da5edbed7334db3cf3ed8e3b4106b087100b8ec5e4')

build() {
  cd "${srcdir}"/Devel-StackTrace-$pkgver

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  perl Makefile.PL
  make
}

check() {
  cd "${srcdir}"/Devel-StackTrace-$pkgver

  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "${srcdir}"/Devel-StackTrace-$pkgver

  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
