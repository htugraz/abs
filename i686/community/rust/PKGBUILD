# $Id: PKGBUILD 133579 2015-05-16 12:53:49Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=rust
pkgver=1.0.0
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
makedepends=('libffi' 'perl' 'python2' 'curl')
source=("http://static.rust-lang.org/dist/rustc-${pkgver/_/-}-src.tar.gz"{,.asc})
sha256sums=('c304cbd4f7b25d116b73c249f66bdb5c9da8645855ce195a41bda5077b995eba'
            'SKIP')
install="$pkgname.install"
options=('staticlibs' '!strip')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>
#gpg --keyserver hkp://keys.gnupg.net --recv-keys 108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE

prepare() {
  cd "rustc-${pkgver/_/-}"

  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules
}

build() {
  cd "rustc-${pkgver/_/-}"

  ./configure --prefix=/usr --disable-docs
  make
}

package() {
  cd "rustc-${pkgver/_/-}"

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  cd "$pkgdir/usr/lib"
  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .
}

# vim:set ts=2 sw=2 et:
