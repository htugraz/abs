# $Id: PKGBUILD 103695 2014-01-10 03:08:10Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=rust
pkgver=0.9
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='A safe, concurrent, practical language'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=(gcc shared-mime-info)
makedepends=(libffi perl python2 curl chrpath)
source=("http://static.rust-lang.org/dist/rust-${pkgver}.tar.gz"
        "http://static.rust-lang.org/dist/rust-${pkgver}.tar.gz.asc")
sha256sums=('c0911c3545b797a1ca16f3d76bf5ed234754b828efd1e22c182c7300ac7dd5d1'
            'SKIP')
install=rust.install
options=(staticlibs)

build() {
  cd rust-$pkgver

  ./configure --prefix=/usr --disable-docs

  # avoid python makedepend (force fallback to python2)
  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules

  make
}

#check() {
  #cd rust-$pkgver
  #make check
#}

package() {
  cd rust-$pkgver
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/vim" "$pkgdir/usr/share/licenses/$pkgname"

  cp -a src/etc/vim "$pkgdir/usr/share/vim/vimfiles"
  find "$pkgdir/usr/share/vim" -type f -exec chmod 644 {} +
  find "$pkgdir/usr/share/vim" -type d -exec chmod 755 {} +

  install -Dm644 src/etc/zsh/_rust "$pkgdir/usr/share/zsh/site-functions/_rust"
  install -Dm644 src/etc/kate/rust.xml "$pkgdir/usr/share/apps/katepart/syntax/rust.xml"
  install -Dm644 src/etc/gedit/share/mime/packages/rust.xml \
    "$pkgdir/usr/share/mime/packages/rust.xml"
  install -Dm644 src/etc/gedit/share/gtksourceview-3.0/language-specs/rust.lang \
    "$pkgdir/usr/share/gtksourceview-3.0/language-specs/rust.lang"

  install -m644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname"

  cd src/etc/emacs
  install -Dm644 rust-mode.el "$pkgdir/usr/share/emacs/site-lisp/rust-mode.el"

  cd "$pkgdir/usr"

  # https://github.com/mozilla/rust/issues/5219
  chrpath -d bin/* lib/*.so lib/rustlib/*/lib/*.so

  cd lib
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/* .
}
