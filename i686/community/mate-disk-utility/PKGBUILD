# $Id: PKGBUILD 105033 2014-01-30 12:06:00Z flexiondotorg $
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-disk-utility
pkgver=1.6.2
pkgrel=1
pkgdesc="Disk management application for MATE."
url="https://github.com/NiceandGently/mate-disk-utility"
arch=('i686' 'x86_64')
license=('GPL')
depends=('avahi' 'dbus' 'gtk2' 'libatasmart' 'libgnome-keyring' 'libnotify'
         'libunique' 'mate-file-manager' 'udisks')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!emptydirs')
source=("https://github.com/NiceandGently/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('ff3b520a06c367124558cdd252f1956d')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-gnome-keyring \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
