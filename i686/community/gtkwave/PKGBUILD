# $Id: PKGBUILD 92578 2013-06-08 21:06:37Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jared Casper <jaredcasper@gmail.com>

pkgname=gtkwave
pkgver=3.3.47
pkgrel=1
pkgdesc='A wave viewer which reads LXT, LXT2, VZT, GHW and VCD/EVCD files'
arch=('i686' 'x86_64')
url='http://gtkwave.sourceforge.net'
license=('GPL' 'MIT')
depends=('bzip2' 'xz' 'gtk2')
makedepends=('gperf')
install='gtkwave.install'
source=("http://gtkwave.sourceforge.net/gtkwave-${pkgver}.tar.gz")
md5sums=('fdb257ed42220a9a7526b70d1746054a')

build() {
  cd "${srcdir}/gtkwave-${pkgver}"

  ./configure \
    --prefix=/usr \
    --disable-tcl

  make CFLAGS=-D_LARGEFILE64_SOURCE
}

package() {
  cd "${srcdir}/gtkwave-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/gtkwave-${pkgver}/LICENSE.TXT" \
    "${pkgdir}/usr/share/licenses/gtkwave/LICENSE.TXT"
  install -D -m644 "${srcdir}/gtkwave-${pkgver}/MIT.TXT" \
    "${pkgdir}/usr/share/licenses/gtkwave/MIT.TXT"
}

