# $Id: PKGBUILD 141697 2015-09-27 13:28:10Z lfleischer $
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=unhide
pkgver=20130526
pkgrel=1
pkgdesc='A forensic tool to find processes hidden by rootkits, LKMs or by other techniques.'
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/unhide/'
license=('GPL3')
depends=('iproute2' 'lsof' 'psmisc' 'net-tools' 'procps-ng')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver.tgz")
md5sums=('f3d68ed8eb239614abb0ff5ace22cf85')

build() {
  cd $pkgname-$pkgver
  cc -Wall -O2 --static -pthread unhide-linux*.c unhide-output.c -o unhide-linux
  cc -Wall -O2 --static unhide_rb.c -o unhide_rb
  cc -Wall -O2 --static unhide-posix.c -o unhide-posix
  cc -Wall -O2 --static unhide-tcp.c unhide-tcp-fast.c unhide-output.c -o unhide-tcp
}

package() {
  cd $pkgname-$pkgver
  for _p in unhide-linux unhide-posix unhide_rb unhide-tcp; do
    install -Dm0755 "$_p" "$pkgdir/usr/bin/$_p"
  done
  ln -s unhide-linux "$pkgdir/usr/bin/unhide"
  install -Dm0644 man/unhide.8 "$pkgdir/usr/share/man/man8/unhide.8"
}

