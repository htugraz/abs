# $Id: PKGBUILD 100748 2013-11-10 00:24:37Z seblu $
# Maintainer: SÃ©bastien Luttringer

pkgbase=python-psutil
pkgname=('python-psutil' 'python2-psutil')
pkgver=1.1.3
pkgrel=1
arch=('i686' 'x86_64')
url='http://code.google.com/p/psutil/'
license=('custom: BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/p/psutil/psutil-$pkgver.tar.gz")
md5sums=('845806dcd0b954144b2451f696db85b0')

build() {
  cd psutil-$pkgver
  python setup.py build --build-lib=build/python
  python2 setup.py build --build-lib=build/python2
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

package_python-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python'
  depends=('glibc' 'python')

  cd psutil-$pkgver
  python setup.py build --build-lib=build/python \
                  install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

package_python2-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python2'
  depends=('glibc' 'python2')

  cd psutil-$pkgver
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
