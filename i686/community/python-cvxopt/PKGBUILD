# $Id: PKGBUILD 125528 2015-01-06 16:20:39Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: BinkyTheClown <binky_at_archlinux_dot_us>

pkgbase=python-cvxopt
pkgname=('python2-cvxopt' 'python-cvxopt')
pkgver=1.1.7
pkgrel=2
pkgdesc="A free software package for convex optimization written in Python"
url="http://cvxopt.org/"
arch=('x86_64' 'i686')
license=('GPL3')
makedepends=('gsl fftw glpk dsdp python python2 lapack')
optdepends=('gsl: for custom random number generators'
            'fftw: for FFTW interface'
            'glpk: solver for linear cone and PWL programming problems'
            'dsdp: solver for linear cone semidefinite programming problems')
source=("https://github.com/cvxopt/cvxopt/archive/${pkgver}.tar.gz")
sha1sums=('199ae22f7175be8e46b277993eea7c39e30da1f1')

prepare() {
  cd "$srcdir"
  cp -a cvxopt-${pkgver} cvxopt-py2-${pkgver}
}

build() {
  cd "$srcdir"/cvxopt-$pkgver

  for lib in GSL FFTW GLPK DSDP; do
    eval "export $'CVXOPT_BUILD_$lib'=1"
  done

  msg "Building Python2"
  cd "$srcdir"/cvxopt-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/cvxopt-${pkgver}
  python setup.py build
}

package_python2-cvxopt() {
  depends=("python2" "lapack")

  cd "$srcdir"/cvxopt-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}

package_python-cvxopt() {
  depends=("python" "lapack")

  cd "$srcdir"/cvxopt-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
