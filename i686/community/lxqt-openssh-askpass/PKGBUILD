# $Id: PKGBUILD 145653 2015-11-02 14:44:39Z jleclanche $
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-openssh-askpass
pkgver=0.10.0
pkgrel=1
pkgdesc="LXQt openssh password prompt"
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("liblxqt")
makedepends=("cmake" "qt5-tools")
source=(
	"http://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz"
	"http://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"fc4138fa071cf6e7a39cb0c069d8dbc31e409f5eaed87b65f7b5fe6241032283"
	"SKIP"
)
validpgpkeys=("169704C6FB490C6892C7F23C37E0AF1FDA48F373")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
