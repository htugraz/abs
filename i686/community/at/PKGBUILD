# $Id: PKGBUILD 109753 2014-04-18 13:33:04Z fyan $
# Maintainer: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Todd Musall <tmusall@comcast.net>

pkgname=at
pkgver=3.1.14
pkgrel=2
pkgdesc="AT and batch delayed command scheduling utility and daemon"
arch=('i686' 'x86_64')
url="http://packages.qa.debian.org/a/at.html"
license=('GPL')
depends=('pam' 'flex')
makedepends=('ssmtp')
backup=('etc/at.deny')
options=('!makeflags')
source=(http://ftp.debian.org/debian/pool/main/a/at/at_${pkgver}.orig.tar.gz
        atd.service)
md5sums=('d41cfd79033b6e49a8838add59a42ac6'
         '58856a1081e101ed6f0f02f9a20ada4e')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sbindir=/usr/bin \
              --with-jobdir=/var/spool/atd \
              --with-atspool=/var/spool/atd
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make IROOT="${pkgdir}" docdir=/usr/share/doc install
  install -D -m644 "${srcdir}/atd.service" "${pkgdir}/usr/lib/systemd/system/atd.service"
}
