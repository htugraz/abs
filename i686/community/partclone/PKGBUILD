# $Id: PKGBUILD 106463 2014-03-04 02:29:43Z bisson $
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contrinutor: Dan Serban
# Contributor: Franz Burgmann
# Contributor: Todd Partridge (Gen2ly)
# Contributor: Ivan Sichmann Freitas

pkgname=partclone
pkgver=0.2.69
pkgrel=2
pkgdesc="Utilities to save and restore used blocks on a partition"
arch=('i686' 'x86_64')
url="http://partclone.org"
license=('GPL')
depends=('progsreiserfs' 'ntfs-3g')
source=(https://downloads.sourceforge.net/project/$pkgname/stable/$pkgver/${pkgname}_$pkgver.orig.tar.gz)
md5sums=('2cc3b4e49819da32adcf63ef9246dca0')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-extfs --enable-reiserfs --enable-fat \
    --enable-hfsp --enable-btrfs --enable-ncursesw --enable-ntfs --sbindir=/usr/bin

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make PREFIX=/usr DESTDIR="$pkgdir"/ install
}
