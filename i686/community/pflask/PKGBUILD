# $Id: PKGBUILD 138502 2015-08-19 06:34:50Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pflask
pkgver=0.2.20150731
_commit=bd93136a22f36ab6481f9d081ec06478cf3c83ec
_wafver=1.8.6
pkgrel=1
pkgdesc="Lightweight process containers for Linux"
arch=('i686' 'x86_64')
url="http://ghedo.github.io/pflask/"
license=('BSD')
depends=('libdbus' 'bash')
makedepends=('python-sphinx' 'git')
source=("git+https://github.com/ghedo/pflask.git#commit=$_commit"
        "http://ftp.waf.io/pub/release/waf-$_wafver")
sha256sums=('SKIP'
            '81c4e6a3144c7b2021a839e7277bdaf1cedbbc87302186897b4ae03f4effcbf5')

prepare() {
  install -Dm755 waf-$_wafver pflask/waf
}

build() {
  cd pflask
  PREFIX=/usr ./waf configure
  ./waf build
}

package() {
  cd pflask
  DESTDIR="$pkgdir/" ./waf install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
