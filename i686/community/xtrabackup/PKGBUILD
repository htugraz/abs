# $Id: PKGBUILD 137916 2015-08-04 08:12:08Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=xtrabackup
pkgver=2.2.12
pkgrel=1
pkgdesc='Non-blocking backup tool for MySQL'
arch=('i686' 'x86_64')
url='http://www.percona.com/software/percona-xtrabackup/'
license=('GPL')
depends=('libaio' 'libgcrypt' 'openssl' 'perl-dbd-mysql')
makedepends=('cmake' 'python-sphinx' 'zlib')
source=(http://www.percona.com/downloads/XtraBackup/LATEST/source/tarball/percona-xtrabackup-$pkgver.tar.gz)
sha256sums=('6e8e3f6e6188321eef649cc1169bbc70200e4ae6a55160f3382db6be02a538d3')

build() {
  cd percona-xtrabackup-$pkgver
  cmake -DBUILD_CONFIG=xtrabackup_release \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd percona-xtrabackup-$pkgver
  make DESTDIR="$pkgdir" install

  cd  "$pkgdir"
  rm -rf usr/xtrabackup-test
  install -dm755 usr/share
  mv build/$pkgname/src/percona-xtrabackup-$pkgver/man usr/share
  rm -rf build
}
