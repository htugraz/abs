# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>


pkgname=xtrabackup
pkgver=2.1.8
pkgrel=2
pkgdesc='Non-blocking backup tool for MySQL'
arch=('i686' 'x86_64')
url='http://www.percona.com/software/percona-xtrabackup/'
license=('GPL')
depends=('libaio' 'libgcrypt' 'openssl' 'perl-dbd-mysql')
makedepends=('cmake' 'python-sphinx' 'zlib')
source=(http://percona.com/redir/downloads/XtraBackup/XtraBackup-$pkgver/source/percona-xtrabackup-$pkgver.tar.gz
        build.patch)

build() {
  cd percona-xtrabackup-$pkgver

  utils/build.sh xtradb55
  cp src/xtrabackup_55 src/xbstream src/xbcrypt .
  utils/build.sh xtradb56
  cp src/xtrabackup_56 .

  cd doc
  make man
}

package() {
  cd percona-xtrabackup-$pkgver

  for file in innobackupex xtrabackup_55 xtrabackup_56 xbcrypt xbstream; do
    install -Dm0755 $file "$pkgdir"/usr/bin/$file
  done

  cd doc/build/man
  for file in *.1; do
    install -Dm0644 $file "$pkgdir"/usr/share/man/man1/$file
  done
}

sha256sums=('2a397372aaa42228b48e74b89c41265d0b0c4be815069a86feb855bc8fbf2ffa'
            'e6f329c061c921be6e44e16fd64d79bcca8c61ea1ce4a1d2f923408d30318b25')
