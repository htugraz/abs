# $Id: PKGBUILD 118821 2014-09-11 10:32:16Z speps $
# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.6.0
pkgrel=1
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('i686' 'x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
depends=('drumstick' 'qt5-x11extras')
makedepends=('cmake' 'docbook-xsl' 'qt5-tools')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('7d1fa749181d58ab4e8335ccb7abc4ce')

prepare() {
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=release
  make
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}
