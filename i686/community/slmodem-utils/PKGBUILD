# $Id: PKGBUILD 92401 2013-06-04 16:47:34Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=slmodem-utils
pkgver=2.9.11
_date=20080126
pkgrel=19
pkgdesc="Userspace tools for the Smartlink winmodems"
arch=('i686')
license=('custom')
url="http://linmodems.technion.ac.il/packages/smartlink/"
depends=('bash' 'alsa-lib')
options=('!libtool')
#source=("http://linmodems.technion.ac.il/packages/smartlink/slmodem-${pkgver}-${_date}.tar.gz"
source=("ftp://ftp.archlinux.org/other/community/slmodem/slmodem-${pkgver}-${_date}.tar.gz"
        'slmodem.service')
md5sums=('8670dd7e1e1a46296ac4c80f7ac0471d'
         '840fed2a25fe7e6c33fd74ed4a23b464')

build() {
  cd "${srcdir}"/slmodem-$pkgver-${_date}

  sed -i 's|<modem.h>|"modem.h"|' modem/*.{c,h}
  sed -i 's|<modem_debug.h>|"modem_debug.h"|' modem/*.{c,h}
  sed -i 's|<modem_defs.h>|"modem_defs.h"|' modem/*.{c,h}
  sed -i 's|<modem_homolog.h>|"modem_homolog.h"|' modem/*.{c,h}
  sed -i 's|<modem_dp.h>|"modem_dp.h"|' modem/*.{c,h}
  sed -i 's|<modem_timer.h>|"modem_timer.h"|' modem/*.{c,h}
  sed -i 's|<modem_param.h>|"modem_param.h"|' modem/*.{c,h}

  unset LDFLAGS
  unset CFLAGS
  make SUPPORT_ALSA=1 all
}

package() {
  cd "${srcdir}"/slmodem-$pkgver-${_date}
  install -D -m755 modem/slmodemd "${pkgdir}"/usr/bin/slmodemd
  install -D -m644 "${srcdir}"/slmodem.service "${pkgdir}"/usr/lib/systemd/system/slmodem.service
  # adding udev symlink for /dev/modem
  install -d "${pkgdir}"/etc/udev/rules.d
  echo 'ACTION=="add", DRIVERS=="slamr", RUN+="/lib/udev/load-modules.sh ppp-generic"' > "${pkgdir}"/etc/udev/rules.d/slmodem.rules
  echo 'KERNEL==slamr[0-9]', MODE="0660", GROUP="tty", SYMLINK+="modem" >> "${pkgdir}"/etc/udev/rules.d/slmodem.rules
  echo 'ACTION=="add", DRIVERS=="slusb", RUN+="/lib/udev/load-modules.sh ppp-generic"' >> "${pkgdir}"/etc/udev/rules.d/slmodem.rules
  echo 'KERNEL==slusb[0-9]', MODE="0660", GROUP="tty", SYMLINK+="modem" >> "${pkgdir}"/etc/udev/rules.d/slmodem.rules
  # install license
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
