# $Id: PKGBUILD 105957 2014-02-19 21:11:15Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Roberto Carvajal <roberto@archlinux.org>

pkgname=multitail
pkgver=6.2.1
pkgrel=1
pkgdesc="Lets you view one or multiple files like the original tail program"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.vanheusden.com/multitail"
depends=('ncurses')
backup=('etc/multitail.conf')
source=("${url}/${pkgname}-${pkgver}.tgz")
md5sums=('f83419bd2b90ae4227c5a3ce2f66b56d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|ncursesw/panel.h|panel.h|' mt.h
  sed -i 's|ncursesw/ncurses.h|ncurses.h|' mt.h
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -D -m 755 multitail      "$pkgdir/usr/bin/multitail"
  install -D -m 644 multitail.1    "$pkgdir/usr/share/man/man1/multitail.1"
  install -D -m 644 multitail.conf "$pkgdir/etc/multitail.conf"
}
# vim: ts=2 sw=2 et ft=sh
