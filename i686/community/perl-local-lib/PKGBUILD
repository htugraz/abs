# $Id: PKGBUILD 107977 2014-03-20 11:32:35Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-local-lib
pkgver=2.000008
pkgrel=1
pkgdesc="Create and use a local lib/ for perl modules with PERL5LIB"
arch=('any')
url="http://search.cpan.org/dist/local-lib/"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/H/HA/HAARG/local-lib-$pkgver.tar.gz")
md5sums=('2dfe859c8fd417363dd5653523b87dcf')

build() {
  cd local-lib-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd local-lib-${pkgver}
  make test
}

package() {
  cd local-lib-${pkgver}
  make DESTDIR="$pkgdir" install
}
