# $Id: PKGBUILD 126130 2015-01-18 20:10:05Z muflone $
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: yugrotavele <yugrotavele at archlinux dot us>

pkgname=gmtp
pkgver=1.3.9
pkgrel=1
pkgdesc="A simple MP3 player client for MTP based devices"
arch=('i686' 'x86_64')
url="http://gmtp.sourceforge.net/"
license=('BSD')
depends=('flac' 'gtk3' 'libmtp' 'libid3tag' 'libvorbis')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz"::"http://sourceforge.net/projects/gmtp/files/gMTP-${pkgver}/${pkgname}-${pkgver}.tar.gz/download")
sha256sums=('5bff5385db66f5c3e82d89edb7ca4a8ccb8c8954faf7341438080f2741dcbd2d')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  make DESTDIR="${pkgdir}" install
}
