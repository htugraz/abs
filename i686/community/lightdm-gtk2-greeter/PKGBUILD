# $Id: PKGBUILD 106115 2014-02-23 10:34:01Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk2-greeter
pkgver=1.8.1
pkgrel=2
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
depends=('gtk2' 'lightdm')
makedepends=('exo' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=("${pkgname/2/}")
conflicts=("${pkgname/2/3}")
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
install="${pkgname/2/}.install"
source=("https://launchpad.net/${pkgname/2/}/${pkgver%.?}/${pkgver}/+download/${pkgname/2/}-${pkgver}.tar.gz"
        'session-preselection.patch')
sha256sums=('dac5ce9ac54a6b3c1ef9f9cc056f13f01352c2a897c5bed11e52505132610bab'
            'f7fad1e4b563ddc3f9035d15defcc87c57e2dd7c290aa78808c0a54bb766d94b')

prepare() {
  cd ${pkgname/2/}-${pkgver}

  patch -Np0 -i ../session-preselection.patch
}

build() {
  cd ${pkgname/2/}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' --libexecdir='/usr/lib/lightdm' --disable-static --with-gtk2
  make
}

package() {
  cd ${pkgname/2/}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
