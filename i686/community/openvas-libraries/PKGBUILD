# $Id: PKGBUILD 146737 2015-11-13 20:15:13Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-libraries
pkgver=8.0.5
pkgrel=1
pkgdesc='The OpenVAS libraries'
arch=('i686' 'x86_64')
url="http://www.openvas.org/"
license=('GPL')
depends=('gnutls' 'libpcap' 'gpgme' 'libssh' 'glib2' 'libldap' 'hiredis')
makedepends=('cmake' 'doxygen')
groups=(openvas)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/2191/$pkgname-${pkgver}.tar.gz"
        "http://wald.intevation.org/frs/download.php/2192/$pkgname-${pkgver}.tar.gz.asc")
sha256sums=('40dad0ea803d8ee7721f2385f2d2f8918b9e6733c8742efc1877210ade6501c1'
            'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

build() {
  cd $pkgname-$pkgver
  LDFLAGS+=" -Wl,--no-as-needed"
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBDIR=/usr/lib -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  rmdir "$pkgdir/var/run"
}
