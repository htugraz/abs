# $Id: PKGBUILD 127856 2015-02-17 12:57:59Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Martin Sugioarto <nakal@web.de>

pkgname=gimp-ufraw
pkgver=0.21
pkgrel=1
pkgdesc='Converter for raw files; utility and GIMP plugin'
url='http://ufraw.sourceforge.net/'
arch=('x86_64' 'i686')
license=('GPL')
depends=('gtkimageview' 'exiv2' 'lcms' 'desktop-file-utils' 'cfitsio' 'lensfun')
makedepends=('gimp' 'cinepaint')
optdepends=('gimp: to use the gimp import plugin for raw images'
            'cinepaint: to use the cinepaint import plugin for raw images')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/ufraw/ufraw-$pkgver.tar.gz")
sha256sums=('2a6a1bcc633bdc8e15615cf726befcd7f27ab00e7c2a518469a24e1a96964d87')

prepare() {
  # One of the include files that comes with cinepaint looks for ../lib/wire/...
  mkdir -p lib
  ln -s /usr/include/cinepaint/wire lib/wire
}

build() {
  cd "ufraw-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-extras \
    --enable-mime \
    --enable-openmp \
    --with-gimp \
    --with-cinepaint \
    --enable-contrast
  sed -i "s/-ffast-math -fomit-frame-pointer -W -Wall -O3/${CFLAGS}/" Makefile
  make
}

package() {
  make -C "ufraw-$pkgver" DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/bin/dcraw"
}

# vim:set ts=2 sw=2 et:
