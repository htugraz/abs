# $Id: PKGBUILD 123166 2014-11-26 23:12:31Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>

# bug, python binding vanished with 3.4
# Could NOT find PythonLibs (missing:  PYTHON_LIBRARIES PYTHON_INCLUDE_DIRS)

pkgname=libftdi
pkgver=1.2
pkgrel=1
pkgdesc="A library to talk to FTDI chips, optional python bindings."
arch=('i686' 'x86_64')
url="http://www.intra2net.com/en/developer/libftdi/download.php"
license=('GPL2' 'LGPL2.1')
depends=('libusb')
optdepends=('python: library bindings')
makedepends=('boost' 'cmake' 'python' 'swig')
# doxygen for docs, confuse for eeprom
source=(http://www.intra2net.com/en/developer/libftdi/download/${pkgname}1-$pkgver.tar.bz2{,.sig})
sha1sums=('636b9fcc745bf4eeb027a1dd891f770158444f7a'
          'SKIP')

prepare() {
  cd "${pkgname}1-$pkgver"
  sed -i 's|LIB_SUFFIX 64|LIB_SUFFIX ""|' CMakeLists.txt
  sed -i "s|MODE=\"0664\", GROUP=\"plugdev\"|TAG+=\"uaccess\"|g" packages/99-libftdi.rules
}

build() {
  cd "$srcdir/${pkgname}1-$pkgver"
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_SKIP_BUILD_RPATH=ON \
    -DCMAKE_BUILD_TYPE=Release -DEXAMPLES=OFF -DFTDI_EEPROM=OFF
  make
}

# needs yet unknown dependencies
#check() {
#  cd "$srcdir/${pkgname}1-$pkgver/build"
#  make check
#}

package() {
  cd "${pkgname}1-$pkgver/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "../packages/99-libftdi.rules" "$pkgdir/usr/lib/udev/rules.d/69-libftdi.rules"
}

