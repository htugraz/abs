# $Id: PKGBUILD 122542 2014-11-14 08:48:46Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=libx86emu
pkgver=1.1.23.1
pkgrel=1
pkgdesc='x86 emulation library'
arch=('x86_64' 'i686')
url='http://download.opensuse.org/source/factory/repo/oss/suse/src/'
license=('BSD')
depends=('glibc')
makedepends=('rpmextract')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/$pkgname-${pkgver%.*.*}-${pkgver#*.*.}.src.rpm")
sha256sums=('2073552362c81bf4e47c20ce8f0f2038641b6f790a2740eb745e284ff44eef10')

prepare() {
  # Wish they would just provide a normal $pkgname-$pkgver.tar.gz...
  rpmextract.sh "${pkgname}-${pkgver%.*.*}-${pkgver#*.*.}.src.rpm"
  tar jxf "$pkgname-${pkgver%.*.*}.tar.bz2"
}

build() {
  make -C "$pkgname-${pkgver%.*.*}" LIBDIR=/usr/lib
}

package() {
  make -C "$pkgname-${pkgver%.*.*}" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname-${pkgver%.*.*}/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
