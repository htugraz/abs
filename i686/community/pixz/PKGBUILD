# Maintainer:  Jonathan Steel <jsteel at archlinux.org>
# Contributor: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=pixz
pkgver=1.0.3
pkgrel=1
pkgdesc="Parallel, indexed xz compressor"
arch=('i686' 'x86_64')
url="https://github.com/vasi/pixz"
license=('custom')
depends=('libarchive')
makedepends=('asciidoc')
source=($pkgname-$pkgver.tar.gz::https://github.com/vasi/pixz/archive/v$pkgver.tar.gz)
md5sums=('d1abe040e91cb32eccf360f255218a45')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh

  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
