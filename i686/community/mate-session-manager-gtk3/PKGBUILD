# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
_pkgbase=mate-session-manager
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.1
pkgrel=1
pkgdesc="The MATE Session Handler (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dbus-glib' 'gtk3' 'libsm' 'libxtst' 'mate-desktop-gtk3>=1.12'
         'mate-polkit-gtk3>=1.12' 'mate-settings-daemon-gtk3>=1.12' 'ttf-dejavu'
         'xdg-user-dirs')
makedepends=('mate-common' 'xmlto' 'xtrans')
optdepends=('lightdm-gtk-greeter: A lightweight display manager'
            'onboard: On-screen keyboard useful for mobility impaired users'
            'orca: Screen reader for blind or visually impaired individuals'
            'xdg-user-dirs-gtk: Add autostart that prompts to rename user directories when switching locales')
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('225e769bade9d4968c0df98695777478f5fbe055')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-upower
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
