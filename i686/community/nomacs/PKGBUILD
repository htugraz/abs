# $Id: PKGBUILD 132229 2015-04-25 14:38:51Z speps $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=nomacs
pkgver=2.4.4
pkgrel=1
pkgdesc="A Qt image viewer"
arch=(i686 x86_64)
url="http://www.nomacs.org/"
license=('GPL3')
depends=('qt4' 'exiv2' 'libraw' 'opencv')
makedepends=('cmake')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver-source.tar.bz2")
md5sums=('88c1e2f9adc37bbd4c2fbbc4b7aabd37')

prepare() {
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
