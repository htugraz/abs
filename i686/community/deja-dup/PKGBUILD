# $Id: PKGBUILD 108944 2014-04-07 21:26:40Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=30.0
pkgrel=1
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=('i686' 'x86_64')
url="https://launchpad.net/deja-dup"
license=('GPL')
depends=('duplicity' 'libpeas' 'libnotify' 'python2-gobject' 'gvfs' 'xdg-utils')
makedepends=('cmake' 'vala' 'intltool' 'itstool' 'nautilus')
optdepends=('gnome-keyring: save passwords'
            'nautilus: backup extension'
            'python2-boto: Amazon S3 backend')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz)
md5sums=('1f714b88cec80ed38f770e95708a16d8')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fix
  sed -i 's/\"python\", \"-c\"/\"python2\", \"-c\"/' libdeja/PythonChecker.vala
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_SYSCONFDIR=/etc \
          -DCMAKE_INSTALL_LIBDIR=/usr/lib \
          -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/$pkgname
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
