# $Id: PKGBUILD 94733 2013-07-29 17:26:19Z seblu $
# Maintainer: SÃ©bastien Luttringer

pkgname=perl-test-output
pkgver=1.02
pkgrel=2
pkgdesc='Utilities to test STDOUT and STDERR messages'
arch=('any')
license=('GPL' 'PerlArtistic')
depends=('perl-sub-exporter'
  'perl-test-tester'
  'perl-test-pod'
  'perl-test-pod-coverage'
  'perl-capture-tiny')
url='http://search.cpan.org/dist/Test-Output'
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/B/BD/BDFOY/Test-Output-$pkgver.tar.gz")
md5sums=('d80890160737cdf4c3241d48428d33ab')

build() {
  cd Test-Output-$pkgver
  USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-Output-$pkgver
  make test
}

package() {
  cd Test-Output-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
