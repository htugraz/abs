# $Id: PKGBUILD 145847 2015-11-03 15:53:13Z spupykin $
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>
# Contributor: Mark Lee <mark@markelee.com>

pkgname=mediastreamer
pkgver=2.12.0
pkgrel=1
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=('i686' 'x86_64')
url="http://www.linphone.org/"
license=('GPL')
depends=('ortp' 'ffmpeg' 'libxv' 'libupnp' 'bzrtp' 'glew' 'libsrtp' 'mbedtls')
# xxd from Vim is needed to build
makedepends=('intltool' 'vim')
source=("http://download-mirror.savannah.gnu.org/releases/linphone/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('9774BC1725758EB16D639F8B3ECD52DEE2F56985')
md5sums=('f4570034ce3875cf238e0e1249b5ec2e'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-glx --enable-xv --enable-zrtp --enable-external-ortp \
    --disable-strict
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
