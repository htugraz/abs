# $Id: PKGBUILD 135904 2015-06-26 14:02:33Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk3
pkgver=0.29
pkgrel=1
pkgdesc="Gtk client and libraries for SPICE remote desktop servers (gtk3 version)"
arch=('i686' 'x86_64')
url="http://spice-space.org"
license=('LGPL2.1')
depends=('celt0.5.1' 'dbus-glib' 'gtk3' 'libcacard' 'libpulse' 'usbredir' 'libsoup')
makedepends=('gobject-introspection' 'intltool' 'python2-pyparsing' 'spice-protocol' 'usbutils'
	     'vala' 'sdl' 'python2-six')
source=("http://www.spice-space.org/download/gtk/spice-gtk-$pkgver.tar.bz2")
sha256sums=('44c7e22713246a2054c3c3b6e0280fd4c1fdbd2c8d33e5eb95bcda4748d5e973')

prepare() {
  cd "$srcdir/spice-gtk-$pkgver"
  sed -i 's,/usr/bin/env python,/usr/bin/python2,' spice-common/spice_codegen.py
}

build() {
  cd "$srcdir/spice-gtk-$pkgver"
  PYTHON=python2 ./configure --prefix=/usr \
    --disable-static --enable-vala --with-gtk=3.0 \
    --with-audio=pulse \
    --with-coroutine=gthread --sbindir=/usr/bin \
    --enable-smartcard
  make
}

package() {
  cd "$srcdir/spice-gtk-$pkgver"
  make DESTDIR="$pkgdir/" install
}
