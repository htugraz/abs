# $Id: PKGBUILD 102477 2013-12-12 03:21:31Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=sunpinyin-data
pkgver=2.0.3.20131212
_lm_sc_ver=20121025
_dict_ver=20131212
pkgrel=1
arch=('any')
pkgdesc="Statistical language model data from open-gram project for sunpinyin"
license=('APACHE')
provides=("sunpinyin-data-le=$pkgver")
url="http://sunpinyin.googlecode.com"
makedepends=("sunpinyin")
source=("http://open-gram.googlecode.com/files/lm_sc.t3g.arpa-${_lm_sc_ver}.tar.bz2"
        "http://open-gram.googlecode.com/files/dict.utf8-${_dict_ver}.tar.bz2")

prepare() {
  mkdir raw && cd raw
  ln -s ../lm_sc.t3g.arpa
  ln -s ../dict.utf8

  ln -sf "/usr/share/doc/sunpinyin/SLM-inst.mk" ./Makefile
}

build() {
  cd raw
  make
}

package() {
  cd raw
  make DESTDIR="$pkgdir" install
}

sha512sums=('3159dd3700382cfa617ba025ce542660580f3057e0dd6bbb92947902495db08e4454c1d09d01a55c90653444513d125dd034e944b060a9d8e009c3633b106d97'
            '3c90965ee389995a9a2801d489fb3f37ecf124d318a159a94145f6db348d7561dfa408dd148ab4eeae3ea466f83c1132558d4117bc6a01ca97cf89ce85ca9b99')
