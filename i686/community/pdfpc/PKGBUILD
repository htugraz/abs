# $Id: PKGBUILD 146465 2015-11-10 03:25:57Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andreas Bilke <andreas@bilke>

pkgname=pdfpc
pkgver=4.0.1
pkgrel=1
pkgdesc='A presenter console with multi-monitor support for PDF files'
arch=('i686' 'x86_64')
url='https://pdfpc.github.io/'
license=('GPL')
depends=('gtk3' 'poppler-glib' 'libgee' 'gstreamer' 'gst-plugins-base')
optdepends=('gst-plugins-good: more codecs for video playback support')
makedepends=('cmake' 'vala')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pdfpc/pdfpc/archive/v$pkgver.tar.gz")
sha256sums=('7c40cb87f9f1ce5e1683e96cc815572ea4c0a30e9db691501401ad271d27a3fb')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cmake  -DCMAKE_INSTALL_PREFIX="/usr/" -DSYSCONFDIR="/etc" .
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}

# vim:set ts=4 sw=4 et:
