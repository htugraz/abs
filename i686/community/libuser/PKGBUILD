# $Id: PKGBUILD 99886 2013-10-31 02:17:17Z allan $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Thomas Dziedzic < gostrc at gmail >

pkgname=libuser
pkgver=0.59
pkgrel=2
pkgdesc='A standardized interface for manipulating and administering user and group accounts.'
arch=('i686' 'x86_64')
license=('LGPL')
url='https://fedorahosted.org/libuser/'
depends=('python2' 'glib2' 'popt')
backup=('etc/libuser.conf')
source=("https://fedorahosted.org/releases/l/i/libuser/libuser-${pkgver}.tar.xz")
md5sums=('22835cbfec894b1e9491845ed5023244')

build() {
  cd ${pkgname}-${pkgver}
  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-gtk-doc-html \
    --disable-rpath
  sed -i 's/SUBDIRS = po docs/SUBDIRS = po/' Makefile
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
