# $Id: PKGBUILD 92399 2013-06-04 16:39:33Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=efax-gtk
pkgver=3.2.12
pkgrel=1
pkgdesc="A GUI front end for the 'efax' fax program"
arch=('i686' 'x86_64')
url="http://efax-gtk.sourceforge.net/"
license=('GPL')
depends=('dbus-glib' 'ghostscript' 'c++-gtk-utils')
makedepends=('pkg-config')
optdepends=('heirloom-mailx: to use the mail_fax script')
backup=('etc/efax-gtkrc')
install=efax-gtk.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.src.tgz")
md5sums=('5d38ca4d4c90e0805f2cf8f191177fb6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-spooldir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -m 755 mail_fax print_fax "${pkgdir}/usr/bin"
}
