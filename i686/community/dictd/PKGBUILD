# $Id: PKGBUILD 116342 2014-07-25 16:35:21Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: SmackleFunky <smacklefunky@optusnet.com.au>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

pkgname=dictd
pkgver=1.12.1
pkgrel=4
pkgdesc="Online dictionary client and server"
url="http://sourceforge.net/projects/dict/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('sh' 'zlib' 'libmaa')
makedepends=('flex')
backup=(etc/dict/dictd.conf
	etc/dict/dict.conf
	etc/dict/site.info
	etc/conf.d/dictd
	etc/xinetd.d/dictd
	etc/dict/colorit.conf)
options=('!emptydirs')
source=("http://downloads.sourceforge.net/dict/$pkgname-$pkgver.tar.gz"
	dict.conf
	dictd.conf
	dictd.confd
	dictd.service
	site.info
	dictd.xinetd
	colorit.conf)
md5sums=('62696491174c22079f664830d07c0623'
         'fb72f7d8c55f84b1b37bbc8d33a8e55a'
         '64cd6ffdb40fb3367224f91ac44926ac'
         '838887fee38ae4cebd652d399b1f2266'
         'd94fa6c43514c158c80e85f856c65365'
         'b8cabf913876e544d919c7f20ff8db9e'
         'bd3fd3f68625bad0c23754686ae2e99c'
         '978c9c079a0fc8d7d42a4cd4000190e7')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --enable-dictorg --prefix=/usr --sysconfdir=/etc/dict --sbindir=/usr/bin
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm 0644 $srcdir/dictd.xinetd $pkgdir/etc/xinetd.d/dictd
  install -Dm 0644 $srcdir/dictd.conf $pkgdir/etc/dict/dictd.conf
  install -Dm 0644 $srcdir/dict.conf $pkgdir/etc/dict/dict.conf
  install -Dm 0644 $srcdir/site.info $pkgdir/etc/dict/site.info
  install -Dm 0644 $srcdir/dictd.confd $pkgdir/etc/conf.d/dictd
  install -Dm 0644 $srcdir/colorit.conf $pkgdir/etc/dict/colorit.conf
  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  # examples
  install -dm0755 $pkgdir/usr/share/doc/$pkgname
  cp -a examples $pkgdir/usr/share/doc/$pkgname/examples
}
