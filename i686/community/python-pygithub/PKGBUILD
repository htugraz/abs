# $Id: PKGBUILD 147053 2015-11-18 07:02:47Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pygithub
pkgname=('python-pygithub' 'python2-pygithub')
pkgver=1.26.0
pkgrel=1
pkgdesc="Use the full Github API v3"
arch=('any')
license=('LGPL')
url="http://jacquev6.github.com/PyGithub"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/PyGithub/PyGithub.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a PyGithub{,-py2}
}

build() {
  cd "$srcdir/PyGithub"
  python setup.py build

  cd "$srcdir/PyGithub-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/PyGithub"
  python setup.py test

  cd "$srcdir/PyGithub-py2"
  python2 setup.py test
}

package_python-pygithub() {
  depends=('python')

  cd PyGithub
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pygithub() {
  depends=('python2')

  cd PyGithub-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
