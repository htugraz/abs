# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cpanplus
pkgver=0.9148
pkgrel=2
pkgdesc='Ameliorated interface to the CPAN'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl perl-log-message perl-archive-extract perl-term-ui perl-log-message-simple perl-module-pluggable perl-object-accessor)
url=https://metacpan.org/release/CPANPLUS
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/CPANPLUS-$pkgver.tar.gz")
md5sums=(e135aab8af0f16e07ddf1fe096680a00)
sha512sums=(321675021f07c440c835c8cd4a074e72257b29b62c2e72367ead7d4005194f6b1fa48198aa3e7bceabf47b0df79f50800ed464da4aa11fe53686a1ddc8112539)
_ddir="CPANPLUS-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
