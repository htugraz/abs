#$Id: PKGBUILD 113078 2014-06-12 21:14:37Z dwallace $
# Maintainer: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>

pkgname=youtube-viewer
_pkgdir=trizen-youtube-viewer
epoch=1
pkgver=3.1.1
pkgrel=1
pkgdesc="A command line utility for viewing youtube-videos in MPlayer."
arch=('any')
url="https://github.com/trizen/youtube-viewer"
license=('GPL3')
source=("$pkgname-$pkgver.zip::https://github.com/trizen/$pkgname/zipball/$pkgver")
sha256sums=('4f0a0a5053ef50f095bf41cf044a8aecfc7ff64488ec8c8259861b622adf3906')

depends=('perl>=5.10.0' 'perl-data-dump' 'perl-libwww' 'perl-xml-fast')
optdepends=('vlc: for playing videos'
			'mplayer: for playing videos'
			'perl-term-readkey: to get the terminal width size'
            'perl-term-readline-gnu: for a better STDIN support'
            'perl-lwp-protocol-https: for login and https support'
            'gcap: for retrieving Youtube closed captions. [in AUR]')


build() {
    cd "$srcdir/$_pkgdir"*/WWW-YoutubeViewer

    perl Build.PL
    perl Makefile.PL INSTALLDIRS=vendor
}

check(){
    cd "$srcdir/$_pkgdir"*/WWW-YoutubeViewer

    ./Build test
}

package() {
    cd "$srcdir/$_pkgdir"*/WWW-YoutubeViewer

    make install DESTDIR=$pkgdir
    rm -r "$pkgdir/usr/lib/"
}
