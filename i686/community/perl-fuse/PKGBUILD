# $Id: PKGBUILD 99530 2013-10-29 10:58:33Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tom K <tomk@runbox.com>

pkgname=perl-fuse
pkgver=0.16
pkgrel=1
pkgdesc="write filesystems in Perl using FUSE"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Fuse"
depends=('perl' 'fuse')
license=('GPL')
source=(http://search.cpan.org/CPAN/authors/id/D/DP/DPATES/Fuse-$pkgver.tar.gz
	build-fix.patch)
md5sums=('d7f36eb55392270d2c6b2f05737b553f'
         '16d985400e620286efdf4610d78cb5d6')

prepare() {
  cd  $srcdir/Fuse-$pkgver
  patch -p1 <$srcdir/build-fix.patch
}

build() {
  cd  $srcdir/Fuse-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  $srcdir/Fuse-$pkgver
  make install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
