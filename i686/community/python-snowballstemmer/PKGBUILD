# $Id: PKGBUILD 150149 2015-12-08 03:53:50Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: xantares

pkgbase=python-snowballstemmer
pkgname=('python-snowballstemmer' 'python2-snowballstemmer')
pkgver=1.2.0
pkgrel=3
arch=('any')
pkgdesc="Snowball stemming library collection for Python"
url='https://github.com/shibukawa/snowball_py'
license=('BSD')
makedepends=('python' 'python2')
source=("https://pypi.python.org/packages/source/s/snowballstemmer/snowballstemmer-${pkgver}.tar.gz")
md5sums=('51f2ef829db8129dd0f2354f0b209970')

prepare() {
  cp -a snowballstemmer-${pkgver}{,-py2}
}

build() {
  cd "$srcdir"/snowballstemmer-${pkgver}
  python setup.py build

  cd "$srcdir"/snowballstemmer-${pkgver}-py2
  python2 setup.py build
}

package_python-snowballstemmer() {
  depends=('python')
  optdepends=('python-pystemmer: for improved performance')

  cd "$srcdir"/snowballstemmer-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

package_python2-snowballstemmer() {
  depends=('python2')
  optdepends=('python2-pystemmer: for improved performance')

  cd "$srcdir"/snowballstemmer-${pkgver}-py2 
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
