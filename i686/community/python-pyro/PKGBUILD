# $Id: PKGBUILD 109125 2014-04-10 14:26:39Z fyan $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.br>

pkgbase=python-pyro
pkgname=("$pkgbase" 'python2-pyro')
pkgver=4.24
pkgrel=1
pkgdesc='Distributed object middleware for Python (RPC)'
url='http://pypi.python.org/pypi/Pyro4/'
arch=('any')
license=('MIT')
depends=('python' 'python2')
source=("http://pypi.python.org/packages/source/P/Pyro4/Pyro4-$pkgver.tar.gz")
sha256sums=('24d2ceaabbd886981d0df56f8f7e5f7f1a9db173778baa4965605f6880c90eb8')

package_python-pyro() {
  depends=('python')
  cd "Pyro4-$pkgver"
  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyro() {
  depends=('python2')
  cd "Pyro4-$pkgver"
  python2 setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
