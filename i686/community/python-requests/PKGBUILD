# $Id: PKGBUILD 118233 2014-09-04 07:22:47Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-requests
pkgname=(python-requests python2-requests)
pkgver=2.4.0
pkgrel=2
_libname=requests
pkgdesc="Python HTTP for Humans."
url="http://python-requests.org"
makedepends=('python-setuptools' 'python2-setuptools')
license=('Apache')
arch=('any')
source=(http://pypi.python.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz
        certs.patch
        226b5c7b0aafc3cb744eb79a666d9a1bd0803a47.patch)
sha256sums=('7007e03cbc73e357b5055c6ea0ad6e447e2afa00f1a1f843cd792a1ebaa3763e'
            '23bbd144905192edb23082c017292356eedbb3b41856c2a46505a2acb4ace831'
            '45cbe79fcf452eebb93444de7b78b35c679a77f268ea7286f5899bdb9ea7153a')

prepare() {
    cd "$srcdir"/$_libname-$pkgver
    patch -p1 -i "$srcdir"/certs.patch
    patch -p1 -i "$srcdir"/226b5c7b0aafc3cb744eb79a666d9a1bd0803a47.patch
    sed -r 's#(\W|^)requests/cacert\.pem(\W|$)##' -i MANIFEST.in
    rm -f requests/cacert.pem
}

build() {
    cd "$srcdir"/$_libname-$pkgver

    rm -rf ../buildpy3; mkdir ../buildpy3
    python setup.py build -b ../buildpy3

    rm -rf ../buildpy2; mkdir ../buildpy2
    python2 setup.py build -b ../buildpy2
    find ../buildpy2 -name \*.py -exec sed -r 's|^#!(.*)python$|#!\1python2|' -i {} +
}

check() {
    cd "$srcdir"/$_libname-$pkgver
    test -f "$(python -m requests.certs)"
}

package_python-requests() {
    depends=(python)

    cd "$srcdir"/$_libname-$pkgver
    rm -rf build; ln -s ../buildpy3 build
    python setup.py install --skip-build -O1 --root="$pkgdir"
    install -m0644 -D "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-requests() {
    depends=(python2)
    optdepends=('python2-ndg-httpsclient: HTTPS requests with SNI support'
                'python2-grequests: asynchronous requests with gevent')

    cd "$srcdir"/$_libname-$pkgver
    rm -rf build; ln -s ../buildpy2 build
    python2 setup.py install --skip-build -O1 --root="$pkgdir"
}
