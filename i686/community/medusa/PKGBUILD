# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=medusa
pkgver=2.1.1
pkgrel=3
pkgdesc="Speedy, massively parallel and modular login brute-forcer for network "
url="http://www.foofus.net/jmk/medusa/medusa.html"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('openssl')
optdepends=(
  'openssl: HTTP, MSSQL, SMBNT and SSL-based connection support'
  'libssh2: SSH support'
  'postgresql-libs: PostgreSQL support'
  'afpfs-ng: AFP support'
  'subversion: Subversion support'
)
makedepends=('libssh2' 'libpqxx' 'afpfs-ng' 'subversion')
source=(${pkgname}-${pkgver}.tar.gz::http://www.foofus.net/jmk/tools/${pkgname}-${pkgver}.tar.gz)
sha512sums=('e8ea4a532e1af6dff9c0bcbf761df94b4e9b6f30da50fe2dec7f3402d70a1fdf5297fad84d12b5125f3edbbf350ca439993f60dabecaa46630615d271275b8e9')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 misc/zsh/_medusa "${pkgdir}/usr/share/zsh/site-functions/_medusa"
  install -Dm 644 doc/*.html README NEWS -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
