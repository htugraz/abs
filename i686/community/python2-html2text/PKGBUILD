# $Id: PKGBUILD 97778 2013-09-29 16:22:13Z jlichtblau $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=python2-html2text
pkgver=2015.11.4
pkgrel=1
pkgdesc="A HTML to markdown-structured text converter"
arch=('any')
url="https://pypi.python.org/pypi/html2text/"
license=('GPL3')
depends=('python2-setuptools')
source=("https://pypi.python.org/packages/source/h/html2text/html2text-$pkgver.tar.gz")
sha256sums=('44531c00fd125d0d987adf4e5ab6b4a8a2b44bfa5c4dc8896f50aa0e35c33708')

build() {
  cd "${srcdir}"/html2text-$pkgver
  python2 setup.py build
}

check() {
  cd "${srcdir}"/html2text-$pkgver
  PYTHONPATH=$PYTHONPATH:. python2 setup.py test --verbose
}

package() {
  cd "${srcdir}"/html2text-${pkgver}
  python2 setup.py install --root="${pkgdir}"
  
  mv "$pkgdir/usr/bin/html2text"{,2}
}
