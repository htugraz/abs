# $Id: PKGBUILD 152143 2015-12-10 06:06:54Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>
 
pkgname=deepin-terminal
pkgver=1.1.1
_commit=bdf2acb68b32ab3e7262146f4d0011d32d2c9e54
pkgrel=1
pkgdesc='Awesome terminal for Linux Deepin'
arch=('any')
url="https://gitcafe.com/Deepin/deepin-terminal"
license=('GPL3')
depends=('python2-deepin-vte' 'desktop-file-utils' 'expect' 'hicolor-icon-theme'
         'python2-dbus' 'python2-deepin-ui' 'xdotool' 'ttf-dejavu')
makedepends=('deepin-gettext-tools' 'git')
groups=('deepin-extra')
install="${pkgname}.install"
source=("git+https://github.com/linuxdeepin/deepin-terminal.git#commit=$_commit"
        use-deepinvte.patch)
sha256sums=('SKIP'
            'b1d0d3a1ac3ea78d235cd721d757ad00715267775e2c219a5ba0474119c2bb3c')
 
prepare(){
  cd deepin-terminal

  # use deepinvte
  patch -p1 --no-backup-if-mismatch -i ../use-deepinvte.patch
 
  # fix python version
  find "${srcdir}" -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}
 
build() {
  cd deepin-terminal
  deepin-generate-mo tools/locale_config.ini
}
 
package() {
  cd deepin-terminal
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
}
