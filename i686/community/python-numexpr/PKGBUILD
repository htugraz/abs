# $Id: PKGBUILD 118244 2014-09-04 08:27:25Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Sebastien Binet <binet@lblbox>

pkgbase=python-numexpr
pkgname=('python2-numexpr' 'python-numexpr')
pkgver=2.4
pkgrel=2
pkgdesc="Fast numerical array expression evaluator for Python, NumPy, PyTables, pandas"
url="https://github.com/pydata/numexpr"
arch=('i686' 'x86_64')
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'python-numpy' 'python2-numpy')
source=(https://github.com/pydata/numexpr/archive/v$pkgver.tar.gz)
md5sums=('c9357862a304b9ad614afaf9e9c8dc78')

prepare() {
  cd "$srcdir"
  cp -a numexpr-$pkgver numexpr-py2-$pkgver
  cd numexpr-py2-$pkgver

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/numexpr-py2-$pkgver
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/numexpr-$pkgver
  python setup.py build
}

package_python2-numexpr() {
  depends=('python2-numpy')

  cd "$srcdir"/numexpr-py2-$pkgver

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-numexpr() {
  depends=('python-numpy')

  cd "$srcdir"/numexpr-$pkgver

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

