# $Id: PKGBUILD 133762 2015-05-19 06:29:50Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgbase=pep8
pkgname=("$pkgbase" "python2-$pkgbase")
pkgver=1.6.2
pkgrel=2
pkgdesc="Python style guide checker"
arch=('any')
url="http://github.com/jcrocholl/pep8/"
license=('custom:Expat')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/jcrocholl/${pkgbase}.git#tag=$pkgver"
        revert-cli-options-only.patch)
sha256sums=('SKIP'
            'ffa25c07cbff7f20e799c5ad6f0bc205c255898ffc7bab3c4157abfb83c43f7b')

prepare() {
  # Cherry-picked from https://github.com/jcrocholl/pep8/commit/435d1cbf995a659a82d1d4b42d25e3459556ef21
  (cd $pkgbase; patch -p1 -i ../revert-cli-options-only.patch)

  cp -a $pkgbase{,-py2}

  sed -i '1s/env python$/python2/' $pkgbase-py2/pep8.py
}

build() {
  cd "${srcdir}/${pkgbase}"
  python setup.py build

  cd "${srcdir}/${pkgbase}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${pkgbase}"
  python setup.py test

  cd "${srcdir}/${pkgbase}-py2"
  python2 setup.py test
}

package_pep8() {
  depends=('python' 'python-setuptools')
  provides=('pep8-python3')
  conflicts=('pep8-python3')
  replaces=('pep8-python3')

  cd $pkgbase
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir/"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pep8() {
  depends=('python2' 'python2-setuptools')
  provides=('pep8-python2')
  conflicts=('pep8-python2')
  replaces=('pep8-python2')

  cd $pkgbase-py2
  python2 setup.py install -O1 --prefix=/usr --root="$pkgdir/"
  mv "$pkgdir/usr/bin/pep8" "$pkgdir/usr/bin/pep8-python2"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
