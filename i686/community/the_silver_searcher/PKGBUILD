# $Id: PKGBUILD 110246 2014-04-27 19:31:42Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jeff Horelick <jdhore1@gmail.com>
# Contributor: skydrome <skydrome@i2pmail.org>

pkgname=the_silver_searcher
pkgver=0.21.1
pkgrel=1
pkgdesc='Code searching tool similar to Ack, but faster'
url='https://github.com/ggreer/the_silver_searcher'
license=('Apache')
depends=('pcre' 'zlib' 'xz')
makedepends=('git')
arch=('x86_64' 'i686')
source=("git://github.com/ggreer/$pkgname.git#commit=ac1e7e4b0cdd37e64f9af7256b8a4568c5546ec2")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"
  
  sed -i 's:configure:configure --prefix=/usr:' build.sh
}

build() {
  cd "$pkgname"

  ./build.sh
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
