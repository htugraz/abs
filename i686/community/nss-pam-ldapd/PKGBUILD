# $Id$
# Maintainer: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=nss-pam-ldapd
pkgver=0.8.14
pkgrel=1
pkgdesc='LDAP module for NSS and PAM'
arch=('i686' 'x86_64')
url="http://arthurdejong.org/$pkgname/"
license=('LGPL2.1')
depends=('krb5')
replaces=('nss_ldap')
conflicts=('nss-pam-ldapd-git' 'pam_ldap')
provides=('nss-pam-ldapd')
install=nss-pam-ldapd.install
backup=('etc/nslcd.conf')
source=("http://arthurdejong.org/$pkgname/$pkgname-$pkgver.tar.gz"
        "nslcd.service"
        "nslcd.tmpfiles")
md5sums=('c6f8876c5d0c476fbf545a6eda80390a'
         'c2edd35bab384c907eb442c606de856f'
         'd78a702ae9e81af03ae0a85a6af4d2f3')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-pam-seclib-dir=/usr/lib/security;

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm644 "$srcdir/nslcd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/nslcd.conf"
  install -Dm644 "$srcdir/nslcd.service" "$pkgdir/usr/lib/systemd/system/nslcd.service"
  install -Dm644 "$srcdir/$pkgname-$pkgver/nslcd.conf" "$pkgdir/etc/nslcd.conf"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
