# $Id$
# Maintainer: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=nss-pam-ldapd
pkgver=0.8.13
pkgrel=5
pkgdesc='LDAP module for NSS and PAM'
arch=('i686' 'x86_64')
url="http://arthurdejong.org/$pkgname/"
license=('LGPL2.1')
depends=('krb5')
replaces=('pam_ldap' 'nss_ldap')
conflicts=('nss-pam-ldapd-git')
provides=('nss-pam-ldapd')
install=nss-pam-ldapd.install
backup=('etc/nslcd.conf')
source=("http://arthurdejong.org/$pkgname/$pkgname-$pkgver.tar.gz"
        "nslcd.service"
        "nslcd.tmpfiles")
sha1sums=('0567cfea104defabeacd88a3a3200b311b8071ec'
          'b564fa5738963a67a9240bf4ce63b2fa494cafb7'
          '6e225e0d849a48e01b58adad493f017a34bdf70c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-pam-seclib-dir=/usr/lib/security;

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm644 "$srcdir/nslcd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/nslcd.conf"
  install -Dm644 "$srcdir/nslcd.service" "$pkgdir/usr/lib/systemd/system/nslcd.service"
  install -Dm644 "$srcdir/$pkgname-$pkgver/nslcd.conf" "$pkgdir/etc/nslcd.conf"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
