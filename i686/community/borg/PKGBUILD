# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=borg
_pkgname=borgbackup
pkgver=0.28.2
pkgrel=2
pkgdesc='Deduplicating backup program with compression and authenticated encryption'
url='https://borgbackup.github.io/borgbackup/borg/'
license=('BSD')
arch=('i686' 'x86_64')
depends=('acl' 'lz4' 'openssl' 'python-msgpack' 'python-setuptools' 'xz')
optdepends=('openssh: repositories on remote hosts'
            'python-llfuse: mounting backups as a FUSE file system')
makedepends=('cython' 'python-sphinx')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytest-benchmark' 'python-mock')
provides=('borgbackup')
replaces=('borgbackup')
source=("https://pypi.python.org/packages/source/b/$_pkgname/$_pkgname-$pkgver.tar.gz"
        borg.coveragerc)
sha256sums=('cf8ee95758242065556ca500e49c62ba48ae62895d7dcf44e49088a0c22af0eb'
            '024bf101b25f9d35b83f86ce794c85f404d7acb212e047b4fca68b108dff21a7')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py build

  cd docs
  make man
}

check() {
  cd "$srcdir/$_pkgname-$pkgver/build/lib.linux-$CARCH-3.5/"
  LANG=en_US.UTF-8 PYTHONPATH="$PWD:$PYTHONPATH" py.test --cov=borg \
    --cov-config="$srcdir/borg.coveragerc" --benchmark-skip --pyargs borg.testsuite -v \
    -k 'not test_non_ascii_acl'
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  install -Dm644 "docs/_build/man/borg-deduplicatingarchiver.1" "$pkgdir/usr/share/man/man1/borg.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  python setup.py -q install --root="$pkgdir" --optimize=1
}
