# $Id: PKGBUILD 129184 2015-03-13 14:27:48Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: sracker <smb.sac@gmail.com>

pkgname=xdebug
pkgver=2.3.1
pkgrel=2
pkgdesc="PHP debugging extension"
arch=('i686' 'x86_64')
url="http://www.xdebug.org"
license=('GPL')
depends=('php')
backup=('etc/php/conf.d/xdebug.ini')
source=(http://www.xdebug.org/files/$pkgname-$pkgver.tgz
	https://github.com/derickr/xdebug/commit/a71762d4ded9ebd8121dfdc8d2af2dbebb5cc3c7.patch
	xdebug.ini)
md5sums=('117d8e54d84b1cb7e07a646377007bd5'
         'a0949c19bd3b486a7fc3eefbfada4f3d'
         '6a1f2ef91c632d4c9b7b218cd2e278ef')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/a71762d4ded9ebd8121dfdc8d2af2dbebb5cc3c7.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  phpize
  ./configure --prefix=/usr --enable-xdebug
  make

  cd debugclient
  ./configure --prefix=/usr
  make
}

package() {
  local PHPVER=`php -r 'echo phpversion();'`

  cd $srcdir/$pkgname-$pkgver/debugclient
  make DESTDIR=$pkgdir install

  cd $srcdir/$pkgname-$pkgver
  make INSTALL_ROOT=$pkgdir install
  install -D -m 644 $srcdir/xdebug.ini $pkgdir/etc/php/conf.d/xdebug.ini
}
