# $Id: PKGBUILD 121059 2014-10-20 10:54:34Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Balló György <ballogyor+arch@gmail.com>
# Contributor: Branchini Massimo <max.bra.gtalk@gmail.com

pkgbase=libdbusmenu
pkgname=('libdbusmenu-glib' 'libdbusmenu-gtk3')
pkgver=12.10.2
pkgrel=4
pkgdesc="A library for passing menus over DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/libdbusmenu"
license=('GPL3')
makedepends=('gnome-doc-utils' 'gobject-introspection' 'gtk3' 'intltool' 'vala')
options=('!emptydirs')
source=("http://launchpad.net/dbusmenu/${pkgver%.?}/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('9d6ad4a0b918b342ad2ee9230cce8a095eb601cb0cee6ddc1122d0481f9d04c9')

build() {
  cd ${pkgbase}-${pkgver}

  export HAVE_VALGRIND_TRUE='#'
  export HAVE_VALGRIND_FALSE=''

  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --disable-{dumper,static,tests}
  make
}

package_libdbusmenu-glib() {
depends=('glib2')

  cd ${pkgbase}-${pkgver}

  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" install
}

package_libdbusmenu-gtk3() {
depends=('gtk3' 'libdbusmenu-glib')

  cd ${pkgbase}-${pkgver}

  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" install
  make -j1 -C libdbusmenu-gtk DESTDIR="${pkgdir}" install
  make -j1 -C libdbusmenu-glib DESTDIR="${pkgdir}" uninstall
}

# vim: ts=2 sw=2 et:
