# $Id: PKGBUILD 109220 2014-04-11 20:03:09Z thestinger $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Andrea Zucchelli <zukka77@gmail.com>

pkgname=tinyproxy
pkgver=1.8.3
pkgrel=8
pkgdesc='A light-weight HTTP proxy daemon for POSIX operating systems.'
arch=('i686' 'x86_64')
url='https://banu.com/tinyproxy/'
license=('GPL')
depends=(glibc)
makedepends=('asciidoc')
install="${pkgname}.install"
backup=('etc/tinyproxy/tinyproxy.conf')
source=("https://banu.com/pub/${pkgname}/1.8/${pkgname}-${pkgver}.tar.bz2"
        'tinyproxy.tmpfiles.conf'
        'tinyproxy.service'
        config.patch)
md5sums=('292ac51da8ad6ae883d4ebf56908400d'
         'b747d0f253ba7bb3f604e69a35a278bf'
         '41938243faca487a14beeee5114f244e'
         '9739e020c40abefd6e921e9cff854f35')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../config.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc/tinyproxy --sbindir=/usr/bin \
     --localstatedir=/var --enable-transparent
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 "${srcdir}/tinyproxy.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/tinyproxy.conf"

  install -Dm0644 "${srcdir}/tinyproxy.service" \
    "${pkgdir}/usr/lib/systemd/system/tinyproxy.service"
}
