# $Id: PKGBUILD 65259 2012-02-20 12:39:55Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Sergej Pupykin <sergej@aur.archlinux.org>

pkgname=xmldiff
pkgver=0.6.10
pkgrel=2
pkgdesc="A python tool that figures out the differences between two similar XML files, in the same way the diff utility does it"
arch=('i686' 'x86_64')
url="http://www.logilab.org/project/xmldiff/"
license=('GPL2')
depends=('python2' 'libxslt' 'jade')
optdepends=('docbook-xsl: render revision info')
#source=(http://ftp.logilab.org/pub/$pkgname/$pkgname-$pkgver.tar.gz)
source=(http://arch.p5n.pp.ru/~sergej/dl/2012/xmldiff-$pkgver.tar.gz)
md5sums=('a61e6e95a130e3bd53f5ea5616cc5314')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' main.py
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -m755 -d "$pkgdir/usr/share/man/man1"
  install -m644 man/*.1 "$pkgdir/usr/share/man/man1"
}
