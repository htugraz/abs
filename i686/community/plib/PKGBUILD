# $Id: PKGBUILD 103334 2014-01-03 11:08:04Z bpiotrowski $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=plib
pkgver=1.8.5
pkgrel=4
pkgdesc="Set of libraries to write games and other realtime interactive applications"
arch=('i686' 'x86_64')
url="http://plib.sourceforge.net/"
license=('custom:LGPL')
makedepends=('mesa' 'libxi' 'libxmu')
source=(http://plib.sourceforge.net/dist/$pkgname-$pkgver.tar.gz)
options=('staticlibs')
md5sums=('47a6fbf63668c1eed631024038b2ea90')
sha1sums=('c2cf7e3e1e58f7b63dae4bb21e4fa82c3e4d4cfc')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  [ "$CARCH" = "x86_64" ] && export CXXFLAGS="$CXXFLAGS -fPIC"
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING
}
