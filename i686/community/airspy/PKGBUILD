# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=airspy
pkgver=1.0.3
pkgrel=1
pkgdesc="Host code for Airspy SDR."
arch=('i686' 'x86_64')
url="http://www.airspy.com"
license=('GPL2')
depends=('libusb>=1.0')
makedepends=('cmake')
source=("https://github.com/airspy/host/archive/v$pkgver.tar.gz")
md5sums=('fd0b80003a1098806ac40a5f42fea05f')

build() {
  cd "$srcdir/host-$pkgver"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -Wno-dev ../
  make
}

package() {
  cd "$srcdir/host-$pkgver/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "../airspy-tools/52-airspy.rules" "$pkgdir/usr/lib/udev/rules.d/52-airspy.rules"
}

# vim:set ts=2 sw=2 et:
