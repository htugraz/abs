# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=airspy
pkgver=1.0.6
pkgrel=1
pkgdesc="Host code for Airspy SDR."
arch=('i686' 'x86_64')
url="http://www.airspy.com"
license=('GPL2')
depends=('libusb>=1.0')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/airspy/host/archive/v$pkgver.tar.gz"
        "airspy.conf")
md5sums=('d42d454d07881e768ea944a68a1e28ff'
         '29e481d56bfc56cface4f1457ca6df23')

build() {
  cd "$srcdir/host-$pkgver"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -Wno-dev ../
  make
}

package() {
  cd "$srcdir/host-$pkgver/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "../airspy-tools/52-airspy.rules" "$pkgdir/usr/lib/udev/rules.d/52-airspy.rules"
  install -Dm644 "$srcdir/airspy.conf" "$pkgdir/etc/modprobe.d/airspy.conf"
}

# vim:set ts=2 sw=2 et:
