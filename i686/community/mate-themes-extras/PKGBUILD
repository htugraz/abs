# $Id: PKGBUILD 120255 2014-10-06 12:56:06Z flexiondotorg $
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-themes-extras
pkgver=1.9.1
pkgrel=2
pkgdesc="Collection of GTK2/3 desktop themes for MATE."
url="https://github.com/NiceandGently/mate-themes-extras"
arch=('any')
license=('GPL')
depends=('gtk2' 'gtk-engine-murrine' 'gtk-engine-unico' 'mate-icon-theme')
makedepends=('mate-common' 'icon-naming-utils' 'perl-xml-parser')
options=('!emptydirs')
source=("https://github.com/NiceandGently/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('970e4088d4da342e217a3545d076f2d56877185c01e8166ba76fc50894318b25')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --enable-Blue-Submarine \
        --enable-clearlooks-phenix \
        --enable-DeLorean-Dark \
        --enable-Faience \
        --enable-Faience-Ocre \
        --enable-Gnome-Cupertino \
        --enable-Gnome-Cupertino-Mint \
        --enable-GnomishBeige \
        --enable-Green-Submarine \
        --enable-Smoothly \
        --enable-Smoothly-Black \
        --enable-Zukitwo \
        --enable-Zukitwo-Brave \
        --enable-Zukitwo-Dust \
        --enable-Zukitwo-Human \
        --enable-Zukitwo-Illustrious \
        --enable-Zukitwo-Noble \
        --enable-Zukitwo-Wine \
        --enable-Zukitwo-Wise \
        --enable-Zukitwo-Colors
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
