# $Id: PKGBUILD 109048 2014-04-09 19:48:24Z bluewind $
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Jan Fader <jan.fader@web.de>
pkgbase=highlight
pkgname=(highlight highlight-gui)
pkgver=3.18
pkgrel=1
url="http://www.andre-simon.de/doku/highlight/highlight.html"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=(qt4 lua boost)
source=(http://www.andre-simon.de/zip/$pkgname-$pkgver.tar.bz2{,.asc})
md5sums=('085d7639a7e05a0d41923a889dc801c9'
         'SKIP')
backup=(etc/highlight/filetypes.conf)

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  make QMAKE=qmake-qt4
  make QMAKE=qmake-qt4 gui
}

package_highlight() {
  pkgdesc="Fast and flexible source code highlighter (CLI version)"
  depends=('lua')
  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" QMAKE=qmake-qt4 install
}

package_highlight-gui() {
  pkgdesc="Fast and flexible source code highlighter (Qt version)"
  depends=('qt4' 'highlight')
  cd "$srcdir/$pkgbase-$pkgver"

  install -dm755 "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir" QMAKE=qmake-qt4 install-gui
}

# vim:set ts=2 sw=2 et:
