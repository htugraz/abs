# $Id: PKGBUILD 127041 2015-02-03 11:01:40Z bluewind $
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Jan Fader <jan.fader@web.de>
pkgbase=highlight
pkgname=(highlight highlight-gui)
pkgver=3.21
pkgrel=1
url="http://www.andre-simon.de/doku/highlight/highlight.html"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=(qt4 lua boost)
source=(http://www.andre-simon.de/zip/$pkgname-$pkgver.tar.bz2{,.asc})
md5sums=('122cd432e84f0b29aed172435af31aed'
         'SKIP')
validpgpkeys=(B8C55574187F49180EDC763750FE0279D805A7C7)

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  make QMAKE=qmake-qt4
  make QMAKE=qmake-qt4 gui
}

package_highlight() {
  pkgdesc="Fast and flexible source code highlighter (CLI version)"
  depends=('lua')
  backup=(etc/highlight/filetypes.conf)
  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" QMAKE=qmake-qt4 install
}

package_highlight-gui() {
  pkgdesc="Fast and flexible source code highlighter (Qt version)"
  depends=('qt4' 'highlight')
  cd "$srcdir/$pkgbase-$pkgver"

  install -dm755 "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir" QMAKE=qmake-qt4 install-gui
}

# vim:set ts=2 sw=2 et:
