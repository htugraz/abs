# $Id: PKGBUILD 124093 2014-12-19 04:02:57Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Aaron, phrakture, Griffin <aaron@archlinux.org>
# Contributor: erkaN kaplaN <erkan@linux-sevenler.org>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

pkgname=gwget
pkgver=1.0.4
pkgrel=9
pkgdesc="Download manager for GNOME"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/gwget/"
license=('GPL')
depends=('gtk3' 'gconf' 'libnotify' 'wget')
makedepends=('intltool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        gwget-1.0.4-notify-0.7.patch
        gwget-1.0.4-glib-2.32.patch
        drop-libgnomeui.patch
        gtk3-port.patch
        wget.patch)
sha256sums=('a65b2b2b3befb6cf34a697a13111b35498a6d63f9a9b048a22b8654f35ef643f'
            '84efbbea9c2a91c60288edb67264bbdb4ec2c579cf95b3baa8282040a1b65c76'
            '319c4795d0034c4adacf302db697d966ca228fc1bbe9778af9dadcfb0c2531c8'
            'afc5a92f9652d19a200a32b1081fb2b928fec42153795a4c77eaac99352fa9de'
            'e85e3eeb091bd88bf11866c6256cd36c375f456d07599a9e6a15400fa26546f8'
            '3900defbf161a657ef3968a0aa1df27605e1fbf6fc34d0fcba748ce274fa4f3c')

prepare() {
  cd $pkgname-$pkgver

  # https://bugzilla.gnome.org/show_bug.cgi?id=634170
  patch -Np1 -i ../gwget-1.0.4-notify-0.7.patch

  # https://bugzilla.gnome.org/show_bug.cgi?id=665286
  patch -Np1 -i ../gwget-1.0.4-glib-2.32.patch

  # https://bugzilla.gnome.org/show_bug.cgi?id=671759
  patch -Np1 -i ../drop-libgnomeui.patch

  # Port to GTK+ 3
  patch -Np1 -i ../gtk3-port.patch

  # Fix with recent wget versions
  patch -Np1 -i ../wget.patch

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
