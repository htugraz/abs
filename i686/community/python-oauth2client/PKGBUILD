# $Id: PKGBUILD 123825 2014-12-13 08:16:18Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Atlanis <emallson@archlinux.us>

pkgbase=python-oauth2client
pkgname=('python2-oauth2client' 'python-oauth2client')
pkgver=1.4.3
pkgrel=1
pkgdesc="A client library for OAuth 2.0"
arch=('any')
url="https://github.com/google/oauth2client"
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools' 'python-httplib2'
             'python-pyasn1' 'python-pyasn1-modules' 'python-rsa'
             'python-six' 'python2-httplib2' 'python2-pyasn1'
             'python2-pyasn1-modules' 'python2-rsa' 'python2-six')
source=("https://pypi.python.org/packages/source/o/oauth2client/oauth2client-${pkgver}.tar.gz")
md5sums=('fcffeeb0e0729a1870484159a835ef3a')

prepare() {
  cd "$srcdir"
  cp -a oauth2client-${pkgver} oauth2client-py2-${pkgver}
  cd oauth2client-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/oauth2client-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/oauth2client-${pkgver}
  python setup.py build
}

package_python2-oauth2client() {
  depends=('python2-httplib2' 'python2-pyasn1=0.1.7' 'python2-pyasn1-modules=0.0.5' 'python2-rsa=3.1.4' 'python2-six')
  optdepends=('python2-gflags: for oauth2client.tools.run function')

  cd "$srcdir"/oauth2client-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}

package_python-oauth2client() {
  depends=('python-httplib2' 'python-pyasn1=0.1.7' 'python-pyasn1-modules=0.0.5' 'python-rsa=3.1.4' 'python-six')

  cd "$srcdir"/oauth2client-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}

