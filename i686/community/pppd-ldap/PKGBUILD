# $Id: PKGBUILD 55777 2011-09-20 06:52:16Z andrea $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=pppd-ldap
pkgver=0.12b
pkgrel=3
pkgdesc="A plugin for PPPD which performs an LDAP-enabled version of pppd"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/pppd-ldap/"
license=('GPL')
depends=('ppp' 'libldap')
options=('docs')
source=("http://downloads.sourceforge.net/${pkgname}/pppd_ldap-$pkgver.tgz")
md5sums=('4d13596297c2728f3cfe0a097c97d8fc')

build() {
  cd "$srcdir/pppd_ldap-$pkgver"

patch -p1 main.c <<EOF
diff pppd_ldap-0.12b/main.c pppd_ldap-0.12b.my/main.c
56a57,58
> #define LDAP_FILT_MAXSIZ        1024
> 
EOF

patch -p1 utmplib.c <<EOF
diff pppd_ldap-0.12b/utmplib.c pppd_ldap-0.12b.my/utmplib.c
157c157
< 			(char*) buf += rc;
---
> 			buf = (char*)buf + rc;
EOF

  make CFLAGS="-I/usr/include/pppd -fPIC"
}

package() {
  cd "$srcdir/pppd_ldap-$pkgver"
  install -D -m0755 pppd_ldap.so $pkgdir/usr/lib/pppd/2.4.4/pppd_ldap.so
  install -D -m0755 README $pkgdir/usr/share/doc/pppd-ldap/README
}
