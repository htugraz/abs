# $Id: PKGBUILD 113558 2014-06-25 16:26:30Z schiv $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: lang2 <wenzhi.liang@gmail.com>

pkgname=imdbpy
_realname=IMDbPY
pkgver=5.0
pkgrel=1
pkgdesc="Python bindings for the Internet Movie Database (IMDb)"
url="http://imdbpy.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('python2')
makedepends=('python2-distribute')
optdepends=('python2-lxml' 'python2-sqlalchemy' 'python2-sqlobject')
source=(http://downloads.sourceforge.net/$pkgname/$_realname-$pkgver.tar.gz)
md5sums=('f0b87950a943762db9e45c1a7c3a5cce')

prepare() {
  cd "$srcdir/$_realname-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
}

package() {
  cd "$srcdir/$_realname-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  # fix paths
  # WTH is upstream doing?
  # TODO: report upstream about this nasty
  mv "$pkgdir/usr/etc" "$pkgdir"
  mkdir -p "$pkgdir/usr/share/doc"
  #mv "$pkgdir/usr/imdb" "$pkgdir/usr/share"
  mv "$pkgdir/usr/doc" "$pkgdir/usr/share/doc/imdb"
}

# vim:set ts=2 sw=2 et:
