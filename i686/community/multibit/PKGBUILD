# $Id: PKGBUILD 110893 2014-05-09 08:52:34Z tredaelli $
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=multibit
pkgver=0.5.18
pkgrel=2
pkgdesc="A lightweight Bitcoin desktop client powered by the BitCoinJ library."
arch=(any)
license=('MIT')
url="https://multibit.org/"
depends=('bash' 'java-runtime')
makedepends=('gendesk' 'zip')
noextract=("multibit-exe-${pkgver}.jar")
source=("multibit-exe-${pkgver}.jar::https://multibit.org/releases/multibit-$pkgver/multibit-exe.jar"
        https://raw.githubusercontent.com/jim618/multibit/v$pkgver/{COPYING,src/main/resources/images/multibit.png}
        multibit)
md5sums=('a9f2276def31a373a4a6383fb347b9ce'
         'd010f6532ce44e1baaaae32b4256850e'
         '41b492c2898448f0199129fcb1178aac'
         '01668ec277d3556174550227f6f0eb8d')

prepare() {
  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc"
}

package() {
  install -D -m755 "multibit" "${pkgdir}/usr/bin/multibit"
  install -D -m644 "multibit-exe-${pkgver}.jar" "${pkgdir}/usr/share/java/multibit/multibit-exe.jar"

  # Desktop launcher with icon
  install -D -m644 "multibit.desktop" "${pkgdir}/usr/share/applications/multibit.desktop"
  install -D -m644 "multibit.png"     "${pkgdir}/usr/share/pixmaps/multibit.png"

  # License
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/COPYING"

  # https://github.com/jim618/multibit/issues/325
  zip -d "${pkgdir}/usr/share/java/multibit/multibit-exe.jar" lib/x86_64/{freebsd,linux}/libscrypt.so
}
