# $Id: PKGBUILD 124740 2014-12-28 11:04:21Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=oniguruma
pkgver=5.9.6
pkgrel=1
pkgdesc="a regular expressions library"
arch=('i686' 'x86_64')
url="http://www.geocities.jp/kosako3/oniguruma/"
license=('BSD')
options=(!libtool)
source=("http://www.geocities.jp/kosako3/oniguruma/archive/onig-$pkgver.tar.gz")
sha256sums=('d5642010336a6f68b7f2e34b1f1cb14be333e4d95c2ac02b38c162caf44e47a7')

build() {
	cd "$srcdir"/onig-$pkgver
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir"/onig-$pkgver
	make DESTDIR="$pkgdir" install
	install -dm755 "$pkgdir"/usr/share/doc
	cp -r doc "$pkgdir"/usr/share/doc/$pkgname
	for licfile in COPYING INSTALL; do
		install -Dm644 $licfile "$pkgdir"/usr/share/licenses/$pkgname/$licfile
	done
}
