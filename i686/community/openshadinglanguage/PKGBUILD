# $Id: PKGBUILD 118558 2014-09-07 08:30:35Z foutrelis $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=openshadinglanguage
pkgver=1.5.10
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=(i686 x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'intel-tbb' 'freetype2' 'libpng' 'libtiff' 'llvm')
makedepends=('boost' 'cmake' 'clang')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz)
md5sums=('b828366f056e115b539ee1780883fc7d')

build() {
  cd OpenShadingLanguage-Release-$pkgver

  # Fails building this file on i686
  sed -i "/src\/testshade/d" CMakeLists.txt

  cd src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLLVM_STATIC=0 \
    ../..
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir"/usr/share/OSL/
  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/LICENSE "$pkgdir"/usr/share/licenses/$pkgname # TODO: Tell upstream about this shit
  mv "$pkgdir"/usr/{CHANGES,README.md,INSTALL} "$pkgdir"/usr/share/OSL/
  mv "$pkgdir"/usr/doc "$pkgdir"/usr/share/OSL/doc
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders
}

# vim:set ts=2 sw=2 et:
