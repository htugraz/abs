# $Id: PKGBUILD 101269 2013-11-21 12:18:33Z bpiotrowski $
# Maintainer:
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Gergely Tamas <dice@mfa.kfki.hu>

pkgname=arj
pkgver=3.10.22
pkgrel=8
pkgdesc="Free and portable clone of the ARJ archiver"
url="http://arj.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glibc')
options=('!makeflags')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        001_arches_align.patch
        002_no_remove_static_const.patch
        003_64_bit_clean.patch
        arj-3.10.22-custom-printf.patch)
md5sums=('f263bf3cf6d42a8b7e85b4fb514336d3'
         '550bc972d825036f17f202a2b11b35c2'
         '395dfa8cc500ffae648777f8f241be88'
         '56b3cf96ec485b0d824761457417fcc0'
         '4630a7254dfc6c8d8c7e696aa4a58f4a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Add gentoo patches
  patch -p1 -i "${srcdir}/001_arches_align.patch"
  patch -p1 -i "${srcdir}/002_no_remove_static_const.patch"
  patch -p1 -i "${srcdir}/003_64_bit_clean.patch"
  patch -p1 -i "${srcdir}/arj-3.10.22-custom-printf.patch"

  cd gnu
  aclocal
  autoconf
  rm -f config.{guess,sub}
  cp /usr/share/automake-1.14/config.{guess,sub} .
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/gnu"
  ./configure --prefix=/usr
  cd ..
  make prepare
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
