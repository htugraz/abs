# $Id: PKGBUILD 151722 2015-12-10 04:01:55Z foutrelis $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Christopher O'Neill <milkytracker@chrisoneill.co.uk>
# Contributor: Maks Verver <maksverver@geocities.com>

pkgname=milkytracker
pkgver=0.90.86
pkgrel=2
pkgdesc='Fast Tracker II inspired music tracker'
arch=('x86_64' 'i686')
url="http://www.milkytracker.org/"
license=('GPL3')
depends=('sdl' 'alsa-lib' 'zlib' 'gcc-libs')
makedepends=('jack-audio-connection-kit' 'gendesk')
optdepends=('jack-audio-connection-kit: JACK audio support')
options=('docs' '!strip')
install="$pkgname.install"
source=("http://milkytracker.org/files/$pkgname-$pkgver.tar.bz2")
sha256sums=('eb93000ed4855e198dfb02df54f3e3536dc891084ca47cdd5393121137ba3bed')

prepare() {
  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --name 'MilkyTracker' \
    --categories 'AudioVideo;Audio;AudioVideoEditing;'
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --with-alsa --with-jack
  make CXXFLAGS="$CXXFLAGS -lasound"
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/usr/share/applications" \
    "$pkgdir/usr/share/doc/milkytracker"
  install -Dm644 resources/pictures/carton.png \
    "$pkgdir/usr/share/pixmaps/milkytracker.png"
  install -m644 "$srcdir/milkytracker.desktop" \
    "$pkgdir/usr/share/applications"
  install -m644 docs/ChangeLog.html "$pkgdir/usr/share/doc/milkytracker"
  install -m644 docs/MilkyTracker.html "$pkgdir/usr/share/doc/milkytracker"
  install -m644 docs/readme_unix "$pkgdir/usr/share/doc/milkytracker"
  install -m644 docs/FAQ.html "$pkgdir/usr/share/doc/milkytracker"
}

# vim:set ts=2 sw=2 et:
