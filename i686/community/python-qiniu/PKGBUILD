# $Id: PKGBUILD 132726 2015-05-04 14:19:14Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-qiniu
pkgname=(python-qiniu python2-qiniu)
_pkgname=qiniu
pkgver=7.0.4
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/q/qiniu/qiniu-$pkgver.tar.gz"
        LICENSE)
sha512sums=('36a984a61ebeaa08974788be6378de761009cd265beec6b8be9a1302eadc1e596d05506ff5bfc07a0ed5177253d2f02edf4f224ed55db861896dcc646426fd34'
            '4fbb1090b3ee0d230d40b45db7bcd164873872d1860bd83b51197ed5d03e9d4be7785a400f1d314d6e3c0a6fcba7e1afdf3fdab04ae9970df59f96b18862c529')

prepare() {
  cp -a ${_pkgname}-$pkgver{,-py2}
}

package_python-qiniu() {
  depends=('python-requests' 'python-setuptools')

  cd ${_pkgname}-$pkgver
  python setup.py install -O1 --root "${pkgdir}"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-qiniu() {
  depends=('python2-requests' 'python2-setuptools')

  cd ${_pkgname}-$pkgver-py2
  python2 setup.py install -O1 --root "${pkgdir}"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Don't conflict with python 3.x version
  mv "$pkgdir"/usr/bin/qiniupy{,2}
}
