# $Id: PKGBUILD 102965 2013-12-25 06:55:50Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: cuihao <cuihao dot leo at gmail dot com>
# Contributor: Guten <ywzhaifei@gmail.com> 

pkgname=goagent
pkgver=3.1.2
pkgrel=1
pkgdesc="A gae proxy forked from gappproxy/wallproxy"
arch=("any")
url="http://goagent.googlecode.com"
license=("GPL2")
depends=('python2' 'python2-pyopenssl')
conflicts=('python2-gevent<0.99')
optdepends=('python2-gevent>0.99: for better performance'
            'python2-crypto: optimization for RC4')
makedepends=('git')
source=("git+https://github.com/goagent/goagent.git#tag=v$pkgver"
        "$pkgname.service")
backup=('etc/goagent')
install=goagent.install

package() {
  cd $pkgname

  sed -i -e "s|^#!/usr/bin/env python|#!/usr/bin/env python2|" local/proxy.py
  
  install -Dm755 local/proxy.py "$pkgdir/usr/share/$pkgname/local/goagent"
  install -Dm644 local/proxy.pac "$pkgdir/usr/share/$pkgname/local/proxy.pac"
  install -Dm644 local/cacert.pem "$pkgdir/usr/share/$pkgname/local/cacert.pem"
  install -Dm644 local/SwitchyOptions.bak "$pkgdir/usr/share/$pkgname/local/SwitchyOptions.bak"
  install -Dm644 local/SwitchySharp.crx "$pkgdir/usr/share/$pkgname/local/SwitchySharp_1_9_52.crx"

  install -Dm640 -g nobody local/proxy.ini "$pkgdir/etc/goagent"
  ln -sf "/etc/goagent" "${pkgdir}/usr/share/$pkgname/local/goagent.ini"
  
  cp -r server "$pkgdir/usr/share/$pkgname/"
  rm "$pkgdir/usr/share/$pkgname/server/uploader.bat"

  # systemd service
  install -Dm644 "${srcdir}/goagent.service" "${pkgdir}/usr/lib/systemd/system/goagent.service"
}

# vim:set ts=2 sw=2 et:
sha512sums=('SKIP'
            '4327d5b874c5150296f9c027a60994853de1b68d4c4e32291a5c41c0e162ffbccf68b6208cbc096d099721b470e1df897ad61596d8ddd705543c15b58e9a0f7c')
