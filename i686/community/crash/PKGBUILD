# $Id: PKGBUILD 133241 2015-05-11 20:18:21Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Bernhard Walle <bernhard.walle@gmx.de>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Contributor: Mathieu Pasquet <mathieui@mathieui.net>

pkgname=crash
pkgver=7.1.0
pkgrel=2
pkgdesc='Linux kernel crashdump analysis tool'
url='http://people.redhat.com/~anderson'
arch=(i686 x86_64)
license=(GPL)
depends=(xz ncurses zlib)
source=(http://people.redhat.com/~anderson/crash-$pkgver.tar.gz
        linux_version_4_support.patch)
sha256sums=('12e17267985ad1339b5da931143d4997fa4dafb82027d5664d4b7826175a21ec'
            'e535507a1b3c6ca366513beb82b205969a33085ecd48952610f96f3d717bc022')

prepare() {
  cd crash-$pkgver

  patch -Np1 -i "${srcdir}/linux_version_4_support.patch"

  make gdb_unzip
  sed -i '/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/' gdb-7.6/libiberty/configure
}

build() {
  cd crash-$pkgver
  make
}

package() {
  cd crash-$pkgver

  mkdir -p "$pkgdir"/usr/bin "$pkgdir"/usr/share/man/man8
  install crash "$pkgdir"/usr/bin/
  install crash.8 "$pkgdir"/usr/share/man/man8/
}
