# $Id: PKGBUILD 140456 2015-09-17 20:51:25Z foutrelis $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Ryan Corder <ryanc@greengrey.org>
# Contributor: Samuel Tardieu <sam@rfc1149.net>

pkgname=ckermit
pkgver=9.0.302
pkgrel=6
pkgdesc="Portable scriptable network and serial communication software"
arch=('i686' 'x86_64')
license=('custom')
depends=('ncurses')
url="http://www.columbia.edu/kermit/ck90.html"
#source=('ftp://kermit.columbia.edu/kermit/archives/cku302.tar.gz' 'lockdir.patch')
source=('ftp://ftp.kermitproject.org/kermit/archives/cku302.tar.gz'
	'lockdir.patch')
md5sums=('eac4dbf18b45775e4cdee5a7c74762b0'
         '5968e55dd472b2efb0e31cb3a6fb1655')

prepare() {
  cd $srcdir
  chmod -R og-rwx ./
  patch -p1 < lockdir.patch
}

build() {
  cd $srcdir
  make linux
  echo "#!/usr/bin/ckermit" > _tmp.ini
  cat ckermit.ini >> _tmp.ini
}

package() {
  cd $srcdir
  install -Dm0755 wermit $pkgdir/usr/bin/ckermit
  ln -s /usr/bin/ckermit $pkgdir/usr/bin/ckermit-sshsub
  install -Dm0644 ckuker.nr $pkgdir/usr//share/man/man1/ckermit.1
  install -Dm0644 COPYING.TXT $pkgdir/usr/share/licenses/$pkgname/License.txt
  install -Dm0755 _tmp.ini $pkgdir/usr/bin/ckermit.ini
}
