# $Id: PKGBUILD 136189 2015-07-01 07:15:06Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=thefuck
pkgver=1.46
pkgrel=2
pkgdesc="Magnificent app which corrects your previous console command"
arch=('any')
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python-setuptools' 'python-psutil' 'python-six' 'python-colorama')
makedepends=('git')
checkdepends=('python-pytest-mock' 'python-mock')
install=thefuck.install
source=("git+https://github.com/nvbn/thefuck.git#tag=$pkgver"
        psutil-3.patch)
sha256sums=('SKIP'
            'cc6d7c5569f46171b89686d4b6dc885a20d03386681add15e40bf9f4d00c4933')

prepare() {
  # pathlib is standard library since Python 3.4
  sed -i "s|install_requires=\['pathlib',|install_requires=[|" thefuck/setup.py

  cd thefuck
  patch -p1 -i ../psutil-3.patch
}

build() {
  cd thefuck
  python setup.py build
}

check() {
  cd thefuck
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test
}

package() {
  cd thefuck
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir/"

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
