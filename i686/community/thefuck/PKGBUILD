# $Id: PKGBUILD 136574 2015-07-09 03:44:13Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=thefuck
pkgver=1.48
pkgrel=1
pkgdesc="Magnificent app which corrects your previous console command"
arch=('any')
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python-setuptools' 'python-psutil' 'python-six' 'python-colorama')
makedepends=('git')
checkdepends=('python-pytest-mock' 'python-mock')
source=("git+https://github.com/nvbn/thefuck.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  # pathlib is standard library since Python 3.4
  sed -i "s|install_requires=\['pathlib',|install_requires=[|" thefuck/setup.py
}

build() {
  cd thefuck
  python setup.py build
}

check() {
  cd thefuck
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test
}

package() {
  cd thefuck
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir/"

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
