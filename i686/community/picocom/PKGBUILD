# $Id: PKGBUILD 138402 2015-08-16 10:26:24Z seblu $
# Maintainer: SÃ©bastien Luttringer
# Contributor: Jens Adam (byte/jra) <j_adam@web.de>

pkgname=picocom
pkgver=1.8
pkgrel=1
pkgdesc='Minimal dumb-terminal emulation program, very much like minicom'
url='https://github.com/npat-efault/picocom'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('glibc')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/npat-efault/picocom/archive/$pkgver.tar.gz")
sha1sums=('2f19b4e0e84f183c7bf1f3879a2d443246857631')

build() {
  cd $pkgname-$pkgver
  make UUCP_LOCK_DIR=/run/lock/picocom
}

package() {
  cd $pkgname-$pkgver
  install -D -m 755 picocom "$pkgdir/usr/bin/picocom"
  install -D -m 644 picocom.8 "$pkgdir/usr/share/man/man8/picocom.8"
  # install tmpfiles for lock files
  # http://lists.freedesktop.org/archives/systemd-devel/2011-March/001823.html
  install -D -m 644 /dev/null "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  echo "d /run/lock/$pkgname 0770 root uucp" \
    > "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
