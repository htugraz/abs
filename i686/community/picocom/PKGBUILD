# $Id: PKGBUILD 98869 2013-10-20 22:35:11Z seblu $
# Maintainer: SÃ©bastien Luttringer
# Contributor: Jens Adam (byte/jra) <j_adam@web.de>

pkgname=picocom
pkgver=1.7
pkgrel=3
pkgdesc='Minimal dumb-terminal emulation program, very much like minicom'
url='http://code.google.com/p/picocom/'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('glibc')
install=$pkgname.install
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.gz")
sha1sums=('bde6e36af71db845913f9d61f28dee1b485218fa')

build() {
  cd $pkgname-$pkgver
  make UUCP_LOCK_DIR=/run/lock/picocom
}

package() {
  cd $pkgname-$pkgver
  install -D -m 755 picocom "$pkgdir/usr/bin/picocom"
  install -D -m 644 picocom.8 "$pkgdir/usr/share/man/man8/picocom.8"
  # install tmpfiles for lock files
  # http://lists.freedesktop.org/archives/systemd-devel/2011-March/001823.html
  install -D -m 644 /dev/null "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  echo "d /run/lock/$pkgname 0770 root uucp" \
    > "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
