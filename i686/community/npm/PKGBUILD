# $Id: PKGBUILD 133966 2015-05-22 08:55:32Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=npm
pkgver=2.10.1
pkgrel=1
pkgdesc='A package manager for javascript'
arch=('any')
url='https://www.npmjs.com/'
license=('custom:Artistic')
depends=('nodejs')
provides=('nodejs-node-gyp')
makedepends=('git' 'procps-ng')
optdepends=('python2: for node-gyp')
options=('!emptydirs')
source=("git+https://github.com/npm/npm.git#tag=v$pkgver")
md5sums=('SKIP')

build() {
  cd npm
  make
}

package() {
  cd npm
  make NPMOPTS="--prefix=\"$pkgdir/usr\"" install
  
  # Fix files owned by nobody:
  chown -R root "$pkgdir/usr/lib/node_modules" 

  # Fix wrong symlinks
  rm -f "$pkgdir"/usr/share/man/{man1,man3,man5,man7}/*
  for _dir in man1 man3 man5 man7; do
    cd "$pkgdir"/usr/lib/node_modules/npm/man/$_dir
    for _file in *; do
      ln -s /usr/lib/node_modules/npm/man/$_dir/$_file "$pkgdir"/usr/share/man/$_dir/
    done
  done
  
  # Make symlink for node-gyp
  ln -s /usr/lib/node_modules/npm/node_modules/node-gyp/bin/node-gyp.js "$pkgdir"/usr/bin/node-gyp
  
  install -Dm644 "$srcdir/npm/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
