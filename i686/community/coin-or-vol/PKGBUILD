# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-vol
pkgver=1.4.4
pkgrel=1
pkgdesc="COIN-OR Volume Algorithm"
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/Vol"
license=('EPL')
groups=('coin-or')
depends=('coin-or-osi')
source=("http://www.coin-or.org/download/source/Vol/Vol-${pkgver}.zip")
sha1sums=('06acb5f84f91269a33f774db1f2644f24a0c87bc')

build() {
  cd Vol-$pkgver
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" -C
  make
}

check() {
  cd Vol-$pkgver
  make test
}

package() {
  cd Vol-$pkgver
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
