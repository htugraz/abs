# $Id: PKGBUILD 136958 2015-07-15 17:44:09Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Corrado 'bardo' Primier <corrado.primier@mail.polimi.it>
# Contributor: cs-cam <me.at.camdaniel.com>

pkgname=wammu
pkgver=0.39
pkgrel=1
pkgdesc="A wxPython-based GUI for Gammu"
arch=('any')
url="http://wammu.eu/"
license=('GPL')
depends=('gammu' 'wxpython')
optdepends=('gnome-bluetooth: Bluetooth support'
            'python2-pybluez: Bluetooth support'
            'notification-daemon: Notifications support')
source=(http://downloads.sourceforge.net/gammu/$pkgname-$pkgver.tar.bz2)
md5sums=('064eefd6477e77193936447826a6afde')


build() {
  cd "$pkgname-$pkgver"

  # python2 fix
  sed -i 's_/usr/bin/env python_/usr/bin/env python2_' setup.py
}

package() {
  cd "$pkgname-$pkgver"

  python2 setup.py install --root="${pkgdir}" --optimize=1
}
