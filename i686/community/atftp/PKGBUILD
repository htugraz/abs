# Maintainer: Benjamin Bukowski <benjamin.bukowski@gmail.com>
# Contributor: Johannes LÃ¶thberg <demizide@gmail.com>

pkgname=atftp
pkgver=0.7.1
pkgrel=1
pkgdesc='Client/server implementation of the TFTP protocol that implements RFCs 1350, 2090, 2347, 2348, and 2349'
arch=(i686 x86_64)
url='http://sourceforge.net/projects/atftp/'
license=(GPL)
depends=(pcre readline)
backup=(etc/conf.d/atftpd)
source=(http://downloads.sf.net/sourceforge/atftp/atftp-$pkgver.tar.gz
        atftpd.service
        atftpd.conf)
sha1sums=('fc9e9f821dfd2f257b4a5c32b948ed60b4e31fd1'
          'fb6a7a03aa2d37caf4202e152d84a18dd20e30f3'
          '0ee061597778d0ca850f053d0e3d9157e3376e79')

build() {
  cd atftp-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --sbindir=/usr/bin \
              --enable-libreadline --disable-libwrap
  make
}

package() {
  cd atftp-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 ../atftpd.service "$pkgdir"/usr/lib/systemd/system/atftpd.service
  install -Dm644 ../atftpd.conf "$pkgdir"/etc/conf.d/atftpd
  install -dm775 --group=nobody "$pkgdir"/srv/atftp 

  # remove in.tftpd link as it conflicts with extra/tftp-hpa
  rm "${pkgdir}/usr/share/man/man8/in.tftpd.8"
  rm "${pkgdir}/usr/bin/in.tftpd"
}
