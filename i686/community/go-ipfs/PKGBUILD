# $Id: PKGBUILD 145370 2015-10-31 14:51:25Z anatolik $
# Maintainer: Anatol Pomozov
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Ian Naval <ianonavy@gmail.com>

pkgname=go-ipfs
pkgver=0.3.9
pkgrel=1
pkgdesc='The InterPlanetary File System is a peer-to-peer hypermedia distribution protocol'
url='http://ipfs.io/'
arch=(i686 x86_64)
license=(MIT)
conflicts=(ipfs)
replaces=(ipfs)
depends=(glibc)
makedepends=(go)
optdepends=('fuse: for mounting/advanced use')
source=(go-ipfs-$pkgver.tar.gz::https://github.com/ipfs/go-ipfs/archive/v$pkgver.tar.gz
	ipfs.service)
sha512sums=('34d441f3dee23fb3002b319a5d925f4a436f80c25cbaf100c5cd13686fd4e0cc18864f11da7ac163fead78149d26a9cfac30a884e191cd019b841bd2672ee643'
            '1a6bc7fffc3c3640cda3f33c107424edd698c62bd8509731856b8827ea18be1a1845ef0d5ca15325d48d7d871a78daacc011ff9eacea926bc35899850990c1c6')

prepare() {
  cd go-ipfs-$pkgver

  mkdir -p .gopath/src/github.com/ipfs
  ln -sf "$PWD" .gopath/src/github.com/ipfs/go-ipfs
}

build() {
  cd go-ipfs-$pkgver/cmd/ipfs
  GOPATH="$PWD/../../.gopath" go build -v
}

package() {
  cd go-ipfs-$pkgver

  install -Dm 755 cmd/ipfs/ipfs "$pkgdir/usr/bin/ipfs"
  install -Dm 644  ../ipfs.service "$pkgdir/usr/lib/systemd/user/ipfs.service"
  install -Dm 644 misc/completion/ipfs-completion.bash "$pkgdir/etc/bash_completion.d/ipfs"
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
