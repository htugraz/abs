# $Id: PKGBUILD 140362 2015-09-16 09:37:58Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-xpybutil
pkgname=python2-xpybutil
pkgver=0.0.1.20150328
_commit=8ee7dc406aabb3d54f38e692bf068b1b61131d83
pkgrel=1
pkgdesc="An incomplete xcb-util port plus some extras"
arch=('any')
url="https://github.com/BurntSushi/xpybutil"
license=('custom:WTFPL')
makedepends=('git' 'python-xcffib' 'python2-xcffib' 'xorg-server-xvfb')
source=("git+https://github.com/BurntSushi/xpybutil.git#commit=$_commit"
        xcffib-compat.patch)
md5sums=('SKIP'
         '6623c64e52c5753d759ef3f4ff6a8a87')

prepare() {
  cd "$srcdir/xpybutil"
  patch -p1 -i ../xcffib-compat.patch
  sed -i "s|except:|except IOError:|" setup.py
}

build() {
  cd "$srcdir/xpybutil"
  xvfb-run python2 setup.py build
}

package_python2-xpybutil() {
  depends=('python2-xcffib')

  cd "${srcdir}/xpybutil"
  xvfb-run python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm664 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
