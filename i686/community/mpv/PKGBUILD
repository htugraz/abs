# $Id: PKGBUILD 133599 2015-05-16 16:23:58Z anatolik $
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv
pkgver=0.9.1
pkgrel=2
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=(
  'ffmpeg' 'lcms2' 'libcdio-paranoia' 'libgl' 'enca'
  'libxinerama' 'libxv' 'libxkbcommon' 'libva' 'wayland'
  'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua52' 'libdvdnav'
  'libguess' 'libxrandr' 'jack' 'smbclient' 'rubberband'
)
makedepends=('mesa' 'python-docutils' 'ladspa' 'hardening-wrapper')
optdepends=('youtube-dl: for video-sharing websites playback')
options=('!emptydirs' '!buildflags')
install=mpv.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/mpv-player/$pkgname/archive/v$pkgver.tar.gz"
  '0001-mpv-lua52.patch')
md5sums=('c766cf3d6a7eb82a74564c47fa14d4a5'
  'cb4fdc39561bf11fefd2b93769c91fec')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < $srcdir/0001-mpv-lua52.patch
  ./bootstrap.py
}

build() {
  cd $pkgname-$pkgver

  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-zsh-comp \
    --enable-libmpv-shared \
    --enable-cdda

  ./waf build
}

package() {
  cd $pkgname-$pkgver
  ./waf install --destdir="$pkgdir"

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
