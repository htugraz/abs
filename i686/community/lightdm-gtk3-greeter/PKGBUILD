# $Id: PKGBUILD 105173 2014-02-02 11:18:41Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk3-greeter
pkgver=1.6.1
pkgrel=2
epoch=1
pkgdesc="GTK+ greeter for LightDM"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('exo' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=("${pkgname/3/}")
conflicts=("${pkgname/3/2}")
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=("https://launchpad.net/${pkgname/3/}/1.6/${pkgver}/+download/${pkgname/3/}-${pkgver}.tar.gz"
        'fix-null-username.patch')
sha256sums=('1f0286ab093d50151f0efc46496aef3f8ed8c7306bbc81807a3fdcbcb2a468d9'
            '27e1ec5f73f963ea5fbc69d4b380dbfa47a4066b35fa1a00341203568d9aa64d')

prepare() {
  cd ${pkgname/3/}-${pkgver}

  patch -Np0 -i ../fix-null-username.patch
}

build() {
  cd ${pkgname/3/}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' --libexecdir='/usr/lib/lightdm' --disable-static
  make
}

package() {
  cd ${pkgname/3/}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
