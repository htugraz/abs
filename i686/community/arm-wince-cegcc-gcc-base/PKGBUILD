# $Id: PKGBUILD 99680 2013-10-30 22:59:10Z allan $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=arm-wince-cegcc-gcc-base
pkgver=0.59.1
pkgrel=3
_prefix=/opt/cegcc
pkgdesc="CE GCC bundle - cross compilation tools for WinCE"
arch=(i686 x86_64)
license=(GPL)
url="http://cegcc.sf.net"
depends=('glibc' 'mpfr' 'cloog')
makedepends=(arm-wince-cegcc-binutils)
source=(http://arch.p5n.pp.ru/~sergej/dl/cegcc-$pkgver.tar.bz2)
md5sums=('5e3c1098abdf8e5db2a3518ee3578b4b')

build() {
  cd $srcdir/cegcc/src

  mkdir build-gcc-base || true
  cd build-gcc-base

  export PATH=$_prefix/bin:$PATH
  export TARGET=arm-wince-cegcc
  unset CFLAGS
  unset LDFLAGS
  unset MAKEFLAGS

  ../gcc-4.4.0/configure               \
	--with-gcc                     \
	--with-gnu-ld                  \
	--with-gnu-as                  \
	--target=arm-wince-cegcc       \
	--build=$CARCH		       \
	--host=$CARCH		       \
	--prefix=${_prefix}            \
	--disable-threads              \
	--disable-nls                  \
	--disable-shared               \
	--enable-languages=c           \
	--disable-win32-registry       \
	--disable-multilib             \
	--disable-interwork            \
	--without-headers              \
	--with-as=$_prefix/bin/arm-wince-cegcc-as \
	--with-ld=$_prefix/bin/arm-wince-cegcc-ld \
	--with-local-prefix=$_prefix \
	--enable-checking

  make all-gcc || (cd gcc && make) && make all-gcc
  make DESTDIR=$pkgdir install-gcc
  find $pkgdir/${_prefix} -type f -name \*.a -exec arm-wince-cegcc-ranlib {} \;
  cd $pkgdir/${_prefix} && ln -s arm-wince-cegcc arm-cegcc
}
