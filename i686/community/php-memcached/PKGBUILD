# $Id: PKGBUILD 95810 2013-08-17 08:23:10Z pierre $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=php-memcached
pkgver=2.1.0
pkgrel=2
pkgdesc="PHP extension for interfacing with memcached via libmemcached library"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/memcached"
license=('PHP')
depends=('php' 'libmemcached')
backup=('etc/php/conf.d/memcached.ini')
source=(http://pecl.php.net/get/memcached-$pkgver.tgz)
sha256sums=('bc4940015be74f47908d410d7b55e10a3d5bf65674036d944c73558227fcc4af')

prepare() {
  cd "$srcdir/memcached-$pkgver"

  # Fix FTBFS with libmemcached >= 1.0.17
  # https://github.com/php-memcached-dev/php-memcached/issues/69#issuecomment-17051990
  echo 'typedef const struct memcached_server_st *memcached_server_instance_st;' \
    >>php_libmemcached_compat.h
}

build() {
  cd "$srcdir/memcached-$pkgver"

  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/memcached-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
  echo ';extension=memcached.so' >memcached.ini
  install -Dm644 memcached.ini "$pkgdir/etc/php/conf.d/memcached.ini"
}

# vim:set ts=2 sw=2 et:
