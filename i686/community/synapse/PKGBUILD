# $Id: PKGBUILD 117225 2014-08-12 01:20:12Z bgyorgy $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=synapse
pkgver=0.2.10
pkgrel=7
pkgdesc="A semantic file launcher"
arch=('i686' 'x86_64')
url="https://launchpad.net/synapse-project"
license=('GPL3')
depends=('gtkhotkey' 'hicolor-icon-theme' 'json-glib' 'libgee' 'libnotify' \
         'libunique' 'libzeitgeist' 'rest' 'xdg-utils')
makedepends=('intltool' 'vala')
optdepends=('banshee: banshee plugin'
            'bc: calculator plugin'
            'devhelp: documentation plugin'
            'gnome-screensaver: screensaver plugin'
            'gnome-dictionary: dictionary plugin'
            'openssh: ssh plugin'
            'pastebinit: pastebin plugin'
            'rhythmbox: rhythmbox plugin'
            'xnoise: xnoise plugin')
install=$pkgname.install
source=("https://launchpad.net/$pkgname-project/0.2/$pkgver/+download/$pkgname-$pkgver.tar.gz"
        "fix-check-desktop.patch"
        "check-null-exec.patch"
        "concrete-gtk-timeout.patch"
        "gee-0.8.patch")
sha1sums=('6e8a800bdbdded4e167734c8e49d95a9e44998ff'
          'b64fa4efc4efd01f77f84d19a7a63c10186d0211'
          'f494e5b36a77421114ce04b7085369abe5c00d68'
          '6ab8b30bb23fb143d641da62dec7caa61fcd4e28'
          '3e3e9359ff8b56fc7fbed405e5936dd97ab99047')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # XDG_CURRENT_DESKTOP fix
  patch -Np1 -i "$srcdir"/fix-check-desktop.patch

  # don't crash on empty Exec field
  patch -Np1 -i "$srcdir"/check-null-exec.patch

  # Namely use Gtk.Timeout:
  patch -Np1 -i "$srcdir"/concrete-gtk-timeout.patch

  # Port to gee-0.8
  patch -Np1 -i "$srcdir"/gee-0.8.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # DSO fix
  export LDFLAGS="$LDFLAGS -lm"

  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
