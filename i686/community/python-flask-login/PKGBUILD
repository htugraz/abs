# $Id: PKGBUILD 142437 2015-10-01 16:05:14Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flask-login
pkgname=('python2-flask-login' 'python-flask-login')
pkgver=0.3.1
_commit=f1d961c0973b83a5f1ab935b20f1be99a445882a
pkgrel=1
pkgdesc="User session management for Flask."
arch=('any')
url="https://pythonhosted.org/Flask-Login/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-flask' 'python2-flask' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-blinker' 'python2-blinker')
source=("git+https://github.com/maxcountryman/flask-login.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cp -a flask-login{,-py2}
}

check() {
  cd "$srcdir/flask-login"
  nosetests3

  cd "$srcdir/flask-login-py2"
  # https://github.com/maxcountryman/flask-login/issues/232
  nosetests2 || warning "Tests failed"
}

package_python-flask-login() {
  depends=('python-flask')

  cd flask-login
  python setup.py install --root="$pkgdir/" --optimize=1

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}

package_python2-flask-login() {
  depends=('python2-flask')

  cd flask-login-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
