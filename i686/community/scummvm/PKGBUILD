# $Id: PKGBUILD 160945 2012-06-07 12:04:14Z juergen $
# Maintainer: juergen <juergen@archlinux.org>
# Contributor: Manolis Tzanidakis, Petter Gundersen

pkgname=scummvm
pkgver=1.7.0
pkgrel=2
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.scummvm.org/"
depends=('libpng' 'libtheora' 'sdl' 'fluidsynth' 'flac' 'faad2' 'libvorbis' 'libmad' 'freetype2'
         'libgl' 'glu')
makedepends=('mesa')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('be91bf3fa2f1b44a82c92cfe8c5cc1c8')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --disable-debug \
    --enable-release \
    --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 dists/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
}
