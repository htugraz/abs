# $Id: PKGBUILD 125194 2015-01-02 23:40:07Z alucryd $
# Maintainer: Maxime Gauduin <alucryd at gmail dot com> 
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>
# Contributor: dcelasun <dcelasun at gmaild dot com>

pkgname=plank
pkgver=0.8.0
pkgrel=1
pkgdesc='Elegant, simple, clean dock'
arch=('i686' 'x86_64')
url='https://launchpad.net/plank'
license=('GPL3')
depends=('bamf' 'libdbusmenu-gtk3' 'libgee')
makedepends=('gnome-common' 'intltool' 'vala')
install="${pkgname}.install"
source=("https://launchpad.net/${pkgname}/1.0/${pkgver}/+download/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6ce80465792f2b89b180c5273b820d05696dc3a59187d33a7fcd29fb528c89eb')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --sysconfdir='/etc' --disable-apport
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
