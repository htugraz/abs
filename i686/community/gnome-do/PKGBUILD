# $Id: PKGBUILD 120417 2014-10-09 00:54:11Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do
pkgver=0.95.1
pkgrel=2
pkgdesc="Quickly search for many items present on your desktop or the web, and perform useful actions on those items"
arch=('i686' 'x86_64')
url="http://cooperteam.net/"
license=('GPL3')
depends=('gconf-sharp' 'gnome-desktop2' 'gnome-keyring-sharp' 'mono-addins' 'notify-sharp' 'gkeyfile-sharp')
makedepends=('intltool' 'gio-sharp')
install=$pkgname.install
source=(http://launchpad.net/do/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        fix-build.patch
        05_do_gtk_threading_correctly.diff)
md5sums=('1f61376718d75d3defcdf81958754296'
         '4ac79a8270bc6c1539f56b8f56043894'
         'f2f0293eb43648d0e04e45030f32b853')

prepare() {
  cd $pkgname-$pkgver

  # Fix build
  patch -Np1 -i ../fix-build.patch

  # Fix crasher
  patch -Np1 -i ../05_do_gtk_threading_correctly.diff
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
