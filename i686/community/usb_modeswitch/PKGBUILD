# $Id: PKGBUILD 147034 2015-11-17 19:27:58Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=usb_modeswitch
pkgver=2.2.6
pkgrel=1
_pkgdata=20151101
pkgdesc="Activating switchable USB devices on Linux."
arch=('i686' 'x86_64')
url="http://www.draisberghof.de/usb_modeswitch/"
license=('GPL')
depends=('libusb' 'tcl')
makedepends=('gcc' 'make')
backup=("etc/$pkgname.conf")
source=("http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-${pkgver}.tar.bz2"
	"http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-data-${_pkgdata}.tar.bz2"
	"usb-modeswitch-${pkgver}-device_reference.txt::http://www.draisberghof.de/usb_modeswitch/device_reference.txt")
md5sums=('ea050e3b44a2b7aa805619042f5448df'
         '21af977bfc4e7a705d318e88d7a63494'
         '080bac4145f002466b1cbd1b973bba44')

prepare() {
  cd $srcdir/usb-modeswitch-$pkgver/
  sed -i 's|/usr/sbin/usb_mode|/usr/bin/usb_mode|g' usb_modeswitch.{conf,tcl,sh}
}

build() {
  cd $srcdir/usb-modeswitch-$pkgver/
  make clean
  make SBINDIR=/usr/bin
}

package() {
  cd $srcdir/usb-modeswitch-$pkgver/

  mkdir -p $pkgdir/usr/lib/udev
  mkdir -p $pkgdir/etc
  mkdir -p $pkgdir/usr/share/man/man1

  make DESTDIR=${pkgdir} UDEVDIR=${pkgdir}/usr/lib/udev SBINDIR=$pkgdir/usr/bin install

  cp $srcdir/usb-modeswitch-${pkgver}-device_reference.txt $pkgdir/etc/usb_modeswitch.setup

  cd $srcdir/usb-modeswitch-data-${_pkgdata}
  make DESTDIR=$pkgdir install RULESDIR=${pkgdir}/usr/lib/udev/rules.d
}
