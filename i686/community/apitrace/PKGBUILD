# $Id: PKGBUILD 111076 2014-05-13 08:12:14Z lcarlier $
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Contributor: Glaucous <glakke1 at gmail dot com>

pkgname=apitrace
pkgver=5.0
pkgrel=1
pkgdesc="Graphics API Tracing"
arch=('i686' 'x86_64')
url="https://github.com/apitrace/apitrace"
license=('custom')
makedepends=('cmake' 'mesa' 'libgl' 'python2' 'qtwebkit' 'qjson')
optdepends=('qtwebkit: GUI support' 'qjson: GUI support')
source=("https://github.com/apitrace/apitrace/archive/${pkgver}.zip")
md5sums=('2a60e8a4c90f841dc95fe659ba26607e')

build() {
  cd ${srcdir}/apitrace-*

  cmake . -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE='/usr/bin/python2'
  make -C build
}

package() {
  depends=('python2' 'libgl')
  cd ${srcdir}/apitrace-*

  make -C build DESTDIR="${pkgdir}/" install

  install -m755 -d "${pkgdir}/usr/share/licenses/apitrace"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/apitrace/"
}
