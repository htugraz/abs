# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-power-manager
pkgver=1.8.0
pkgrel=4
pkgdesc="Power management tool for the MATE desktop."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'libcanberra' 'libgnome-keyring' 'libnotify'
         'libunique' 'mate-panel' 'upower')
makedepends=('docbook2x' 'docbook-xml' 'mate-common' 'xmlto' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz"
        "00-add-dbus-interface-to-kbdbacklight.diff"
        "10-upower-0.99.diff"
        "20-improve-upower-1.0-support.diff"
        "30-more-upower-0.99-api-changes.diff")
sha1sums=('5cde22f2d6498294a76e3bf9005b4f73bbdd0db8'
          'f1acff9fa6140c56cdb39531fc02e886f5a7437d'
          '210d0558d4d6eab23c5d55073f891610bd79225b'
          '15116bb83763c0b1a82d0aae870ed204fb4f418c'
          'dd8fdd1f17ad1be4b8ae5562d6ed856c1d3fe2e6')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    #Work around a problem where the .sgml fail to parse.
    sed -e 's:@HAVE_DOCBOOK2MAN_TRUE@.*::' -i man/Makefile.in
    
    # Add upower 0.99 compatibility
    patch -Np1 -i "${srcdir}/00-add-dbus-interface-to-kbdbacklight.diff"
    patch -Np1 -i "${srcdir}/10-upower-0.99.diff"
    patch -Np1 -i "${srcdir}/20-improve-upower-1.0-support.diff"
    patch -Np1 -i "${srcdir}/30-more-upower-0.99-api-changes.diff"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --enable-applets \
        --enable-unique
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
