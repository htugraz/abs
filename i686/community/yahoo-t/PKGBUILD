# $Id: PKGBUILD 90743 2013-05-13 14:43:11Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=yahoo-t
pkgver=0.4
pkgrel=7
pkgdesc="jabber yahoo transport"
arch=('any')
url="http://xmpppy.sourceforge.net/yahoo/"
license=('GPL')
depends=('python2' 'xmpppy')
backup=(etc/ejabberd/yahoo.xml)
source=(http://downloads.sourceforge.net/xmpppy/yahoo-transport-$pkgver.tar.gz
	yahoo-t.service)
md5sums=('ce928c4e444b83f10fca57f77e40b558'
         'e093f7f45c1bd755b6b0f13e83ec3276')

package() {
  cd $srcdir
  rm -rf yahoo-transport-$pkgver/curphoo/CVS
  install -D -m0644 ./yahoo-transport-$pkgver/config_example.xml $pkgdir/etc/ejabberd/yahoo.xml

  install -d -m0755 $pkgdir/usr/lib
  cp -r yahoo-transport-$pkgver $pkgdir/usr/lib

  mv $pkgdir/usr/lib/yahoo-transport-$pkgver $pkgdir/usr/lib/yahoo-t
  ln -s ../../../etc/ejabberd/yahoo.xml $pkgdir/usr/lib/yahoo-t/config.xml
  find $pkgdir -type f -name \*.py -exec sed -i 's#/usr/bin/env python#/usr/bin/env python2#' {} \;

  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
}
