# $Id: PKGBUILD 123465 2014-12-05 02:17:14Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=lwqq
pkgver=0.4.1
_gitver="tag=v$pkgver"
#_gitver='commit=472881c1b122bad32089c1488a43414e1f85e077'
pkgrel=1
pkgdesc="A library for webqq protocol"
arch=('i686' 'x86_64')
url="https://github.com/xiehuc/lwqq"
license=('GPL3')
depends=('curl' 'libev' 'js17' 'sqlite')
makedepends=('git' 'cmake')
source=("git://github.com/xiehuc/lwqq.git#$_gitver")
sha512sums=('SKIP')

build() {
  cd $pkgname
  
  sed -e 's/set(LIBSUFFIX "64")/set(LIBSUFFIX "")/' \
      -i CMakeLists.txt
      
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
