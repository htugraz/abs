# $Id: PKGBUILD 115604 2014-07-12 16:34:00Z speps $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=synthv1
pkgver=0.5.0
pkgrel=1
pkgdesc="An old-school all-digital 4-oscillator subtractive polyphonic synthesizer with stereo fx"
arch=(i686 x86_64)
url="http://$pkgname.sourceforge.net/$pkgname-index.html"
license=('GPL')
groups=('lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt4' 'shared-mime-info')
install="$pkgname.install"
source=("http://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('8c39ce2180abb6dedca4906fe1c499ed')

prepare() {
  cd $pkgname-$pkgver

  # x86_64 lib path fix
  sed -i "s/lib64/lib/" src/src_lv2{,ui}.pro

  # i686 -reduce-relocations (Qt5)
#  if [[ $CARCH = i686 ]]; then
#    export CXXFLAGS+=" -fPIE"
#  fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
