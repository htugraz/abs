# $Id: PKGBUILD 138035 2015-08-06 17:39:18Z stativ $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=cdrtools
pkgver=3.01a31
pkgrel=1
pkgdesc="Original cdrtools supporting CD, DVD and BluRay burning"
arch=('i686' 'x86_64')
url="http://cdrecord.org/"
license=('CDDL')
depends=('acl' 'libcap')
conflicts=('cdrkit')
provides=('cdrkit')
options=(!makeflags)
backup=('etc/default/cdrecord' 'etc/default/rscsi')
source=(http://downloads.sourceforge.net/cdrtools/cdrtools-$pkgver.tar.bz2)
md5sums=('78172557894f469b4584d008e93ec469')

build() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}
  sed -i 's|/opt/schily|/usr|g' DEFAULTS/Defaults.linux
  sed -i 's|DEFINSGRP=.*|DEFINSGRP=root|' DEFAULTS/Defaults.linux
  # install rscsi to /usr/bin instead of /usr/sbin
  sed -i 's|INSDIR=.*|INSDIR=bin|' rscsi/Makefile
  
  export GMAKE_NOWARN=true
  make INS_BASE=/usr INS_RBASE=/
}

package() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}

  export GMAKE_NOWARN=true
  make INS_BASE=/usr INS_RBASE=/ DESTDIR="$pkgdir" install

  # remove unneeded files that may cause conflicts
  rm -rf "$pkgdir"/usr/include
  rm -rf "$pkgdir"/usr/lib/{*.a,profiled}
  rm -rf "$pkgdir"/usr/share/man/man3

  # autoload 'sg' module needed by cdrecord
  install -d -m755 "$pkgdir"/usr/lib/modules-load.d/
  echo sg > "$pkgdir"/usr/lib/modules-load.d/cdrecord.conf

  # create symlinks for cdrkit compatibility
  cd "$pkgdir"/usr/bin
  ln -s cdrecord wodim
  ln -s readcd readom
  ln -s mkisofs genisoimage
  ln -s cdda2wav icedax
}

# vim:set ts=2 sw=2 et:
