# $Id: PKGBUILD 142247 2015-09-30 08:46:36Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-setproctitle
pkgname=(python-setproctitle python2-setproctitle)
pkgver=1.1.9
pkgrel=2
pkgdesc="Allows a python process to change its process title"
license=("BSD")
url="https://github.com/dvarrazzo/py-setproctitle"
makedepends=('python' 'python2')
arch=('i686' 'x86_64')
source=(http://pypi.python.org/packages/source/s/setproctitle/setproctitle-$pkgver.tar.gz)
sha256sums=('1c3414d18f9cacdab78b0ffd8e886d56ad45f22e55001a72aaa0b2aeb56a0ad7')

build() {
    cd "$srcdir"/setproctitle-$pkgver

    rm -rf ../buildpy3; mkdir ../buildpy3
    python setup.py build -b ../buildpy3

    rm -rf ../buildpy2; mkdir ../buildpy2
    python2 setup.py build -b ../buildpy2
    find ../buildpy2 -name \*.py -exec sed -r '1 s|^#!(.*)python$|#!\1python2|' -i {} +
}

package_python-setproctitle() {
    depends=(python)

    cd "$srcdir"/setproctitle-$pkgver
    rm -rf build; ln -s ../buildpy3 build
    python setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}

package_python2-setproctitle() {
    depends=(python2)

    cd "$srcdir"/setproctitle-$pkgver
    rm -rf build; ln -s ../buildpy2 build
    python2 setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
