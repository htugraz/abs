# $Id: PKGBUILD 127528 2015-02-10 17:50:47Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: jlvsimoes

pkgname=osiris
pkgver=4.2.3
pkgrel=8
pkgdesc="A file integrity management system"
arch=('i686' 'x86_64')
url="https://launchpad.net/osiris"
license=('custom')
depends=('openssl' 'readline')
install=$pkgname.install
#source=(http://osiris.shmoo.com/data/osiris-$pkgver.tar.gz
#source=(http://arch.p5n.pp.ru/~sergej/dl/2011/osiris-$pkgver.tar.gz
source=(https://launchpad.net/osiris/main/$pkgver/+download/osiris-$pkgver.tar.gz
	osirismd.service
	osirisd.service)
md5sums=('1951c7dc0fe729af9ffaf58910340d12'
         '8398d60bcbf0c90034473365abc3ece6'
         '758d029a1ff53be11756330de595e77f')

build() {
  cd $srcdir/$pkgname-$pkgver

  sed -i "s|/sbin|/bin|g" src/install/install.sh.in

  ./configure --prefix=/usr --sbindir=/usr/bin \
    --enable-fancy-cli --with-md-root-dir=/usr/share/$pkgname
  make all

  # Arch-ify install locations
  sed -i "s|^INSTALL_OSIRISM=0|INSTALL_OSIRISM=1|" src/install/install.sh
  sed -i "s|^INSTALL_OSIRISD=0|INSTALL_OSIRISD=1|" src/install/install.sh
  sed -i "s|^ROOT_GROUP=wheel|ROOT_GROUP=root|" src/install/install.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver

  mkdir -p $pkgdir/usr/share/licenses/$pkgname/
  install -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/license.txt

  make DESTDIR=$pkgdir install

  rm -rf $pkgdir/usr/osiris $pkgdir/etc/init.d

  install -Dm0644 $srcdir/osirisd.service $pkgdir/usr/lib/systemd/system/osirisd.service
  install -Dm0644 $srcdir/osirismd.service $pkgdir/usr/lib/systemd/system/osirismd.service
}
