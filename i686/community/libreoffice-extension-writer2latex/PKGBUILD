# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Uwe Koloska <kolewu@koloro.de>
# Contributor: EpocSquadron <crimsonmage a gmail>

pkgname=libreoffice-extension-writer2latex
_javaname=writer2latex
_exts=( writer2latex writer2xhtml writer4latex )
pkgver=1.2.1
pkgrel=8
pkgdesc="A java program and collection of LibreOffice extensions for converting to and working with LaTeX in LibreOffice"
arch=('any')
url="http://writer2latex.sourceforge.net"
license=('LGPL')
groups=('libreoffice-extensions')
depends=('libreoffice-common' 'java-runtime-headless')
makedepends=('libreoffice-writer')

_srcname=${_javaname}${pkgver//.}
_dlpath="http://downloads.sourceforge.net/project/writer2latex/writer2latex/Writer2LaTeX%20${pkgver%.*}"
source=("${_dlpath}/${_srcname}.zip"
        'writer2latex.sh')
md5sums=('643906496a5ec519cd4ee044305675bc'
         'f2d20081ef6d5c905d54385697856642')

build() {
    cd $_srcname/doc
    libreoffice --headless --convert-to pdf user-manual.odt
}

package() {
    cd $_srcname

    #Install the extensions
    for _ext in "${_exts[@]}"; do
	install -dm755  "$pkgdir/usr/lib/libreoffice/share/extensions/$_ext"
	bsdtar -xf "$_ext.oxt" -C "$pkgdir/usr/lib/libreoffice/share/extensions/$_ext"
    done
    
    #Install the java converter
    install -D -m644 "$_javaname.jar" "$pkgdir/usr/share/java/$_javaname/$_javaname.jar"
    
    #Install shell script to execute java program
    install -D -m755 "$srcdir/$_javaname.sh" "$pkgdir/usr/bin/$_javaname"
    
    #Install documentation
    _docpath="$pkgdir/usr/share/doc/$pkgname"
    install -D -m644 Readme.txt "${_docpath}/Readme.txt"
    install -D -m644 changelog.txt "${_docpath}/changelog.txt"
    install -D -m644 History.txt "${_docpath}/History.txt"
    install -D -m644 doc/* "${_docpath}/"
}
