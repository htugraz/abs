# $Id: PKGBUILD 151967 2015-12-10 04:10:21Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Neil Lin <neil@vip.url.com.tw>
# Contributor: Cravix < dr dot neemous at gmail dot com >

pkgbase=stardict
pkgname=(stardict stardict-lite)
pkgver=3.0.6
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.stardict.org/'
license=('GPL')
makedepends=('intltool' 'popt' 'gnome-doc-utils' 'gnome-common' 'libmariadbclient' 'enchant' 'gtk2' 'libsigc++' 'libsm' 'espeak' 'libgnome')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/project/stardict-4/$pkgver/$pkgbase-$pkgver.tar.bz2"
        'gcc46.patch'
        'glib2.patch')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -p1 -i ../gcc46.patch
  patch -p1 -i ../glib2.patch

  cp -a "${srcdir}/${pkgbase}-${pkgver}" "${srcdir}/${pkgbase}-${pkgver}-lite"
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  CXXFLAGS+=" -std=gnu++11"
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --disable-schemas-install \
    --disable-gucharmap \
    --disable-festival \
    --disable-updateinfo
  make

  cd "${srcdir}/${pkgbase}-${pkgver}-lite"
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --disable-gnome-support \
    --disable-schemas-install \
    --disable-espeak \
    --disable-gucharmap \
    --disable-festival \
    --disable-updateinfo
  make
}

package_stardict() {
  pkgdesc='International dictionary software'
  depends=('enchant' 'gtk2' 'libsigc++' 'libsm' 'espeak' 'libgnome')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_stardict-lite() {
  pkgdesc='International dictionary software - lite version without gnome and espeak support'
  depends=('enchant' 'gtk2' 'libsigc++' 'libsm')
  provides=("stardict=$pkgver")
  conflicts=(stardict)

  cd "${srcdir}/${pkgbase}-${pkgver}-lite"
  make DESTDIR="${pkgdir}" install
}

md5sums=('93371b35482e6380e8bd4ba5d256b864'
         '52ff47441c503de07add4b7688510bc5'
         'b2f216897c9053c7e4666c3d78705cf1')
