# $Id: PKGBUILD 100213 2013-11-01 21:20:33Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=libosip2
epoch=1
pkgver=4.0.0
pkgrel=2
pkgdesc="oSIP is an implementation of SIP"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/osip/"
license=('LGPL')
depends=(glibc)
options=(!emptydirs)
source=(http://ftp.gnu.org/gnu/osip/libosip2-${pkgver/_/-}.tar.gz)
md5sums=('c6a2c175d39ce75c15026c5738732dab')

build() {
  cd $srcdir/libosip2-${pkgver/_/-}/

  patch -p1 <<EOF
diff -wbBur libosip2-3.1.0/src/osip2/port_sema.c libosip2-3.1.0.my/src/osip2/port_sema.c
--- libosip2-3.1.0/src/osip2/port_sema.c	2007-06-05 14:17:50.000000000 +0400
+++ libosip2-3.1.0.my/src/osip2/port_sema.c	2008-10-22 17:47:37.000000000 +0400
@@ -22,6 +22,14 @@
 #include <stdlib.h>
 #include <stdio.h>

+union semun
+{
+     int val;
+     struct semid_ds *buf;
+     unsigned short int *array;
+     struct seminfo *__buf;
+};
+
 #include <osip2/internal.h>

 #include <osip2/internal.h>
EOF

  ./configure --prefix=/usr --disable-semaphore --enable-sysv
  make
}

package() {
  cd $srcdir/libosip2-${pkgver/_/-}/
  make DESTDIR=$pkgdir install
}
