# $Id: PKGBUILD 104326 2014-01-17 15:42:38Z lfleischer $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>
# Contributor: Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>

pkgname=cgit
pkgver=0.10
pkgrel=1
pkgdesc="A web interface for git written in plain C"
arch=('i686' 'x86_64')
url="http://hjemli.net/git/cgit"
license=('GPL2')
depends=('openssl' 'luajit')
makedepends=('zlib' 'curl' 'asciidoc')
install=cgit.install
source=("http://git.zx2c4.com/cgit/snapshot/cgit-$pkgver.tar.xz"
	"apache.example.conf")
md5sums=('19944c17ecea1b1d1944718ce8ce6b61'
         'bd5696bea0654bfcf98f249b1a36737d')

prepare() {
  cd "$pkgname-$pkgver"
  make get-git
}

build() {
  cd "$pkgname-$pkgver"
  make
  make doc-man
}

package() {
  cd "$pkgname-$pkgver"

  make CGIT_SCRIPT_PATH=/usr/share/webapps/cgit DESTDIR="$pkgdir" prefix=/usr install install-man

  install -vd "$pkgdir/var/cache/$pkgname"
  install -vDm0644 "$srcdir/apache.example.conf" "$pkgdir/etc/webapps/cgit/apache.example.conf"
  mkdir -p "$pkgdir/usr/lib/cgit"
  mv "$pkgdir/usr/share/webapps/cgit/cgit.cgi" "$pkgdir/usr/lib/cgit"
  ln -sf ../../../lib/cgit/cgit.cgi "$pkgdir/usr/share/webapps/cgit/cgit.cgi"
}
