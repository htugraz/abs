# $Id: PKGBUILD 126390 2015-01-22 03:12:30Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-pie
_pkgname=Gnome-Pie
pkgver=0.5.6
pkgrel=1
pkgdesc="Circular application launcher for GNOME"
arch=('i686' 'x86_64')
url="http://gnome-pie.simonschneegans.de/"
license=('GPL')
depends=('libunique3' 'bamf' 'libgee' 'gnome-menus')
makedepends=('cmake' 'vala')
install=$pkgname.install
source=($_pkgname-$pkgver.tar.gz::https://github.com/Simmesimme/$_pkgname/archive/v$pkgver.tar.gz
        gee-0.8.patch
        gnome-session.patch)
md5sums=('aeabdb76c1516ccd9afd6e32a70d1fec'
         'ad0f33d00a2d11bd180714c45651b47f'
         '7e2875e5ace6eb0742389e85c9145871')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../gee-0.8.patch
  patch -Np1 -i ../gnome-session.patch
}

build() {
  cd $_pkgname-$pkgver
  [[ -d build ]] || mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd $_pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
