# $Id: PKGBUILD 145400 2015-11-01 09:10:40Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-pie
_pkgname=Gnome-Pie
pkgver=0.6.8
pkgrel=1
pkgdesc="Circular application launcher for GNOME"
arch=('i686' 'x86_64')
url="http://gnome-pie.simonschneegans.de/"
license=('GPL')
depends=('bamf' 'libarchive' 'libgee' 'gnome-menus')
makedepends=('cmake' 'vala')
install=$pkgname.install
source=($_pkgname-$pkgver.tar.gz::https://github.com/Simmesimme/$_pkgname/archive/v$pkgver.tar.gz)
md5sums=('d031616c872d9926c958a917b348f69c')

build() {
  cd $_pkgname-$pkgver
  ./resources/locale/compile-po.sh
  [[ -d build ]] || mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd $_pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
