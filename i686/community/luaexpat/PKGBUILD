# $Id: PKGBUILD 108781 2014-04-03 16:52:35Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Lucas Hermann Negri <kkndrox@gmail.com>

pkgbase=luaexpat
pkgname=(lua-expat lua51-expat)
pkgver=1.3.0
pkgrel=1
pkgdesc='SAX XML parser based on the Expat library'
arch=('i686' 'x86_64')
url='http://matthewwild.co.uk/projects/luaexpat/'
license=('MIT')
makedepends=('gcc' 'lua' 'lua51' 'expat')
source=("http://matthewwild.co.uk/projects/luaexpat/luaexpat-$pkgver.tar.gz"
	    'config.51'
	    'config.52')
md5sums=('3c20b5795e7107f847f8da844fbfe2da'
         '64abbf5b602e0e31b68db2925a5e6b2f'
         '60ae0633ac6f829cc3359f3533252eb0')

build() {
  # 5.1
  msg2 "Building $pkgbase with lua 5.1"
  cp -a $pkgbase-$pkgver $pkgbase-$pkgver-51
  cd $pkgbase-$pkgver-51
  cp -L "$srcdir/config.51" config
  make LUA_V=5.1
  # 5.2
  msg2 "Building $pkgbase with lua 5.2"
  cd "$srcdir/$pkgbase-$pkgver"
  cp -L "$srcdir/config.52" config
  make LUA_V=5.2
}

package_lua-expat() {
  depends=('lua' 'expat')

  cd $pkgbase-$pkgver
  make LUA_V=5.2 DESTDIR="$pkgdir" install
  install -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

package_lua51-expat() {
  depends=('lua51' 'expat')
  replaces=('luaexpat')
  conflicts=('luaexpat')

  cd $pkgbase-$pkgver-51
  make LUA_V=5.1 DESTDIR="$pkgdir" install
  install -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}
