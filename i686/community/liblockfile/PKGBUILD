# $Id: PKGBUILD 102144 2013-12-05 21:13:52Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=liblockfile
pkgver=1.08_4
_basever=1.08
pkgrel=4
pkgdesc="a library with NFS-safe locking functions"
arch=('i686' 'x86_64')
license=('GPL')
url="http://packages.debian.org/unstable/libs/liblockfile1"
depends=('glibc')
makedepends=()
source=(http://ftp.debian.org/debian/pool/main/libl/$pkgname/${pkgname}_${_basever}.orig.tar.gz
	http://ftp.debian.org/debian/pool/main/libl/$pkgname/${pkgname}_${pkgver/_/-}.debian.tar.bz2)
md5sums=('c24e2dfb4a2aab0263fe5ac1564d305e'
         '50c0c049d2838704f5720f5fd9eaddac')

build() {
  cd $srcdir/$pkgname-${_basever}
  for p in `cat ../debian/patches/series` ; do
    patch -p1 < ../debian/patches/$p
  done
  LDCONFIG=/usr/bin/true ./configure --enable-shared --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd $srcdir/$pkgname-${_basever}
  mkdir -p $pkgdir/usr/{lib,bin,include,share/man/man1,share/man/man3}
  make install prefix=$pkgdir/usr mandir=$pkgdir/usr/share/man
}
