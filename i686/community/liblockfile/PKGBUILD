# $Id: PKGBUILD 129574 2015-03-19 13:54:44Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=liblockfile
pkgver=1.09_6
_basever=1.09
pkgrel=1
pkgdesc="a library with NFS-safe locking functions"
arch=('i686' 'x86_64')
license=('GPL')
url="http://packages.debian.org/unstable/libs/liblockfile1"
depends=('glibc')
makedepends=()
source=(http://ftp.debian.org/debian/pool/main/libl/$pkgname/${pkgname}_${_basever}.orig.tar.gz
	http://ftp.debian.org/debian/pool/main/libl/$pkgname/${pkgname}_${pkgver/_/-}.debian.tar.bz2)
md5sums=('2aa269e4405ee8235ff17d1b357c6ae8'
         'a2811807e63a526c07b0f60626e329a2')

build() {
  cd $srcdir/$pkgname-${_basever}
  for p in `cat ../debian/patches/series` ; do
    patch -p1 < ../debian/patches/$p
  done
  LDCONFIG=/usr/bin/true ./configure --enable-shared --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd $srcdir/$pkgname-${_basever}
  mkdir -p $pkgdir/usr/{lib,bin,include,share/man/man1,share/man/man3}
  make install prefix=$pkgdir/usr mandir=$pkgdir/usr/share/man
}
