# $Id: PKGBUILD 126227 2015-01-19 16:51:12Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=gcompris
pkgver=14.12
pkgrel=1
pkgdesc="Educational software suite comprising of numerous activities for children aged 2 to 10"
arch=('i686' 'x86_64')
url="http://gcompris.net/"
license=('GPL3')
depends=('pygtk' 'librsvg' 'gstreamer0.10-base-plugins')
optdepends=('gnucap: for computer simulation within the electricity activity'
            'tuxpaint: for the painting activity')
makedepends=('texinfo' 'texi2html' 'intltool')
source=(http://gcompris.net/download/$pkgname-$pkgver.tar.bz2)
sha256sums=('bf0beafb03c0f4ffe05bbfcda54b91a5e8a034b5d813cb4492d6f59a21f9cdcf')

prepare() {
  cd $pkgname-$pkgver

  sed -i 's/"$GNUCHESS"/"gcompris-gnuchess"/' configure
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  depends+=("gcompris-data=$pkgver")

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install-exec
}

# vim:set ts=2 sw=2 et:
