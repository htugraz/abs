# $Id: PKGBUILD 128833 2015-03-06 19:15:20Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=gcompris
pkgver=15.02
pkgrel=1
pkgdesc="Educational software suite comprising of numerous activities for children aged 2 to 10"
arch=('i686' 'x86_64')
url="http://gcompris.net/"
license=('GPL3')
depends=('pygtk' 'librsvg' 'gstreamer0.10-base-plugins')
optdepends=('gnucap: for computer simulation within the electricity activity'
            'tuxpaint: for the painting activity')
makedepends=('texinfo' 'texi2html' 'intltool')
source=(http://gcompris.net/download/$pkgname-$pkgver.tar.bz2)
sha256sums=('c3e2f84f459d747b853b0424e366d0ce7b1f4ef00d660cdde5084712cf5c3ad4')

prepare() {
  cd $pkgname-$pkgver

  sed -i 's/"$GNUCHESS"/"gcompris-gnuchess"/' configure
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  depends+=("gcompris-data=$pkgver")

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install-exec
}

# vim:set ts=2 sw=2 et:
