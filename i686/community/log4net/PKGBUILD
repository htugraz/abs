# $Id: PKGBUILD 97238 2013-09-15 15:35:57Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Frane Bandov <frane@offbyte.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>

pkgname=log4net
pkgver=1.2.12
pkgrel=1
pkgdesc="A tool to help the programmer output log statements to a variety of output targets"
arch=('any')
url="http://logging.apache.org/log4net/"
license=('Apache')
depends=('mono')
source=(http://www.apache.org/dist/logging/$pkgname/source/$pkgname-$pkgver-src.zip
        $pkgname.pc)
md5sums=('6cd79437bf1b6dd80f7983704abc9aad'
         'a292da72afbd6bd705e02f4ddb564fd7')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "s/@VERSION@/$pkgver/" $srcdir/$pkgname.pc
  mkdir bin

  dmcs -t:library -out:bin/$pkgname.dll -keyfile:$pkgname.snk -d:NET_2_0 \
       -r:System,System.Data,System.Xml,System.Web,System.Net,System.Configuration \
       $(find src -name "*.cs")
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  gacutil -i bin/$pkgname.dll -package $pkgname-$pkgver -root "$pkgdir/usr/lib/"
  install -Dm644 "$srcdir/$pkgname.pc" "$pkgdir/usr/lib/pkgconfig/$pkgname.pc"
}
