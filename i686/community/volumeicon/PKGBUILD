# $Id: PKGBUILD 113124 2014-06-13 21:48:36Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Maato <maato softwarebakery com>

# TODO: Consider adding an install-time message about
#       removing ~/.config/volumeicon/volumeicon if there are
#       problems running volumeicon. Ref FS#34239

pkgname=volumeicon
pkgver=0.5.0
pkgrel=3
pkgdesc='Volume control for your system tray'
arch=('x86_64' 'i686')
url='http://softwarebakery.com/maato/volumeicon.html'
license=('GPL3')
depends=('gtk3' 'alsa-lib' 'libnotify')
makedepends=('intltool' 'git')
source=('git://github.com/Maato/volumeicon.git#commit=9e3755fdbe')
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"

  head -1 ChangeLog | cut -d: -f1
}

prepare() {
  cd "$pkgname"

  #aclocal
  #autoconf
  #automake --add-missing
  ./autogen.sh
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix=/usr \
    --enable-notify
  make
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
