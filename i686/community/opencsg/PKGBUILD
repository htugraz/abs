# $Id: PKGBUILD 120557 2014-10-12 07:31:44Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: Xyne <ca dot archlinux at xyne, backwards>
# Contributor: nikor

pkgname=opencsg
pkgver=1.3.3
pkgrel=2
pkgdesc="Library for image-based CSG rendering using OpenGL."
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glew' 'glut' 'libgl')
url="http://www.opencsg.org"
source=("http://www.opencsg.org/OpenCSG-${pkgver}.tar.gz")
sha1sums=('c01050679968c96e1b0ef4b5138a76a635e21d93')

build() {
  cd "$srcdir/OpenCSG-$pkgver"
  sed -i 's|-rpath,../lib|-rpath,/usr/lib|' src/Makefile
  make
}

package() {
  cd "$srcdir/OpenCSG-$pkgver"
  install -D lib/libopencsg.so.$pkgver "$pkgdir/usr/lib/libopencsg.so.$pkgver"
  install -D include/opencsg.h "$pkgdir/usr/include/opencsg.h"
  cd "$pkgdir/usr/lib/"
  ln -s libopencsg.so.$pkgver libopencsg.so.1.3
  ln -s libopencsg.so.$pkgver libopencsg.so.1
  ln -s libopencsg.so.$pkgver libopencsg.so
}

