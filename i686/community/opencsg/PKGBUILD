# $Id: PKGBUILD 105548 2014-02-10 04:53:52Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: Xyne <ca dot archlinux at xyne, backwards>
# Contributor: nikor

pkgname=opencsg
pkgver=1.3.2
pkgrel=2
pkgdesc="Library for image-based CSG rendering using OpenGL."
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glew' 'glut' 'mesa')
url="http://www.opencsg.org"
source=(http://www.opencsg.org/OpenCSG-${pkgver}.tar.gz)
sha1sums=('e2b4abf169ae3e319eb5f6d9ae6136fa96710a05')

build() {
  cd -- "${srcdir}/OpenCSG-${pkgver}"
  sed -i 's|-rpath,../lib|-rpath,/usr/lib|' src/Makefile
  make
}

package() {
  cd -- "${srcdir}/OpenCSG-${pkgver}"
  install -D lib/libopencsg.so.${pkgver} "$pkgdir/usr/lib/libopencsg.so.${pkgver}"
  install -D include/opencsg.h "$pkgdir/usr/include/opencsg.h"
  cd "$pkgdir/usr/lib/"
  ln -s libopencsg.so.${pkgver} libopencsg.so.1.3
  ln -s libopencsg.so.${pkgver} libopencsg.so.1
  ln -s libopencsg.so.${pkgver} libopencsg.so
}

