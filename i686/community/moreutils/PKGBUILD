# $Id: PKGBUILD 123797 2014-12-11 22:46:44Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

pkgname=moreutils
pkgver=0.54
pkgrel=1
pkgdesc="A growing collection of the unix tools that nobody thought to write thirty years ago"
arch=('i686' 'x86_64')
url="http://joeyh.name/code/moreutils/"
license=('GPL')
makedepends=('docbook2x' 'docbook-xml' 'perl-xml-sax')
depends=('perl' 'perl-ipc-run' 'perl-timedate' 'perl-time-duration')
options=('!emptydirs' 'zipman')
replaces=(moreutils-svn)
source=(http://ftp.de.debian.org/debian/pool/main/m/moreutils/moreutils_$pkgver.orig.tar.gz)
md5sums=('1ced0ba9ac133cb81bbe2424f60d0a8a')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's|schema/dtd/4.4|xml-dtd-4.5|g' *.docbook
  make DOCBOOK2XMAN=docbook2man
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=$pkgdir/usr install
  chmod 0644 $pkgdir/usr/share/man/man1/*

  mv $pkgdir/usr/share/man/man1/ts.1 $pkgdir/usr/share/man/man1/ts.1moreutils

  mv $pkgdir/usr/share/man/man1/parallel.1 $pkgdir/usr/share/man/man1/parallel-moreutils.1
  mv $pkgdir/usr/bin/parallel $pkgdir/usr/bin/parallel-moreutils
}
