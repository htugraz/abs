# Maintainer: Mihails Strasunse <public@dicebot.lv>

pkgname=('dtools')
pkgver=2.066.1
pkgrel=1
pkgdesc="Ancilliary tools for the D programming language"
license=("custom")
groups=('dlang')
arch=('i686' 'x86_64')
url="http://www.dlang.org"
# makedepends=('d-compiler' 'd-runtime' 'git')
makedepends=('ldc' 'git')
# depends=('d-runtime' 'curl')
depends=('liblphobos')

source=(
    "git+http://github.com/D-Programming-Language/tools#tag=v$pkgver"
    "git+https://github.com/Dicebot/Arch-PKGBUILDs.git"
    "http://www.boost.org/LICENSE_1_0.txt"
)
sha1sums=(
    'SKIP'
    'SKIP'
    '3cba29011be2b9d59f6204d6fa0a386b1b2dbd90'
)

build()
{
    # DMD=`$srcdir/Arch-PKGBUILDs/d-compiler.sh`
    DMD=ldmd

    cd $srcdir/tools

    # rdmd is used to build others
    $DMD -od. -ofdtools-rdmd rdmd.d

    for tool in DustMite/dustmite ddemangle catdoc detab tolf dget; do
        ./dtools-rdmd --compiler=$DMD --build-only -od. -ofdtools-`basename $tool` $tool.d
    done
}

package()
{
    cd $srcdir/tools

    mkdir -p $pkgdir/usr/bin
    for tool in rdmd ddemangle catdoc detab tolf dget dustmite; do
        install -m755 -t $pkgdir/usr/bin $pkgname-$tool
    done

    cd $pkgdir
    ln -s dtools-rdmd usr/bin/rdmd
    ln -s dtools-dustmite usr/bin/dustmite

    install -Dm644 $srcdir/LICENSE_1_0.txt usr/share/licenses/$pkgname/LICENSE
}
