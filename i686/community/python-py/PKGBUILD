# $Id: PKGBUILD 115887 2014-07-18 02:48:32Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>
# Contributor: Wieland Hoffmann <themineo+aur@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-py
pkgname=('python-py' 'python2-py')
pkgver=1.4.22
pkgrel=1
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="http://pylib.readthedocs.org/en/latest/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/p/py/py-$pkgver.tar.gz")
sha512sums=('3cbc75257a7de345f0f23b6374a542608392d05f6600ba8ab743eea4fc46cc88ccaa829c1e2370eabb9892fbef8a16df9f5eb7956571d6f2b465a75817f70d7a')

prepare() {
  cp -a py-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/py-${pkgver}"
  python setup.py build

  cd "$srcdir/py-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/py-${pkgver}"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" LANG=en_US.utf8 py.test

  cd "$srcdir/py-${pkgver}-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" LANG=en_US.utf8 py.test2
}

package_python-py() {
  depends=('python')

  cd py-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-py() {
  depends=('python2')

  cd py-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

