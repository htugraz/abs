# $Id: PKGBUILD 108508 2014-03-27 14:48:33Z fyan $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgbase=python-unidecode
pkgname=('python-unidecode' 'python2-unidecode')
pkgver=0.04.14
pkgrel=2
pkgdesc='ASCII transliterations of Unicode text'
arch=('any')
url='http://pypi.python.org/pypi/Unidecode/'
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools')
options=('!emptydirs')
source=("http://pypi.python.org/packages/source/U/Unidecode/Unidecode-${pkgver}.tar.gz")
sha256sums=('a66f8a527ee0709742caa16745cc882b85d6cfc12b37fa2f56024d07031f230c')

prepare() {
  for d in python{,2}-unidecode; do
    cp -r Unidecode-${pkgver} $d
  done
}

build() {
  for d in python{,2}-unidecode; do
    pushd $d
    ${d%-*} setup.py build
    popd
  done
}

package_python-unidecode() {
  cd python-unidecode

  python setup.py install --root="${pkgdir}" --optimize='1'
}

package_python2-unidecode() {
  cd python2-unidecode

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
