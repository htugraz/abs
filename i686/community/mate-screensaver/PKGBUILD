# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=mate-screensaver
pkgname=(${_pkgbase})
pkgver=${_ver}.2
pkgrel=1
pkgdesc='Screensaver for MATE (GTK2 version)'
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libmatekbd>=1.10' 'libnotify' 'libxss' 'mate-desktop>=1.10'
         'mate-menus>=1.10' 'mate-session-manager>=1.10')
makedepends=('mate-common' 'xmlto')
optdepends=('mate-power-manager: Add power management support.'
            'mate-screensaver-hacks: Additional screensavers.'
            'rss-glx: Really slick screensavers.')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('75715c27ded17df37390df13fc7d4452419a87d3')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --with-xscreensaverdir=/usr/share/xscreensaver/config \
        --with-xscreensaverhackdir=/usr/lib/xscreensaver \
        --with-mit-ext \
        --with-libnotify \
        --enable-locking \
        --with-gtk=2.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
