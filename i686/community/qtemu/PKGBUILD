# $Id: PKGBUILD 109966 2014-04-22 15:33:22Z spupykin $
# Maintainer: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Previous Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=qtemu
pkgver=1.0.5
pkgrel=9
pkgdesc="Graphical user interface for QEMU written in Qt4"
url="http://qtemu.org"
license=("GPL")
arch=('i686' 'x86_64')
makedepends=('cmake')
depends=('qemu' 'qt4')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
	http://qtemu.svn.sourceforge.net/viewvc/qtemu/trunk/qtemu/misc/qtemu.desktop)
md5sums=('3b93e209dc62e52a1d94c8eb24d0b149'
         '3906b73a731799061f6e10110fea5235')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/opt/qtemu .
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/bin"
  ln -s /opt/qtemu/bin/qtemu "$pkgdir/usr/bin/qtemu"
  install -Dm0644 $srcdir/qtemu.desktop $pkgdir/usr/share/applications/qtemu.desktop
  install -Dm0644 images/crystal/new.png $pkgdir/usr/share/icons/qtemu.png
}
