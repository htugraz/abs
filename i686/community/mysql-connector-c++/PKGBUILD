# $Id: PKGBUILD 127410 2015-02-09 16:14:17Z eworm $
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Marcel Korpel <marcel[dot]korpel[at]gmail>
# Contributor: Muflone http://www.muflone.com/contacts/english/
# Contributor: Ilya Kuznetsov <monochrome.r42@gmail.com>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=mysql-connector-c++
pkgver=1.1.5
pkgrel=3
pkgdesc='A MySQL database connector for C++'
arch=('i686' 'x86_64')
url='http://dev.mysql.com/doc/connector-cpp/en/'
license=('GPL')
depends=('libmysqlclient' 'boost-libs')
makedepends=('cmake' 'boost')
validpgpkeys=('A4A9406876FCBD3C456770C88C718D3B5072E1F5')
source=("http://cdn.mysql.com/Downloads/Connector-C++/${pkgname}-${pkgver}.tar.gz"{,.asc}
	'mysql_cxx_linkage.patch'
	'mariadb_api.patch')
sha256sums=('f3696726da29d56e0daad7046b30aa51444360a3ff112c079eb942929eb1cce8'
            'SKIP'
            '65d3375f2c5a066d92a400a8eea90bf6001eafc57ec7aac56610f8829b42bb8c'
            '1694ead0b9c9cb7803a76f56e3871b4f64f045a07fa390cf18bc15be798035ee')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}/"

	patch -Np1 < "${srcdir}/mysql_cxx_linkage.patch"
	patch -Np1 < "${srcdir}/mariadb_api.patch"
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}/"

	cmake . \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib \
		-DMYSQLCPPCONN_BUILD_EXAMPLES=OFF \
		-DMYSQL_LIB=/usr/lib/libmysqlclient.so
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}/"

	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/{ANNOUNCEMENT,COPYING,README,INSTALL,Licenses_for_Third-Party_Components.txt}
}
