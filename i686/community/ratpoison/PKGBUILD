# $Id: PKGBUILD 105159 2014-02-02 00:22:45Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: juergen <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=ratpoison
pkgver=1.4.6
pkgrel=2
pkgdesc="A simple keystroke-driven window manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libxinerama' 'readline' 'bash' 'perl' 'libxtst' 'libxft' 'texinfo')
url="http://www.nongnu.org/ratpoison/"
install="${pkgname}.install"
source=("http://savannah.nongnu.org/download/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
md5sums=('5fbaab737cf7fcc8e5eb2d619aaa0eab'
         '29c3cb9be59758e39d8471391231a74a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#38726, v1.4.6-2 
  sed -i 's|PRINT_ERROR (("XGetWMName|PRINT_DEBUG (("XGetWMName|' src/manage.c

  ./configure --prefix=/usr
  make CFLAGS="$CFLAGS -DHAVE_GETLINE"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # fix permissions
  chmod a+x "${pkgdir}/usr/share/ratpoison/"{allwindows.sh,clickframe.pl,rpshowall.sh,rpws,split.sh}

  # Not useful outside the source tree. Kill it
  rm "${pkgdir}/usr/share/ratpoison/genrpbindings"

  cd contrib
  ./genrpbindings
  install -dm755 "${pkgdir}/usr/share/ratpoison/bindings"
  install -m644 {Ratpoison.pm,ratpoison-cmd.el,ratpoison.rb,ratpoison.lisp,ratpoison.py} \
    "${pkgdir}/usr/share/ratpoison/bindings/"

  install -Dm644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/etc/X11/sessions/${pkgname}.desktop"

  install -Dm644 "${pkgdir}/usr/share/ratpoison/ratpoison.el" \
    "${pkgdir}/usr/share/emacs/site-lisp/ratpoison.el"
}
