# $Id: PKGBUILD 143448 2015-10-08 19:59:30Z heftig $
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname=notification-daemon
pkgver=3.18.0
pkgrel=1
pkgdesc="Notification daemon for the desktop notifications framework"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org/"
depends=(gtk3 libcanberra)
makedepends=(intltool)
source=(http://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('3bdbb2615c511227b79d0b59b025f24f2dc11be93c225cde965ccd70c99d326c')

prepare() {
    cd ${pkgname}-${pkgver}
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/notification-daemon-1.0 \
        --localstatedir=/var --disable-static
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
