# $Id: PKGBUILD 94080 2013-07-14 10:09:43Z bpiotrowski $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>
# Contributor: Henning Garus <henning.garus@gmail.com>

pkgname=xdelta3
pkgver=3.0.7
pkgrel=1
pkgdesc="Diff utility which works with binary files"
arch=('i686' 'x86_64')
url="http://xdelta.org/"
license=('GPL')
depends=('xz')
source=(http://xdelta.googlecode.com/files/$pkgname-$pkgver.tar.gz
        xdelta-3.0.7-regtest_size_t.patch)
md5sums=('ef6631e4f9219a80bcb9e3020962b6ec'
         '450492f0065160c2287b7e89223726e0')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../xdelta-3.0.7-regtest_size_t.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  ./xdelta3 test
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
