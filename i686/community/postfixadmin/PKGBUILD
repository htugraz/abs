# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Filip <fila pruda.com>
pkgname=postfixadmin
pkgver=2.3.7
pkgrel=1
pkgdesc="A web based interface used to manage mailboxes, virtual domains and aliases created for Postfix"
arch=('any')
license=('GPL')
url="http://postfixadmin.sourceforge.net/"
backup=(etc/webapps/postfixadmin/config.inc.php
        etc/webapps/postfixadmin/.htaccess
	    etc/webapps/postfixadmin/motd.txt
        etc/webapps/postfixadmin/motd-users.txt)
depends=('php')
optdepends=('mariadb: MySQL server'
		    'postgresql: PostgreSQL server')
        source=(http://downloads.sourceforge.net/project/postfixadmin/postfixadmin/postfixadmin-$pkgver/postfixadmin-$pkgver.tar.gz)
md5sums=('35265807e815fbfbec5d7246e5b5e978')

package() {
  _destdir=$pkgdir/usr/share/webapps/postfixAdmin
  _destdir_etc=$pkgdir/etc/webapps/postfixadmin
  _destdir_doc=$pkgdir/usr/share/doc/postfixadmin
  
  install -dm755 ${_destdir}
  install -dm755 ${_destdir_etc}
  install -dm755 ${_destdir_doc}
  
  cd $srcdir/$pkgname-$pkgver
  cp -r * ${_destdir}
  
  # config
  mv ${_destdir}/config.inc.php ${_destdir_etc}
  ln -s /etc/webapps/postfixadmin/config.inc.php ${_destdir}/config.inc.php
  echo "#deny from all" >${_destdir_etc}/.htaccess
  ln -s /etc/webapps/postfixadmin/.htaccess ${_destdir}/.htaccess
  mv ${_destdir}/motd.txt ${_destdir_etc}
  mv ${_destdir}/motd-users.txt ${_destdir_etc}
  ln -s /etc/webapps/postfixadmin/motd.txt ${_destdir}/motd.txt
  ln -s /etc/webapps/postfixadmin/motd-users.txt ${_destdir}/motd-users.txt
  
  # docs
  mv ${_destdir}/{*.TXT,tests,ADDITIONS,DOCUMENTS,VIRTUAL_VACATION} ${_destdir_doc}
  rm -rf ${_destdir}/debian
}
