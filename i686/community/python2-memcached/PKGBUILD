# $Id: PKGBUILD 95905 2013-08-18 15:46:52Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=python2-memcached
pkgdesc='Python interface to memcached'
pkgver=1.53
pkgrel=2
depends=('python2')
url='https://bugs.launchpad.net/python-memcached'
arch=('any')
license=('Python')
makedepends=('python2-setuptools')
source=("ftp://ftp.tummy.com/pub/python-memcached/python-memcached-$pkgver.tar.gz"
        'LICENSE')
sha256sums=('af04ea031b271a54f085166773e028fe053fc1d9a58cd8b3c3a57945990bfb48'
            '9f3f9438250f8aa5bdcbb0dee700f237932fbda985646c61c85d2a25a2510f36')

prepare() {
  cd "$srcdir/python-memcached-$pkgver"

  sed -i 's:/usr/bin/env python:#/usr/bin/python2:' memcache.py
}

package() {
  cd "$srcdir/python-memcached-$pkgver"

  python2 setup.py install --root=$pkgdir --optimize=1
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

check() {
  cd "$srcdir/python-memcached-$pkgver"

  python2 setup.py check
}

# vim:set ts=2 sw=2 et:
