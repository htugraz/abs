# $Id: PKGBUILD 82073 2013-01-07 10:20:03Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: tdy <tdy@gmx.com>
# Contributor: nut543 <kfs1@online.no>

pkgname=vor
pkgver=0.5.5
pkgrel=1
pkgdesc='Classic spaceship versus asteroid field arcade game'
arch=('i686' 'x86_64')
url="http://jasonwoof.org/vor?"
license=('GPL')
depends=('sdl_image' 'sdl_mixer')
makedepends=('gendesk')
optdepends=('netpbm: for customizing game graphics'
            'povray: for customizing game graphics')
options=('!emptydirs')
source=("http://qualdan.com/$pkgname/$pkgname-$pkgver.tar.bz2"
	"$pkgname.png")
sha256sums=('ead1b9786741e26d37ff7c5185b7fe1e91f90f76ebed3785225312e323b7c7da'
            '07a805928f5cf2b3351e2b9ff820ea69352ad80faad43311889ffbd5f62da872')
_exec=('vor -f')

build() {
  cd "$srcdir"
  gendesk
  
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make LDFLAGS=-lm
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  make prefix=/usr DESTDIR="$pkgdir" install
  install -Dm644 "../$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "../$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
