# $Id: PKGBUILD 129799 2015-03-23 10:26:53Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Aur√©lien Wailly <aurelien.wailly@gmail.com>

pkgname=netcf
pkgver=0.2.6
pkgrel=1
pkgdesc="A library for configuring network interfaces"
arch=('i686' 'x86_64')
license=('LGPL')
url="https://fedorahosted.org/netcf/"
depends=('augeas>=0.7.4' 'netctl' 'libxslt' 'libxml2' 'libnl')
makedepends=('gcc')
source=("https://fedorahosted.org/released/netcf/$pkgname-$pkgver.tar.gz")
md5sums=('042f1c67f0d63d612d7481492eb91b54')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  aclocal
  automake --add-missing || true
  autoreconf
  ./configure --prefix=/usr \
	--disable-static \
	--with-init-script=none \
	--with-driver=redhat
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
  rm -rf $pkgdir/etc/rc.d
}
