# $Id: PKGBUILD 101796 2013-12-01 11:32:10Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

pkgname=ncmpcpp
pkgver=0.5.10
pkgrel=5
pkgdesc='Almost exact clone of ncmpc with some new features'
arch=('i686' 'x86_64')
url='http://ncmpcpp.rybczak.net/'
license=('GPL')
depends=('curl' 'libmpdclient' 'taglib' 'ncurses' 'fftw')
install=ncmpcpp.install
source=(http://ncmpcpp.rybczak.net/stable/$pkgname-$pkgver.tar.bz2)
md5sums=('298ed66325bcfe358e37e186c7ea4861')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-unicode \
    --enable-visualizer \
    --with-curl \
    --with-fftw \
    --with-taglib
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  make -C extras
  install -Dm755 extras/artist_to_albumartist "$pkgdir"/usr/bin/
}
