# $Id: PKGBUILD 96596 2013-09-02 07:16:12Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: nofxx <x@nofxx.com>

pkgname=iodine
pkgver=0.6.0rc1
pkgrel=2
pkgdesc='Tunnel IPv4 data through a DNS server'
arch=('i686' 'x86_64')
url='http://code.kryo.se/iodine'
license=('custom:ISC')
depends=('zlib')
source=("http://code.kryo.se/${pkgname}/${pkgname}-${pkgver/rc/-rc}.tar.gz")

build() {
  cd ${pkgname}-${pkgver/rc/-rc}/

  make
}

package() {
  cd ${pkgname}-${pkgver/rc/-rc}/

  make prefix=/usr sbindir=/usr/bin DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/licenses/iodine"
  sed -n '/AUTHORS & LICENSE/,$p' README > "${pkgdir}/usr/share/licenses/iodine/LICENSE"
}

sha256sums=('dacf950198b68fd1dae09fe980080155b0c75718f581c08e069eee0c1b6c5e60')
