# $Id: PKGBUILD 146432 2015-11-09 15:15:55Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-construct
pkgname=('python-construct' 'python2-construct')
pkgver=2.5.2
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="http://construct.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/construct/construct.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a construct{,-py2}
}

build() {
  cd "$srcdir/construct"
  python setup.py build

  cd "$srcdir/construct-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/construct"
  nosetests3 tests

  cd "$srcdir/construct-py2"
  nosetests2 tests
}

package_python-construct() {
  depends=('python-six')

  cd construct
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-construct() {
  depends=('python2-six')

  cd construct-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
