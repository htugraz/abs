# $Id: PKGBUILD 145103 2015-10-27 10:42:27Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>

pkgname=workrave
pkgver=1.10.8
pkgrel=1
pkgdesc="a tool to help RSI"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://workrave.org"
depends=('libxtst' 'gtkmm3' 'gdome2' 'gstreamer' 'libsm' 'libice' 'libxss')
makedepends=('intltool' 'gobject-introspection' 'python2-cheetah' 'autoconf-archive')
optdepends=('gst-plugins-good: to play sound events')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/rcaelers/workrave/archive/v${pkgver//./_}.tar.gz)
md5sums=('29b2e49439d605bc55fd4e67a11b42c1')

build() {
  cd $srcdir/workrave-${pkgver//./_}
  export PYTHON=python2
  ./autogen.sh
  ./configure --prefix=/usr --enable-dbus --sysconfdir=/etc --libexecdir=/usr/lib/workrave \
	--disable-pulse
  make
}

package() {
  cd $srcdir/workrave-${pkgver//./_}
  make DESTDIR=$pkgdir install
}
