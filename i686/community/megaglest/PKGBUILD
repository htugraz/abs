# $Id: PKGBUILD 94891 2013-08-01 06:55:07Z bpiotrowski $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=megaglest
pkgver=3.8.0beta1
_pkgver=3.8.0-beta1
pkgrel=2
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/megaglest/"
license=('GPL3')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl' 'libvorbis' 'openal' 'libgl' 'lua51' 'icu' 'ftgl' 'glew'
'libircclient' 'miniupnpc' 'wxgtk' 'glu')
makedepends=('ftjam' 'cmake' 'mesa')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-source-${_pkgver}.tar.xz")
md5sums=('1da8707d435ffb2342f03ca466d4eeed')

build() {
  cd "${srcdir}"/"${pkgname}"-"${_pkgver}"/

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DWANT_SVN_STAMP=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/"${pkgname}"-"${_pkgver}"/

  cd build
  make DESTDIR="$pkgdir" install
}

# vim: sw=2:ts=2 et:
