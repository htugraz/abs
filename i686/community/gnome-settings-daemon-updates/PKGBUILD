# $Id: PKGBUILD 109431 2014-04-14 20:16:52Z jgc $
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>

_pkgname=gnome-settings-daemon
pkgname=$_pkgname-updates
pkgver=3.12.0.1
pkgrel=1
pkgdesc="Updates plugin for the GNOME Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-packagekit' "$_pkgname")
makedepends=('intltool' 'gtk-doc' 'gnome-desktop' 'gnome-common' 'xf86-input-wacom')
options=('!emptydirs')
install=$_pkgname.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz"
	'packagekit-0.8.patch'
	'arch.patch')
sha256sums=('c21cfcee008da6935ed7f7e814a788a951ba67cc728e43f888b174701cd96847'
            '2fd5ee457f5d57bfd9edef049719f55ab1cab5fe6d0fb422f91fe363c02e6efd'
            '217004c112b18cf136c535ef3d6cc90f0cecc495573cfb8653ff089230d248bb')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -RNp1 -i "$srcdir/packagekit-0.8.patch"
  patch -Np1 -i "$srcdir/arch.patch"
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  autoreconf -fi
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libexecdir=/usr/lib/gnome-settings-daemon \
              --disable-static

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C 'gnome-settings-daemon'
  make -C 'plugins/updates'
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -C 'plugins/updates' DESTDIR="${pkgdir}" install
}
