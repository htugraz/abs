# $Id: PKGBUILD 105476 2014-02-08 20:07:52Z arodseth $
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Kristoffer Fossgård <kfs1@online.no>
# Contributor: clonejo <clonejo@shakik.de>

pkgname=terminus-font
pkgver=4.38
pkgrel=4
pkgdesc='Superb, monospace bitmap font (for X11 and console)'
arch=('any')
url='http://sourceforge.net/projects/terminus-font/'
license=('GPL2' 'custom:OFL')
makedepends=('xorg-bdftopcf' 'fontconfig' 'xorg-mkfontscale' 'xorg-mkfontdir')
optdepends=('xorg-fonts-alias')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
install='terminus-font.install'
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('f6f4876a4dabe6a37c270c20bb9e141e38fb50e0bba200e1b9d0470e5eed97b7')

prepare() {
  chmod +x "$pkgname-$pkgver/configure"
}

build() { 
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --x11dir=/usr/share/fonts/local \
    --psfdir=/usr/share/kbd/consolefonts
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname-$pkgver/75-yes-terminus.conf" \
    "$pkgdir/etc/fonts/conf.avail/75-yes-terminus.conf"
  install -Dm644 "$srcdir/$pkgname-$pkgver/OFL.TXT" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
