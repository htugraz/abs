# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=gmp-ecm
pkgver=6.4.4
pkgrel=1
pkgdesc='Elliptic Curve Method for Integer Factorization'
url='http://ecm.gforge.inria.fr/'
license=('GPL' 'LGPL')
depends=('gmp')
options=('!libtool')
arch=('i686' 'x86_64')
source=("http://gforge.inria.fr/frs/download.php/32159/ecm-${pkgver}.tar.gz")
sha1sums=('003d259772bd7748854f0fd8722299505c7d5259')

build() {
	cd ecm-$pkgver
	[[ $CARCH = *86* ]] && export CFLAGS+=' -msse'
	./configure --prefix=/usr --enable-shared --enable-openmp
	# make ecm-params
	make
}

check() {
	cd ecm-$pkgver
	make check
}

package() {
	cd ecm-$pkgver
	make DESTDIR="$pkgdir" install
}
