# $Id: PKGBUILD 105570 2014-02-10 12:39:42Z spupykin $
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=poedit
pkgver=1.6.3
pkgrel=2
pkgdesc="Cross-platform gettext catalogs (.po files) editor"
arch=('i686' 'x86_64')
url="http://www.poedit.net/"
license=('MIT')
depends=('wxgtk' 'desktop-file-utils' 'lucene++')
makedepends=('boost' 'libsm')
install=poedit.install
source=(http://www.poedit.net/dl/poedit-$pkgver.tar.gz)
md5sums=('f58be3cee666d3c586a85c26abba7816')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's|Wx/filename.h|wx/filename.h|' src/tm/transmem.cpp
    sed -i 's|libexec/poedit|lib/poedit/poedit|g' src/tm/tm_migrate.cpp
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --libexecdir=/usr/lib/poedit
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
