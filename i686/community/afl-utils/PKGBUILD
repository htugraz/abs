# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=afl-utils
pkgver=1.17a
pkgrel=1
pkgdesc="Utilities for automated crash sample processing/analysis, easy afl-fuzz job management and corpus optimization"
url="https://github.com/rc0r/afl-utils"
arch=('any')
license=('Apache')
depends=('afl' 'coreutils' 'python')
optdepends=(
  'sqlite: database support'
  'screen: interactive/screen mode support'
  'gdb: gdb script execution support'
  'python-twitter: afl-stat support'
)
makedepends=('python-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rc0r/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('2188def4866fe4a44a9482e763cdb09342da3a58850efccae11970dcbf719811879111030ec10492105c77e58e0d4b37359789ff0c0fd3969c0eadb5c473f560')

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 docs/CHANGELOG.md "${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG.md"
  install -Dm 644 afl-stats.conf.sample "${pkgdir}/usr/share/doc/${pkgname}/afl-stats.conf.sample"
}

# vim: ts=2 sw=2 et:
