# $Id: PKGBUILD 96460 2013-08-29 16:47:40Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Paolo Stivanin <admin AT polslinux DOT it>

pkgname=winff
pkgver=1.5.2
pkgrel=1
pkgdesc='GUI for ffmpeg'
url='http://code.google.com/p/winff/'
license=('GPL3')
arch=('x86_64' 'i686')
depends=('ffmpeg' 'gtk2' 'xterm' 'hicolor-icon-theme')
makedepends=('lazarus' 'lazarus-gtk2' 'gendesk' 'tar')
install='winff.install'
source=("https://$pkgname.googlecode.com/files/WinFF-$pkgver-source.tar.gz")
noextract=("WinFF-$pkgver-source.tar.gz")
sha256sums=('9ee1c68173263dad8d07df2bd62e830d73e6546a0fa86b35de9be8b4c363434c')

prepare() {
  gendesk -n \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --genericname 'Video converter' \
    --categories 'AudioVideo;AudioVideoEditing;GTK'

  # The file provided by upstream is an "exploding tarball" where the files
  # are not extracted in a directory. Also everything is set as executable. m)
  mkdir -p "$pkgname"
  cd "$pkgname"
  tar zxf "../WinFF-$pkgver-source.tar.gz"
}

build() {
  cd "$pkgname"

  lazbuild --lazarusdir=/usr/lib/lazarus winff.lpr --ws=gtk2
}

package() {
  cd "$pkgname"
  
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -d "$pkgdir/usr/share/winff/"
  find . -name "*.xml" \
    -exec install -Dm644 {} "$pkgdir/usr/share/winff/" \; &> /dev/null
  install -Dm644 "$srcdir/winff.desktop" \
    "$pkgdir/usr/share/applications/winff.desktop"
  install -Dm644 'winff-icons/48x48/winff.png' \
    "$pkgdir/usr/share/pixmaps/winff.png"
  for size in 16x16 24x24 32x32 48x48; do
    install -Dm644 "winff-icons/$size/winff.png" \
      "$pkgdir/usr/share/icons/hicolor/$size/apps/winff.png"
  done
  install -dm755 "$pkgdir/usr/share/winff/languages/"
  find . -name "*.po" \
    -exec install -Dm644 {} "$pkgdir/usr/share/winff/languages/" \; &> /dev/null
  rm -f 'presets.xml'
}

# vim:set ts=2 sw=2 et:
