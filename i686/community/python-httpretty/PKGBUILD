# $Id: PKGBUILD 113342 2014-06-19 04:46:57Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-httpretty
pkgname=("python-httpretty" "python2-httpretty")
pkgver=0.8.1
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="http://github.com/gabrielfalcao/httpretty"
license=('MIT')
makedepends=('python-urllib3' 'python2-urllib3' 'python-requests' 'python2-requests')
checkdepends=('python-nose' 'python2-nose' 'python-coverage' 'python2-coverage' 'python-mock' 'python2-mock'
              'python-sure' 'python2-sure' 'python-tornado' 'python2-tornado' 'python-requests' 'python2-requests'
              'python-httplib2' 'python2-httplib2')
source=("https://pypi.python.org/packages/source/h/httpretty/httpretty-$pkgver.tar.gz")
md5sums=('8f4d4339711551ff8b3d94bf4389754c')

prepare() {
  cp -r "httpretty-$pkgver"{,-py2}

  # Fix the encoding issue
  sed -i "78s/f)).read()/f), encoding='utf8').read()/" httpretty-$pkgver/setup.py
  sed -i "88s/readme.rst/README.rst/" httpretty-$pkgver{,-py2}/setup.py
}

check() {
  cd httpretty-$pkgver
  # Disabled due to hanging
  #nosetests3 -s tests

  cd ../httpretty-$pkgver-py2
  nosetests2 -s tests || warning "Tests failed"
}

package_python-httpretty() {
  depends=('python-urllib3' 'python-requests')

  cd httpretty-$pkgver
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/python-httpretty/COPYING"
}

package_python2-httpretty() {
  depends=('python2-urllib3' 'python2-requests')

  cd httpretty-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/python2-httpretty/COPYING"
}

