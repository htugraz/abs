# $Id: PKGBUILD 139210 2015-09-04 15:01:51Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_name=owncloudclient
pkgname=owncloud-client
pkgver=2.0.1
pkgrel=2
pkgdesc='ownCloud client based on mirall'
arch=(i686 x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain-qt5 qt5-webkit hicolor-icon-theme xdg-utils)
makedepends=(cmake python-sphinx qt5-tools doxygen)
optdepends=('python2-nautilus: integration with Nautilus')
install=$pkgname.install
backup=('etc/ownCloud/sync-exclude.lst')
source=("https://download.owncloud.com/desktop/stable/$_name-$pkgver.tar.xz"{,.asc} fix-tray-icon.patch)
md5sums=('e87a01e464f005cf95cde83f46cf318d'
         'SKIP'
         '12f59eb804822ec50045a46ed8586029')
validpgpkeys=('F05F7DD7953A07DF36579DAA498C45EBE94E7B37')

prepare() {
  mkdir -p build

# Fix tray icon in Plasma 5 
  cd $_name-$pkgver
  patch -p1 -i ../fix-tray-icon.patch
}

build() {
  cd build

  cmake ../$_name-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/$pkgname \
        
  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
