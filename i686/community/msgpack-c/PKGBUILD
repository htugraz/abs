# Contributor: Leen Jewel <leenjewel@gmail.com>
# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=0.5.9
_tag=cpp-$pkgver
pkgrel=1
pkgdesc='An efficient object serialization library'
arch=(i686 x86_64)
url='http://msgpack.org/'
license=(Apache)
depends=(gcc-libs)
checkdepends=(gtest)
source=(https://github.com/msgpack/msgpack-c/archive/$_tag.zip
        fix_test_failure.patch)
sha1sums=('04829b0555f3e4fa7970a15518e39e6ef87363e0'
          'c6abea80cb55279c024d20fc632422c54a603461')

prepare() {
  cd msgpack-c-$_tag
  # upstream fix https://github.com/msgpack/msgpack-c/pull/95
  patch -p1 < ../fix_test_failure.patch
}

build() {
  cd msgpack-c-$_tag 
  ./bootstrap
  ./configure --prefix=/usr
  make
}

check() {
  cd msgpack-c-$_tag
  make check  
}

package() {
  cd msgpack-c-$_tag
  make DESTDIR="$pkgdir" install
}
