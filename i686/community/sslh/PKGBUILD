# $Id: PKGBUILD 129043 2015-03-11 02:16:27Z seblu $
# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Le_suisse <lesuisse.dev+aur at gmail dot com>
# Contributor: Jason Rodriguez <jason-aur@catloaf.net>

pkgname=sslh
pkgver=1.17
pkgrel=2
pkgdesc='SSL/SSH/OpenVPN/XMPP/tinc port multiplexer'
arch=('i686' 'x86_64')
url='http://www.rutschle.net/tech/sslh.shtml'
license=('GPL2')
depends=('libcap' 'libconfig')
backup=('etc/sslh.conf')
install=$pkgname.install
source=("http://www.rutschle.net/tech/$pkgname-v$pkgver.tar.gz"
        'sslh.conf'
        'sslh-select.service'
        'sslh-fork.service')
md5sums=('1fc3ada4bafaca5a9786cc1431f48ed4'
         'd5405c7ca7e1813e4d49a473e5834640'
         '0f3f9e3ac2ac4b576d684b21b566aeb9'
         '4e64f0850ec9bd44071ae8d5369316e5')

build() {
  cd $pkgname-v$pkgver
  make VERSION=\"v$pkgver\" USELIBCAP=1
}

package() {
  # default arch config
  install -Dm 644 sslh.conf "$pkgdir/etc/sslh.conf"
  # manually install to have both ssl-fork and ssl-select
  cd $pkgname-v$pkgver
  install -Dm 755 sslh-fork "$pkgdir/usr/bin/sslh-fork"
  install -Dm 755 sslh-select "$pkgdir/usr/bin/sslh-select"
  ln -s sslh-fork "$pkgdir/usr/bin/sslh"
  # install manpage
  install -Dm 644 sslh.8.gz "$pkgdir/usr/share/man/man8/sslh.8.gz"
  # install examples files
  install -Dm 644 basic.cfg "$pkgdir/usr/share/doc/$pkgname/basic.cfg"
  install -Dm 644 example.cfg "$pkgdir/usr/share/doc/$pkgname/example.cfg"
  # systemd
  install -Dm 644 $srcdir/sslh-fork.service \
    "$pkgdir/usr/lib/systemd/system/sslh-fork.service"
  install -Dm 644 $srcdir/sslh-select.service \
    "$pkgdir/usr/lib/systemd/system/sslh-select.service"
}

# vim:set ts=2 sw=2 et:
