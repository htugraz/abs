#$Id: PKGBUILD 131649 2015-04-18 20:09:49Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=qmmp
pkgver=0.8.4
pkgrel=1
pkgdesc="Qt4 based audio-player"
arch=('i686' 'x86_64')
url="http://qmmp.ylsoftware.com/"
license=('GPL')
depends=('alsa-lib' 'curl' 'hicolor-icon-theme' 'libmad' 'libvorbis' 'libogg'  'qt4' 'taglib' 'xdg-utils')
makedepends=('cmake' 'flac' 'jack' 'libmpcdec' 'pulseaudio' 'ffmpeg' 'libcdio-paranoia' 'libcddb' 'libmms'
             'libsamplerate' 'libmodplug' 'libsndfile' 'wavpack' 'projectm' 'mesa')
optdepends=('flac: native FLAC support'
            'jack: JACK sound output'
            'libmpcdec: Musepack support'
            'pulseaudio: PulseAudio output'
            'libcdio-paranoia: Compact Disc input and control support'
            'libcddb: CDDB server support'
            'libmms: MMS stream protocol support'
            'libmodplug: MOD playing library'
            'libsndfile: sampled sound support'
            'projectm: visual efects'
            'ffmpeg' 'libsamplerate' 'wavpack')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://sourceforge.net/projects/qmmp-dev/files/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('aef9f7685b5b5d79949bf501ac5420883ad2bceeeb6f57f9a7632a7f7d4fa6d1')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  if [[ $CARCH == 'x86_64' ]]; then
    mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib
  fi
}
