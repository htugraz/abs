#$Id: PKGBUILD 120624 2014-10-13 19:47:42Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=qmmp
pkgver=0.8.2
pkgrel=1
pkgdesc="Qt4 based audio-player"
arch=('i686' 'x86_64')
url="http://qmmp.ylsoftware.com/"
license=('GPL')
depends=('alsa-lib' 'curl' 'hicolor-icon-theme' 'libmad' 'libvorbis' 'libogg'  'qt4' 'taglib' 'xdg-utils')
makedepends=('cmake' 'flac' 'jack' 'libmpcdec' 'pulseaudio' 'ffmpeg' 'libcdio-paranoia' 'libcddb' 'libmms' 'libsamplerate' 'libmodplug' 'libsndfile' 'wavpack' 'projectm' 'mesa')
optdepends=('flac: native FLAC support'
            'jack: JACK sound output'
            'libmpcdec: Musepack support'
            'pulseaudio: PulseAudio output'
            'libcdio-paranoia: Compact Disc input and control support'
            'libcddb: CDDB server support'
            'libmms: MMS stream protocol support'
            'libmodplug: MOD playing library'
            'libsndfile: sampled sound support'
            'projectm: visual efects'
            'ffmpeg' 'libsamplerate' 'wavpack')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://qmmp.ylsoftware.com/files/$pkgname-$pkgver.tar.bz2)
sha256sums=('a4b1d3cefc4e361f91a08c754abae150ababd5c182c389679946beb0d7f092ef')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  if [[ $CARCH == 'x86_64' ]]; then
    mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib
  fi
}
