# $Id: PKGBUILD 111151 2014-05-14 19:43:14Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Marco Bartholomew <marco@marcobartholomew.com>

pkgname=monit
pkgver=5.8.1
pkgrel=1
pkgdesc="Utility for managing and monitoring, processes, files, directories and devices on a *NIX system"
arch=('i686' 'x86_64')
url="http://mmonit.com/monit/"
license=('GPL3')
depends=('openssl')
backup=('etc/monitrc')
changelog=$pkgname.changelog
source=(http://mmonit.com/$pkgname/dist/$pkgname-$pkgver.tar.gz \
        monit.service)
sha256sums=('a25e4b79257ac29ebaf46605dccb7ed693c8e001669c0ccc8feb22e7d4c870e5'
            'cf607209e9e5d39e708152dc181525570371fab750f828e890e2b6a00b81ebc4')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
  install -D -m700 monitrc "${pkgdir}"/etc/monitrc
  install -D -m644 "${srcdir}"/$pkgname.service "${pkgdir}"/usr/lib/systemd/system/$pkgname.service
}
