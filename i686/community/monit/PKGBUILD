# $Id: PKGBUILD 120815 2014-10-16 13:00:08Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Marco Bartholomew <marco@marcobartholomew.com>

pkgname=monit
pkgver=5.9
pkgrel=1
pkgdesc="Utility for managing and monitoring, processes, files, directories and devices on a *NIX system"
arch=('i686' 'x86_64')
url="http://mmonit.com/monit/"
license=('GPL3')
depends=('openssl')
backup=('etc/monitrc')
changelog=$pkgname.changelog
source=(http://mmonit.com/$pkgname/dist/$pkgname-$pkgver.tar.gz
        $pkgname.service)
sha256sums=('274044485170b7498de29e7876cd86488378f50d69df4303a14930ba1f5e4a11'
            'ec4b120876fd71fa98cbaaf52b06f461f451b3ae4aaa7d48ae39ed86e02e1f22')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -D -m700 monitrc "${pkgdir}"/etc/monitrc
  install -D -m644 "${srcdir}"/$pkgname.service "${pkgdir}"/usr/lib/systemd/system/$pkgname.service
}
