# $Id: PKGBUILD 147127 2015-11-19 05:05:51Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ngircd
pkgver=23
pkgrel=1
pkgdesc="Next Generation IRC Daemon"
arch=('x86_64' 'i686')
backup=(etc/ngircd.conf)
url="http://ngircd.barton.de/"
license=('GPL')
depends=('openssl' 'libident' 'zlib')
source=("http://ngircd.barton.de/pub/ngircd/ngircd-$pkgver.tar.gz"
        ngircd.service)
sha256sums=('99b8b67a975a9ae9b81c96bdee02133a10f515c718825d34cedcb64f1fc95e73'
            'f02e30f6864ba1130bcc85bedc44ad782687f572c06f10e0501b0ddcf532b404')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
  	--sbindir=/usr/bin \
  	--mandir=/usr/share/man \
  	--with-ident \
  	--with-openssl \
  	--enable-ipv6
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 ../ngircd.service "$pkgdir/usr/lib/systemd/system/ngircd.service"
}

# vim:set ts=2 sw=2 et:
