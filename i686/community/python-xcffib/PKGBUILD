# $Id: PKGBUILD 125945 2015-01-15 05:16:40Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgbase=python-xcffib
pkgname=(python-xcffib python2-xcffib)
pkgver=0.1.10
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('i686' 'x86_64')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi'
             'python-six' 'python2-six' 'libxcb')
source=("https://pypi.python.org/packages/source/x/xcffib/xcffib-$pkgver.tar.gz")
md5sums=('4481d40c16fa5511d2d0c9fdc05645ab')

prepare() {
  cp -a xcffib-$pkgver{,-py2}
}

package_python-xcffib() {
  depends=('python-six' 'python-cffi' 'libxcb')

  cd "$srcdir/xcffib-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-xcffib() {
  depends=('python2-six' 'python2-cffi' 'libxcb')

  cd "$srcdir/xcffib-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
