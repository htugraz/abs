# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cgi-fast
pkgver=2.03
pkgrel=1
pkgdesc='CGI Interface for Fast CGI'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' 'perl-cgi>=4' 'perl-fcgi>=0.67')
checkdepends=('perl-test-deep>=0.11')
url=https://metacpan.org/release/CGI-Fast
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEEJO/CGI-Fast-$pkgver.tar.gz")
md5sums=(bbf816e387b387a7d4db67347100a047)
sha512sums=(ccf3385a6315f914541388ff1f590ecc20de66477348498ae12ed5e38d5c7dbcf6891c640ced2b94a07d10cb6379c34bc3b0ac71d4ce72a7732f73854a9565c1)
_ddir="CGI-Fast-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
