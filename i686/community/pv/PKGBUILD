# $Id: PKGBUILD 98159 2013-10-06 19:16:16Z jlichtblau $
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=pv
pkgver=1.4.12
pkgrel=1
pkgdesc='A terminal-based tool for monitoring the progress of data through a pipeline.'
arch=('i686' 'x86_64')
url='http://www.ivarch.com/programs/pv.shtml'
license=('custom:Artistic 2.0')
depends=('glibc')
source=("http://www.ivarch.com/programs/sources/$pkgname-$pkgver.tar.bz2")
md5sums=('605adc0f369496bca92b0656cf86b25e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}
  
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
  install -Dm0644 doc/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
