# $Id: PKGBUILD 144982 2015-10-26 00:47:38Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2015.1.6
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator' 'python-six' 'python2-six' 'python-appdirs' 'python2-appdirs')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('07d54c1f04767b1e4345746d4da81256b79570a884404b6c811b304974d0cdf1246a3fb65c1fca9d1a7d5a934fca5ee55e0a49957a0cae8aecd29c4cd9a56579')

prepare() {
   cp -a pytools-${pkgver}{,-python2}
}

build() {
   cd "$srcdir/pytools-$pkgver"
   python setup.py build

   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py build
}

check() {
   cd "$srcdir/pytools-$pkgver"
   PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test

   cd "$srcdir/pytools-$pkgver-python2"
   PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2
}

package_python2-pytools() {
   depends=('python2-six' 'python2-decorator' 'python2-appdirs')
   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

package_python-pytools(){
   depends=('python-six' 'python-decorator' 'python-appdirs')
   cd "$srcdir/pytools-$pkgver"
   python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

