# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Dmitry A. Ilyashevich <dmitry.ilyashevich@gmail.com>

pkgname=distorm
pkgver=3.3.0
pkgrel=2
pkgdesc="Powerful disassembler library for x86/AMD64"
url='https://github.com/gdabah/distorm'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('python2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/gdabah/distorm/archive/v${pkgver}.tar.gz)
sha512sums=('5582d4f52eeb0eee5a3fa352af97c785281b0b4bd42b237d5ca9354114d1311b0976b34f6949c1b14848c9c7a1b972775b7c2644d557700b4878b6c154161cb8')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|(install)|\1 -D|g' -i make/linux/Makefile
  sed -r 's|/usr/local/lib|${DESTDIR}/usr/lib/${TARGET}|g' -i make/linux/Makefile
  sed -r 's|(CFLAGS	)=|\1+=|g' -i make/linux/Makefile
  sed -e '1i#!/usr/bin/env python2' -i python/distorm3/sample.py
}

build() {
  cd ${pkgname}-${pkgver}
  make -C make/linux
}

package() {
  cd ${pkgname}-${pkgver}

  make -C make/linux DESTDIR="${pkgdir}" install
  python2 setup.py install --root="${pkgdir}" -O1

  install -Dm 755 python/distorm3/sample.py "${pkgdir}/usr/bin/disasm"
  install -Dm 644 include/*.h -t "${pkgdir}/usr/include"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
