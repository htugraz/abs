# $Id: PKGBUILD 99788 2013-10-30 23:43:21Z allan $
# Contributor: Michael Kanis <mkanis@gmx.de>

pkgname=gsql
pkgver=0.2.2
pkgrel=6
pkgdesc="Integrated database development tool for GNOME"
url="http://gsql.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gtk2>=2.12' 'gtksourceview2' 'libglade' 'gconf'
	 'desktop-file-utils' 'libnotify' 'hicolor-icon-theme')
makedepends=('vte' 'libmariadbclient' 'postgresql-libs' 'libssh')
optdepends=('libmariadbclient: MySQL engine'
            'libssh: SSH tunneling plugin'
            'postgresql-libs: PostgreSQL engine'
            'vte: Terminal session plugin')
install=gsql.install
options=(!emptydirs)
source=(http://gsql.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2)
md5sums=('8f3322a75390584729e84a8570e08bea')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/notify_notification_new (subj, message, NULL, NULL);/notify_notification_new (subj, message, NULL);/' libgsql/notify.c
  LDFLAGS="$LDFLAGS `pkg-config --libs gthread-2.0`" ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-static --without-gnome
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
