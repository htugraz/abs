# $Id: PKGBUILD 125525 2015-01-06 15:32:52Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Cristóvão D. Sousa <crisjss@gmail.com>
pkgname=dsdp
pkgver=5.8
pkgrel=1
pkgdesc="A free open source implementation of an interior-point method for semidefinite programming"
arch=('i686' 'x86_64')
url="http://www.mcs.anl.gov/hs/software/DSDP"
license=('custom')
depends=("lapack" "blas")
source=("http://www.mcs.anl.gov/hs/software/${pkgname^^}/${pkgname^^}$pkgver.tar.gz"
        "make.include.patch")
md5sums=('37c15a3c6c3f13e31262f65ac4364b5e'
         'd90d4bf5ce00f6f86722db2624675ae5')

build() {
  cd "$srcdir/${pkgname^^}$pkgver"

  patch make.include < "$srcdir/make.include.patch"

  make DSDPROOT="$srcdir/${pkgname^^}$pkgver" dsdpapi
}

package() {
  cd "$srcdir/${pkgname^^}$pkgver"
  
  install -d "$pkgdir"/usr/{lib,include/dsdp}
  install -Dm755 bin/dsdp5 "$pkgdir"/usr/bin/dsdp5
  install -Dm644 lib/* "$pkgdir"/usr/lib/
  install -Dm644 include/*.h "$pkgdir"/usr/include/dsdp/
  install -Dm644 dsdp-license "$pkgdir"/usr/share/licenses/$pkgname/dsdp-license
}
