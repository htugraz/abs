# $Id: PKGBUILD 68265 2012-03-20 22:39:06Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=agave
pkgver=0.4.7
pkgrel=4
pkgdesc="Colorscheme designer tool for GNOME"
arch=('i686' 'x86_64')
url="http://home.gna.org/colorscheme/"
license=('GPL')
depends=('libglademm' 'gconfmm' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('gnome-doc-utils' 'intltool' 'boost')
install=$pkgname.install
source=("http://download.gna.org/colorscheme/releases/$pkgname-$pkgver.tar.bz2"
        agave-0.4.7-mdv-fix-str-fmt.patch
        schemebox.patch
        drop-libgnome.patch)
md5sums=('93097881f28dcae1eca2800a763f77c3'
         'd96ddfe751d8fd4d13a443b94fafb0b6'
         'c41adfd52ecbf837f4338e67693640fc'
         '7419d23329f847dcde12baa467b52135')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/agave-0.4.7-mdv-fix-str-fmt.patch" # Fedora patch
  patch -Np1 -i "$srcdir/schemebox.patch" # gcs-schemebox patch
  patch -Np1 -i "$srcdir/drop-libgnome.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-scrollkeeper --disable-gnome --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
