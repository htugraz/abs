# $Id: PKGBUILD 129363 2015-03-16 14:38:50Z flexiondotorg $
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=marco
pkgver=1.8.3
pkgrel=1
pkgdesc="A window manager for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libcanberra' 'libgtop' 'libice' 'libsm' 'libxt' 'mate-desktop'
         'startup-notification' 'zenity')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
replaces=('mate-window-manager')
provides=('mate-window-manager')
conflicts=('mate-window-manager')
options=('!emptydirs')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz")
sha1sums=('388c7e22bf84e8ba1fc9955858a681bb93206575')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-startup-notification \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
