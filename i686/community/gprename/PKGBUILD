# $Id: PKGBUILD 113702 2014-06-28 14:25:14Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Nick Bolten <Shirakawasuna at gmail _dot_ com>

pkgname=gprename
pkgver=20140325
pkgrel=1
pkgdesc="A GTK2 batch renamer for files and directories"
arch=('any')
url="http://gprename.sourceforge.net/"
license=('GPL3')
depends=('gtk2-perl' 'perl-libintl-perl' 'perl-locale-gettext')
makedepends=('patch')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        $pkgname-build.patch)
sha256sums=('8322a37ba720d58c520d29985175b86e9a4a74ce7465df8a5d67339dffb6140e'
            '8ade8ded3e4185d0d465668d2d519bc6eaf668f354109c45f42eeb05ba502cc0')

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}"/$pkgname-build.patch

  make PREFIX=/usr DESTDIR="${pkgdir}"/usr install

#29940 fix
  sed 's#/usr/local#/usr#' -i "${pkgdir}"/usr/bin/$pkgname
  sed 's#/usr/local#/usr#' -i "${pkgdir}"/usr/share/applications/$pkgname*.*
}
