# $Id: PKGBUILD 113352 2014-06-19 16:10:54Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Hannes Rist <hrist@phreeknet.org>

pkgname=mg
pkgver=20140414
pkgrel=1
pkgdesc="mg is Micro GNU/emacs, this is a portable version of the mg maintained by the OpenBSD team."
arch=('i686' 'x86_64')
url="http://homepage.boetes.org/software/mg/"
depends=('ncurses' 'libbsd')
makedepends=('libclens')
license=('custom')
source=(http://homepage.boetes.org/software/mg/mg-$pkgver.tar.gz
	cleanup.patch)
md5sums=('ad4a99a5e26d25db75af7936c6657fbe'
         '545eac98585e2f5fe0781093ddc07b36')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/cleanup.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make prefix=/usr
}

package() {
  cd $srcdir/$pkgname-$pkgver

  mkdir -p $pkgdir/usr/bin
  make install INSTALL=install prefix=/usr DESTDIR=$pkgdir BINDIR=/usr/bin

  mkdir -p $pkgdir/usr/share/
  install -D -m0644 README $pkgdir/usr/share/licenses/$pkgname/README
}
