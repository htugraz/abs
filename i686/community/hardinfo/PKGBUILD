# $Id: PKGBUILD 115006 2014-07-05 03:39:06Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=hardinfo
pkgver=0.5.1
pkgrel=6
pkgdesc="A system information and benchmark tool."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/hardinfo.berlios/"
license=('GPL2')
depends=('gtk2' 'libsoup')
options=('!emptydirs')
source=(http://sourceforge.net/projects/hardinfo.berlios/files/${pkgname}-${pkgver}.tar.bz2
        fixsensors.patch hardinfo.distro)
sha1sums=('983f0445aa60e02156bc5a5eaedeffb30b8e4d64'
          'd2151dce76dca238102937d0e15541f172bca530'
          '4c31cbbfbdeb48593641ff600c8d4fb7bda2b01c')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/fixsensors.patch"
  sed -i 's|/usr/lib64|/usr/lib|' configure
  sed -i 's|lib64|lib|' binreloc.c
  sed -i 's|{ DB_PREFIX "debian_version",	"deb"  },|{ DB_PREFIX "hardinfo.distro",       "arch"  },{ DB_PREFIX "debian_version",	"deb"  },|' computer.h
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/hardinfo.distro" "${pkgdir}/etc/hardinfo.distro"
}
