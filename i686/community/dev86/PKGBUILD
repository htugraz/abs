# $Id: PKGBUILD 100687 2013-11-08 09:34:27Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Suat SARIALP <muhendis.suat@gmail.com>

pkgname=dev86
pkgver=0.16.20
pkgrel=1
pkgdesc="Simple C compiler to generate 8086 code"
arch=('i686' 'x86_64')
url="http://v3.sk/~lkundrak/dev86/"
license=(GPL)
if [ "${CARCH}" == "x86_64" ]; then
  depends=('lib32-glibc')
  makedepends=('bin86' 'gcc-multilib')
else
  makedepends=('bin86')
fi
options=('!libtool' '!strip' '!makeflags' 'staticlibs')
source=(http://v3.sk/~lkundrak/dev86/Dev86src-$pkgver.tar.gz
	mandir.patch)
md5sums=('567cf460d132f9d8775dd95f9208e49a'
         '509df653dec8956faf1a40a8c544d9ce')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/mandir.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DIST="$pkgdir"
}

package() {
  cd $srcdir/$pkgname-$pkgver
  mkdir -p $pkgdir/usr/share/man/man1
  make install-all DIST="$pkgdir"
  # remove all the stuff supplied by bin86
  rm $pkgdir/usr/bin/{as,ld,nm,objdump,size}86
  rm $pkgdir/usr/share/man/man1/{as,ld}86.1
}
