# $Id: PKGBUILD 143917 2015-10-13 14:09:54Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Samuel Littley <aur@toastwaffle.com>
# Contributor: rnons <remotenonsense at gmail dot com>

pkgbase=python-whoosh
pkgname=(python-whoosh python2-whoosh)
pkgver=2.7.0
pkgrel=1
pkgdesc="Fast, pure-Python full text indexing, search, and spell checking library"
arch=('any')
url="http://bitbucket.org/mchaput/whoosh/wiki/Home"
license=("BSD")
makedepends=('python-setuptools' 'python2-setuptools' 'mercurial')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("hg+https://bitbucket.org/mchaput/whoosh#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a whoosh{,-py2}
}

build() {
  cd "$srcdir/whoosh"
  python setup.py build

  cd "$srcdir/whoosh-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/whoosh"
  python setup.py ptr || warning "Tests failed"

  cd "$srcdir/whoosh-py2"
  python2 setup.py ptr || warning "Tests failed"
}

package_python-whoosh() {
  depends=('python')

  cd "${srcdir}/whoosh"
  python setup.py install -O1 --root="${pkgdir}"
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_python2-whoosh() {
  depends=('python2')

  cd "${srcdir}/whoosh-py2"
  python2 setup.py install -O1 --root="${pkgdir}"
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
