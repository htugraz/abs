# $Id: PKGBUILD 122452 2014-11-12 18:28:15Z foutrelis $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Denis (dtonator@gmail.com)

pkgname=xmoto
pkgver=0.5.11
pkgrel=1
pkgdesc="A challenging 2D motocross platform game, where physics play an important role."
arch=('i686' 'x86_64')
url="http://xmoto.tuxfamily.org"
license=('GPL')
depends=('libjpeg' 'libpng' 'lua' 'sdl_mixer' 'ode' 'curl' 'sqlite3' 'sdl_ttf'
         'desktop-file-utils' 'sdl_net' 'glu' 'libxdg-basedir' 'libxml2')
makedepends=('mesa')
install=xmoto.install
source=("http://download.tuxfamily.org/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
        'xmoto-0.5.10-libpng15.patch'
        'system.cpp.patch')
sha1sums=('a795616fb359e60be343e339f7acf5e95e0f1ce2'
          '9d13fa09f1c558a0fc504f7e7b1ceac6b4b7b20d'
          'e741f769d1fc5779a6688970ee97e2a61a50c7ae')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/xmoto-0.5.10-libpng15.patch"
  patch -Np1 -i "${srcdir}/system.cpp.patch"

  # Remove bundled ode library because its headers get picked up during build
  rm -r src/ode
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-sdltest
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" mangdir=/usr/share/man/man6 install

  # install desktop file
  install -Dm0644 "${srcdir}/${pkgname}-${pkgver}/extra/xmoto.desktop" \
        "${pkgdir}/usr/share/applications/xmoto.desktop"

  # install icon for desktop file
  install -Dm0644 "${srcdir}/${pkgname}-${pkgver}/extra/xmoto.xpm" \
        "${pkgdir}/usr/share/pixmaps/xmoto.xpm"
}
