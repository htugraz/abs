# $Id: PKGBUILD 130678 2015-04-04 10:06:46Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tikysal <tikysal@gmail.com>
# Derived from Daniel Ehlers'

pkgname=coin-or-coinutils
pkgver=2.10.6
pkgrel=1
pkgdesc="COIN-OR collection of utility classes"
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/CoinUtils"
license=('EPL')
depends=('zlib' 'bzip2' 'glpk' 'lapack')
makedepends=('gcc-fortran')
groups=('coin-or')
source=("http://www.coin-or.org/download/source/CoinUtils/CoinUtils-${pkgver}.tgz")
sha1sums=('1df4007a8f7962921e6fab7a5bc7fb44443e86f3')

build() {
  cd CoinUtils-$pkgver

  ./configure --prefix=/usr --with-blas-lib='-lblas' --with-lapack-lib='-llapack' --with-glpk-lib='-lglpk' --enable-dependency-linking
  make
}

check() {
  cd CoinUtils-$pkgver
  make test
}

package() {
  cd CoinUtils-$pkgver
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir" install
}
