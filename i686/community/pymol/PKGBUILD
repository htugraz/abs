# $Id: PKGBUILD 105733 2014-02-14 15:40:11Z arcanis $
# Maintainer: Evgeniy Alekseev <arcanis.arch at gmail dot com>
# Contributor: graysky
# Contributor: Tomasz Å»ok <tomasz dot zok at gmail dot com>

pkgname=pymol
pkgver=1.7.0.0
pkgrel=2
pkgdesc="Molecular visualization system on an Open Source foundation"
arch=('i686' 'x86_64')
url="http://pymol.org/"
license=('custom')
depends=('glew' 'glut' 'mesa' 'python2-numpy' 'python2-pmw' 'tcsh')
makedepends=('desktop-file-utils' 'gendesk')
source=("http://downloads.sourceforge.net/project/pymol/pymol/1.7/pymol-v${pkgver}.tar.bz2"
        "${pkgname}-38899.patch")
install="${pkgname}.install"
md5sums=('36fc735f6104e59802f109d0d25b6676'
         '1f8152c4604ba2939b24e25a022937ca')

prepare() {
  # create desktop file
  gendesk -f --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" --name="PyMOL" --categories="Science;Chemistry"
  
  # suppress non-zero exit code that breaks makepkg
  sed -i '/sys.exit/ s,2,0,' "${srcdir}/pymol/setup.py"
  # change python to python2
  sed -i "s|python|python2|g" "${srcdir}/pymol/test/cyg"
  sed -i "s|python|python2|g" "${srcdir}/pymol/test/run"
  sed -i "s|python|python2|g" "${srcdir}/pymol/test/show"
  sed -i "s|python|python2|g" "${srcdir}/pymol/modules/pmg_tk/startup/apbs_tools.py"
  
  # fix FS#38899
  patch -p0 -i "${pkgname}-38899.patch"
  mv "${srcdir}/${pkgname}/modules/web" "${srcdir}/${pkgname}/modules/pymolweb"
}

build() {
  cd "${srcdir}/${pkgname}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
  sed -i "s|"${pkgdir}"||g" pymol
  install -Dm755 "pymol" "${pkgdir}/usr/bin/pymol"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/pymol.desktop" "${pkgdir}/usr/share/applications/pymol.desktop"
  install -Dm644 "${srcdir}/pymol.png" "${pkgdir}/usr/share/pixmaps/pymol.png"
}

# vim:set ts=2 sw=2 et:
