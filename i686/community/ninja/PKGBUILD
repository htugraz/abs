# $Id: PKGBUILD 123108 2014-11-25 17:04:50Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Filip Brcic <brcha@gna.org>
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=ninja
pkgver=1.5.3
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=(i686 x86_64)
url='http://martine.github.io/ninja/'
license=(Apache)
depends=(gcc-libs)
makedepends=(python2 re2c emacs-nox)
source=(https://github.com/martine/ninja/archive/v$pkgver.zip)
sha1sums=('1506e0f5ab59b9896d5f3af1edfc11454d35ea5d')

build() {
  cd ninja-$pkgver

  python2 configure.py --bootstrap
  emacs -Q --batch -f batch-byte-compile misc/ninja-mode.el
}

check() {
  cd ninja-$pkgver

  python2 ./configure.py
  ./ninja ninja_test
  ./ninja_test --gtest_filter=-SubprocessTest.SetWithLots
}

package() {
  cd ninja-$pkgver

  install -m755 -D ninja "$pkgdir/usr/bin/ninja"
  install -m644 -D doc/manual.asciidoc "$pkgdir/usr/share/doc/ninja/manual.asciidoc"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  install -m644 -D misc/ninja-mode.el "$pkgdir/usr/share/emacs/site-lisp/ninja-mode.el"
  install -m644 -D misc/ninja-mode.elc "$pkgdir/usr/share/emacs/site-lisp/ninja-mode.elc"
  install -m644 -D misc/ninja.vim "$pkgdir/usr/share/vim/vimfiles/syntax/ninja.vim"

  install -m644 -D misc/bash-completion "$pkgdir/usr/share/bash-completion/completions/ninja"
  install -m644 -D misc/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_ninja"
}
