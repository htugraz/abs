# $Id: PKGBUILD 108015 2014-03-21 00:37:09Z eric $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=hercules
pkgver=3.10
pkgrel=1
pkgdesc='Software implementation of System/370 and ESA/390'
arch=('i686' 'x86_64')
url='http://www.hercules-390.eu/'
license=('custom')
depends=('bzip2' 'libgcrypt' 'zlib' 'perl')
options=('!makeflags')
source=("http://downloads.hercules-390.eu/$pkgname-$pkgver.tar.gz")
sha256sums=('26264569b7d78bbc3b6221926051ac3761c4a792dfc84d591d3230de40aa46fa')

build() {
  cd $pkgname-$pkgver
  # Change module extension from .la to .so.
  sed '/HDL_MODULE_SUFFIX/ s/\.la/.so/' -i hdl.h
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYRIGHT \
    "$pkgdir/usr/share/licenses/hercules/qpl1"
}

# vim:set ts=2 sw=2 et:
