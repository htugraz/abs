# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libcliquer
pkgver=1.21
pkgrel=1
pkgdesc="A set of C routines for finding cliques in an arbitrary weighted graph"
arch=('i686' 'x86_64')
url="http://users.aalto.fi/~pat/cliquer.html"
license=('GPL')
depends=('glibc')
source=("http://users.aalto.fi/~pat/cliquer/cliquer-$pkgver.tar.gz" 'Makefile.patch')
md5sums=('ac65de2c89134abe2be36542b9465b16'
         'a38360e029771417aca9ff542943d6be')

prepare() {
  cd cliquer-$pkgver

# build shared lib
  patch -i $srcdir/Makefile.patch
}

build() {
  cd cliquer-$pkgver

  export CFLAGS="$CFLAGS -fPIC"
  export SAGESOFLAGS="-shared -Wl,-soname,libcliquer.so"
  make
}

package() {
  cd cliquer-$pkgver
  mkdir -p "$pkgdir"/usr/{lib,include/cliquer}
  install -m644 libcliquer.so "$pkgdir"/usr/lib
  install -m644 *.h "$pkgdir"/usr/include/cliquer
}
