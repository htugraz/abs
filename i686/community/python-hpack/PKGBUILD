# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hpack
pkgname=(python-hpack python2-hpack)
_pkgname=hpack
pkgver=1.0.1
pkgrel=1
pkgdesc="Pure-Python HPACK header compression"
arch=('any')
url="http://hyper.rtfd.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver::https://github.com/Lukasa/hpack/archive/v${pkgver}.tar.gz")
sha512sums=('d2e0e6e76f697c81fb9be64ae16a086d68e3bfb8971b66844a58e5fb76a28d3a895cb42f000e142481c4d41e931e92a90d2e9190d192c0053d2569f177d0f674')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build

  cd ../$_pkgname-$pkgver-py2
  python2 setup.py build
}

check() {
  cd $_pkgname-$pkgver
  py.test

  cd ../$_pkgname-$pkgver-py2
  py.test2
}

package_python-hpack() {
  depends=('python')

  cd $_pkgname-$pkgver
  python setup.py install -O1 --root "${pkgdir}"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-hpack() {
  depends=('python2')

  cd $_pkgname-$pkgver-py2
  python2 setup.py install -O1 --root "${pkgdir}"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

