# $Id: PKGBUILD 109078 2014-04-10 09:02:19Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=dillo
pkgver=3.0.4
pkgrel=1
pkgdesc="A small, fast graphical web browser built on FLTK"
arch=(i686 x86_64)
url="http://www.dillo.org"
license=('GPL')
depends=('fltk' 'libjpeg' 'perl' 'openssl' 'libpng' 'gcc-libs' 'libxcursor'
         'libxi' 'libxinerama')
backup=(etc/dillo/{dillorc,dpidrc})
source=(http://www.dillo.org/download/$pkgname-$pkgver.tar.bz2)
md5sums=('c85aab6b840527e1dd71b220a1dcfbab')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-cookies --enable-dlgui \
              --enable-ssl
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
