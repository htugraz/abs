# $Id: PKGBUILD 142473 2015-10-01 16:06:42Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: shamrok <szamrok@gmail.com>
# Contributor: scj <scj archlinux us>

pkgbase=python-levenshtein
pkgname=(python-levenshtein python2-levenshtein)
pkgver=0.12.0
pkgrel=3
pkgdesc="Python extension for computing string edit distances and similarities"
url="http://pypi.python.org/pypi/python-Levenshtein/"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/p/python-Levenshtein/python-Levenshtein-$pkgver.tar.gz")
sha512sums=('95df064490970618b003bccbef9071b1a2a3ee4645ac8e851f4205bd4e8123d21ee0ee733adfeec79085faa01ba39902e2a4a26bfb21b70678377579157df4a5')

prepare() {
  cp -a python-Levenshtein-$pkgver python2-Levenshtein-$pkgver
}

package_python-levenshtein() {
  depends=('python')

  cd python-Levenshtein-$pkgver
  python setup.py build install -O1 --prefix=/usr --root="$pkgdir"
}

package_python2-levenshtein() {
  depends=('python2')

  cd python2-Levenshtein-$pkgver
  python2 setup.py build install -O1 --prefix=/usr --root="$pkgdir"
}
