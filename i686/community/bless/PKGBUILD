# $Id: PKGBUILD 98640 2013-10-16 03:44:17Z bgyorgy $
# Maintainer:  TDY <tdy@gmx.com>
# Contributor: dcraven <dcraven@gmail.com>

pkgname=bless
pkgver=0.6.0
pkgrel=4
pkgdesc="A high-quality, full-featured hex editor"
arch=('any')
url="http://home.gna.org/bless/"
license=('GPL')
depends=('gtk-sharp-2>=2.8')
makedepends=('gettext' 'pkgconfig' 'rarian')
source=(http://download.gna.org/bless/bless-$pkgver.tar.gz)
md5sums=('b4c20d113281dfedb67d9f3ff8b174e3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Build for mono 4.0 (FS#37299)
  sed -i 's/gmcs/dmcs/' configure{,.ac} builder/ModuleBuilder.cs
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --without-scrollkeeper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
