# $Id: PKGBUILD 105286 2014-02-04 16:31:06Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Víctor Martínez Romanos <vmromanos@gmail.com>

pkgname=qucs
pkgver=0.0.17
pkgrel=2
pkgdesc="An integrated circuit simulator with a graphical user interface"
arch=('i686' 'x86_64')
url="http://qucs.sourceforge.net"
license=('GPL')
depends=('gcc-libs' 'qt4')
optdepends=('freehdl: to permit digital circuit simulation'
	    'asco: to enable circuit optimization'
	    'perl')
#source=("http://downloads.sourceforge.net/project/qucs/qucs/$(echo $pkgver | cut -f1 -ds)-snapshots/qucs-${pkgver/s/.}.tar.gz")
source=("http://downloads.sourceforge.net/project/qucs/qucs/$pkgver/qucs-$pkgver.tar.gz")
md5sums=('4b93ffcc2ef3dbbd6fa53a5743721532')

prepare() {
  cd $srcdir/$pkgname-${pkgver/s/.}
  sed -i 's|Icon=.*|Icon=/usr/share/qucs/bitmaps/big.qucs.xpm|' debian/qucs.desktop
}

build() {
  cd $srcdir/$pkgname-${pkgver/s/.}
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-${pkgver/s/.}
  make DESTDIR=$pkgdir install

  install -Dm0644 debian/qucs.desktop $pkgdir/usr/share/applications/qucs.desktop
}
