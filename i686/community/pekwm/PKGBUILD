# $Id: PKGBUILD 102208 2013-12-06 13:59:32Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Eddie Lozon <almostlucky@attbi.com>

pkgname=pekwm
pkgver=0.1.17
pkgrel=2
pkgdesc='Small, fast, functional, and flexible window manager'
arch=('x86_64' 'i686')
license=('GPL')
url='https://www.pekwm.org/projects/pekwm'
depends=('gcc-libs' 'libjpeg' 'libpng' 'libxft' 'libxinerama' 'libxpm' 'libxrandr')
makedepends=('gendesk>=0.6')
optdepends=('lxappearance: feature-rich GTK+ theme switcher'
            'nitrogen: background browser and setter'
            'tint2: basic, good-looking task manager')
backup=(etc/pekwm/autoproperties
        etc/pekwm/autoproperties_typerules
        etc/pekwm/config
        etc/pekwm/config_system
        etc/pekwm/keys
        etc/pekwm/menu
        etc/pekwm/mouse
        etc/pekwm/mouse_click
        etc/pekwm/mouse_sloppy
        etc/pekwm/mouse_system
        etc/pekwm/start
        etc/pekwm/vars)
source=("http://www.pekwm.org/projects/pekwm/files/$pkgname-$pkgver.tar.bz2")
sha256sums=('8a1fd3bf9f38e8c7bb2b2864c090f986b60cec2281ecf1bba462d120fb327d00')

prepare() {
  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --name PekWM
  gendesk -f -wm --pkgname "${pkgname}_kdm" --exec "$pkgname" --name PekWM
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/xsessions/$pkgname.desktop"
  install -Dm644 "$srcdir/${pkgname}_kdm.desktop" \
    "$pkgdir//usr/share/apps/kdm/sessions/$pkgname.desktop"
}

# vim: ts=2 sw=2 et
