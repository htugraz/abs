# $Id: PKGBUILD 123022 2014-11-24 00:56:10Z speps $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=guitarix2
pkgver=0.32.0
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('i686' 'x86_64')
url="http://guitarix.sourceforge.net/"
license=('GPL')
depends=('gtkmm' 'librsvg' 'liblrdf' 'lilv' 'bluez-libs' 'boost-libs'
         'zita-convolver' 'zita-resampler' 'desktop-file-utils')
makedepends=('python' 'boost' 'eigen' 'gperf' 'intltool' 'lv2')
optdepends=('meterbridge: sound meters')
provides=('guitarix' 'gx_head')
conflicts=('guitarix' 'gx_head')
replaces=('guitarix' 'gx_head')
install="$pkgname.install"
source=("http://download.sourceforge.net/project/guitarix/guitarix/$pkgname-$pkgver.tar.bz2")
md5sums=('450159e9ee430c59ac29b3f4b558394a')

build() {
  cd guitarix-$pkgver
  python waf configure --prefix=/usr \
                        --no-desktop-update \
                        --no-ldconfig \
                        --no-faust \
                        --shared-lib \
                        --lib-dev
  python waf build
}

package() {
  cd guitarix-$pkgver
  python waf install --destdir="$pkgdir"
}
