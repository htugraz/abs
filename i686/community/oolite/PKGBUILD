# $Id: PKGBUILD 118008 2014-08-28 09:07:54Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>
pkgname=oolite
pkgver=1.80
pkgrel=2
pkgdesc="A space-sim based on the classic Elite"
arch=('i686' 'x86_64')
url="http://oolite-linux.berlios.de"
license=('GPL2')
depends=('gnustep-gui' 'sdl_mixer' 'glu' 'espeak' 'nspr' 'python2' 'icu' 'gnustep-base' 'openal')
makedepends=('gcc-objc' 'gnustep-make' 'curl' 'zip' 'mesa')
#options=(makeflags)
source=(oolite
        "https://github.com/OoliteProject/oolite/releases/download/1.80b/oolite-source-${pkgver}.tar.bz2")
md5sums=('24d8d3db1bdfb3d6238c4cb81c4306a2'
         'ded6afaf83f97624122c2dd999319166')

build() {
  cd oolite-source-${pkgver}
  unset CPPFLAGS
  sed -i '/void png_error/d' src/Core/Materials/OOPNGTextureLoader.m
  sed -i "/ADDITIONAL_OBJCFLAGS/s/=/= -fobjc-exceptions/" GNUmakefile
  make -f Makefile release
}

package() {
  cd oolite-source-${pkgver}

  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/share/{oolite,applications,pixmaps,doc/oolite}

  cp -r oolite.app/* ${pkgdir}/usr/share/oolite/
#install shellscript and icon
  install -D -m755 ../oolite ${pkgdir}/usr/bin/oolite
  install -D -m644 installers/FreeDesktop/oolite-icon.png ${pkgdir}/usr/share/pixmaps/oolite-icon.png
# copy  .desktop file
  install -D -m644 installers/FreeDesktop/oolite.desktop ${pkgdir}/usr/share/applications/oolite.desktop
# documentation
  install -D -m644 Doc/AdviceForNewCommanders.pdf Doc/OoliteReadMe.pdf Doc/OoliteRS.pdf ${pkgdir}/usr/share/doc/oolite/
 }
# vim:set ts=2 sw=2 et:
