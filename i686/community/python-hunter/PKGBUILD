# $Id: PKGBUILD 141472 2015-09-22 16:00:29Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hunter
pkgname=('python-hunter' 'python2-hunter')
pkgver=0.5.0
pkgrel=1
arch=('any')
pkgdesc='Hunter is a flexible code tracing toolkit'
url='https://github.com/ionelmc/python-hunter'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-fields' 'python2-fields'
             'python-colorama' 'python2-colorama' 'git')
checkdepends=('python-pytest-capturelog' 'python2-pytest-capturelog'
              'python-pytest-cov' 'python2-pytest-cov')
source=("git+https://github.com/ionelmc/python-hunter.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a python-hunter{,-py2}
}

build() {
  cd python-hunter
  python setup.py build

  cd ../python-hunter-py2
  python2 setup.py build
}

check() {
  cd python-hunter
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test tests || warning "Tests failed"

  cd ../python-hunter-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2 tests || warning "Tests failed"
}

package_python-hunter() {
  depends=('python')

  cd python-hunter
  python setup.py install --root "${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-hunter() {
  depends=('python2')

  cd python-hunter-py2
  python2 setup.py install --root "${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
