# $Id: PKGBUILD 135137 2015-06-10 14:42:41Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SpepS <dreamspepser@yahoo.it>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgbase=python-routes
pkgname=(python-routes python2-routes)
pkgver=2.1
pkgrel=2
pkgdesc="Routing recognition and generation tools"
arch=("any")
url="https://pypi.python.org/pypi/Routes"
license=("custom:BSD")
makedepends=('python-setuptools' 'python2-setuptools' 'python-repoze.lru' 'python2-repoze.lru')
checkdepends=('python-nose' 'python2-nose' 'python-webob' 'python2-webob' 'python-coverage' 'python2-coverage')
source=("https://pypi.python.org/packages/source/R/Routes/Routes-${pkgver}.tar.gz")
md5sums=('e6e463318a9dc6ad2f1b3040e998f0b4')

prepare() {
  cp -a Routes-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/Routes-${pkgver}/"
  python setup.py build

  cd "${srcdir}/Routes-${pkgver}-py2/"
  python2 setup.py build
}

check() {
  cd "${srcdir}/Routes-${pkgver}/"
  nosetests3

  cd "${srcdir}/Routes-${pkgver}-py2/"
  nosetests2
}

package_python-routes() {
  depends=("python-repoze.lru")

  cd "${srcdir}/Routes-${pkgver}/"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-routes() {
  depends=("python2-repoze.lru")

  cd "${srcdir}/Routes-${pkgver}-py2/"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
