# $Id: PKGBUILD 99910 2013-10-31 02:25:20Z allan $
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=metakit
pkgver=2.4.9.7
pkgrel=5
pkgdesc='MetaKit is an efficient database library with a small footprint'
arch=('i686' 'x86_64')
url='http://www.equi4.com/metakit/'
makedepends=('tcl' 'python2')
license=('BSD')
source=("http://www.equi4.com/pub/mk/${pkgname}-${pkgver}.tar.gz")
md5sums=('17330257376eea657827ed632ea62c9e')

build() {
  cd ${pkgname}-${pkgver}/builds

  sed -i 's_"${prefix}/include/python2.5"_"${prefix}/include/python2.7"_' ../unix/configure
  sed -i 's_"${prefix}/lib/python2.5/site-packages"_"${prefix}/lib/python2.7/site-packages"_' ../unix/configure

  ../unix/configure \
    --prefix=/usr \
    --with-python \
    --with-tcl=/usr/include 

  make
}

package() {
  cd ${pkgname}-${pkgver}/builds

  mkdir -p ${pkgdir}/usr/lib/python2.7/site-packages

  make DESTDIR=${pkgdir} install

  install -D -m644 ../license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
