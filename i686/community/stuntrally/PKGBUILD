# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Jason Melton <jason.melton@gmail.com>

pkgname=stuntrally
pkgver=2.3
pkgrel=2
pkgdesc='Stunt Rally game with track editor, based on VDrift'
arch=('x86_64' 'i686')
license=('GPL3')
url='http://code.google.com/p/vdrift-ogre'
depends=('libvorbis' 'mygui' 'sdl2' 'enet' 'gtk-update-icon-cache' 'stuntrally-data')
makedepends=('cmake' 'boost' 'git')
install=stuntrally.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/stuntrally/stuntrally/archive/${pkgver}.tar.gz")
sha256sums=('828ea8e4a8ea73005e88ef015cb9808cc75d0cb50693a0f4a7d8b09edd0765fd')

build() {
  cd "$srcdir/stuntrally-$pkgver/"

  rm -rf build
  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DSHARE_INSTALL="share/stuntrally"
  make
}

package() {
  cd "$srcdir/stuntrally-$pkgver/build/"

  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share/stuntrally/"
}

# vim:set ts=2 sw=2 et:
