# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: MThinkCpp <mtc.maintainer[at]outlook.com>
pkgname=libc++
pkgver=3.4.2
pkgrel=1
pkgdesc='A new implementation of the C++ standard library, targeting C++11.'
url='http://libcxx.llvm.org'
license=('custom:University of Illinois/NCSA Open Source License')
arch=('i686' 'x86_64')
# gcc-libs needed for libgcc_s, because Arch doesn't use a pure compiler-rt clang
depends=('glibc' 'libc++abi' 'gcc-libs')
makedepends=('clang' 'subversion' 'cmake')
source=("http://www.llvm.org/releases/${pkgver}/libcxx-${pkgver}.src.tar.gz"
        "http://www.llvm.org/releases/3.4.2/libcxx-${pkgver}.src.tar.gz.sig")
md5sums=('da134c8bb4576e6168fb4872526bb8b3'
         'SKIP')
install=${pkgname}.install

build() {
  mkdir -p libcxx-${pkgver}.src/build
  cd libcxx-${pkgver}.src/build
  CC=clang CXX=clang++ cmake -G "Unix Makefiles" -DLIBCXX_CXX_ABI=libcxxabi \
    -DLIBCXX_LIBCXXABI_INCLUDE_PATHS=/usr/include \
    -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr "$srcdir/libcxx-${pkgver}.src"
  make
}
package() {
  install -Dm644 libcxx-${pkgver}.src/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/license.txt"
  cd libcxx-${pkgver}.src/build
  make DESTDIR="$pkgdir" install
}
