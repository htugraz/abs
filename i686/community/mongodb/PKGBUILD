# $Id: PKGBUILD 110766 2014-05-06 17:08:14Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas

pkgname=mongodb
pkgver=2.6.1
pkgrel=1
pkgdesc='A high-performance, open source, schema-free document-oriented database'
arch=('i686' 'x86_64')
url='http://www.mongodb.org'
license=('AGPL3')
depends=('boost-libs' 'pcre' 'snappy' 'openssl' 'gperftools' 'libsasl') # 'libyaml' 'yaml-cpp' 'v8')
makedepends=('scons' 'boost' 'readline' 'ncurses' 'libpcap')
checkdepends=('python2-pymongo')
optdepends=('libpcap: needed for mongosniff')
backup=('etc/mongodb.conf')
install=mongodb.install
source=("http://downloads.mongodb.org/src/mongodb-src-r${pkgver}.tar.gz"
        'mongodb.conf' 'mongodb.service' 'removeWerror.patch')

build() {
  # fucking mongo aint no fun to package
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  # failed to build with -Werror since 2.4.0
  patch -Np1 -i "$srcdir/removeWerror.patch"

  scons all \
    --use-system-boost \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --use-sasl-client \
    --ssl
    # --use-system-yaml
    # --use-system-v8
}

check() {
  # Tests not passing
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons smokeAll --smokedbprefix="$srcdir" || :
}

package() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons install \
    --use-system-boost \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --use-sasl-client \
    --ssl \
    --prefix="$pkgdir/usr"
    # --use-system-yaml
    # --use-system-v8

  install -Dm644 "$srcdir/mongodb.conf" "$pkgdir/etc/mongodb.conf"
  install -Dm644 "$srcdir/mongodb.service" "$pkgdir/usr/lib/systemd/system/mongodb.service"
  install -dm700 "$pkgdir/var/lib/mongodb"
  install -dm755 "$pkgdir/var/log/mongodb"
}

sha512sums=('a07168a8fa609d2a22b04db6b761e486e58fb63c66ea4974e5cc53a1255d514cd065a84b4dc62efbb27c39a6e3881d98a3f7025c2391a78d91d5cd515b8a2e56'
            '05dead727d3ea5fe8af1a3c3888693f6b3e2b8cb7f197a5d793352e10d2c524e96c9a5c55ad2e88c1114643a9612ec0b26a2574b48a5260a9b51ec8941461f1c'
            '177251404b2e818ae2b546fe8b13cb76e348c99e85c7bef22a04b0f07b600fd515a309ede50214f4198594388a6d2b31f46e945b9dae84aabb4dfa13b1123bb9'
            '868e5f93ba3c087f80982ffbe01411644d01329a51e4a1811cf8dd6de5e155f604a924be933623b70903fb4dabb27eed100a7eb6f6edcf4bb7a0fdfddd485270')
