# $Id: PKGBUILD 100663 2013-11-07 22:07:24Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas_weissschuh lavabit com>
# Contributor: fauno <fauno@kiwwwi.com.ar>
# Contributor: SanskritFritz

pkgname=transmission-remote-cli
pkgver=1.6.3
pkgrel=1
pkgdesc="Curses interface for the daemon of the BitTorrent client Transmission"
arch=('any')
url='https://github.com/fagga/transmission-remote-cli'
license=('GPL3')
depends=('python2')
optdepends=('python2-geoip: Guess which country peers come from'
            'adns-python: Resolve IPs to host names')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fagga/transmission-remote-cli/tarball/v$pkgver")
sha256sums=('ad2c2269d77f31f691c84b3c62886a17441ac5e90780fbbb01a038ec74db7371')

build() {
  cd "$(ls -1td fagga-transmission-remote-cli-* | head -n 1)"
  sed -i "1s/python/python2/" transmission-remote-cli
}

package() {
  cd "$(ls -1td fagga-transmission-remote-cli-* | head -n 1)"

  install -Dm755 transmission-remote-cli "$pkgdir/usr/bin/transmission-remote-cli"
  install -Dm644 transmission-remote-cli.1 "$pkgdir/usr/share/man/man1/transmission-remote-cli.1"
  install -Dm755 completion/bash/transmission-remote-cli-bash-completion.sh \
    "$pkgdir/usr/share/bash-completion/completions/transmission-remote-cli"
}
