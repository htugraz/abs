# $Id: PKGBUILD 115273 2014-07-07 09:30:48Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Giovanni Scafora <linuxmania@gmail.com>
# Contributor: simo <simo@archlinux.org>

pkgname=xcircuit
pkgver=3.8.69
pkgrel=1
pkgdesc="A program for drawing publishable-quality electrical circuit schematic diagrams"
arch=('i686' 'x86_64')
url="http://opencircuitdesign.com/xcircuit"
license=('GPL2')
depends=('tk' 'libxpm' 'zlib')
makedepends=('chrpath' 'gendesk')
_categories='Application;Electronics;Engineering'
source=(http://opencircuitdesign.com/xcircuit/archive/$pkgname-$pkgver.tgz)
md5sums=('dbdcf28e8af273bc2d1e97f5f843e833')

build() {
  cd $srcdir/$pkgname-$pkgver
  gendesk $startdir/PKGBUILD
  ./configure --prefix=/usr --with-tcl=/usr/lib --with-tk=/usr/lib --mandir=/usr/share/man
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  mkdir -p $pkgdir/usr/share/man/man1
  mv $pkgdir/usr/lib/xcircuit-3.8/man/* $pkgdir/usr/share/man/man1/
  rm -rf $pkgdir/usr/lib/xcircuit-3.8/man/
  chrpath -d $pkgdir/usr/lib/xcircuit-3.8/xcircexec
  install -Dm0644 xcircuit.desktop $pkgdir/usr/share/applications/xcircuit.desktop
  install -Dm0644 xcircuit.png $pkgdir/usr/share/icons/xcircuit.png
}
