# $Id: PKGBUILD 145888 2015-11-04 02:51:43Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_pkgname=WebTest
pkgname=(python-webtest python2-webtest)
pkgver=2.0.20
pkgrel=1
pkgdesc="Helper to test WSGI applications"
arch=('any')
url='http://webtest.pythonpaste.org/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-webob' 'python2-webob' 'python-six' 'python2-six'
             'python-beautifulsoup4' 'python2-beautifulsoup4' 'python-waitress' 'python2-waitress')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-coverage' 'python2-coverage'
              'python-pyquery' 'python2-pyquery'  'python-pastedeploy' 'python2-pastedeploy'
              'python-wsgiproxy2' 'python2-wsgiproxy2')
source=("http://pypi.python.org/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha256sums=('bb137b96ce300eb4e43377804ed45be87674af7d414c4de46bba4d251bc4602f')

prepare() {
  cp -a "$srcdir/$_pkgname-${pkgver}"{,-py2}
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py build

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/$_pkgname-${pkgver}"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.5:$PYTHONPATH" nosetests3

  cd "$srcdir/$_pkgname-${pkgver}-py2"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" nosetests2
}

package_python-webtest() {
  depends=('python-webob' 'python-six' 'python-beautifulsoup4' 'python-waitress')
  optdepends=('python-pyquery: for response.pyquery'
              'python-lxml: for response.lxml')

  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 "docs/license.rst" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-webtest() {
  depends=('python2-webob' 'python2-six' 'python2-beautifulsoup4' 'python2-waitress')
  optdepends=('python2-pyquery: for response.pyquery'
              'python2-lxml: for response.lxml')

  cd "$srcdir/$_pkgname-$pkgver-py2"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 "docs/license.rst" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
