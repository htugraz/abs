# $Id: PKGBUILD 57215 2011-10-24 18:32:20Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=clamz
pkgver=0.5
pkgrel=1
pkgdesc="Command-line program to download MP3 files from Amazon.com's music store."
arch=('i686' 'x86_64')
url='http://code.google.com/p/clamz/'
license=('GPL3')
depends=('libgcrypt' 'curl' 'expat' 'shared-mime-info' 'desktop-file-utils')
install='clamz.install'
source=("http://clamz.googlecode.com/files/clamz-${pkgver}.tar.gz")
md5sums=('84543c9346b285bdc218879e241941a8')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Fix issue with update-mime-database
	sed -i 's|$(UPDATE_DESKTOP_DATABASE)\ $(DESTDIR)$(applications_dir)||' Makefile.in
	sed -i 's|$(UPDATE_MIME_DATABASE)\ $(DESTDIR)$(mime_dir)||' Makefile.in

	./configure --prefix=/usr/
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
