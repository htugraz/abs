# $Id: PKGBUILD 130057 2015-03-28 09:36:08Z fyan $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mikael Eriksson <mikael_eriksson@miffe.org>
# Originally by: Denis Smirnov <detanator@gmail.com>

pkgname=intel-gpu-tools
pkgver=1.10
pkgrel=2
pkgdesc="Tools for development and testing of the Intel DRM driver"
arch=(i686 x86_64)
license=(MIT)
url='http://cgit.freedesktop.org/xorg/app/intel-gpu-tools/'
depends=(libdrm libpciaccess cairo python)
makedepends=(swig gtk-doc xorg-util-macros)
source=(http://xorg.freedesktop.org/releases/individual/app/$pkgname-$pkgver.tar.bz2)
md5sums=('4d208c9294adcc7ea34b960e9950dd1e')

build() {
  cd $pkgname-$pkgver
  export LANG=en_US.UTF-8
  ./configure --prefix=/usr --disable-tests --disable-selective-werror
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/intel-gpu-tools/COPYING"
}
