# $Id: PKGBUILD 104634 2014-01-23 14:58:20Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

pkgname=jemalloc
pkgver=3.5.0
pkgrel=1
pkgdesc="General-purpose scalable concurrent malloc implementation"
arch=('i686' 'x86_64')
license=('BSD')
url="http://www.canonware.com/jemalloc/"
depends=('glibc')
makedepends=('autoconf' 'make' 'bash')
optdepends=(
	'perl: memory profiler'
)
source=(http://www.canonware.com/download/jemalloc/$pkgname-$pkgver.tar.bz2)

build() {
	cd "$srcdir/$pkgname-$pkgver"
	CFLAGS="$CFLAGS -std=gnu11" ./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	mv "$pkgdir"/usr/bin/{,jemalloc-}pprof
	chmod 644 "$pkgdir"/usr/lib/*.a
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

sha256sums=('5b13dbef27c46e9efbe19b5eef574e298409ed9dbbde85ec805e29d04c05e473')
