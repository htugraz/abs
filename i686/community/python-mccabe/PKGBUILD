# $Id: PKGBUILD 108576 2014-03-29 02:24:57Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>

pkgbase=python-mccabe
pkgname=(python-mccabe python2-mccabe)
_reponame=mccabe
pkgver=0.2.1
pkgrel=5
pkgdesc="McCabe complexity checker for Python"
arch=('any')
url="https://github.com/flintwork/mccabe"
license=('custom:Expat')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/flintwork/${_reponame}/archive/${pkgver}.tar.gz")
md5sums=('fcba311ebd999f48359a8ab28da94b30')

prepare() {
  cp -r ${_reponame}-${pkgver}{,-py2}
}

package_python-mccabe() {
  depends=('python')

  cd ${_reponame}-${pkgver}
  python setup.py install --root="${pkgdir}/" --prefix=/usr --optimize=1

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-mccabe() {
  depends=('python2')

  cd ${_reponame}-${pkgver}
  python2 setup.py install --root="${pkgdir}/" --prefix=/usr --optimize=1

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
