# $Id: PKGBUILD 118199 2014-09-02 18:22:53Z jelle $
# Contributor: Pierre Buard <pierre.buard@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Sebastien Piccand <sebcactus gmail com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=('python-pyenchant' 'python2-pyenchant')
pkgver=1.6.6
pkgrel=1
arch=('any')
url="https://pypi.python.org/pypi/pyenchant"
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools' 'enchant')
source=("http://pypi.python.org/packages/source/p/pyenchant/pyenchant-${pkgver}.tar.gz")
md5sums=('9f5acfd87d04432bf8df5f9710a17358')

build() {
  cp -r pyenchant-${pkgver} pyenchant2-${pkgver}

  cd ${srcdir}/pyenchant-${pkgver}

  python3 setup.py build

  cd ${srcdir}/pyenchant2-${pkgver}

  python2 setup.py build
}

package_python-pyenchant() {
  pkgdesc="PyEnchant is a spellchecking library for Python3 based on the Enchant library"
  depends=('python' 'enchant')

  cd pyenchant-${pkgver}
  
  python3 setup.py install --root=${pkgdir} --optimize=1
}

package_python2-pyenchant() {
  pkgdesc="PyEnchant is a spellchecking library for Python2 based on the Enchant library"
  depends=('python2' 'enchant')

  cd pyenchant2-${pkgver}
  
  python2 setup.py install --root=${pkgdir} --optimize=1
}
