# $Id: PKGBUILD 145886 2015-11-04 02:49:37Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-wsgiproxy2
pkgname=('python-wsgiproxy2' 'python2-wsgiproxy2')
pkgver=0.4.2
pkgrel=1
pkgdesc="A WSGI Proxy with various http client backends"
arch=('any')
license=('MIT')
url="https://github.com/gawel/WSGIProxy2/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-webob' 'python2-webob'
             'python-six' 'python2-six' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-requests' 'python2-requests'
              'python-webtest' 'python2-webtest' 'python-waitress' 'python2-waitress')
source=("git+https://github.com/gawel/WSGIProxy2.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a WSGIProxy2{,-py2}
}

build() {
  cd "$srcdir/WSGIProxy2"
  python setup.py build

  cd "$srcdir/WSGIProxy2-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/WSGIProxy2"
  nosetests3

  cd "$srcdir/WSGIProxy2-py2"
  # We don't provide restkit (looks obsolete to me)
  nosetests2 || warning "Tests failed"
}

package_python-wsgiproxy2() {
  depends=('python-webob' 'python-six')

  cd WSGIProxy2
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-wsgiproxy2() {
  depends=('python2-webob' 'python2-six')

  cd WSGIProxy2-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
