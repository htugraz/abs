# $Id: PKGBUILD 104802 2014-01-26 12:17:40Z andyrtr $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=pinot
pkgver=1.06
pkgrel=3
pkgdesc='Personal search and metasearch tool'
arch=('i686' 'x86_64')
url='http://code.google.com/p/pinot-search/'
license=('GPL')
depends=('gtkmm3' 'xapian-core' 'libtextcat' 'sqlite' 'libxml++' 'curl' 'gmime' 
         'dbus-glib' 'shared-mime-info' 'libexif' 'taglib' 'hicolor-icon-theme' 
         'cairo' 'exiv2' 'boost-libs')
makedepends=('boost' 'desktop-file-utils')
optdepends=('unzip: ZIP files extraction'
            'poppler: PDF to text conversion'
            'catdvi: DVI to text conversion'
            'djvulibre: DjVu text extraction'
            'unrtf: RTF to HTML conversion'
            'antiword: MS Word to text conversion'
            'catdoc: XLS and PPT to text conversion')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://pinot-search.googlecode.com/files/$pkgname-$pkgver.tar.gz namespace_clash.patch)
sha256sums=('bba6f412553890ef7cccd6d7bb28df9a6c4d122ea74a2902e5de33953cd1872d'
            '7b4fee7b43f73539803954075af2ce577b65ca732ab8115d257068ffaa5a750f')

build() {
  cd $srcdir/$pkgname-$pkgver
  x='/usr/share/libtextcat/'
  sed -i 's|/LM/russian.lm|/LM/russian-windows1251.lm|' textcat_conf.txt
  sed -i "s|$x|${x}LM/|" \
    textcat_conf.txt \
    textcat3_conf.txt \
    textcat31_conf.txt \
    textcat32_conf.txt
  sed -i -e "s|.*russian$|${x}LM/russian-iso8859_5.lm   russian-iso8859_5\n\
${x}LM/russian-koi8_r.lm      russian-koi8_r\n\
${x}LM/russian-windows1251.lm russian-windows1251|" textcat3_conf.txt

  patch -Np1 -i $srcdir/namespace_clash.patch


  LIBS="-lboost_system" ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --enable-gtkmm3=yes \
    --enable-mempool=no
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
