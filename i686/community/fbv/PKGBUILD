# $Id: PKGBUILD 98028 2013-10-04 09:54:24Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Aectann <aectann@infoline.su>

pkgname=fbv
pkgver=1.0b
pkgrel=8
pkgdesc="FrameBuffer image viewer"
arch=('i686' 'x86_64')
url="http://s-tech.elsat.net.pl/fbv/"
license=('GPL')
depends=('libpng' 'giflib' 'libjpeg')
source=(http://s-tech.elsat.net.pl/fbv/$pkgname-$pkgver.tar.gz
        giflib-5.0.patch)
md5sums=('3e466375b930ec22be44f1041e77b55d'
         '6310899c97c4226352ad409c1c7fefb3')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man
  sed -i 's|LIBS.*|LIBS=-lpng -ljpeg -lgif|' Make.conf
  sed -i 's|setjmp(png_ptr->jmpbuf)|setjmp(png_jmpbuf(png_ptr))|' png.c
  patch -Np1 -i ../giflib-5.0.patch
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  mkdir -p $pkgdir/usr/bin
  mkdir -p $pkgdir/usr/share/man/man1
  make DESTDIR=$pkgdir install
}
