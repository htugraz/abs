# $Id: PKGBUILD 124132 2014-12-19 21:46:49Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andreas B. Wagner <AndreasBWagner@pointfree.net>
# Contributor: Sebastien Binet <binet@farnsworth>pkgname=cblas

pkgname=cblas
pkgver=3.5.0
pkgrel=1
pkgdesc="C interface to BLAS"
url="http://www.netlib.org/blas"
arch=('i686' 'x86_64')
license=('custom')
depends=('blas')
makedepends=('gcc-fortran')
source=('http://www.netlib.org/blas/blast-forum/cblas.tgz' 'cblas.patch' 'Makefile.in' 'LICENSE')
md5sums=('1e8830f622d2112239a4a8a83b84209a'
         'e779fc195f8f48672656522bcfd642e4'
         'a4dc51bcd46f80b921be46f1fa855f00'
         '38b6acb8ed5691d25863319d30a8b365')

prepare() {
  cd CBLAS

  /bin/cp $srcdir/Makefile.in .
  patch -p0 -i $srcdir/cblas.patch

  install -d src/lib
}

build() {
  cd CBLAS
  make alllib
}

package() {
  install -d "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include

  install -m755 CBLAS/src/lib/* "$pkgdir"/usr/lib
  install -m644 CBLAS/include/cblas.h "$pkgdir"/usr/include

  install -m755 -d "$pkgdir"/usr/share/licenses/cblas
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/cblas/
}

