# $Id: PKGBUILD 126023 2015-01-17 03:46:59Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=paxd
pkgver=25
pkgrel=1
pkgdesc='PaX exception daemon'
arch=(i686 x86_64)
url='https://github.com/thestinger/paxd/'
license=(MIT)
depends=(glib2)
source=(https://github.com/thestinger/paxd/archive/$pkgver.tar.gz 10-enable-pax.conf)
sha1sums=('def8d90bacdad1339312b5a9bb54067975a7ecd6'
          '8ee73dd288bc32438078e032dfc50a2f8ea6a5f2')
backup=(etc/paxd.conf)

build() {
  cd $pkgname-$pkgver
  make CC=gcc
}

package() {
  cd $pkgname-$pkgver
  mkdir -p "$pkgdir/usr/lib/systemd/system/sysinit.target.wants"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 ../10-enable-pax.conf "$pkgdir/usr/lib/sysctl.d/10-enable-pax.conf"
}
