# $Id: PKGBUILD 123413 2014-12-03 15:12:24Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=paxd
pkgver=19
pkgrel=1
pkgdesc='PaX exception daemon'
arch=(i686 x86_64)
url='https://github.com/thestinger/paxd/'
license=(MIT)
depends=(glibc)
source=(https://github.com/thestinger/paxd/archive/$pkgver.tar.gz 10-enable-pax.conf)
sha1sums=('9bd83b83b9d5337dabd93dae51ddd12619b5573b'
          '8ee73dd288bc32438078e032dfc50a2f8ea6a5f2')
backup=(etc/paxd.conf)

build() {
  cd $pkgname-$pkgver
  make CC=gcc
}

package() {
  cd $pkgname-$pkgver
  mkdir -p "$pkgdir/usr/lib/systemd/system/sysinit.target.wants"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 ../10-enable-pax.conf "$pkgdir/usr/lib/sysctl.d/10-enable-pax.conf"
}
