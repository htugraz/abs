# $Id: PKGBUILD 100265 2013-11-02 08:45:37Z spupykin $
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Matthias Bauch <matthias.bauch@gmail.com>
# Contributor: Laszlo Papp <djszapi2 at gmail com>
# Contributor: Samuel Tardieu <sam@rfc1149.net>

pkgname=openocd
pkgver=0.7.0
pkgrel=2
pkgdesc='Debugging, in-system programming and boundary-scan testing for embedded target devices'
arch=('i686' 'x86_64')
url='http://openocd.berlios.de'
license=('GPL')
depends=('libftdi')
options=(!strip)
install=openocd.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('52237b786530c8460b221556c26fa4779f668b7dcb83ff14b8c5eb2050f38e63')

_features=(amtjtagaccel arm-jtag-ew at91rm9200 buspirate ep93xx ft2232_libftdi ftdi gw16012 jlink oocd_trace opendous osbdm parport presto_libftdi remote-bitbang rlink stlink ti-icdi ulink usbprog vsllink)

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr ${_features[@]/#/--enable-} --disable-werror
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
