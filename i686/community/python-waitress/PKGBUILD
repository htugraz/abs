# $Id: PKGBUILD 145882 2015-11-04 02:28:06Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-waitress
pkgname=('python-waitress' 'python2-waitress')
pkgver=0.8.10
pkgrel=1
pkgdesc="Waitress WSGI server"
arch=('any')
license=('ZPL')
url="https://github.com/Pylons/waitress"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/Pylons/waitress.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a waitress{,-py2}
}

build() {
  cd "$srcdir/waitress"
  python setup.py build

  cd "$srcdir/waitress-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/waitress"
  nosetests3

  cd "$srcdir/waitress-py2"
  nosetests2
}

package_python-waitress() {
  depends=('python-setuptools')

  cd waitress
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-waitress() {
  depends=('python2-setuptools')

  cd waitress-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1

  mv "$pkgdir"/usr/bin/waitress-serve{,2}
}
