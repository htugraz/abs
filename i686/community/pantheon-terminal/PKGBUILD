# $Id: PKGBUILD 112592 2014-06-04 07:22:28Z alucryd $
# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=pantheon-terminal
pkgver=0.3.0.1
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgname}"
license=('GPL3')
depends=('desktop-file-utils' 'granite' 'libnotify' 'vte3')
makedepends=('cmake' 'vala')
install="${pkgname}.install"
source=("${url}/0.3.x/${pkgver}/+download/${pkgname}-${pkgver}.tgz")
sha256sums=('ea9718b409baa3ebdc175b5d51ed7825f0199e3c3de467a5f7bea6733bbe9a93')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
