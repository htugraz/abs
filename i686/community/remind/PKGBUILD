# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Sebastian Sontberg <ssontberg@h3c.de>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=remind
pkgver=3.1.15
_pkgver=03.01.15
pkgrel=1
pkgdesc='A sophisticated calendar and alarm program.'
arch=('i686' 'x86_64')
url='http://www.roaringpenguin.com/penguin/open_source_remind.php'
license=('GPL')
optdepends=('tk: for tkremind'
            'tcllib: for tkremind'
            'tcl: for cm2rem.tcl')
source=("http://www.roaringpenguin.com/files/download/${pkgname}-${_pkgver}.tar.gz"{,.sig})
options=('!emptydirs')
md5sums=('089c51c060fdda2357a833800a5bfe39'
         'SKIP')
validpgpkeys=('FC2E9B645468698FD7B21655C1842E2A126F42E0') # David F. Skoll

build() {
  cd "${pkgname}-${_pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
