# $Id: PKGBUILD 128459 2015-03-01 10:19:59Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: grimsock <lord.grimsock at gmail dot com>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>
# Contributor: Vladimir Chizhov <jagoterr@gmail.com>
# Contributor: Henry Tang <henryykt@gmail.com>

pkgname=phantomjs
pkgver=2.0.0
pkgrel=2
pkgdesc="Headless WebKit with JavaScript API"
url="http://www.phantomjs.org/"
license=('BSD' 'LGPL' 'MIT')
arch=('i686' 'x86_64')
depends=('icu' 'libjpeg-turbo' 'libpng' 'fontconfig' 'gperf' 'ruby' 'python2' 'gstreamer0.10-base')
makedepends=('git')
source=("git+https://github.com/ariya/${pkgname}.git#tag=$pkgver")

build() {
  cd $pkgname
  
  mkdir "$srcdir/python2-path"
  ln -s /usr/bin/python2 "$srcdir/python2-path/python"
  export PATH="$srcdir/python2-path:$PATH" PYTHON=/usr/bin/python2 
  
  # shared build requires qt 5.3
  #sed -i "s/export QMAKE=qmake/export QMAKE=qmake-qt5/" build.sh
  ./build.sh --confirm --qtdeps=system --qt-config '-no-rpath' # --qtwebkit=system
}

package() {
  install -Dm755 "$srcdir/$pkgname/bin/phantomjs" "$pkgdir/usr/bin/phantomjs"

  mkdir -p "$pkgdir/usr/share/$pkgname"
  cp -r "$srcdir/$pkgname/examples" "$pkgdir/usr/share/$pkgname"/

  install -Dm644 "$srcdir/$pkgname/LICENSE.BSD" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.BSD"
  install -Dm644 "$srcdir/$pkgname/third-party.txt" "$pkgdir/usr/share/licenses/$pkgname/third-party.txt"
}

sha512sums=('SKIP')
