# $Id: PKGBUILD 92959 2013-06-20 20:17:58Z kkeen $
#Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Kevin Piche <kevin@archlinux.org>

pkgname=bochs
pkgver=2.6.2
pkgrel=1
pkgdesc="A portable x86 PC emulation software package"
arch=('i686' 'x86_64')
url="http://bochs.sourceforge.net/"
license=('LGPL')
depends=('gcc-libs' 'libxrandr')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('82ecaff9826d4f29fa46f3062e2957b8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --without-wx --enable-cpu-level=6 \
              --enable-fpu --enable-3dnow --enable-disasm
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m 644 .bochsrc "${pkgdir}/etc/bochsrc-sample.txt"
}
