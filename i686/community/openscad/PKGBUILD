# $Id: PKGBUILD 144607 2015-10-21 21:56:01Z foutrelis $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Eric Anderson <ejona86 at gmail.com>
# Contributor: Pierre DOUCET <pierre at equinoxefr.org>
pkgname=openscad
pkgver=2015.03.01
_ver=2015.03-1  # hopefully -X is temporary
pkgrel=6
pkgdesc="The programmers solid 3D CAD modeller"
url="http://openscad.org/"
arch=('i686' 'x86_64')
license=('GPL2')
install=openscad.install
changelog=CHANGELOG
depends=('qt4' 'qscintilla' 'cgal' 'opencsg' 'boost-libs' 'shared-mime-info')
makedepends=('eigen' 'boost' 'imagemagick')
# full tests need imagemagick and an X11 or framebuffer session
checkdepends=('cmake' 'xproto' 'libx11' 'python2')
source=("http://files.openscad.org/openscad-$_ver.src.tar.gz")
md5sums=('c5994220078f5f5c13984da304e4a2fe')

build() {
    cd "$srcdir/$pkgname-$_ver"
    qmake-qt4 PREFIX="/usr"
    make
    convert "icons/$pkgname.png" -resize 128x128\> "icons/$pkgname-128.png"
}

check() {
    # see doc/testing.txt about enabling
    # normally will display stuff, run image diffs
    cd "$srcdir/$pkgname-$_ver/tests"
    sed -i 's/exit 1/exit 0/' virtualfb.sh
    cmake .
    # pkgconfig weirdness
    sed -i 's/-lmpfr/& -lX11/' CMakeFiles/openscad_nogui.dir/link.txt 
    make
    sed -i 's/ python$/&2/' *.py
    sed -i 's|/usr/bin/python"|/usr/bin/python2"|g' CTestTestfile.cmake
    sed -i 's|/usr/bin/python |/usr/bin/python2 |g' CTestCustom.cmake
    msg "Running headless tests..."
    ctest -R 'dumptest|echotest|csgtexttest|csgtermtest|openscad-nonascii|openscad-override'
}

package() {
    cd "$srcdir/$pkgname-$_ver"
    make INSTALL_ROOT="$pkgdir" install
    # can't remove earlier, py files needed for tests
    rm "$pkgdir/usr/share/openscad/libraries/MCAD/"*.py
    install -Dm644 "icons/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm644 "icons/$pkgname-128.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
    install -Dm644 "icons/$pkgname.xml" "$pkgdir/usr/share/mime/packages/$pkgname.xml"
}
