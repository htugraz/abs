# $Id: PKGBUILD 73557 2012-07-09 19:15:21Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jared Casper <jaredcasper@gmail.com>
# Contributor: Paulo Matias <matias archlinux-br org>

pkgname=iverilog
pkgver=0.9.7
pkgrel=3
pkgdesc='Icarus Verilog compiler and simulation tool'
arch=('x86_64' 'i686')
url='http://iverilog.icarus.com/'
# http://iverilog.wikia.com/wiki/Release_Notes_Icarus_Verilog_0_9
license=('GPL')
makedepends=('readline' 'gperf')
depends=('bzip2' 'sh' 'gcc-libs' 'zlib')
options=('staticlibs')
source=("ftp://ftp.icarus.com/pub/eda/verilog/v0.9/verilog-$pkgver.tar.gz")
sha256sums=('7a5e72e17bfb4c3a59264d8f3cc4e70a7c49c1307173348fdd44e079388e7454')

build() {
  cd "${pkgname/i}-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  make -C "${pkgname/i}-$pkgver" -j1 prefix="$pkgdir/usr" install
}

# vim:set ts=2 sw=2 et:
