# $Id: PKGBUILD 117871 2014-08-25 13:09:30Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=freetalk
pkgver=4.0rc5
pkgrel=1
pkgdesc="A console based Jabber client"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/freetalk"
license=('GPL')
depends=('loudmouth' 'guile' 'jansson' 'curl')
install=freetalk.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/GNUFreetalk/freetalk/archive/v$pkgver.tar.gz")
md5sums=('a19a6025a13ef9f922606a843f892b4d')

build() {
  cd $pkgname-$pkgver
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr --disable-rpath
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/share/info/dir"
}
