# $Id: PKGBUILD 96902 2013-09-07 15:43:01Z seblu $
# Maintainer: SÃ©bastien Luttringer
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>

pkgname=stow
pkgver=2.2.0
pkgrel=2
pkgdesc='Manage installation of multiple softwares in the same directory tree'
url='http://www.gnu.org/software/stow/'
arch=('any')
source=("ftp://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
license=('GPL2')
depends=('perl')
makedepends=('texi2html')
checkdepends=('perl-test-output')
install=$pkgname.install
md5sums=('902564d502975f353d3b0514a21863b1')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info \
    --with-pmdir=/usr/lib/perl5/vendor_perl/
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
