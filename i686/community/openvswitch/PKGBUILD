# $Id: PKGBUILD 107605 2014-03-18 09:51:41Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Pedro Martinez-Julia (pedromj@um.es)
# Contributor: Matt Monaco <net 0x01b dgbaley27>

pkgname=openvswitch
pkgver=2.0.1
pkgrel=1
pkgdesc="Production Quality, Multilayer Open Virtual Switch"
url="http://openvswitch.org"
license=('APACHE')
arch=(x86_64 i686)
install=openvswitch.install
source=("http://openvswitch.org/releases/openvswitch-$pkgver.tar.gz"
	openvswitch.install
	openvswitch.tmpfiles
	ovsdb-server.service
	ovs-vswitchd.service)
depends=('openssl')
makedepends=('python2')
optdepends=('python2')
sha256sums=('014907bcf2f98228dfa4472ea1547871d24b8fb8192dcfff3ead1af93c433a67'
            '9d30ed859214f4cdeb6b6450d5c5defaf31f2c593c05201109df66fa9373ef0c'
            'e8dc21e50fc886bfd6aa55991bdb3cb66907e11b071045452bb12de01a3ecbd9'
            '5900728bca0292ec66e6da234eaa6f32f28ead6bda163d89a480eac08558c657'
            'c2cb5e2685240ea50b1aba4e77784f4dd66d157e7cd05a17853868aa8d8a2a45')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./boot.sh
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-rundir=/run/openvswitch \
		--sbindir=/usr/bin \
		PYTHON=/usr/bin/python2
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm0644 $srcdir/openvswitch.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/openvswitch.conf"
	install -Dm0644 $srcdir/ovsdb-server.service "$pkgdir/usr/lib/systemd/system/ovsdb-server.service"
	install -Dm0644 $srcdir/ovs-vswitchd.service "$pkgdir/usr/lib/systemd/system/ovs-vswitchd.service"
	install -dm0755 "$pkgdir/etc/openvswitch"
	rm -rf $pkgdir/run
}
