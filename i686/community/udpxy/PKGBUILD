# $Id: PKGBUILD 108157 2014-03-24 07:46:34Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Konstantin Shabanov <kes.eclipse@gmail.com>
# Contributor: Evka
# Maintainer: Jose Riha <jose 1711 gmail com>

pkgname=udpxy
pkgver=1.0.23.0
_pkgver=1.0.23-0
pkgrel=3
pkgdesc="small-footprint UNIX/Linux daemon to relay multicast UDP traffic to client's TCP (HTTP) connection."
arch=(i686 x86_64)
url="http://udpxy.sf.net"
license=('GPL3')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgname/Chipmunk-1.0/$pkgname.${_pkgver}-prod.tar.gz
	$pkgname.service
	pidfile.patch)
md5sums=('3dd99ba264078e873cbe1d98369ed423'
         'f47aeec41aa03acd4b61a6bfa03b0dff'
         '7a268f58ee964895377c95d16c9c633a')

prepare() {
  cd "$srcdir/$pkgname-${_pkgver}"
  patch -Np1 -i "${srcdir}/pidfile.patch"
  sed -i '/ln -s $(INSTALLROOT)\/bin\/$(EXEC) $(INSTALLROOT)\/bin\/$(UDPXREC/s%$(INSTALLROOT)%/usr%' Makefile
}

build() {
  cd "$srcdir/$pkgname-${_pkgver}"
  make
}

package() {
  cd "$srcdir/$pkgname-${_pkgver}"
  make INSTALLROOT="$pkgdir/usr" install
  install -D -m644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
}
