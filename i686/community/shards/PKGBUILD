# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=shards
pkgver=0.5.3
pkgrel=1
pkgdesc='The package manager for the Crystal language'
arch=(i686 x86_64)
url='https://github.com/ysbaddaden/shards'
license=(Apache)
depends=(libyaml git libpcl libevent gc)
makedepends=(crystal)
source=(shards-$pkgver.tar.gz::https://github.com/ysbaddaden/shards/archive/v$pkgver.tar.gz)
sha256sums=('33a42709dc7f69b892f551b6a2d44b49d9d75b6e54e186fcb7534c8485f90139')

build() {
  cd shards-$pkgver
  crystal build --release -o bin/shards src/shards.cr
}

check() {
  cd shards-$pkgver
  ./bin/shards install
  make test
}

package() {
  cd shards-$pkgver

  install -Dm755 bin/shards "$pkgdir/usr/bin/shards"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
