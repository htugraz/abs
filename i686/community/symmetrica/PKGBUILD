# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=symmetrica
pkgver=2.0
pkgrel=4
pkgdesc="Algorithms for symmetric groups and classical groups"
arch=('i686' 'x86_64')
url="http://www.algorithm.uni-bayreuth.de/en/research/SYMMETRICA/"
license=('custom')
depends=()
options=('staticlibs')
source=('http://www.algorithm.uni-bayreuth.de/en/research/SYMMETRICA/SYM2_0_tar.gz' 'int32.patch' 'LICENSE')
md5sums=('c901a30ffb44715adc85ec43a9838069'
         'cc057ecdfc5f0d629d13f6b2999ba23c'
         'c385bb47c0759cb6464e55702875b7a0')

prepare() {
  sed -i -e 's/-O2/$(CFLAGS)/' makefile
# fix integer overflow on 64 bits
  patch -p1 -i int32.patch

# disable banner
  sed -i 's|no_banner = FALSE|no_banner = TRUE|' de.c
}

build() {
  export CFLAGS="-fPIC $CFLAGS"
  make
  ar crs libsymmetrica.a *.o
}

package() {
  install -D -m 644 libsymmetrica.a "$pkgdir"/usr/lib/libsymmetrica.a
  mkdir -p "$pkgdir"/usr/include/symmetrica/
  install -m 644 *.h "$pkgdir"/usr/include/symmetrica/

  mkdir -p "$pkgdir"/usr/share/licenses/symmetrica
  install LICENSE "$pkgdir"/usr/share/licenses/symmetrica
}

