# $Id: PKGBUILD 99528 2013-10-29 10:58:03Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: hydro <hydro@freenet.de>

pkgname=mediainfo-gui
pkgver=0.7.64
pkgrel=1
pkgdesc="GUI for mediainfo"
arch=('i686' 'x86_64')
url="http://mediainfo.sourceforge.net"
license=('GPL')
depends=('libmediainfo' 'wxgtk')
makedepends=('libtool' 'automake' 'autoconf')
install=mediainfo-gui.install
source=(http://downloads.sourceforge.net/mediainfo/mediainfo_${pkgver}.tar.bz2)
md5sums=('f5cb798cf3b121359ab3173d70526518')

build() {
  cd $srcdir/MediaInfo/Project/GNU/GUI
  sh ./autogen
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/MediaInfo/Project/GNU/GUI
  make DESTDIR=$pkgdir install
  install -D -m 644 $srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png \
                    $pkgdir/usr/share/icons/hicolor/128x128/apps/mediainfo.png
  install -D -m 644 $srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png \
                    $pkgdir/usr/share/pixmaps/mediainfo-gui.png
  install -D -m 644 $srcdir/MediaInfo/Project/GNU/GUI/mediainfo-gui.desktop \
                    $pkgdir/usr/share/applications/mediainfo-gui.desktop
}
