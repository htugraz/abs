# $Id: PKGBUILD 117546 2014-08-18 21:49:47Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Daniele Paolella <dp@mcrservice.it>

pkgname=supervisor
pkgver=3.1.1
pkgrel=1
pkgdesc="A system for controlling process state under UNIX"
arch=('any')
url="http://supervisord.org"
license=('custom')
depends=('python2-meld3' 'python2-setuptools')
backup=('etc/supervisord.conf')
source=("http://pypi.python.org/packages/source/s/$pkgname/$pkgname-$pkgver.tar.gz"
        'supervisord.service'
        'supervisor.logrotate'
        'supervisord.conf')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	rm -rf "supervisor/medusa/"{debian,demo,docs,test}
	find -type f -name "*.py" -exec sed -i 's|/usr/bin/env python|/usr/bin/env python2|' {} \;
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	python2 setup.py build
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	python2 setup.py install -O1 --skip-build --prefix=/usr --root="$pkgdir"
	install -Dm600 "$srcdir/supervisord.conf" "$pkgdir/etc/supervisord.conf"
	install -Dm644 "$srcdir/supervisor.logrotate" "$pkgdir/etc/logrotate.d/supervisor"
	install -Dm644 "$srcdir/supervisord.service" "$pkgdir/usr/lib/systemd/system/supervisord.service"
	install -d "$pkgdir/usr/share/licenses/$pkgname"
	install -d "$pkgdir/var/log/$pkgname"
	install -d "$pkgdir/etc/supervisor.d"
	mv LICENSES.txt COPYRIGHT.txt "$pkgdir/usr/share/licenses/$pkgname"
}

sha256sums=('e3c3b35804c24b6325b5ba462553ebee80d5f4d1766274737b5c532cd4a11d59'
            '7c83b32ccd256a08b323ed972cff25ad29d44b18b1138624c74ead4929809676'
            '7347d79c8b7f5cc59d5ffa31e4df64f92e89dee0dc84bdb3bdfcba212de93f4b'
            'fe0a60e34f67bf6ed7ca8731d9ce7657e00f37a43e0dfb96aa0df57adc4cd423')
