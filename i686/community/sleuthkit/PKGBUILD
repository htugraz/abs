# $Id: PKGBUILD 118701 2014-09-09 08:27:03Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=sleuthkit
pkgver=4.1.3
pkgrel=2
pkgdesc='File system and media management forensic analysis tools'
arch=('i686' 'x86_64')
url="http://www.sleuthkit.org/sleuthkit"
license=('GPL2' 'CPL' 'custom:"IBM Public Licence"')
depends=('perl')
makedepends=('libewf')
optdepends=('libewf: support of the Expert Witness Compression Format')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('67f9d2a31a8884d58698d6122fc1a1bfa9bf238582bde2b49228ec9b899f0327')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

#licenses
  install -d "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm0644 licenses/* "${pkgdir}"/usr/share/licenses/${pkgname}
}
