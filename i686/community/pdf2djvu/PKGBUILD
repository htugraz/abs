# $Id: PKGBUILD 127119 2015-02-04 16:00:26Z andyrtr $
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=pdf2djvu
pkgver=0.7.17
pkgrel=6
pkgdesc="Creates DjVu files from PDF files"
arch=('i686' 'x86_64')
url="http://pdf2djvu.googlecode.com"
license=('GPL')
depends=('poppler' 'djvulibre' 'libxslt' 'gcc-libs' 'graphicsmagick')
makedepends=('pstreams' 'python2-nose' 'ttf-liberation')
source=(http://pdf2djvu.googlecode.com/files/${pkgname}_${pkgver}.tar.gz{,.asc})
validpgpkeys=('CDB5A1243ACDB63009AD07212D4EB3A6015475F5')
md5sums=('fedec591afb14b144808d1a4facab174'
         'SKIP')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver}

    sed -i 's/nosetests/nosetests2/' tests/Makefile
    make test
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make install DESTDIR=${pkgdir}
    install -Dm644 doc/${pkgname}.1 ${pkgdir}/usr/share/man/man1/${pkgname}.1
}

