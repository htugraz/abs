# $Id: PKGBUILD 127137 2015-02-04 23:02:34Z bluewind $
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>

pkgname=archlinux-themes-kdm
pkgver=1.4
pkgrel=4
pkgdesc="Arch Linux branded KDM greeter themes for KDE"
arch=('any')
url="http://www.archlinux.org/"
license=('CCPL:cc-by-sa')
source=("https://sources.archlinux.org/other/artwork/${pkgname}-${pkgver}.tar.gz"{,.sig})
md5sums=('7469d94881c323fb0535deef5ca64e83'
         'SKIP')
validpgpkeys=('4FCF887689C41B09506BE8D5F3E1D5C5D30DB0AD') # Andrea Scarpino

package() {
  cd $pkgname-$pkgver
  for t in simplyblack soft-grey; do
    install -d "${pkgdir}"/usr/share/apps/kdm/themes/archlinux-$t
    install -Dm644 archlinux-$t/{AUTHORS,COPYING,KdmGreeterTheme.desktop,TRADEMARKS,archlinux.png,background.png,kdm.xml,screenshot.png} \
      "${pkgdir}"/usr/share/apps/kdm/themes/archlinux-$t
  done
}
