# $Id: PKGBUILD 143059 2015-10-06 18:40:48Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=muon
pkgver=5.4.2
pkgrel=1
pkgdesc='A collection of package management tools for KDE'
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/muon'
license=(LGPL)
depends=(packagekit-qt5 plasma-framework knewstuff appstream-qt archlinux-appstream-data hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('05b79b89c9708c046775448ec846c513')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../muon-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
