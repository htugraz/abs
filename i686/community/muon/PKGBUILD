# $Id: PKGBUILD 126717 2015-01-28 23:48:00Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=muon
pkgver=5.2.0
pkgrel=2
pkgdesc='A collection of package management tools for KDE'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/muon'
#groups=('plasma')
license=('LGPL')
depends=('packagekit-qt5' 'plasma-framework' 'knewstuff' 'appstream-qt' 'archlinux-appstream-data')
makedepends=('extra-cmake-modules' 'python')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('72cd26214b3e0aa018c7606ea0782c98')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../muon-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
