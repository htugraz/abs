# $Id: PKGBUILD 106365 2014-03-01 08:29:21Z andrea $
# Contributor: Paulo Matias <matias.archlinux-br.org>
# Contributor: Leslie P. Polzer <polzer.gnu.org>
# Contributor: erm67 <erm67.yahoo.it>
# Contributor: Daniel J Griffiths
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=djview4
pkgver=4.9
pkgrel=2
pkgdesc='Portable DjVu viewer and browser plugin'
url='http://djvu.sourceforge.net/djview4.html'
license=('GPL')
arch=('i686' 'x86_64')
depends=('qt4' 'djvulibre')
source=("http://downloads.sourceforge.net/djvu/djview-${pkgver}.tar.gz")
sha1sums=('07f97e32919bfb34ff2d59b928c47ffbc96dcfc5')

install=install

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr
	sed 's/netscape/mozilla/g' -i nsdejavu/Makefile
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install-djview install-nsdejavu

	cd desktopfiles
	install -Dm644 hi32-djview4.png "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/djvulibre-djview4.png
	install -Dm644 hi64-djview4.png "${pkgdir}"/usr/share/icons/hicolor/64x64/apps/djvulibre-djview4.png
	install -Dm644 hi64-djview4.png "${pkgdir}"/usr/share/pixmaps/djvulibre-djview4.png
	install -Dm644 djvulibre-djview4.desktop "${pkgdir}"/usr/share/applications/djvulibre-djview4.desktop
}
