# $Id: PKGBUILD 122763 2014-11-19 16:02:55Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Anatol Pomozov <anatol.pomozov gmail>
# Contributor: RunningDroid <runningdroid AT zoho.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gflags
pkgver=2.1.1
pkgrel=4
pkgdesc='C++ Library for commandline flag processing'
arch=(i686 x86_64)
url='https://github.com/schuhschuh/gflags'
license=(BSD)
depends=(gcc-libs)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::https://github.com/schuhschuh/gflags/archive/v$pkgver.tar.gz)
sha256sums=('432edece8102fc141c2e3381e2eb5eabb1ce7614851cc96bdec4715105086238')

build() {
  cd gflags-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=ON -DBUILD_STATIC_LIBS=OFF -DBUILD_TESTING=ON
  make
}

check() {
  cd gflags-$pkgver
  make test
}

package() {
  cd gflags-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING.txt "$pkgdir"/usr/share/licenses/$pkgname/COPYING.txt
}
