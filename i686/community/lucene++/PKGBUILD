# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Rafael Ferreira <josephgbr@archlinux.info>

pkgname=lucene++
pkgver=3.0.4
pkgrel=1
pkgdesc='C++ port of the popular Java Lucene library'
arch=(i686 x86_64)
url='https://github.com/luceneplusplus/LucenePlusPlus'
license=('GPL' 'Apache')
depends=('boost-libs' 'gcc-libs')
makedepends=('boost' 'cmake')
source=($url/archive/rel_$pkgver.tar.gz
        0001-Add-support-for-compiling-with-c-11.patch
        0002-Turn-off-custom-allocator-by-default.patch
        0003-Fix-accidental-use-of-operator-instead-of-operator.patch)
md5sums=('64c34aa66b3870cc015c5d084a1a2b4e'
         '243c87bed96250043f21d4d6ea6b6c6e'
         'ab8c8d5484411a9af55121b4d565f746'
         '2c1758953b1b3d7c3c14c88b770bcb26')

prepare() {
  mkdir build

  cd LucenePlusPlus-rel_$pkgver
    
  # http://www.poedit.net/trac/ticket/568
  patch -p1 -i ../0001-Add-support-for-compiling-with-c-11.patch
  patch -p1 -i ../0002-Turn-off-custom-allocator-by-default.patch
  patch -p1 -i ../0003-Fix-accidental-use-of-operator-instead-of-operator.patch
}

build() {
  cd build

  cmake ../LucenePlusPlus-rel_$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib \
    -DBoost_LIBRARY_DIR=/usr/lib \
    -DENABLE_CUSTOM_ALLOCATOR='FALSE'

  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
