# $Id: PKGBUILD 121299 2014-10-23 17:15:52Z eric $
# Maintainer: 

pkgname=perl-anyevent-i3
pkgver=0.16
pkgrel=1
pkgdesc="Communicate with the i3 window manager"
arch=('any')
url="http://search.cpan.org/~mstplbg/AnyEvent-I3/"
license=('GPL')
depends=('perl' 'perl-anyevent')
checkdepends=('perl-json-xs')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/MS/MSTPLBG/AnyEvent-I3-${pkgver}.tar.gz)
md5sums=('88bd53dbfe356aa8aa136fe19ab7d8e6')

build() {
  cd AnyEvent-I3-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd AnyEvent-I3-${pkgver}
  make test
}

package() {
  cd AnyEvent-I3-${pkgver}
  make install DESTDIR="${pkgdir}"
}
