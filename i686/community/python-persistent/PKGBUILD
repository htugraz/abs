# $Id: PKGBUILD 145717 2015-11-03 05:39:51Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-persistent
pkgname=('python-persistent' 'python2-persistent')
pkgver=4.1.1
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=('i686' 'x86_64')
license=('ZPL')
url="http://www.zope.org/Products/ZODB"
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/zopefoundation/persistent.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a persistent{,-py2}
}

build() {
  cd "$srcdir/persistent"
  python setup.py build

  cd "$srcdir/persistent-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/persistent"
  nosetests3

  cd "$srcdir/persistent-py2"
  nosetests2
}

package_python-persistent() {
  depends=('python-zope-interface')

  cd persistent
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-persistent() {
  depends=('python2-zope-interface')

  cd persistent-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
