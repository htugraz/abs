# $Id: PKGBUILD 70305 2012-05-04 08:21:08Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python2-greenlet
pkgver=0.4.1
pkgrel=1
pkgdesc="python coroutine library"
license=("MIT")
url="http://pypi.python.org/pypi/greenlet"
depends=('python2')
source=(http://pypi.python.org/packages/source/g/greenlet/greenlet-$pkgver.zip)
arch=('i686' 'x86_64')

build() {
	cd "$srcdir/greenlet-$pkgver"
	python2 setup.py build
}

package() {
	cd "$srcdir/greenlet-$pkgver"
	python2 setup.py install --root="$pkgdir"
	install -Dm0644 LICENSE.PSF "$pkgdir/usr/share/licenses/$pkgname/LICENSE.PSF"
}

sha512sums=('bb33db07b8b86ba8ccec920748cfc207d27eed0b76218cf06c3b1706a1e9350be62a99a92e05c8e683a7c3ab1e232dbb46259380c7e5f2c6b140a3a96a36f8bf')
