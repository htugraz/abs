# $Id: PKGBUILD 144561 2015-10-21 12:30:07Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: TDY <tdy@gmx.com>

pkgname=parole
pkgver=0.8.1
pkgrel=2
pkgdesc="A modern media player based on the GStreamer framework"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/parole/"
license=('GPL')
depends=('gstreamer' 'gst-plugins-base-libs' 'hicolor-icon-theme' 'libnotify>=0.4.1'
         'libxfce4ui>=4.12.0' 'taglib>=1.4' 'desktop-file-utils' 'gst-plugins-good'
         'clutter-gtk')
makedepends=('xfce4-dev-tools')
optdepends=('gst-libav'
	    'gst-plugins-bad'
	    'gst-plugins-base'
	    'gst-plugins-ugly')
install=parole.install
source=(http://git.xfce.org/apps/parole/snapshot/parole-$pkgver.tar.bz2)
md5sums=('5927c57432e457b4b94b8b8177866073')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib \
    --enable-taglib --enable-libnotify --with-gstreamer=1.0 \
    --enable-clutter
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
