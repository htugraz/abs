# $Id: PKGBUILD 112233 2014-05-30 11:54:15Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Roman Kyrylych <roman@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gimmage
pkgver=0.2.3
pkgrel=5
pkgdesc="A gtkmm image viewer"
arch=('i686' 'x86_64')
url="http://gimmage.berlios.de"
license=('GPL')
depends=('gtkmm' 'curl' 'file')
makedepends=('pkgconfig')
changelog=ChangeLog
source=(http://download.berlios.de/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('f51cca53b3d33415e75a91cf81b6ac39')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  rm gimmage.desktop
  sed -i 's|Exec=@prefix@/bin/gimmage|Exec=@prefix@/bin/gimmage %U|' gimmage.desktop.in
  sed -i '1,1i#include <string.h>' src/FileManager.cpp
  sed -i '1,1i#include <unistd.h>' src/FileManager.cpp
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
