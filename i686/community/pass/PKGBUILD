# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Simon Zimmermann <simon@insmo.com>

pkgbase=pass
pkgname=('pass' 'passmenu')
pkgver=1.6.5
pkgrel=1
url='http://zx2c4.com/projects/password-store/'
source=("http://git.zx2c4.com/password-store/snapshot/password-store-${pkgver}.tar.xz")
md5sums=('2c4468360c678184051e76f03c2f6b04')
license=('GPL2')
arch=('any')

package_pass() {
  pkgdesc='Stores, retrieves, generates, and synchronizes passwords securely'
  depends=('xclip' 'bash' 'gnupg' 'pwgen' 'tree')
  optdepends=('git: for Git support')
  provides=(pass)
  conflicts=(pass-git)

  cd "${srcdir}/password-store-$pkgver/"

  make DESTDIR="${pkgdir}" FORCE_ALL=1 install
}

package_passmenu() {
  pkgdesc='A dmenu interface to pass'
  depends=('bash' 'dmenu' 'pass')
  optdepends=('xdotool: for type support')

  cd "${srcdir}/password-store-$pkgver/contrib/dmenu/"

  install -Dm0755 passmenu "${pkgdir}/usr/bin/passmenu"
}
