# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Archan Paul <archan.paul@gmail.com>
# Contributor: aexoxea <digitaleon@internode.on.net>

pkgname=idle3-tools
pkgver=0.9.1
pkgrel=3
pkgdesc='A linux/unix utility that can disable, get and set the value of the idle3 timer found on recent Western Digital Hard Disk Drives.'
arch=(i686 x86_64)
url='http://idle3-tools.sourceforge.net/'
license=(GPL)
depends=(glibc)
source=(http://download.sourceforge.net/idle3-tools/idle3-tools-$pkgver.tgz)
md5sums=('797d8775b80b7b7b67a1f8b0a5b41f30')

prepare() {
  cd idle3-tools-$pkgver
  sed -i 's/\bsbin\b/bin/g' Makefile
}

build() {
  cd idle3-tools-$pkgver
  make
}

package() {
  cd idle3-tools-$pkgver
  make binprefix=/usr DESTDIR=$pkgdir install
}
