# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgbase=python-gmpy2
pkgname=('python2-gmpy2' 'python-gmpy2')
pkgver=2.0.4
pkgrel=1
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic."
arch=('i686' 'x86_64')
url="http://code.google.com/p/gmpy/"
license=('LGPL2.1')
makedepends=('python2' 'python' 'libmpc')
source=("https://pypi.python.org/packages/source/g/gmpy2/gmpy2-${pkgver}.zip")
md5sums=('b5f0e5bbb64fcd1582ddd5079c9a7b73')

package_python2-gmpy2() {
  depends=('python2' 'libmpc')

  cd gmpy2-$pkgver
  python2 setup.py install --root=$pkgdir --optimize=1

  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

package_python-gmpy2() {
  depends=('python' 'libmpc')
  
  cd gmpy2-$pkgver 
  python setup.py install --root=$pkgdir --optimize=1
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

