# $Id: PKGBUILD 144278 2015-10-17 12:39:17Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>
# Contributor: Attila Bukor <r1pp3rj4ck@w4it.eu>

pkgname=guake
pkgver=0.8.1
pkgrel=1
pkgdesc='Drop-down terminal for GNOME'
arch=('any')
url="http://guake-project.org/"
license=('GPL')
depends=('python2-notify' 'vte' 'python2-gconf' 'python2-dbus' 'python2-keybinder2' 'python2-xdg' 'libutempter')
makedepends=('intltool' 'gnome-common')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/Guake/$pkgname/archive/$pkgver.tar.gz)
md5sums=('9158b0121ae603e6c14160882fdd037e')

prepare() {
  cd $pkgname-$pkgver

  # Python2 fix
  sed -i 's|/usr/bin/env python|/usr/bin/python2|' src/guake/prefs.py
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-static --disable-schemas-install \
               --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
               PYTHON=python2
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
