# $Id: PKGBUILD 109430 2014-04-14 20:16:51Z jgc $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgname=gnome-packagekit
pkgver=3.12.1
pkgrel=1
pkgdesc='Collection of graphical tools for PackageKit to be used in the GNOME desktop'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'hicolor-icon-theme' 'libnotify'
         'packagekit')
makedepends=('intltool' 'itstool' 'docbook-xsl' 'libcanberra')
optdepends=('gnome-settings-daemon-updates: update and message notifications')
install="$pkgname.install"
source=("http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        "packagekit-0.8.patch")
sha256sums=('fec804f75d178bf2984828873491733338ed6f549e287afe16d7302da26c5ead'
            '717ba4d281adbafc9409202538ad1c8ce9a2bb41a6a0bc127e8dd5c4c1da1d65')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -RNp1 -i "$srcdir/packagekit-0.8.patch"

  _mime='application/x-xz-compressed-tar;application/x-servicepack;'
  sed -i "s@MimeType=.*@MimeType=$_mime@" data/gpk-install-local-file.desktop.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --disable-schemas-compile \
              PYTHON=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
