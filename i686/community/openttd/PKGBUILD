# $Id: PKGBUILD 121623 2014-10-31 09:28:44Z lcarlier $
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=openttd
pkgver=1.4.4
pkgrel=1
pkgdesc='An engine for running Transport Tycoon Deluxe.'
arch=('i686' 'x86_64')
url='http://www.openttd.org'
license=('GPL')
depends=('libpng' 'sdl' 'icu' 'fontconfig' 'lzo' 'hicolor-icon-theme' 'desktop-file-utils' 'xz')
install=openttd.install
optdepends=('openttd-opengfx: free graphics' 
            'openttd-opensfx: free soundset')
source=("http://binaries.openttd.org/releases/${pkgver}/${pkgname}-${pkgver}-source.tar.xz")
sha256sums=('70f430b883c44f5e62111ea2dfd83f4e1c67835d8cd40dbb73bf89becac2d3f7')

build() {
  cd ${pkgname}-${pkgver} 

 ./configure \
    --prefix-dir=/usr \
    --binary-name=${pkgname} \
    --binary-dir=bin \
    --data-dir=share/${pkgname} \
    --install-dir=${pkgdir} \
    --doc-dir=share/doc/${pkgname} \
    --menu-name="OpenTTD" \
    --personal-dir=.${pkgname}    

  make
}

package() {
  cd ${pkgname}-${pkgver} 

  make install
}
