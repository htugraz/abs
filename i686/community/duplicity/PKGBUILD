# $Id: PKGBUILD 126743 2015-01-29 10:04:51Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=0.7.01
pkgrel=2
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm.'
arch=('i686' 'x86_64')
url='http://www.nongnu.org/duplicity/'
license=('GPL')
depends=('gnupg' 'librsync' 'ncftp' 'python2-paramiko' 'python2-lockfile')
makedepends=('python2-setuptools')
optdepends=('lftp: FTPS backend'
            'python2-boto: Amazon S3 and Google Cloud Storage backend'
            'python2-dropbox: Dropbox backend'
            'python2-gobject: GIO backend'
            'gvfs: GIO backend'
            'python2-gdata: Google Docs backend'
            'rsync: rsync backend')
source=("https://launchpad.net/$pkgname/0.7-series/${pkgver}/+download/$pkgname-$pkgver.tar.gz"{,.sig}
        1058.patch)
md5sums=('de6ce2b7336f45e9b39a3b83030a29f5'
         'SKIP'
         '63a59f9295b9651b91065aa3e623c48b')
validpgpkeys=('9D95920CED4A8D5F8B086A9F8B6F8FF4E654E600')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i ../1058.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python$_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python$_#!/usr/bin/env python2_' $file
  done
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  python2 setup.py install --root="$pkgdir" --optimize=1

  # fix broken documentation permissions until upstream does (FS#27301)
  chmod 644 "$pkgdir/usr/share/man/man1/$pkgname.1"
  chmod 644 "$pkgdir/usr/share/man/man1/rdiffdir.1"
  chmod 644 "$pkgdir/usr/share/doc/$pkgname-$pkgver"/*
}
