# $Id: PKGBUILD 128649 2015-03-03 17:41:21Z ttoepper $
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: J0k3r <moebius282 e4a t gmail D0_T com>
# Contributor: Alois Nespor <alois.nespor@gmail.com>
# Contributor: JerichoKru   <jerichokru(AT)hotmail.com>

pkgname=pragha
pkgver=1.3.2.1
pkgrel=1
pkgdesc="A lightweight GTK+ music manager - fork of Consonance Music Manager."
arch=('i686' 'x86_64')
url="https://pragha-music-player.github.io/"
license=('GPL3')
depends=('libnotify' 'libcdio-paranoia' 'libclastfm' 'gst-plugins-base'
         'taglib' 'hicolor-icon-theme' 'dbus-glib' 'desktop-file-utils'
         'libpeas')
optdepends=('notification-daemon: OSD notification'
            'gst-plugins-good: A collection of gstreamer codecs'
            'gst-plugins-bad: A collection of gstreamer codecs'
            'gst-plugins-ugly: A collection of gstreamer codecs')
makedepends=('intltool' 'xfce4-dev-tools')
replaces=('dissonance')
install=pragha.install
source=("$pkgname-$pkgver.zip::https://github.com/pragha-music-player/pragha/archive/v${pkgver}.zip")
sha1sums=('9dde9db2bd8f09798019fb7ae9378225c74124fb')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh
  CPPFLAGS="-DHAVE_PARANOIA_NEW_INCLUDES" LIBS+="-ldbus-glib-1" \
    ./configure --prefix=/usr \
    --with-gstreamer=1.0
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -m 644 "data/$pkgname.desktop" "$pkgdir/usr/share/applications"
  install -d "$pkgdir/usr/share/pixmaps"
  install -m 644 "data/$pkgname.png" "$pkgdir/usr/share/pixmaps/"
  install -m 644 "data/$pkgname.1" "$pkgdir/usr/share/man/man1/"

}

# vim:ts=2:sw=2:expandtab

