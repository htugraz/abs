# $Id: PKGBUILD 123347 2014-12-02 09:35:39Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-mvtools
pkgver=4
pkgrel=1
pkgdesc='MVTools plugin for VapourSynth'
arch=('i686' 'x86_64')
url='https://github.com/dubhater/vapoursynth-mvtools'
license=('GPL2')
depends=('vapoursynth')
makedepends=('yasm')
source=("vapoursynth-plugin-mvtools-${pkgver}.tar.gz::https://github.com/dubhater/vapoursynth-mvtools/archive/v${pkgver}.tar.gz")
sha256sums=('1671ec4c02a78f76876eb2f2070d9be4ea64db3d030c8cb4337883ee97dc1edd')

build() {
  cd vapoursynth-mvtools-${pkgver}

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib/vapoursynth'
  make
}

package(){
  cd vapoursynth-mvtools-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/share/doc/vapoursynth/plugins/MVTools
  install -m 644 readme.rst "${pkgdir}"/usr/share/doc/vapoursynth/plugins/MVTools/README
}

#vim: ts=2 sw=2 et:
