# $Id: PKGBUILD 100811 2013-11-11 11:10:24Z bpiotrowski $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=catfish
pkgver=0.8.2
pkgrel=1
pkgdesc='Versatile file searching tool'
arch=('any')
url='https://launchpad.net/catfish-search'
license=('GPL')
install=$pkgname.install 
depends=('gtk3' 'python-gobject' 'pyxdg' 'xdg-utils' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('zeitgeist: Zeitgeist integration'
            'findutils: filename search'
            'locate:    filename search'
            'tracker:   fulltext search'
            'strigi:    fulltext search'
            'pinot:     fulltext search')
source=(https://launchpad.net/catfish-search/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.bz2)
md5sums=('813666069e7b51d27722fcc6c5f36bf9')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --python=python2

  # Correct symlinks
  sed -i 's|ln -sf $(DESTDIR)/$(PREFIX)/share|ln -sf ..|' Makefile
  make
}

package () {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/pixmaps
  ln -s /usr/share/icons/hicolor/scalable/apps/$pkgname.svg \
    "$pkgdir"/usr/share/pixmaps/$pkgname.svg

  sed -i '1s/python/python2/' "$pkgdir"/usr/share/catfish/bin/catfish.py \
    "$pkgdir"/usr/share/catfish/catfish_lib/pexpect/FSM.py
}
