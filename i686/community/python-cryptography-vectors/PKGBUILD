# $Id: PKGBUILD 131220 2015-04-12 04:52:37Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cryptography-vectors
pkgname=('python-cryptography-vectors' 'python2-cryptography-vectors')
pkgver=0.8.2
pkgrel=1
pkgdesc="Test vectors for the cryptography package"
arch=('any')
license=('Apache')
url="http://pypi.python.org/pypi/cryptography-vectors"
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/c/cryptography-vectors/cryptography_vectors-${pkgver}.tar.gz")
md5sums=('c7dcbad5b5daf6f7f8b80ac42fb66515')

prepare() {
   cp -a cryptography_vectors-$pkgver{,-python2}
}

package_python-cryptography-vectors() {
   depends=('python')
 
   cd cryptography_vectors-$pkgver
   python3 setup.py install --root="$pkgdir" --optimize=1
}
 
package_python2-cryptography-vectors() {
   depends=('python2')
 
   cd cryptography_vectors-$pkgver-python2
   python2 setup.py install --root="$pkgdir" --optimize=1
}
