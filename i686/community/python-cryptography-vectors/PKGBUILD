# $Id: PKGBUILD 117020 2014-08-08 04:31:28Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-cryptography-vectors
pkgname=('python-cryptography-vectors' 'python2-cryptography-vectors')
pkgver=0.5.3
pkgrel=1
pkgdesc="Test vectors for the cryptography package"
arch=('any')
license=('Apache')
url="http://pypi.python.org/pypi/cryptography-vectors"
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/c/cryptography-vectors/cryptography_vectors-${pkgver}.tar.gz")
md5sums=('481253babcde2f66fcfa323fc06ad79b')

prepare() {
   cp -a cryptography_vectors-${pkgver}{,-python2}
}

package_python-cryptography-vectors() {
   depends=('python')
 
   cd cryptography_vectors-${pkgver}
   python3 setup.py install --root="${pkgdir}" --optimize=1
}
 
package_python2-cryptography-vectors() {
   depends=('python2')
 
   cd cryptography_vectors-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1
}
