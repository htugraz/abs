# $Id: PKGBUILD 73684 2012-07-14 05:00:28Z dwallace $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Byron Clark <byron@theclarkfamily.name>
pkgname=udiskie
pkgver=0.5.0
pkgrel=1
pkgdesc="Removable disk automounter using udisks"
arch=('any')
url="https://pypi.python.org/pypi/udiskie"
license=('MIT')
depends=('udisks' 'python2-dbus' 'python2-gobject2' 'python2-notify')
makedepends=('asciidoc' 'python2-setuptools')
options=(!emptydirs)
replaces=('python2-udiskie')
source=("https://pypi.python.org/packages/source/${pkgname:0:1}/$pkgname/$pkgname-$pkgver-1.tar.gz")
md5sums=('dc20e7d5332383f3865e26dfd7905af8')

package() {
  cd "$srcdir/$pkgname-$pkgver-1"

  python2 setup.py install --root="$pkgdir" --optimize=1

  make -C doc
  install -m 0644 -D "doc/${pkgname}.8" "$pkgdir/usr/share/man/man8/${pkgname}.8"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

}

# vim:set ts=2 sw=2 et:
