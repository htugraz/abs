# $Id: PKGBUILD 130394 2015-03-31 09:21:25Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Julien MISCHKOWITZ <wain@archlinux.fr>
# Contributor: Panagiotis Papadopoulos pano_90 (AT) gmx (DOT) net

pkgname=languagetool
pkgver=2.9
pkgrel=1
pkgdesc="An open source language checker"
arch=('any')
url="http://www.languagetool.org" 
license=("LGPL")
depends=('java-runtime-headless>=7')
makedepends=('unzip')
optdepends=('java-runtime: needed for the GUI version'
            'libxtst: needed for the GUI version'
            'gtk2: needed for the GUI version')
source=($pkgname-$pkgver.zip::"http://www.languagetool.org/download/LanguageTool-stable.zip?$pkgver" "$pkgname.sh")
noextract=($pkgname-$pkgver.zip)
md5sums=('2f4639bf4e0634d1259aad48adb0e1e5'
         '136387ef7979e23004330d553736a89b')

package() {
  cd "$srcdir"
  install -d "$pkgdir"/usr/{bin,share/java/$pkgname}
  unzip -q $pkgname-$pkgver.zip -d "$pkgdir"/usr/share
  cd "$pkgdir"/usr/share/
  mv LanguageTool-$pkgver $pkgname
  cd $pkgname
  mv *.jar "$pkgdir"/usr/share/java/$pkgname
  mv libs/*.jar "$pkgdir"/usr/share/java/$pkgname
  rm -rf libs
  install -m755 "$srcdir"/$pkgname.sh "$pkgdir"/usr/bin/$pkgname
}

