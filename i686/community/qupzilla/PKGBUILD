# $Id: PKGBUILD 96570 2013-09-01 16:36:31Z speps $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=qupzilla
pkgver=1.4.4
pkgrel=1
pkgdesc="Cross-platform QtWebKit browser"
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
depends=('qtwebkit' 'hunspell')
optdepends=('bash-completion: bash completion support')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/QupZilla/$pkgname/tarball/v$pkgver")
md5sums=('cfbf5cd23d184275c389c79a39a28631')

build() {
  cd "$srcdir/QupZilla-$pkgname-"*

  # enable webgl support
  export USE_WEBGL=true

  qmake-qt4 QUPZILLA_PREFIX=/usr/
  make
}

package() {
  cd "$srcdir/QupZilla-$pkgname-"*
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/zsh_completion.sh \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}

# vim:set ts=2 sw=2 et:
