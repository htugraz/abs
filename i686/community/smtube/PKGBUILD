# $Id: PKGBUILD 140340 2015-09-15 23:55:12Z speps $
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Andreas Schnaiter <rc.poison@gmail.com>

pkgname=smtube
pkgver=15.8.0
pkgrel=1
pkgdesc="A small youtube browser"
arch=('i686' 'x86_64')
url="http://www.smtube.org/"
license=('GPL')
#depends=('qt5-script' 'qt5-webkit' 'desktop-file-utils' 'hicolor-icon-theme')
#makedepends=('qt5-tools') # qt5 build does not work
depends=('qtwebkit')
optdepends=('mplayer: play videos with MPlayer'
            'smplayer: play videos with SMPlayer'
            'gnome-mplayer: play videos with GNOME MPlayer'
            'kdemultimedia-dragonplayer: play videos with Dragon Player'
            'totem: play videos with Totem'
            'vlc: play videos with VLC'
            'mpv: play videos with MPV'
            'youtube-dl: download videos')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/smtube/SMTube/$pkgver/smtube-$pkgver.tar.bz2")
md5sums=('e826605b099774a1a779933deab7dfbe')

build() {
  cd $pkgname-$pkgver
  make PREFIX=/usr QMAKE=qmake-qt4 LRELEASE=lrelease-qt4
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir/" install
}
