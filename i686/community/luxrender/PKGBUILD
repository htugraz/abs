# $Id: PKGBUILD 122205 2014-11-08 05:52:10Z foutrelis $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: flixie <69one@gmx.net>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
pkgname=luxrender
pkgver=1.3.1
_pkgver=d0b0e20c47cc
pkgrel=11
pkgdesc="Rendering system for physically correct, unbiased image synthesis"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('boost-libs' 'freeimage' 'openexr' 'libpng' 'libcl' 'libgl' 'fftw')
optdepends=('luxblend25: Blender exporter' 'qt4: Qt GUI' \
            'python: pylux Python interface'
            'opencl-nvidia: OpenCL support for nVidia GPUs' \
            'amdapp-sdk: OpenCL support for AMD GPUs' \
            'intel-opencl-runtime: OpenCL support for Intel CPUs')
# luxrender is constantly broken due to various GCC bugs, the latest one being FS#40596
makedepends=('cmake' 'boost' 'mesa' 'qt4' "luxrays=$pkgver" 'python' 'opencl-headers' 'clang')
source=(https://bitbucket.org/luxrender/lux/get/$_pkgver.tar.bz2 \
        force_python3.diff)
md5sums=('cbe749f56a1e1976745f5458100efa8a'
         '42692e65eabc5828693e2682e94b7c64')

prepare() {
  cd "$srcdir"/luxrender-lux-$_pkgver

  patch -Np1 < "$srcdir/force_python3.diff" || true
  # workaround QTBUG-22829
  find . -type f -exec sed -i 's|^#include .*boost/.*|#ifndef Q_MOC_RUN\n&\n#endif|' '{}' ';'
}

build() {
  cd "$srcdir"/luxrender-lux-$_pkgver

  export CC=clang
  export CXX=clang++

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DLUXRAYS_DISABLE_OPENCL=OFF \
    -DPYTHON_CUSTOM=ON \
    -DPYTHON_LIBRARIES=/usr/lib/libpython3.4m.so \
    -DPYTHON_INCLUDE_PATH=/usr/include/python3.4m/ \
    -DCMAKE_EXE_LINKER_FLAGS=-lpthread \
    .
  make
}

package() {
  cd "$srcdir"/luxrender-lux-$_pkgver
  make DESTDIR="$pkgdir" install

  # fix library path on x86_64
  [ "$CARCH" = "x86_64" ] && mv "$pkgdir"/usr/lib64 "$pkgdir"/usr/lib

  #install pylux
  install -D -m644 pylux.so "$pkgdir"/usr/lib/python3.4/pylux.so
}

# vim:set ts=2 sw=2 et:
