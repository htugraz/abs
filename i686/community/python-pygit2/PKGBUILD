# $Id: PKGBUILD 98636 2013-10-15 23:04:31Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgbase=python-pygit2
pkgname=(python-pygit2 python2-pygit2)
pkgver=0.19.1
pkgrel=1
pkgdesc='Python bindings for libgit2'
url="https://github.com/libgit2/pygit2"
depends=(libgit2)
makedepends=(python python2)
arch=(i686 x86_64)
license=('GPL2')
source=(https://github.com/libgit2/pygit2/archive/v${pkgver}.tar.gz)
md5sums=('98a98d8052a4066ab48b74ebd05c90e1')

package_python-pygit2() {
  cd "$srcdir/pygit2-$pkgver"
  depends+=(python)
  replaces=(python-libgit2)
  conflicts=(python-libgit2)
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pygit2() {
  cd "$srcdir/pygit2-$pkgver"
  depends+=(python2)
  replaces=(python2-libgit2)
  conflicts=(python2-libgit2)
  python2 setup.py install --root="$pkgdir" --optimize=1
}
