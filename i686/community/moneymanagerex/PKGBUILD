# $Id: PKGBUILD 146828 2015-11-14 19:16:46Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=moneymanagerex
pkgver=1.2.3
pkgrel=2
pkgdesc="An easy-to-use personal finance suite"
arch=('i686' 'x86_64')
url="http://www.moneymanagerex.org/"
license=('GPL')
depends=('wxgtk' 'webkitgtk2' 'hicolor-icon-theme')
makedepends=('git' 'boost' 'gettext')
optdepends=('cups: for printing support')
replaces=('mmex')
install=$pkgname.install
source=(git+https://github.com/moneymanagerex/moneymanagerex.git#tag=v${pkgver})
sha256sums=('SKIP')

prepare() {
  cd "${srcdir}"/$pkgname

  git submodule update --init
}

build() {
  cd "${srcdir}"/$pkgname

  ./configure --prefix=/usr --disable-shared --enable-unicode
  make
  make po
}

package() {
  cd "${srcdir}"/$pkgname

  make DESTDIR="${pkgdir}" install
}
