# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-control-center
pkgver=1.8.2
pkgrel=2
pkgdesc="The Control Center for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja' 'dbus-glib' 'dconf' 'desktop-file-utils' 'gsettings-desktop-schemas'
         'gtk2' 'mate-desktop' 'mate-icon-theme' 'mate-menus' 'mate-settings-daemon'
         'marco' 'libgtop' 'libmatekbd' 'libunique' 'librsvg' 'libxss'
         'libxt' 'startup-notification')
makedepends=('docbook2x' 'mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz"
        'remove-segfault-when-more-custom-keybindings-configured.diff')
sha1sums=('581439b1e9d042f6cf5707e041fc521fa3fe63f8'
          'f94b3c5e42b0cc8404c59231389ece3e6d93ca0d')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # https://github.com/mate-desktop/mate-control-center/issues/80
    patch -Np1 -i "${srcdir}/remove-segfault-when-more-custom-keybindings-configured.diff"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --disable-static \
        --disable-update-mimedb
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
