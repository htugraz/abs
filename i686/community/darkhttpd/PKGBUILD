pkgname=darkhttpd
pkgver=1.11
pkgrel=2
pkgdesc="A small and secure static webserver"
arch=('i686' 'x86_64')
url="http://dmr.ath.cx/net/darkhttpd/"
license=('BSD')
depends=()
source=("$url/$pkgname-$pkgver.tar.bz2"
        'darkhttpd.service')
sha256sums=('afad244add43c53010b36ada6ccdf1a854885cb37d6aeff103ad0aa4aed96c51'
            '2ba69fb1e5e11772ac8ec1662eb575a1070eb73311e7c5547883a4cbe1883793')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # install darkhttpd
  install -Dm755 "$srcdir/$pkgname-$pkgver/darkhttpd" "$pkgdir/usr/bin/darkhttpd"

  # install systemd files
  install -Dm644 "$srcdir/darkhttpd.service" "$pkgdir/usr/lib/systemd/system/darkhttpd.service"

  # install license
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n 18 darkhttpd.c > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
