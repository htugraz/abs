pkgname=darkhttpd
pkgver=1.10
pkgrel=2
pkgdesc="A small and secure static webserver"
arch=('i686' 'x86_64')
url="http://dmr.ath.cx/net/darkhttpd/"
license=('BSD')
depends=()
source=("$url/$pkgname-$pkgver.tar.bz2"
        'darkhttpd.service')
sha256sums=('b5a9bcfe6e65a3fc20f96e6badb5da7ba776a792f13fe90015fe9f63b3c2eb63'
            'c0c8885682aca80474986080b5426f73dbf37237d02e3fc9035ac72ba74cdb1b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # install darkhttpd
  install -Dm755 "$srcdir/$pkgname-$pkgver/darkhttpd" "$pkgdir/usr/bin/darkhttpd"

  # install systemd files
  install -Dm644 "$srcdir/darkhttpd.service" "$pkgdir/usr/lib/systemd/system/darkhttpd.service"

  # install license
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n 18 darkhttpd.c > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
