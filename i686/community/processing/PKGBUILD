# $Id: PKGBUILD 143907 2015-10-13 11:44:35Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=processing
pkgver=3.0
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Programming environment for creating images, animations and interactions'
url='http://www.processing.org/'
license=('GPL' 'LGPL')
depends=('libgl' 'libxxf86vm')
makedepends=('gendesk')
options=('!strip')
source_x86_64=("http://download.$pkgname.org/$pkgname-$pkgver-linux64.tgz")
source_i686=("http://download.$pkgname.org/$pkgname-$pkgver-linux32.tgz")
md5sums_x86_64=('330af285d98ceae65f5f182c758942c5')
md5sums_i686=('244a6319d234fd3bc70496e780633598')

prepare() {
  gendesk -n --pkgname="$pkgname" --pkgdesc="$pkgdesc"
}

package() {
  cd "$pkgname-$pkgver"
  
  install -d "$pkgdir/usr/bin/" "$pkgdir/usr/share/$pkgname/"
  cp -r * "$pkgdir/usr/share/$pkgname/"
  ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  ln -s "/usr/share/$pkgname/$pkgname-java" "$pkgdir/usr/bin/$pkgname-java"

  # Desktop icon / application shortcut
  install -Dm644 "lib/icons/pde-128.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
