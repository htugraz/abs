# $Id: PKGBUILD 140038 2015-09-13 15:29:04Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=processing
pkgver=3.0b6 # the latest official stable release, according to upstream
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Programming environment for creating images, animations and interactions'
url='http://www.processing.org/'
license=('GPL' 'LGPL')
depends=('libgl' 'libxxf86vm')
makedepends=('gendesk')
options=('!strip')
source_x86_64=("http://download.$pkgname.org/$pkgname-$pkgver-linux64.tgz")
source_i686=("http://download.$pkgname.org/$pkgname-$pkgver-linux32.tgz")
md5sums_x86_64=('72df3f06a8d86e0c190f4240fd9d91d7')
md5sums_i686=('973c4a4011af6a81e7e7c8a1139dae38')

prepare() {
  gendesk -n --pkgname="$pkgname" --pkgdesc="$pkgdesc"
}

package() {
  cd "$pkgname-$pkgver"
  
  install -d "$pkgdir/usr/bin/" "$pkgdir/usr/share/$pkgname/"
  cp -r * "$pkgdir/usr/share/$pkgname/"
  ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  ln -s "/usr/share/$pkgname/$pkgname-java" "$pkgdir/usr/bin/$pkgname-java"

  # Desktop icon / application shortcut
  install -Dm644 "lib/icons/pde-128.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
