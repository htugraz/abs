# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-file-archiver
pkgver=1.6.1
pkgrel=1
pkgdesc="Archive manipulator for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('bzip2' 'desktop-file-utils' 'gtk2' 'gzip' 'libarchive'
         'mate-file-manager' 'tar')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
optdepends=('arj: for ARJ support'
            'p7zip: for 7zip support'
            'unrar: for RAR extract support'
            'unace: for ACE extract support'
            'unzip: for ZIP extract support'
            'zip: for ZIP support')
options=('!emptydirs')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz"
        fr-rpm-bsdtar.patch)
sha1sums=('53a28d6fb559151c0b8d6f9a658113c77cdb938a'
          '219b05a979bf6f249aaae27964f02345fd81168d')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"    
    # This patch 'depends' on libarchive
    patch -Np1 -i "${srcdir}/fr-rpm-bsdtar.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --disable-scrollkeeper \
        --disable-packagekit
    mate-doc-prepare --force
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
