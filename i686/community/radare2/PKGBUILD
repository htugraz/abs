# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=0.9.9
pkgrel=1
pkgdesc="Open-source tools to disasm, debug, analyze and manipulate binary files"
url="http://radare.org"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/${pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('89844d44a2fb8170b245761ef9f235231338ed1f0a1ecfe18d38c779fe699dc8eee4fda5446f898d24af928aa6b3dc63bbae46890323fa77be262373108b5947')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-syscapstone
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
