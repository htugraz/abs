# $Id: PKGBUILD 99994 2013-10-31 02:53:44Z allan $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=stunnel
pkgver=4.56
pkgrel=3
pkgdesc="A program that allows you to encrypt arbitrary TCP connections inside SSL"
arch=('i686' 'x86_64')
url="http://www.stunnel.org"
license=('GPL')
depends=('openssl')
install=$pkgname.install
source=("ftp://ftp.stunnel.org/stunnel/$pkgname-$pkgver.tar.gz"{,.asc}
        'Makefile.patch'
        'stunnel.tmpfiles.conf')

md5sums=('ac4c4a30bd7a55b6687cbd62d864054c'
         'd68c7dd65d515c678cdde15bbf0ce344'
         'f15398497e10e080c2406d2fc541660c'
         '2e065a39998e57727ee79887bbd1751e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # don't create a certificate...
  patch -p0 < "$srcdir/Makefile.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-ipv6 \
    --disable-libwrap \
    --disable-fips

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  for i in ca.pl importCA.sh; do
    install -Dm755 "tools/$i" "$pkgdir/usr/share/stunnel/$i"
  done

  install -Dm644 tools/stunnel.cnf "$pkgdir/etc/stunnel/stunnel.cnf"
  install -Dm644 "$srcdir/stunnel.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/stunnel.conf"

  # move systemd service into place
  install -dm755 "$pkgdir/usr/lib/systemd/system"
  mv "tools/stunnel.service" "$pkgdir/usr/lib/systemd/system/"

  sed -e "s:/usr/var/lib/stunnel/:/var/run/stunnel:g" \
      -e "s:/usr/etc/stunnel/:/etc/stunnel/:g" \
      -e "s:no\(body\|group\):stunnel:g" \
      -i "$pkgdir/etc/stunnel/stunnel.conf-sample"
}
