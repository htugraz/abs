# $Id: PKGBUILD 119734 2014-09-29 02:38:05Z thestinger $
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: <kang@insecure.ws>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Connor Behan <connor.behan@gmail.com>
# Contributor: henning mueller <henning@orgizm.net>

pkgname=audit
pkgver=2.3.7
pkgrel=1
pkgdesc='Userspace components of the audit framework'
url='http://people.redhat.com/sgrubb/audit'
arch=(i686 x86_64)
depends=(krb5 libcap-ng)
makedepends=(libldap swig linux-headers python2)
license=(GPL)
options=(emptydirs)
backup=(
  etc/libaudit.conf
  etc/audit/audit.rules
  etc/audit/auditd.conf
  etc/audisp/audispd.conf
  etc/audisp/audisp-remote.conf
  etc/audisp/zos-remote.conf
  etc/audisp/plugins.d/af_unix.conf
  etc/audisp/plugins.d/audispd-zos-remote.conf
  etc/audisp/plugins.d/au-remote.conf
  etc/audisp/plugins.d/syslog.conf
)
source=("$url/$pkgname-$pkgver.tar.gz")
sha256sums=('0aa585ec7569825d88d78f4c03ea1e253bdaa0a8205046f9ce4fa1b61fd78f89')
install=$pkgname.install

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=/usr/bin/python2
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/audit \
    --with-python=yes \
    --enable-gssapi-krb5=yes \
    --enable-systemd=yes \
    --with-libcap-ng=yes
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  cd "$pkgdir"
  install -d var/log/audit
  rm -rf etc/rc.d etc/sysconfig usr/lib/audit

  sed -ri 's|/sbin|/usr/bin|' \
    etc/audit/*.conf \
    etc/audisp/plugins.d/*.conf \
    usr/lib/systemd/system/auditd.service

  chmod 644 usr/lib/systemd/system/auditd.service
  mv "$pkgdir"/etc/audit/{rules.d/,}audit.rules
}
