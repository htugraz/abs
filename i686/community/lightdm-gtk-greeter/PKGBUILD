# $Id: PKGBUILD 114606 2014-07-02 08:51:42Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgbase=lightdm-gtk-greeter
pkgname=('lightdm-gtk2-greeter' 'lightdm-gtk3-greeter')
pkgver=1.8.5
pkgrel=1
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgbase}"
license=('GPL3' 'LGPL3')
makedepends=('exo' 'gnome-doc-utils' 'gobject-introspection' 'gtk2' 'gtk3' 'intltool' 'lightdm')
backup=("etc/lightdm/${pkgbase}.conf")
install="${pkgbase}.install"
source=("${url}/${pkgver%.?}/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('22386e787dc64ac372c63cf2cdce47bfa1c791d1cf8b5d3df68df24ecfbf7d68')

prepare() {
  cp -r ${pkgbase}-${pkgver} lightdm-gtk2-greeter
  mv ${pkgbase}-${pkgver} lightdm-gtk3-greeter
}

build() {
  cd lightdm-gtk2-greeter

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' \
              --libexecdir='/usr/lib/lightdm' --disable-static --with-gtk2
    make

  cd ../lightdm-gtk3-greeter

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' \
              --libexecdir='/usr/lib/lightdm' --disable-static
    make
}

package_lightdm-gtk2-greeter() {
depends=('gtk2' 'lightdm')
provides=("${pkgbase}")
conflicts=("${pkgbase}")
  cd lightdm-gtk2-greeter

  make DESTDIR="${pkgdir}" install
}

package_lightdm-gtk3-greeter() {
depends=('gtk3' 'lightdm')
provides=("${pkgbase}")
conflicts=("${pkgbase}")
  cd lightdm-gtk3-greeter

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
