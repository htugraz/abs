# $Id: PKGBUILD 148952 2015-12-06 17:17:32Z foutrelis $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>

pkgname=botan
pkgver=1.11.24
pkgrel=2
pkgdesc='Crypto library written in C++'
license=('BSD')
arch=('x86_64' 'i686')
url='http://botan.randombit.net/'
depends=('gcc-libs' 'sh' 'asio')
makedepends=('python' 'asio' 'git')
source=("http://botan.randombit.net/releases/Botan-${pkgver}.tgz"{,.asc})
#source=("git://github.com/randombit/botan.git#commit=8e19ecf11c")
md5sums=('9281c80751d066bf09d4303223325761'
         'SKIP')
# Botan Distribution Key. To import: gpg --import botan.key
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')

build() {
  cd "${pkgname^}-$pkgver"

  python configure.py \
    --prefix=/usr \
    --destdir="$pkgdir/usr"
  make
}

check() {
  cd "${pkgname^}-$pkgver"

  ./botan-test
}

package() {
  cd "${pkgname^}-$pkgver"

  make DESTDIR="$pkgdir/usr" install
  find "$pkgdir/usr/share/doc" -type f -exec chmod 0644 {} \;
  install -Dm644 doc/license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: botan.randombit.net
# vim:set ts=2 sw=2 et:
