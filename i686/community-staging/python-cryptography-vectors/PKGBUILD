# $Id: PKGBUILD 140904 2015-09-19 15:29:14Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cryptography-vectors
pkgname=('python-cryptography-vectors' 'python2-cryptography-vectors')
pkgver=1.0.1
pkgrel=2
pkgdesc="Test vectors for the cryptography package"
arch=('any')
license=('Apache')
url="http://pypi.python.org/pypi/cryptography-vectors"
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/c/cryptography-vectors/cryptography_vectors-${pkgver}.tar.gz")
md5sums=('3a587ed331b34a4bfdec686f060f8e46')

prepare() {
   cp -a cryptography_vectors-$pkgver{,-python2}
}

package_python-cryptography-vectors() {
   depends=('python')
 
   cd cryptography_vectors-$pkgver
   python3 setup.py install --root="$pkgdir" --optimize=1
}
 
package_python2-cryptography-vectors() {
   depends=('python2')
 
   cd cryptography_vectors-$pkgver-python2
   python2 setup.py install --root="$pkgdir" --optimize=1
}
