# $Id: PKGBUILD 118184 2014-09-02 11:14:17Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgbase=python-gammu
pkgname=('python-gammu' 'python2-gammu')
pkgver=2.4
pkgrel=2
pkgdesc="Python bindings for Gammu library"
arch=('i686' 'x86_64')
url="http://wammu.eu/python-gammu/"
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools' 'gammu')
source=("https://pypi.python.org/packages/source/p/python-gammu/python-gammu-$pkgver.tar.gz")
md5sums=('a396cd7a8944d1133a2c7713a7333a35')

prepare() {
  cp -a python-gammu-$pkgver{,-py2}
}

build() {
  cd "$srcdir/python-gammu-$pkgver"
  python setup.py build

  cd "$srcdir/python-gammu-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/python-gammu-$pkgver"
  python setup.py test || warning "Tests failed"

  cd "$srcdir/python-gammu-$pkgver-py2"
  python2 setup.py test || warning "Tests failed"
}

package_python-gammu() {
  depends=('python' 'gammu')

  cd python-gammu-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-gammu() {
  depends=('python2' 'gammu')

  cd python-gammu-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
