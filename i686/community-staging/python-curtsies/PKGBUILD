# $Id: PKGBUILD 141272 2015-09-20 13:23:35Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-curtsies
pkgname=(python-curtsies python2-curtsies)
_pkgname=curtsies
# the author of this package intends it to be used for bpython only
# so the version is stuck at whatever bpython's release supports
# dear users, do not flag this package out of date
pkgver=0.1.18
pkgrel=2
pkgdesc="Curses-like terminal wrapper with a display based on compositing 2d arrays of text"
arch=('any')
url="https://github.com/thomasballinger/curtsies"
license=('MIT')
depends=('python-blessings')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/c/curtsies/curtsies-${pkgver}.tar.gz")
md5sums=('077cf97028e342abe1cad94ff98fbd66')

prepare() {
  cd "$srcdir"
  cp -r curtsies-$pkgver curtsies2-$pkgver
}

build() {
  cd "$srcdir/curtsies-$pkgver"
  python3 setup.py build

  cd "$srcdir/curtsies2-$pkgver"
  python2 setup.py build
}

package_python-curtsies() {
  cd "$srcdir/curtsies-$pkgver"
  python3 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python-curtsies/LICENSE"
}

package_python2-curtsies() {
  depends=('python2-blessings')
  cd "$srcdir/curtsies2-$pkgver"
  python2 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python2-curtsies/LICENSE"
}
