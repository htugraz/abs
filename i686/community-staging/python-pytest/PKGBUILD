# $Id: PKGBUILD 140709 2015-09-19 09:27:49Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest
pkgname=('python-pytest' 'python2-pytest')
pkgver=2.8.0
pkgrel=2
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py')
checkdepends=('lsof' 'python-nose' 'python2-nose' 'python-twisted' 'python2-twisted'
              'python-mock' 'python2-mock' 'python-yaml' 'python2-yaml' 'python2-enum34'
              'python-pexpect' 'python2-pexpect' 'python-pytest-xdist' 'python2-pytest-xdist')
source=("https://pypi.python.org/packages/source/p/pytest/pytest-$pkgver.zip")
sha512sums=('98f7131c3396848ef7dbcf67e21e0329f8a3b771b3f8b832c1894758b34a2f852209f809001da856cfe8ea3773b257405ae2d5f8a04152242b8692e1511289ed')

prepare() {
  cp -a pytest-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/pytest-${pkgver}"
  python setup.py build

  cd "$srcdir/pytest-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/pytest-${pkgver}"
  python setup.py test

  cd "$srcdir/pytest-${pkgver}-py2"
  python2 setup.py test
}

package_python-pytest() {
  depends=('python' 'python-py' 'python-setuptools')

  cd pytest-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pytest() {
  depends=('python2' 'python2-py' 'python2-setuptools')

  cd pytest-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/py.test" "${pkgdir}/usr/bin/py.test2"
}

