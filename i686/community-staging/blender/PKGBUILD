# $Id: PKGBUILD 111359 2014-05-17 12:31:51Z fyan $
# Contributor: John Sowiak <john@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

# Sometimes blender.org takes some time to release patch releases and because Arch users
# are impatient, we sometimes need to build from git directly.
# Update because I get so many queries on this:
# Due to our other rolling deps, it's sometimes not possible to build Blender stable releases.
# More often than not, a new openshadinglanguage breaks it and I could either backport fixes
# or simply roll with a new version. I usually choose the latter when the former seems
# unreasonable.

_gittag=v2.70a
#_gitcommit=ffffff

pkgname=blender
pkgver=2.70a
[[ -n $_gitcommit ]] && pkgver=2.70.$_gitcommit
pkgrel=3
epoch=14
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.blender.org"
depends=('libpng' 'libtiff' 'openexr' 'python' 'desktop-file-utils'
         'shared-mime-info' 'hicolor-icon-theme' 'xdg-utils' 'glew'
         'freetype2' 'openal' 'ffmpeg' 'fftw' 'boost-libs' 'opencollada' 'llvm'
         'openimageio' 'libsndfile' 'jack' 'opencolorio' 'openshadinglanguage')
makedepends=('cmake' 'boost' 'subversion' 'mesa' 'git')
[[ $CARCH == x86_64 ]] && makedepends+=('cuda')
optdepends=('cuda: cycles renderer cuda support')
options=(!strip)
install=blender.install
source=("git://git.blender.org/blender-addons.git"
        "git://git.blender.org/blender-addons-contrib.git"
        "git://git.blender.org/blender-translations.git"
        "git://git.blender.org/scons.git")
if [[ -n $_gittag ]]; then
    source+=("${pkgname}-${pkgver}::git://git.blender.org/blender.git#tag=${_gittag}")
else
    source+=("${pkgname}-${pkgver}::git://git.blender.org/blender.git#commit=${_gitcommit}")
fi
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')
prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  git submodule init
  git config submodule."release/scripts/addons".url ${srcdir}/blender-addons
  git config submodule."release/scripts/addons_contrib".url ${srcdir}/blender-addons-contrib
  git config submodule."release/datafiles/locale".url ${srcdir}/blender-translations
  git config submodule."scons".url ${srcdir}/scons
  git submodule update
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir build && cd build

  [[ $CARCH == i686 ]] && BUILDCUDA="OFF" || BUILDCUDA="ON"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_INSTALL_PORTABLE=OFF \
    -DWITH_PYTHON_INSTALL=OFF \
    -DWITH_OPENCOLLADA=ON \
    -DOPENIMAGEIO_ROOT_DIR=/usr \
    -DWITH_GAMEENGINE=ON \
    -DWITH_JACK=ON \
    -DWITH_PLAYER=ON \
    -DWITH_CODEC_FFMPEG=ON \
    -DWITH_CODEC_SNDFILE=ON \
    -DWITH_CYCLES=ON \
    -DWITH_LLVM=ON \
    -DLLVM_VERSION=3.4 \
    -DLLVM_STATIC=OFF \
    -DWITH_CYCLES_CUDA_BINARIES=$BUILDCUDA \
    -DWITH_CYCLES_OSL=ON \
    -DWITH_FFTW3=ON \
    -DWITH_MOD_OCEANSIM=ON \
    -DPYTHON_VERSION=3.4 \
    -DPYTHON_LIBPATH=/usr/lib \
    -DPYTHON_LIBRARY=python3.4m \
    -DPYTHON_INCLUDE_DIRS=/usr/include/python3.4m
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR="${pkgdir}" install
  python -m compileall "${pkgdir}/usr/share/blender"
}
