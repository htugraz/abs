# $Id: PKGBUILD 150035 2015-12-07 16:14:46Z foutrelis $
# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
# Contributor: Bogdan Szczurek <thebodzio(at)gmail.com>
# Contributor: Vaclav Kramar <vaclav.kramar@tiscali.cz>
# Contributor: Archie <mymaud@gmail.com>

pkgname=rawtherapee
pkgver=4.2+448.g26d182d
_pkgver=26d182dabc6f133c27bffa3aac3f91b7ae55d578
pkgrel=2
epoch=1
pkgdesc="Raw photo editor"
arch=('i686' 'x86_64')
url="http://www.rawtherapee.com/"
license=('GPL3')
depends=('fftw' 'gtkmm' 'libcanberra' 'libiptcdata' 'lcms2' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('gtk-engines: workaround theme bug under GNOME')
makedepends=('cmake' 'git')
install=rawtherapee.install
#source=("http://rawtherapee.com/shared/source/rawtherapee-${pkgver}.tar.xz")
#md5sums=('e6510ed56fdc35aa712b4c0f54c52ac0')
source=("rawtherapee::git+https://github.com/Beep6581/RawTherapee.git#commit=$_pkgver")
md5sums=('SKIP')

build() {
  mkdir -p "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  cmake "$srcdir/$pkgname" \
    -DCMAKE_CXX_FLAGS="-std=c++11" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
