# $Id: PKGBUILD 107323 2014-03-17 04:20:50Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgbase=pep8
pkgname=("$pkgbase-python2" "$pkgbase-python3")
pkgver=1.4.6
pkgrel=4
pkgdesc="Python style guide checker"
arch=('any')
url="http://github.com/jcrocholl/pep8/"
license=('custom:Expat')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/jcrocholl/$pkgbase/archive/${pkgver}.tar.gz")
sha256sums=('723ec96fc01a1c2d8f9e7d4239a769cd5b499951a1d6c4edc3eb28c6801ed0f8')

build() {
  cp -r $pkgbase-$pkgver{,-python2}
}

package_pep8-python2() {
  depends=('python2' 'python2-setuptools')
  provides=('pep8')
  conflicts=('pep8')
  replaces=('pep8')
  
  cd $pkgbase-$pkgver-python2
  python2 setup.py install --prefix=/usr --root="$pkgdir/"
  mv "$pkgdir/usr/bin/pep8" "$pkgdir/usr/bin/pep8-python2"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pep8-python3() {
  depends=('python3' 'python-setuptools')
  
  cd $pkgbase-$pkgver
  python3 setup.py install --prefix=/usr --root="$pkgdir/"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
