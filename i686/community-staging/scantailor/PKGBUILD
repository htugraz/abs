# $Id: PKGBUILD 149554 2015-12-07 04:53:59Z fyan $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Denis Terskov aka neurosurgeon <terskov.den@gmail.com>

pkgname=scantailor
pkgver=0.9.11.1
pkgrel=5
_realver=EXPERIMENTAL_2015_10_27
pkgdesc="Interactive post-processing tool for scanned pages"
arch=(i686 x86_64)
url="http://scantailor.org/"
license="GPL"
depends=('qt5-base' 'libtiff' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools' 'eigen' 'boost')
install=scantailor.install
source=($pkgname-$_realver.tar.gz::https://github.com/Tulon/$pkgname/archive/$_realver.tar.gz
        scantailor.desktop)
md5sums=('1638dec878e9f35e9c0af1ab222280dd'
         '7fdbdef6167d124762421ec99a98f8de')

build() {
  cd $srcdir/$pkgname-$_realver
  cmake \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	.
  make
}

package() {
  cd $srcdir/$pkgname-$_realver
  make DESTDIR="$pkgdir" install
  install -Dm0644 "resources/appicon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/scantailor.svg"
  install -Dm0644 "$srcdir/scantailor.desktop" "$pkgdir/usr/share/applications/scantailor.desktop"
}
