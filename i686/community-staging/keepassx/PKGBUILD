# $Id: PKGBUILD 149456 2015-12-07 01:08:24Z bpiotrowski $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Greg Bouzakis <gregbouzakis@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Philipp Giebel <arch@ambience-design.net>

pkgname=keepassx
pkgver=0.4.3
pkgrel=7
pkgdesc='Password manager'
arch=('i686' 'x86_64')
url='http://www.keepassx.org'
license=('GPL2')
depends=('libxtst' 'qt4' 'shared-mime-info')
makedepends=('optipng')
install=keepassx.install
source=(https://www.keepassx.org/releases/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('1df67bb22b2e08df49f09e61d156f508')

prepare() {
  cd $pkgname-$pkgver
  sed -i '1,1i#include <unistd.h>' src/lib/random.cpp
  find -name '*.png' -exec optipng -quiet -force -fix {} \;
}

build() {
  cd $pkgname-$pkgver
  qmake-qt4
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
