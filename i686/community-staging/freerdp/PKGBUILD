# $Id: PKGBUILD 120634 2014-10-14 09:54:52Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

pkgname=freerdp
pkgver=1.1.0_beta+2013071101
_pkgver=${pkgver/_/-}
_pkgver=${_pkgver/+/-}
pkgrel=1
pkgdesc="Free RDP client"
arch=('i686' 'x86_64')
url="http://freerdp.sourceforge.net"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
	 'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'libpulse')
makedepends=('krb5' 'cmake' 'damageproto' 'xmlto' 'docbook-xsl')
conflicts=('freerdp-git')
changelog=${pkgname}.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/FreeRDP/archive/${pkgver/_/-}.tar.gz
	ffmpeg2.0.patch
	patch_numblock.patch
	CVE-2014-0791.patch
	stuck_winkey.patch)
md5sums=('37259601bb43bb8f04d65f4ce1057582'
         'ce01b9a60f8c57154de425e20f5884ca'
         'ac581d445ded7cdbd59082a48d9c28ac'
         '62d0ab241c16ee5d85ff89183df9bbc0'
         '1c1a8f77ebcaa7256968a9506ed3e772')

prepare() {
  cd $srcdir/FreeRDP-${_pkgver}
  patch -Np1 -i ../ffmpeg2.0.patch
#  patch -Np1 -i ../patch_numblock.patch
#  patch -Np1 -i ../CVE-2014-0791.patch
#  patch -Np1 -i ../stuck_winkey.patch
}

build() {
  cd $srcdir/FreeRDP-${_pkgver}
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PULSEAUDIO=ON \
	.
  make
}

package() {
  cd $srcdir/FreeRDP-${_pkgver}
  make DESTDIR="${pkgdir}" install
}
