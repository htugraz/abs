# $Id: PKGBUILD 130676 2015-04-04 09:55:36Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.2.2
pkgrel=2
pkgdesc='A general-purpose subtitle editor with ASS/SSA support'
arch=('i686' 'x86_64')
url='http://www.aegisub.org'
license=('GPL' 'BSD')
depends=('boost-libs' 'desktop-file-utils' 'ffms2' 'fftw' 'hunspell' 'wxgtk')
makedepends=('boost' 'intltool' 'mesa')
install='aegisub.install'
source=("http://ftp.aegisub.org/pub/archives/releases/source/aegisub-${pkgver}.tar.xz")
sha256sums=('c55e33945b82d8513c02ea6e782f0d72c726adcd3707e95b8c0022f6151e6885')

build() {
  cd aegisub-${pkgver}

  ./configure \
    --prefix='/usr' \
    --without-{portaudio,openal,oss} \
    --disable-update-checker
  make
}

package() {
  cd aegisub-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub
  install -m 644 LICENCE "${pkgdir}"/usr/share/licenses/aegisub/LICENSE
}

# vim: ts=2 sw=2 et:
