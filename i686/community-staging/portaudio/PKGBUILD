# $Id: PKGBUILD 148849 2015-12-06 11:27:51Z bpiotrowski $
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Bob Finch <w9ya@qrparci.net>

pkgname=portaudio
pkgver=19_20140130
pkgrel=3
pkgdesc='A free, cross-platform, open source, audio I/O library.'
arch=('i686' 'x86_64')
url='http://www.portaudio.com/'
license=('custom')
depends=('gcc-libs' 'jack')
conflicts=('portaudio_cpp')
provides=('portaudio_cpp')
replaces=('portaudio_cpp')
options=('!makeflags')
source=("http://www.portaudio.com/archives/pa_stable_v$pkgver.tgz")
md5sums=('7f220406902af9dca009668e198cbd23')

build() {
  cd "$srcdir/$pkgname"

  ./configure --prefix=/usr --enable-cxx
  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
