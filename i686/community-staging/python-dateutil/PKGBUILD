#/Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
#Contributor: lilydjwg <lilydjwg@gmail.com>

pkgbase=python-dateutil
pkgname=('python-dateutil' 'python2-dateutil')
pkgver=2.4.2
pkgrel=2
pkgdesc="Provides powerful extensions to the standard datetime module"
arch=('any')
license=('custom')
url="https://github.com/dateutil/dateutil"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=("$pkgbase-$pkgver.tar.gz"::"https://github.com/dateutil/dateutil/archive/$pkgver.tar.gz")
#source=(http://pypi.python.org/packages/source/p/python-dateutil/python-dateutil-$pkgver.tar.gz)
md5sums=('b22e2b212813f06955bbb36cada93551')

build() {

  cd $srcdir
  cp -r  dateutil-$pkgver python2-dateutil-$pkgver
}

package_python-dateutil()
{
depends=('python-six')
  cd dateutil-$pkgver
  python3 setup.py install --root=$pkgdir --optimize=1
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

package_python2-dateutil()
{
depends=('python2-six')
  cd $srcdir/python2-dateutil-$pkgver
  python2 setup.py install --root=$pkgdir --optimize=1
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

check() {
  cd dateutil-$pkgver
  python3 updatezinfo.py
  python3 setup.py test

  cd $srcdir/python2-dateutil-$pkgver
  python2 updatezinfo.py
  python2 setup.py test
}
