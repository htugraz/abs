# $Id: PKGBUILD 135254 2015-06-13 03:53:49Z fyan $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>
# Contributor: marc0s <marc0s@fsfe.org>

pkgname=pcsc-perl
pkgver=1.4.13
pkgrel=4
pkgdesc="A Perl Module for PC/SC SmartCard access"
arch=('i686' 'x86_64')
url="http://ludovic.rousseau.free.fr/softwares/pcsc-perl/"
license=('GPL' 'PerlArtistic')
depends=('pcsclite' 'perl')
makedepends=('pkgconfig')
options=('!emptydirs')
source=("http://ludovic.rousseau.free.fr/softwares/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('758ca469b79667fa8c41422df86a088b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
