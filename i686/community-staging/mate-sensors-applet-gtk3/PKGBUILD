# $Id: PKGBUILD 133194 2015-05-10 18:25:30Z flexiondotorg $
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=mate-sensors-applet
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="A MATE Panel applet to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings. (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'gtk3' 'libatasmart' 'libnotify' 'libxnvctrl' 'lm_sensors'
         'mate-panel-gtk3>=1.10')
makedepends=('docbook-xml' 'mate-common' 'rarian' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('ef88898390ca6ea0f6c2aa099bf0b9ef023b7562')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --with-gtk=3.0 \
        --disable-static
  make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
