# $Id: PKGBUILD 137629 2015-07-30 13:57:34Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mihai Militaru <mihai militaru at xmpp dot ro>
# Contributor: carstene1ns <arch carsten-teibes.de>

pkgname=mbedtls
pkgver=2.0.0
pkgrel=2
pkgdesc="Portable cryptographic and SSL/TLS library, aka polarssl"
arch=('i686' 'x86_64')
url="https://tls.mbed.org"
license=('Apache')
provides=('polarssl')
replaces=('polarssl')
conflicts=('polarssl')
#source=("https://polarssl.org/download/$pkgname-$pkgver-gpl.tgz"
source=("https://tls.mbed.org/download/mbedtls-$pkgver-gpl.tgz")
sha1sums=('a456be169003b4644931a90613fdaa0429af06a7')
depends=('glibc')
options=('staticlibs')

# http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/net-libs/polarssl/files/polarssl-1.3.8-ssl_pthread_server.patch
# https://github.com/alucryd/aur-alucryd/blob/master/personal/polarssl/PKGBUILD
# https://aur.archlinux.org/packages/po/polarssl-git/PKGBUILD

prepare() {
  cd "$pkgname-$pkgver"
  # enable flags for non-embedded systems
  sed -i 's|//\(#define MBEDTLS_THREADING_C\)|\1|' include/mbedtls/config.h
  sed -i 's|//\(#define MBEDTLS_THREADING_PTHREAD\)|\1|' include/mbedtls/config.h
  # bug added in 2.0.0
  sed -i 's|/libmbedtls.\*|/libmbed*|' Makefile
}

build() {
  cd "$pkgname-$pkgver"
  LDFLAGS+=" -I../include " make SHARED=1 no_test
}

check() {
  cd "$pkgname-$pkgver"
  make SHARED=1 check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/usr" install
}
