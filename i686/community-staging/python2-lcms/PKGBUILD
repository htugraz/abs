# $Id: PKGBUILD 66130 2012-02-23 01:39:55Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=python2-lcms
pkgver=1.19
_mver=1.19
pkgrel=6
pkgdesc="LittleCMS Python bindings"
arch=(i686 x86_64)
license=('CUSTOM')
depends=('libtiff' 'python2' 'lcms')
url="http://www.littlecms.com"
source=(http://downloads.sourceforge.net/project/lcms/lcms/${_mver}/lcms-$pkgver.tar.gz)
md5sums=('8af94611baf20d9646c7c2c285859818')

build() {
  cd $srcdir/lcms-${_mver}
  ./configure --prefix=/usr --with-python
  make
}

package() {
  cd $srcdir/lcms-${_mver}
  make DESTDIR=$pkgdir install
  install -D -m0644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  rm -rf $pkgdir/usr/bin \
	 $pkgdir/usr/include \
	 $pkgdir/usr/lib/pkgconfig \
	 $pkgdir/usr/lib/liblcms.* \
	 $pkgdir/usr/share/man/man1/icc2ps.1 \
	 $pkgdir/usr/share/man/man1/icclink.1 \
	 $pkgdir/usr/share/man/man1/jpegicc.1 \
	 $pkgdir/usr/share/man/man1/tifficc.1 \
	 $pkgdir/usr/share/man/man1/wtpt.1
}
