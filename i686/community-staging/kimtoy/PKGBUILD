# $Id: PKGBUILD 119969 2014-10-01 13:37:00Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: CSSlayer <wengxt@gmail.com>

pkgname=kimtoy
pkgver=1.9.2
pkgdesc="Standalone input method panel"
pkgrel=3
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/KIMToy?content=140967"
license=('GPL')
makedepends=('cmake' 'automoc4' 'fcitx' 'scim' 'dbus-c++') #ibus) ibus support not working due to upstream ABI change
depends=('kdelibs')
optdepends=('fcitx: fcitx support'
            'scim: scim support'
            'dbus-c++: scim support')
            #'ibus: ibus support') ibus support not working due to upstream ABI change
install=$pkgname.install
source=(http://kde-apps.org/CONTENT/content-files/140967-kimtoy-${pkgver}.tar.bz2)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  mv "$pkgdir/usr/share/locale" "$srcdir/tmp_locale"
  mkdir -p "$pkgdir/usr/share/locale"
  mv "$srcdir/tmp_locale" "$pkgdir/usr/share/locale/kde4"
}

md5sums=('94f09a33940d189059f9620427689af5')
