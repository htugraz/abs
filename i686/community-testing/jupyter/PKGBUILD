# $Id: PKGBUILD 138381 2015-08-15 11:46:54Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=jupyter
pkgname=(jupyter ipython2-notebook)
pkgver=4.0.2
pkgrel=2
pkgdesc="A language-agnostic web-based interactive shell/notebook server."
arch=('any')
url="http://jupyter.org/"
license=('BSD')
depends=('ipython' 'python-setuptools'
         'python-jinja' 'python-pyzmq' 'python-tornado' 'python-jsonschema'
         'python-terminado' 'python-mistune' 'python-pygments')
makedepends=('python-setuptools' 'python2-setuptools' 'npm' 'git')
optdepends=('python-pyqt5: qtconsole'
            'python-sip: qtconsole')
source=("jupyter_core.tgz::https://github.com/jupyter/jupyter_core/archive/$pkgver.tar.gz"
        "ipykernel.tgz::https://github.com/ipython/ipykernel/archive/4.0.3.tar.gz"
        "ipywidgets.tgz::https://github.com/ipython/ipywidgets/archive/4.0.2.tar.gz"
        "nbconvert.tgz::https://github.com/jupyter/nbconvert/archive/4.0.0.tar.gz"
        "nbformat.tar.gz::https://github.com/jupyter/nbformat/archive/4.0.0.tar.gz"
        "jupyter_console.tgz::https://github.com/jupyter/jupyter_console/archive/4.0.0.tar.gz"
        "jupyter_client.tgz::https://github.com/jupyter/jupyter_client/archive/4.0.0.tar.gz"
        "notebook.tgz::https://github.com/jupyter/notebook/archive/4.0.1.tar.gz"
        "https://pypi.python.org/packages/source/q/qtconsole/qtconsole-4.0.0.tar.gz")
md5sums=('ae0d0197c4febf43c050a97ac6277263'
         '690f424a89e38a5559802bb58223142e'
         '1ced3f6b0893f7a43c6392252c0f80ab'
         '9661620b1e10a7b46f314588d2d0932f'
         '7cf61359fa4e9cf3ef5e969e2fcb909e'
         'c34deb24f1c14208b2cb4c60f346d298'
         '00fa63c67cb3adf359d09dc4d803aff5'
         '1b66b9074df8e3d9d53ee00dde31cea1'
         '1e26773423044dba4ec138f1725d5582')

# notebook optdeps ?
# 'haskell-pandoc: ipython notebook conversion'
# 'texlive-bin: notebook pdf export'

prepare() {
  cd "$srcdir"
  cp -r jupyter_core-$pkgver python2-jupyter_core-$pkgver
  cp -r jupyter_client-4.0.0 python2-jupyter_client-4.0.0
  cp -r ipykernel-4.0.3      python2-ipykernel-4.0.3
  cp -r qtconsole-4.0.0      python2-qtconsole-4.0.0
}

build_jupyter() {
  cd "$srcdir"
}

build_ipython2-notebook() {
  cd "$srcdir"
}

package_jupyter() {
  provides=('ipython-notebook')
  #replaces=('ipython-notebook')

  cd "$srcdir/jupyter_core-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipykernel-4.0.3"
  # this is in the ipython package
  #python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipywidgets-4.0.2"
  # needs node
  # not sure why this is broken
  # weird read perms error on ~/.config/configstore/bower-github.yml ?
  # multiple conflicting versions of bower ?
  # relative paths inside 'less' ?
  #python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/notebook-4.0.1"
  # needs node
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/nbconvert-4.0.0"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/nbformat-4.0.0"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/jupyter_console-4.0.0"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/jupyter_client-4.0.0"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/qtconsole-4.0.0"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir"
  install -Dm644 "jupyter_core-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_ipython2-notebook() {
  pkgdesc="An enhanced Interactive Python2 shell."
  depends=('ipython2' 'jupyter')

  cd "$srcdir/python2-ipykernel-4.0.3"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-jupyter_core-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-jupyter_client-4.0.0"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-qtconsole-4.0.0"
  # needed for spyder2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$pkgdir/usr/bin"
  mv jupyter jupyter-py2
  mv jupyter-kernelspec jupyter-kernelspec-py2
  mv jupyter-migrate jupyter-migrate-py2
  mv jupyter-qtconsole jupyter-qtconsole-py2

  cd "$srcdir"
  install -d "$pkgdir/usr/share/jupyter/kernels/python2"
  echo '{"display_name": "IPython 2", "language": "python2", "argv": ["python2", "-c", "from IPython.kernel.zmq.kernelapp import main; main()", "-f", "{connection_file}"],"codemirror_mode": {"version": 2, "name": "ipython2"}}' > "$pkgdir/usr/share/jupyter/kernels/python2/kernel.json"

  install -Dm644 "jupyter_core-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

