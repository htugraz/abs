# $Id: PKGBUILD 136919 2015-07-14 16:51:10Z bpiotrowski $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: cdhotfire <cdhotfire@gmail.com>

pkgname=rawstudio
pkgver=2.0
pkgrel=14
pkgdesc="An open source raw-image converter written in GTK+"
arch=('i686' 'x86_64')
license=('GPL')
url="http://rawstudio.org/"
depends=('gconf' 'lcms' 'exiv2' 'fftw' 'lensfun' 'flickcurl' 'sqlite'
	 'libgphoto2' 'gtk-engines' 'desktop-file-utils' 'icu')
install=${pkgname}.install
source=(https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz
	libpng15.patch
	lensfun03.patch)
md5sums=('b2f86b8ca6b83ad954e3104c4cb89e9b'
         '2f8b7acbe10d8096ce613cbea7986fd3'
         'a606b05fa260a7afec791f8db92d9a5e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../libpng15.patch
  patch -Np1 -i ../lensfun03.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export LDFLAGS="$LDFLAGS `pkg-config --libs gthread-2.0`"
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  mkdir $pkgdir/usr/lib/rawstudio
  mv $pkgdir/usr/share/rawstudio/plugins $pkgdir/usr/lib/rawstudio
  ln -s ../../lib/rawstudio/plugins $pkgdir/usr/share/rawstudio/plugins
}
