# $Id: PKGBUILD 124725 2014-12-27 16:43:09Z anatolik $
# Maintainer: Anatol Pomozov
# Contributor: Byron Clark <byron@theclarkfamily.name>

# This package contains only thrift compiler and C/C++ libraries
# for all the language binding one should use corresponding language-specific package manager:
# CPAN for perl, gem for ruby, pip for python etc... Or better to create arch packages that install
# the binding from the language specific managers python2-thrift, perl-thrift, ruby-thrift, ...

pkgname=thrift
pkgver=0.9.2
pkgrel=3
pkgdesc='Scalable cross-language services framework for IPC/RPC'
arch=(i686 x86_64)
url='http://thrift.apache.org/'
license=(APACHE)
depends=(gcc-libs openssl)
makedepends=(qt4 boost libevent emacs-nox)
optdepends=(
  'qt4: TQTcpServer support'
)
source=(http://www.apache.org/dist/thrift/$pkgver/thrift-$pkgver.tar.gz{,.asc}
        fix_cglib_compile_options.patch)
sha1sums=('02f78b158da795ea89a26ce41964fbe562cc4235'
          'SKIP'
          '96650c499ab45824be04070d868114205fb4538c')
validpgpkeys=(9782694B8B54B4ADD345E52ABB06368F66B778F9)

prepare() {
  cd thrift-$pkgver
  patch -p1 < ../fix_cglib_compile_options.patch
}

build() {
  cd thrift-$pkgver

  ./configure --prefix=/usr \
              --with-cpp \
              --with-c_glib \
              --with-qt4 \
              --with-libevent \
              --with-csharp \
              --without-haskell \
              --without-php \
              --without-ruby \
              --without-python \
              --without-erlang \
              --without-perl \
              --without-java \
              --without-c_sharp \
              --without-d \
              --without-php \
              --without-go \
              --without-lua \
              --without-nodejs
              
  make

  emacs -Q --batch -f batch-byte-compile contrib/thrift.el
}

check() {
  cd thrift-$pkgver
  make check
}

package() {
  cd thrift-$pkgver
  make DESTDIR="$pkgdir" install

  install -m0644 -D contrib/thrift.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/thrift.vim
  install -m0644 -D contrib/thrift.el "$pkgdir"/usr/share/emacs/site-lisp/thrift.el
  install -m0644 -D contrib/thrift.elc "$pkgdir"/usr/share/emacs/site-lisp/thrift.elc
}
