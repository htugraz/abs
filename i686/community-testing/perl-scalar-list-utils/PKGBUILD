# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-scalar-list-utils
pkgver=1.38
pkgrel=2
pkgdesc='Common Scalar and List utility subroutines'
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/Scalar-List-Utils
source=("http://search.cpan.org/CPAN/authors/id/P/PE/PEVANS/Scalar-List-Utils-$pkgver.tar.gz")
md5sums=(cae0bff5dea8a14962229369edc02bac)
sha512sums=(4c494ff2959ebccf5cf1799ccdf4878fd9af7a488c4ccd7e192aad2ead2c1979eb91a248b3ac80458c796020ea9d97e4b531aac6a7f75a7e296d45a42855c30d)
_ddir=Scalar-List-Utils-$pkgver

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
