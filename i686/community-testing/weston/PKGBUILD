# $Id: PKGBUILD 134682 2015-06-03 07:33:33Z seblu $
# Maintainer: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=weston
pkgver=1.8.0
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor'
arch=('i686' 'x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('libxkbcommon' 'libinput' 'libunwind' 'poppler-glib' 'mtdev' 'libxcursor' 'glu' 'pango' 'colord')
source=("http://wayland.freedesktop.org/releases/$pkgname-$pkgver.tar.xz")
sha1sums=('d49e06e8dfbef9bcba8589a20b5bfa2dddc4a7bd')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/weston \
		--enable-libinput-backend \
		--enable-demo-clients-install
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	# license
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
