# $Id: PKGBUILD 141841 2015-09-28 10:46:19Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_pypiname=dnslib
pkgbase=python-$_pypiname
pkgname=(python-$_pypiname python2-$_pypiname)
pkgver=0.9.4
pkgrel=2
pkgdesc="Simple library to encode/decode DNS wire-format packets"
arch=('any')
url=("https://bitbucket.org/paulc/dnslib")
license=('custom:BSD')
makedepends=('python' 'python2')
source=("https://pypi.python.org/packages/source/d/$_pypiname/$_pypiname-${pkgver}.tar.gz")
md5sums=('8a8fb0a228dc1b6e3d058e8396d02430')

prepare() {
  cp -a ${_pypiname}-${pkgver} python2-${_pypiname}-${pkgver}
}

package_python-dnslib() {
  depends=("python")

  cd ${_pypiname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
}

package_python2-dnslib() {
  depends=("python2")

  cd python2-${_pypiname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
