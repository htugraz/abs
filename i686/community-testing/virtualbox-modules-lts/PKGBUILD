# $Id: PKGBUILD 140629 2015-09-17 22:52:59Z seblu $
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sébastien Luttringer

pkgbase=virtualbox-modules-lts
pkgname=('virtualbox-host-modules-lts' 'virtualbox-guest-modules-lts')
pkgver=5.0.4
pkgrel=2
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-lts>=3.14.42' 'linux-lts<3.15'
             'linux-lts-headers>=3.14.42' 'linux-lts-headers<3.15'
             "virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver")

# remember to also adjust the .install files and the package deps below
_extramodules=extramodules-3.14-lts

build() {
  _kernver=$(cat /usr/lib/modules/$_extramodules/version)
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
  # build guest modules
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-host-modules-lts(){
  _kernver=$(cat /usr/lib/modules/$_extramodules/version)
  pkgdesc='Host kernel modules for VirtualBox'
  depends=('linux-lts>=3.14.40' 'linux-lts<3.15')
  replaces=('virtualbox-modules-lts')
  conflicts=('virtualbox-modules-lts')
  provides=("virtualbox-host-modules=$pkgver")
  install=virtualbox-host-modules-lts.install

  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

package_virtualbox-guest-modules-lts(){
  _kernver=$(cat /usr/lib/modules/$_extramodules/version)
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=('linux-lts>=3.14.40' 'linux-lts<3.15')
  replaces=('virtualbox-archlinux-modules-lts')
  conflicts=('virtualbox-archlinux-modules-lts')
  provides=("virtualbox-guest-modules=$pkgver")
  install=virtualbox-guest-modules-lts.install

  cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

# vim:set ts=2 sw=2 et:

