# $Id: PKGBUILD 139085 2015-09-01 21:26:07Z anatolik $
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: David Rosenstrauch <darose@darose.net>
# Contributor: Erik Mank <erik@braindisorder.org>

pkgname=tt-rss
pkgver=r7807
pkgrel=1
epoch=1
pkgdesc='Web-based news feed (RSS/Atom) aggregator'
arch=('any')
url='http://tt-rss.org/redmine/'
license=('GPL')
depends=('php')
optdepends=('mysql'
            'postgresql'
            'php-gd: for coloured feed badges')
makedepends=('git')
install=tt-rss.install
_commit=2606a4e3aec72d8d0d8e5170c9ee2dfea25b9111
# periodic releases are over https://tt-rss.org/forum/viewtopic.php?f=10&t=3262
source=(git+https://tt-rss.org/git/tt-rss.git#commit=$_commit
        service)
sha1sums=('SKIP'
          'e3277cd1d717e7803cdc41a0275dcc7c188ef8db')

pkgver() {
  cd tt-rss
  echo r$(git rev-list --count HEAD)
}

package() {
  cd tt-rss
  _instdir="$pkgdir"/usr/share/webapps/$pkgname

  install -d "$_instdir"
  cp -ra * "$_instdir/"
  rm -rf "$_instdir"/debian

  install -d "$pkgdir"/etc/webapps/tt-rss
  install -gm640 -g http config.php-dist "$pkgdir"/etc/webapps/tt-rss/config.php-dist
  ln -s /etc/webapps/tt-rss/config.php "$_instdir"/config.php

  install -d "$pkgdir"/var/lib/tt-rss
  mv "$_instdir"/{lock,feed-icons,cache} "$pkgdir"/var/lib/tt-rss
  ln -s /var/lib/tt-rss/lock "$_instdir"/lock
  ln -s /var/lib/tt-rss/feed-icons "$_instdir"/feed-icons
  ln -s /var/lib/tt-rss/cache "$_instdir"/cache
  chown -R 33:33 "$pkgdir"/var/lib/tt-rss
  chmod -R 777 "$pkgdir"/var/lib/tt-rss/*

  install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/tt-rss.service
}
