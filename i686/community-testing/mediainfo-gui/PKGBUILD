# $Id: PKGBUILD 117154 2014-08-11 11:55:24Z bluewind $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: hydro <hydro@freenet.de>

pkgname=mediainfo-gui
pkgver=0.7.69
pkgrel=2
pkgdesc="GUI for mediainfo"
arch=('i686' 'x86_64')
url="http://mediainfo.sourceforge.net"
license=('GPL')
depends=('libmediainfo' 'wxgtk' 'libsm' 'desktop-file-utils')
makedepends=('imagemagick')
install=mediainfo-gui.install
source=(http://downloads.sourceforge.net/mediainfo/mediainfo_${pkgver}.tar.bz2)
md5sums=('ec6fd980cc02f072c8671bdef158e6eb')

build() {
  cd MediaInfo/Project/GNU/GUI
  sh ./autogen
  ./configure --prefix=/usr
  make
}

package() {
  cd MediaInfo/Project/GNU/GUI
  make DESTDIR="$pkgdir" install
  _iconsize=`identify $srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png | grep -oP ' \d+x\d+ ' | tr -d ' '`
  install -D -m 644 "$srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png" \
                    "$pkgdir/usr/share/icons/hicolor/${_iconsize}/apps/mediainfo.png"
  install -D -m 644 "$srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png" \
                    "$pkgdir/usr/share/pixmaps/mediainfo-gui.png"
  install -D -m 644 "$srcdir/MediaInfo/Project/GNU/GUI/mediainfo-gui.desktop" \
                    "$pkgdir/usr/share/applications/mediainfo-gui.desktop"
}
