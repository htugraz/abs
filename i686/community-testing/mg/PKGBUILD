# $Id: PKGBUILD 140218 2015-09-15 01:58:32Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Hannes Rist <hrist@phreeknet.org>

pkgname=mg
pkgver=20150316
pkgrel=2
pkgdesc="Micro GNU/emacs, this is a portable version of the mg maintained by the OpenBSD team."
arch=('i686' 'x86_64')
url="http://homepage.boetes.org/software/mg/"
depends=('ncurses' 'libbsd')
makedepends=('libclens')
license=('custom')
source=(http://homepage.boetes.org/software/mg/mg-$pkgver.tar.gz
	cleanup.patch)
md5sums=('9e6211b8aa714143919fa0f61da65acc'
         '545eac98585e2f5fe0781093ddc07b36')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/cleanup.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make prefix=/usr mandir=/usr/share/man
}

package() {
  cd $srcdir/$pkgname-$pkgver

  mkdir -p $pkgdir/usr/bin
  make install INSTALL=install prefix=/usr mandir=/usr/share/man DESTDIR=$pkgdir BINDIR=/usr/bin

  mkdir -p $pkgdir/usr/share/
  install -D -m0644 README $pkgdir/usr/share/licenses/$pkgname/README
}
