# $Id: PKGBUILD 103070 2013-12-25 21:35:35Z andyrtr $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=mailutils
pkgver=2.2
pkgrel=10
pkgdesc="MUA command line tool (mailx)"
arch=(i686 x86_64)
url="http://www.gnu.org/software/mailutils/"
license=('GPL')
depends=('libldap' 'pam' 'gnutls')
makedepends=('emacs')
install=mailutils.install
options=(zipman !emptydirs)
source=(http://ftp.gnu.org/gnu/mailutils/mailutils-$pkgver.tar.bz2
	build-fix.patch)
md5sums=('834d2f616b922856127ecfe6c2de7d1a'
         'e83709ffdcdb257dfab443ef77c89574')

prepare() {
  cd "$srcdir/mailutils-$pkgver"
  patch -p1 <$srcdir/build-fix.patch
}

build() {
  cd "$srcdir/mailutils-$pkgver"
  ./configure --prefix=/usr --with-gdbm --with-gnutls \
	--libexecdir=/usr/lib/$pkgname --without-fribidi \
	--without-tcp-wrappers --sbindir=/usr/bin \
	--without-python --without-guile
  make -j1
}

package() {
  cd "$srcdir/mailutils-$pkgver"
  make -j1 DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/info/dir
  mv $pkgdir/usr/bin/mail $pkgdir/usr/bin/gnu-mail
  mv $pkgdir/usr/share/man/man1/mail.1 $pkgdir/usr/share/man/man1/gnu-mail.1
  chown -R root:root $pkgdir
}
