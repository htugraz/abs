# $Id: PKGBUILD 110325 2014-04-28 04:21:41Z td123 $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=hedgewars
pkgver=0.9.20.5
pkgrel=2
pkgdesc="turn based strategy, artillery, action and comedy game, featuring the
 antics of pink hedgehogs with attitude as they battle from the depths of hell
 to the depths of space."
arch=('i686' 'x86_64')
url="http://hedgewars.org"
license=('GPL' 'custom')
depends=('qt4' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'lua51' 'ffmpeg' 'glut')
makedepends=('fpc' 'cmake'
	     # haskell specific
	     'ghc'
	     'haskell-network'
	     'haskell-bytestring-show'
	     'haskell-utf8-string'
	     'haskell-dataenc'
	     'haskell-hslogger'
	     'haskell-vector'
	     'haskell-random')
source=(http://download.gna.org/hedgewars/hedgewars-src-$pkgver.tar.bz2
        hedgewars.png
        'fix-ghc-7.8-build-failure.diff')
md5sums=('c61eb01466e86da656e1e74ad70a3217'
         'eeb14d50df39063549ac5eca9dbc65d1'
         '84a85365cbbf7a1aabe4d0f37bd5f03f')

build() {
  cd $pkgname-src-*

  patch -Np1 -i ${srcdir}/fix-ghc-7.8-build-failure.diff

  cmake \
	-DCMAKE_BUILD_TYPE="Release" \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DDATA_INSTALL_DIR=/usr/share/hedgewars \
	-DNOSERVER=0 .

  make
}

package() {
  cd $pkgname-src-*

  make DESTDIR=$pkgdir install

  install -D -m644 Fonts_LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/Fonts_LICENSE.txt
  install -D -m644 $srcdir/hedgewars.png $pkgdir/usr/share/pixmaps/hedgewars.png
  install -D -m644 share/hedgewars/Data/misc/hedgewars.desktop $pkgdir/usr/share/applications/hedgewars.desktop
}
