# $Id: PKGBUILD 141812 2015-09-28 10:45:26Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-betamax-matchers
pkgname=('python-betamax-matchers' 'python2-betamax-matchers')
pkgver=0.2.0
pkgrel=2
pkgdesc="Experimental set of Matchers for Betamax that may possibly end up in the main package"
arch=('any')
license=('GPL')
url='https://github.com/sigmavirus24/betamax_matchers'
makedepends=('python-setuptools' 'python2-setuptools' 'python-betamax' 'python2-betamax'
             'python-requests-toolbelt' 'python2-requests-toolbelt' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("git+https://github.com/sigmavirus24/betamax_matchers.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a betamax_matchers{,-py2}
}

check() {
  cd "$srcdir/betamax_matchers"
  python setup.py ptr

  cd "$srcdir/betamax_matchers-py2"
  python2 setup.py ptr
}

package_python-betamax-matchers() {
  depends=('python-requests-toolbelt' 'python-betamax')

  cd "${srcdir}/betamax_matchers"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-betamax-matchers() {
  depends=('python2-requests-toolbelt' 'python2-betamax')

  cd "${srcdir}/betamax_matchers-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
