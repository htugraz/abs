# $Id: PKGBUILD 140240 2015-09-15 01:59:02Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=pinfo
pkgver=0.6.10
pkgrel=4
pkgdesc="A hypertext info file viewer"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/pinfo/"
license=('GPL')
depends=('ncurses' 'readline')
#source=(https://alioth.debian.org/frs/download.php/file/3351/$pkgname-$pkgver.tar.bz2)
source=(http://arch.p5n.pp.ru/~sergej/dl/2014/pinfo-$pkgver.tar.bz2)
md5sums=('fe3d3da50371b1773dfe29bf870dbc5b')

build() {
  cd $srcdir/$pkgname-$pkgver
  unset LDFLAGS
  [ -x configure ] || ./autogen.sh
  sed -i 's|nogroup|nobody|' src/pinforc.in src/utils.c
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --enable-cursor \
	--mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/info/dir
}
