# $Id: PKGBUILD 110291 2014-04-28 04:21:07Z td123 $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=haddock
pkgver=2.14.2
pkgrel=1
pkgdesc='Tool for generating documentation for Haskell libraries'
url='http://hackage.haskell.org/package/haddock/'
license=('custom:BSD3')
arch=('x86_64' 'i686')
makedepends=('alex' 'happy')
depends=('ghc' 'haskell-xhtml' 'haskell-ghc-paths')
install="$pkgname.install"
source=("http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('33df230cd8baeed87caa31fc13257a921778b6b700c78bd9237024e08d942641')
options=('staticlibs')

build() {
  cd "$pkgname-$pkgver"

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/"$pkgname" \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$pkgname-$pkgver"
  
  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh \
    "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s "/usr/share/doc/$pkgname/html" \
    "$pkgdir/usr/share/doc/ghc/html/libraries/$pkgname"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
  mv "$pkgdir/usr/bin/haddock" "$pkgdir/usr/bin/haddock-cabal"
}

# vim:set ts=2 sw=2 et:
