# $Id: PKGBUILD 150817 2015-12-08 19:42:52Z foutrelis $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: ViNS <gladiator@fastwebnet.it>
# Contributor: Daenyth
pkgname=nethogs
pkgver=0.8.0
pkgrel=5
pkgdesc="A net top tool which displays traffic used per process instead of per IP or interface"
url="http://nethogs.sourceforge.net"
license=('GPL')
depends=('libpcap' 'ncurses' 'gcc-libs')
arch=('i686' 'x86_64')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz gcc44.patch)
md5sums=('d6fb12b46e80a50c9b9f91dd48e2b234'
         '62eb824fbbadd1c60d0749593c16d293')

build() {
  cd "$srcdir/$pkgname"

  sed -i 's/sbin/bin/g' Makefile

  # Patch the code to work with gcc 4.4
  patch -Np1 -i "$srcdir/gcc44.patch"

  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/usr" install
}
# vim:set ts=2 sw=2 et:
