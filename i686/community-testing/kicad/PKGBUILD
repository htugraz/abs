# $Id: PKGBUILD 103568 2014-01-08 00:47:21Z eric $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=20130518
_pkgver=${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}
_pkgbzr=4017
pkgrel=2
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
url="http://iut-tice.ujf-grenoble.fr/kicad/"
license=('GPL')
depends=('glu' 'wxgtk' 'hicolor-icon-theme' 'desktop-file-utils' 'libsm')
makedepends=('cmake' 'zlib' 'mesa')
optdepends=('kicad-docs-bzr: for documentation' 
            'kicad-library-bzr: for footprints')
install=kicad.install
source=(http://iut-tice.ujf-grenoble.fr/cao/${pkgname}-sources-stable_${_pkgver}_BZR${_pkgbzr}.zip
        kicad-boost-polygon-declare-gtlsort-earlier.patch
        wxgtk3.0.patch)
md5sums=('9e6aab0f2ad01a17c8ec96cd9162dc79'
         'a2c39704238946e74a5ed0c38326345f'
         'ec3e7581c814ca2b76becc7b30a8529f')

build() {
  cd "$srcdir/stable_${_pkgver}_BZR${_pkgbzr}"

  patch -p0 < "$srcdir/kicad-boost-polygon-declare-gtlsort-earlier.patch"
  patch -p1 < "$srcdir/wxgtk3.0.patch"

  # harmless build blocking error
  # see https://lists.launchpad.net/kicad-developers/msg07841.html
  # for proper fix
  sed -i 's|^#.*error "You must use|//&|' include/common.h

  # FS#31559
  sed -i 's/-O2/-O1/' CMakeLists.txt

  # build and install kicad
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DKICAD_STABLE_VERSION=ON    \
              -DCMAKE_BUILD_TYPE=Release   \
              -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/stable_${_pkgver}_BZR${_pkgbzr}/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"
}

