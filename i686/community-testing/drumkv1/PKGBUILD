# $Id: PKGBUILD 150493 2015-12-08 19:34:08Z foutrelis $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=drumkv1
pkgver=0.7.1
pkgrel=2
pkgdesc="An old-school drum-kit sampler"
arch=(i686 x86_64)
url="http://$pkgname.sourceforge.net/$pkgname-index.html"
license=('GPL')
groups=('lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt4' 'shared-mime-info')
install="$pkgname.install"
source=("http://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('e037552988d30a0010c318e55f0d41f0')

prepare() {
  cd $pkgname-$pkgver

  # x86_64 lib path fix
  sed -i "s/lib64/lib/" src/src_lv2{,ui}.pro

  # i686 -reduce-relocations (Qt5)
#  if [[ $CARCH = i686 ]]; then
#    export CXXFLAGS+=" -fPIE"
#  fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
