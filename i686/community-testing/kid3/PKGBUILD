# $Id: PKGBUILD 119279 2014-09-19 22:29:08Z bpiotrowski $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

pkgbase=kid3
pkgname=('kid3' 'kid3-qt')
pkgver=3.1
pkgrel=3
arch=('i686' 'x86_64')
url="http://kid3.sourceforge.net/"
license=('GPL')
makedepends=('automoc4' 'cmake' 'docbook-xml' 'docbook-xsl' 'phonon-qt4' 'chromaprint' 'id3lib' 'taglib' 'libmp4v2' 'kdebase-runtime')
install=$pkgbase.install
changelog=$pkgbase.changelog
source=(http://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver.tar.gz)
sha256sums=('08661266250670620a51e3d55b97b00fe545625c74f7cf0834b5308572ffcc51')

build() {
  cd "${srcdir}"
  [[ ! -d build ]] && mkdir build
  mkdir build-qt

#kid3
  cd build

  cmake \
    ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_APPS="CLI;KDE"

  make

#kid3-qt
  cd "$srcdir"/build-qt

  cmake \
    ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_APPS="CLI;Qt"

  make
}

package_kid3() {
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, KDE version"
depends=('chromaprint' 'id3lib' 'taglib' 'libmp4v2' 'kdebase-runtime')
conflicts=('kid3-qt')

  cd "$srcdir"/build

  make DESTDIR="${pkgdir}" install
}

package_kid3-qt() {
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, QT version"
depends=('chromaprint' 'id3lib' 'taglib' 'libmp4v2' 'qt4' 'phonon-qt4')
conflicts=('kid3')
provides=('kid3')

  cd "$srcdir"/build-qt

  make DESTDIR="${pkgdir}" install
}
