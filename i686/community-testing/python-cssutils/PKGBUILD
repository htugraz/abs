# $Id: PKGBUILD 141833 2015-09-28 10:45:58Z fyan $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Michal Marek <reqamst at gmail dot com>
# Contributor: Rick W. Chena <stuffcorpse@archlinux.us>
# Contributor: Andrea Fagiani <andfagiani _at_ gmail dot com>

pkgbase=python-cssutils
pkgname=('python2-cssutils' 'python-cssutils')
pkgver=1.0
pkgrel=3
pkgdesc="A CSS Cascading Style Sheets library for Python"
arch=('any')
url="http://cthedot.de/cssutils/"
license=('LGPL3')
makedepends=('python2-setuptools' 'python-setuptools')
# We need to include mock as a check dependency due to the following pbr bug:
# https://bugs.launchpad.net/pbr/+bug/1493735
checkdepends=('python2-mock')
source=("http://pypi.python.org/packages/source/c/cssutils/cssutils-${pkgver}.zip")
md5sums=('0c0b9df329ec1461c732d0f3cba05e93')

build() {
  cd "${srcdir}"

  # Create python2 build dir
  cp -a cssutils-${pkgver} cssutils-py2-${pkgver}

  # Use #!/usr/bin/python3 
  cd cssutils-${pkgver}/src
  find . -maxdepth 2 -mindepth 1 -type f -iname '*.py' -exec sed -i 's/env python/python3/' '{}' \;

  cd "${srcdir}/cssutils-py2-${pkgver}"
  find . -maxdepth 2 -mindepth 1 -type f -iname '*.py' -exec sed -i -e 's:#!/usr/bin/env python:#!/usr/bin/env python2:g' '{}' \;
}

package_python2-cssutils() {
  depends=('python2')

  cd "${srcdir}/cssutils-py2-${pkgver}"

  python2 setup.py install --root="${pkgdir}/" --optimize=1

  # Don't install tests (FS#27567)
  rm -rf "${pkgdir}/usr/lib/python2.7/site-packages/tests"
}

package_python-cssutils() {
  depends=('python')

  cd "${srcdir}/cssutils-${pkgver}"

  python3 setup.py install --root="${pkgdir}/" --optimize=1

  # Avoid a conflict: only keep the Py2 executables.
  rm -rf "${pkgdir}/usr/bin"

  # Don't install tests (FS#27567)
  rm -rf "${pkgdir}/usr/lib/python3.3/site-packages/tests"
}

check() {
  cd "${srcdir}/cssutils-${pkgver}"
  # Currently broken..
#  python3 setup.py test
  python2 setup.py test
}
