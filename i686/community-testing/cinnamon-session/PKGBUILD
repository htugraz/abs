# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=2.0.6
pkgrel=3
pkgdesc="The Cinnamon Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(cinnamon-desktop libsm upower)
makedepends=(intltool mesa json-glib xtrans gnome-common)
options=('!emptydirs')
install=cinnamon-session.install
url="https://github.com/linuxmint/cinnamon-session"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-session/archive/${pkgver}.tar.gz
        cinnamon-session-upower.patch
        remove_sessionmigration.patch
        timeout.patch)
sha256sums=('831a63f31bd5c0f5dcaee833d3e43e50e8c9d540cbe931e71b8e0f79ede14567'
            '349845122c14db73e20eef6f7e5fdadf7987ef9a8c72f14a7bbb249892333b1e'
            'f3c0bc1a3debfaa686230f19ba371b08dd4645064d90ead0c34f465c7d416736'
            'ef421a14814c7858490b3a806568ab4ec8a0dc21d390e94f801771c1261bb24a')

prepare() {
  cd $pkgname-$pkgver

  # Port to upower 0.99
  patch -Np1 -i ../cinnamon-session-upower.patch

  # Do not try to execute session-migration script (it's specific to Ubuntu)
  patch -Np1 -i ../remove_sessionmigration.patch

  # Increase timeout, for slow machines
  patch -Np1 -i ../timeout.patch
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/cinnamon-session \
      --disable-schemas-compile --enable-systemd --disable-gconf
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Prefix 'Startup Applications' with 'Cinnamon' to avoid confusion with gnome-session
  sed -i 's/^Name\(.*\)=\(.*\)/Name\1=Cinnamon \2/' \
      "$pkgdir/usr/share/applications/cinnamon-session-properties.desktop"
}
