# $Id: PKGBUILD 118883 2014-09-12 19:28:03Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Bernhard Walle <bernhard.walle@gmx.de>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Contributor: Mathieu Pasquet <mathieui@mathieui.net>

pkgname=crash
pkgver=7.0.8
pkgrel=1
pkgdesc='Linux kernel crashdump analysis tool'
url='http://people.redhat.com/~anderson'
arch=(i686 x86_64)
license=(GPL)
depends=(xz ncurses zlib)
source=(http://people.redhat.com/~anderson/crash-$pkgver.tar.gz)
sha256sums=('9afc3295b5f946ba940c0d9a0771856156152eb274813351afd8064a3cc32644')

prepare() {
  cd crash-$pkgver
  make gdb_unzip
  sed -i '/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/' gdb-7.6/libiberty/configure
}

build() {
  cd crash-$pkgver
  make
}

package() {
  cd crash-$pkgver

  mkdir -p "$pkgdir"/usr/bin "$pkgdir"/usr/share/man/man8
  install crash "$pkgdir"/usr/bin/
  install crash.8 "$pkgdir"/usr/share/man/man8/
}
