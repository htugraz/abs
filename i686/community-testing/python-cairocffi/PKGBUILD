# $Id: PKGBUILD 141822 2015-09-28 10:45:41Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Florijan Hamzic <florijanh@gmail.com>

pkgbase=python-cairocffi
pkgname=(python-cairocffi python2-cairocffi)
pkgver=0.7.2
pkgrel=2
pkgdesc="cairocffi is a CFFI-based drop-in replacement for Pycairo, a set of Python bindings and object-oriented API for cairo."
arch=('any')
url="http://pythonhosted.org/cairocffi/"
license=('BSD')
makedepends=('python-cffi' 'python2-cffi' 'python-setuptools' 'python2-setuptools' 'cairo' 'gdk-pixbuf2')
checkdepends=('python-pytest' 'python2-pytest' 'python-xcffib' 'python2-xcffib')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/SimonSapin/cairocffi/archive/v${pkgver}.tar.gz")
md5sums=('cb4c10bc2ac4da78ea9ae89bee9c8069')

prepare() {
  cp -a cairocffi-$pkgver{,-py2}
}

build() {
  cd "$srcdir/cairocffi-$pkgver"
  python setup.py build

  cd "$srcdir/cairocffi-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/cairocffi-$pkgver/cairocffi"
  LC_CTYPE=en_US.utf8 py.test || warning "Tests failed"

  cd "$srcdir/cairocffi-$pkgver-py2/cairocffi"
  LC_CTYPE=en_US.utf8 py.test2 || warning "Tests failed"
}

package_python-cairocffi() {
  depends=('python-cffi' 'cairo')
  optdepends=('python-xcffib: for xcb support'
              'gdk-pixbuf2: for cairocffi.pixbuf')

  cd "$srcdir/cairocffi-$pkgver"
  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-cairocffi() {
  depends=('python2-cffi' 'cairo')
  optdepends=('python2-xcffib: for xcb support'
              'gdk-pixbuf2: for cairocffi.pixbuf')

  cd "$srcdir/cairocffi-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
