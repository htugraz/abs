# $Id: PKGBUILD 150444 2015-12-08 19:32:54Z foutrelis $
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=codeblocks
pkgver=13.12
pkgrel=5
pkgdesc='Cross-platform C/C++ IDE'
arch=('x86_64' 'i686')
url='http://www.codeblocks.org/'
license=('GPL3')
depends=('boost-libs' 'desktop-file-utils' 'gamin' 'gtk-update-icon-cache'
         'hicolor-icon-theme' 'hunspell' 'shared-mime-info' 'wxgtk2.8' 'libsm' 'webkitgtk2')
makedepends=('boost' 'zip' 'webkitgtk2')
install=codeblocks.install
source=("http://sourceforge.net/projects/codeblocks/files/Sources/$pkgver/codeblocks_$pkgver-1.tar.gz")
sha256sums=('772450046e8c8ba2ea0086acf433a46b83e6254fae64df9c8ca132a22f949610')

build() {
  cd "$pkgname-$pkgver"

# Fix build with boost 1.59
  export CPPFLAGS+=" -DBOOST_ERROR_CODE_HEADER_ONLY"
  export WX_CONFIG_PATH=/usr/bin/wx-config-2.8
  ./configure --prefix=/usr --with-contrib-plugins=all
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
