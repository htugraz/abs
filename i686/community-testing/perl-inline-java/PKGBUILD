# $Id: PKGBUILD 135367 2015-06-15 08:53:06Z bluewind $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>

pkgname=perl-inline-java
pkgver=0.53
pkgrel=7
pkgdesc="The Inline::Java module allows you to put Java source code directly inline in a Perl script or module."
arch=('i686' 'x86_64')
depends=('perl' 'perl-inline' 'java-runtime')
makedepends=('java-environment')
license=("GPL" "PerlArtistic")
url="http://search.cpan.org/dist/Inline-Java"
source=(http://search.cpan.org/CPAN/authors/id/P/PA/PATL/Inline-Java-$pkgver.tar.gz)
md5sums=('23f96c2ac4915302c3647c0abc92916f')
options=('!emptydirs')

build() {
  cd $srcdir/Inline-Java-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor J2SDK=/usr/lib/jvm/java-7-openjdk
  make java
  make
}
package() {
  cd $srcdir/Inline-Java-$pkgver
  make install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
