# $Id: PKGBUILD 120949 2014-10-18 18:27:44Z andyrtr $
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.2.1
pkgrel=2
pkgdesc='A general-purpose subtitle editor with ASS/SSA support'
arch=('i686' 'x86_64')
url='http://www.aegisub.org'
license=('GPL' 'BSD')
depends=('boost-libs' 'desktop-file-utils' 'ffms2' 'fftw' 'hunspell' 'wxgtk')
makedepends=('boost' 'intltool' 'mesa')
install="${pkgname}.install"
source=("http://ftp.aegisub.org/pub/archives/releases/source/${pkgname}-${pkgver}.tar.xz")
sha256sums=('961a28f2528a3c6330f06d0f2aef5fa71d93ca2ae1e6dcfabe347befbb6df8dc')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --without-{portaudio,openal,oss} \
              --disable-update-checker
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub
  install -m 644 LICENCE "${pkgdir}"/usr/share/licenses/aegisub/LICENSE
}

# vim: ts=2 sw=2 et:
