# $Id: PKGBUILD 145624 2015-11-02 13:12:54Z foutrelis $
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=pdf2djvu
pkgver=0.9.2
pkgrel=2
pkgdesc="Creates DjVu files from PDF files"
arch=('i686' 'x86_64')
url="http://pdf2djvu.googlecode.com"
license=('GPL')
depends=('poppler' 'djvulibre' 'libxslt' 'gcc-libs' 'graphicsmagick')
makedepends=('pstreams' 'python2-nose' 'ttf-liberation')
source=(https://bitbucket.org/jwilk/pdf2djvu/downloads/pdf2djvu-$pkgver.tar.xz{,.asc})
validpgpkeys=('CDB5A1243ACDB63009AD07212D4EB3A6015475F5')
md5sums=('c9fef5b886046e3f2868537b6a5e6fee'
         'SKIP')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver}

    sed -i 's/nosetests/nosetests2/' tests/Makefile
    make test
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make install DESTDIR=${pkgdir}
    install -Dm644 doc/${pkgname}.1 ${pkgdir}/usr/share/man/man1/${pkgname}.1
}
