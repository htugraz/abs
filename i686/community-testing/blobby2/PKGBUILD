# $Id: PKGBUILD 101894 2013-12-02 16:38:46Z bpiotrowski $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=blobby2
pkgver=1.0rc4
arch=('x86_64' 'i686')
pkgrel=1
pkgdesc="A beach ball game with blobs of goo"
url="http://sourceforge.net/projects/blobby/"
license=('GPL')
depends=('sdl' 'sdl2' 'physfs' 'libgl')
makedepends=('cmake' 'boost' 'mesa' 'zip')
source=(http://downloads.sourceforge.net/blobby/$pkgname-linux-$pkgver.tar.gz
	    $pkgname.desktop
        $pkgname.png)
md5sums=('0124ebbb6cf06d053d6a1bcc12b1a89d'
         'ab05bed794ee78db693fd3036393275a'
         'c1bc427b41a0a3facd771ac83c7fb412')

build() {
  cd $srcdir/blobby-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/blobby-$pkgver

  make DESTDIR=$pkgdir install

  # install .desktop file and icon
  install -dm755 $pkgdir/usr/share/{applications,pixmaps}
  install -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications
  install -m644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps
}
