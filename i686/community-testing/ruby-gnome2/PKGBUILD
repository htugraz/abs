# $Id: PKGBUILD 111159 2014-05-14 23:50:19Z anatolik $
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Eric Bélanger
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Bjorn Lindeijer <bjorn@lindeijer.nl>
# Contributor: kritoke <kritoke@nospam.gmail.com>

pkgbase=ruby-gnome2
pkgver=2.2.0
pkgrel=1
pkgname=('ruby-gtk3' 'ruby-gdk3' 'ruby-atk' 'ruby-gdkpixbuf2' 'ruby-gio2' 'ruby-glib2'
         'ruby-gtk2' 'ruby-pango' 'ruby-gobject-introspection')
arch=('x86_64' 'i686')
url='http://ruby-gnome2.sourceforge.jp/'
license=('LGPL')
makedepends=('ruby-pkgconfig' 'ruby-cairo' 'gtk2' 'gtk3' 'gobject-introspection')
source=("http://downloads.sourceforge.net/ruby-gnome2/ruby-gnome2-all-$pkgver.tar.gz")
sha256sums=('73b2366152a15533d8e7056b30c76db6d5bdc6e5a1dc9c72320cbef91ff63c83')

prepare() {
  cd "ruby-gnome2-all-$pkgver"
  LANG="en_US.UTF-8" ruby extconf.rb gtk3 gdk3 atk gdk_pixbuf2 gio2 glib2 gtk2 pango gobject-introspection --vendor
}

build() {
  cd "ruby-gnome2-all-$pkgver"
  make
}

package_ruby-atk() {
  pkgdesc='Ruby bindings for atk'
  depends=("ruby-glib2=$pkgver" 'atk')

  cd "ruby-gnome2-all-$pkgver/atk"
  make DESTDIR="$pkgdir" install -j1
}

package_ruby-gdkpixbuf2() {
  pkgdesc='Ruby bindings for gdkpixbuf2'
  depends=("ruby-glib2=$pkgver" 'gdk-pixbuf2')

  cd "ruby-gnome2-all-$pkgver/gdk_pixbuf2"
  make DESTDIR="$pkgdir" install -j1
}

package_ruby-gio2() {
  pkgdesc='Ruby bindings for gio2'
  depends=("ruby-glib2=$pkgver")

  cd "ruby-gnome2-all-$pkgver/gio2"
  make DESTDIR="$pkgdir" install -j1
}

package_ruby-glib2() {
  pkgdesc='Ruby bindings for glib2'
  depends=('glib2' 'ruby')

  cd "ruby-gnome2-all-$pkgver/glib2"
  make DESTDIR="$pkgdir" install -j1
}

package_ruby-gtk2() {
  pkgdesc='Ruby bindings for gtk2'
  depends=('gtk2' "ruby-glib2=$pkgver" "ruby-pango=$pkgver" "ruby-atk=$pkgver"
           "ruby-gdkpixbuf2=$pkgver")

  cd "ruby-gnome2-all-$pkgver/gtk2"
  make DESTDIR="$pkgdir" install -j1
}

package_ruby-pango() {
  pkgdesc='Ruby bindings for pango'
  depends=("ruby-glib2=$pkgver" 'ruby-cairo' 'pango')

  cd "ruby-gnome2-all-$pkgver/pango"
  make DESTDIR="$pkgdir" install -j1
}

package_ruby-gtk3() {
  pkgdesc='Ruby bindings for gtk3'
  depends=('gtk3' "ruby-glib2=$pkgver" "ruby-pango=$pkgver" "ruby-atk=$pkgver"
           "ruby-gdkpixbuf2=$pkgver" "ruby-gdk3=$pkgver")

  cd "ruby-gnome2-all-$pkgver/gtk3"
  make DESTDIR="$pkgdir" install -j1
}

package_ruby-gdk3() {
  pkgdesc='Ruby bindings for gdk3'
  depends=('ruby' 'gtk3')

  cd "ruby-gnome2-all-$pkgver/gdk3"
  make DESTDIR="$pkgdir" install -j1
}

package_ruby-gobject-introspection() {
  pkgdesc='Ruby bindings of GObjectIntrospection'
  depends=('ruby' 'gobject-introspection')

  cd "ruby-gnome2-all-$pkgver/gobject-introspection"
  make DESTDIR="$pkgdir" install -j1
}
