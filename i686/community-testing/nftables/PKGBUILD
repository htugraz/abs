# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgname=nftables
pkgver=0.099
pkgrel=1
pkgdesc='Netfilter nftables userspace tools'
arch=('i686' 'x86_64')
url='http://netfilter.org/projects/nftables/'
license=('GPL2')
depends=('libmnl' 'libnftnl' 'gmp' 'readline' 'ncurses')
source=("http://netfilter.org/projects/nftables/files/nftables-$pkgver.tar.bz2"{,.sig}
        '01-fix-nftables-rename.patch')
sha1sums=('c8fb650263f68f43498306af36910791871ec1e1'
          'SKIP'
          '38d7c1e4bc8590a549e16a2385af37704460642b')

prepare() {
  cd $pkgname-$pkgver
  patch -p 1 -i "$srcdir/01-fix-nftables-rename.patch"
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
