# $Id: PKGBUILD 119723 2014-09-28 22:38:32Z heftig $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
pkgver=1.0.0
pkgrel=13
pkgdesc="remote desktop client written in GTK+"
arch=(i686 x86_64)
url="http://remmina.sourceforge.net/"
license=('GPL')
depends=('gtk3' 'zlib' 'libjpeg' 'libssh' 'libunique' 'avahi' 'vte3' 'libgcrypt' 'libxdmcp')
makedepends=('intltool' 'pkgconfig' 'cmake' 'libgnome-keyring'
             'avahi' 'libxkbfile' 'freerdp' 'telepathy-glib')
optdepends=('libxkbfile'
            'freerdp'
            'telepathy-glib'
            'gnome-keyring'
            'nxproxy'
            'xorg-server-xephyr: XDMCP support')
replaces=('remmina-plugins')
provides=('remmina-plugins')
install=remmina.install
source=("https://github.com/downloads/FreeRDP/Remmina/Remmina-$pkgver.tar.gz"
        '569d9bb7.patch'
        'f7d1038b.patch'
        '1aebfc02.patch'
        '3ebdd6e7.patch'
        '97c2af8c.patch'
        '84327f81.patch'
        'c1ef3a16.patch'
        '6ee20289.patch'
        'b2277827.patch'
        'cb0b209b.patch'
        'vte2.91.patch')
md5sums=('701c540acaab961bc3adf130a2ddb8b1'
         '9862af7dce8ff1f7ebc3f75d24f45c01'
         'a54f63bce0986e72efd5fb74f55d1fe3'
         '5275b8bcb30befa189c8a949c9548df8'
         '7bcbb2c12bda8d5288458f5be2c25a82'
         '93cdd96d5f6274a8bb412076b64c7843'
         'eb389439a6d41b711abc00e0b1776ede'
         '2555c8bd32e6ee5f4bdabe69b8f6ff05'
         'b45cd76a3e3f4b667967f5cdc932a6fd'
         'cd09458b305159fbf408c95520ba062c'
         '80bcf469fca7ee88eb7dd8a4f914f1d2'
         'a784e86b570e68afffe6ad4191f1bd00')

prepare() {
  cd ${srcdir}/FreeRDP-Remmina-*/

  for i in "${source[@]}"; do
    if [[ $i == *.patch ]]; then
      patch -p1 -i "${srcdir}/${i}"
    fi
  done
}

build() {
  cd ${srcdir}/FreeRDP-Remmina-*/

  export LDFLAGS="-lX11 -lgdk_pixbuf-2.0 -lgio-2.0 -lgobject-2.0 -lgmodule-2.0 -lgthread-2.0 -lpthread -lgnutls"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd ${srcdir}/FreeRDP-Remmina-*/
  make DESTDIR=${pkgdir} install
}
