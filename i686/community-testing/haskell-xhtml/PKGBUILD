# $Id: PKGBUILD 110324 2014-04-28 04:21:40Z td123 $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=xhtml
pkgname=haskell-xhtml
pkgver=3000.2.1
pkgrel=7
pkgdesc='Combinators for producing XHTML 1.0'
url='http://hackage.haskell.org/package/xhtml'
license=('custom:BSD3')
arch=('x86_64' 'i686')
depends=('ghc=7.8.2-2' 'sh')
source=("http://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
install="$pkgname.install"
options=('staticlibs')
sha256sums=('33020782170c1c083bc59fc3bfcb72cec2db223e02d1181c07ae23b9fa7fdcd8')

build() {
    cd "$_hkgname-$pkgver"

    runhaskell Setup configure -O -p \
      --enable-split-objs \
      --enable-shared \
      --prefix=/usr \
      --docdir="/usr/share/doc/$pkgname" \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$_hkgname-$pkgver"
  
  install -Dm744 register.sh \
    "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh \
    "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s "/usr/share/doc/$pkgname/html" \
    "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
