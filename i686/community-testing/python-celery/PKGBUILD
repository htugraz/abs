# $Id: PKGBUILD 141824 2015-09-28 10:45:44Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin Kolenda <marcinkolenda419@gmail.com>
# Contributor: Valentin Haloiu <vially.ichb+aur@gmail.com>
# Contributor: Lukas Linhart <bugs@almad.net>
# Contributor: Marco Elver <marco.elver AT gmail.com>
# Contributor: apkawa <apkawa@gmail.com>

pkgbase=python-celery
pkgname=('python-celery' 'python2-celery')
pkgver=3.1.18
pkgrel=2
pkgdesc='Distributed Asynchronous Task Queue'
arch=('any')
url='http://celeryproject.org/'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-kombu' 'python2-kombu' 'python-billiard' 'python2-billiard'
             'python-pytz' 'python2-pytz')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-pyopenssl' 'python2-pyopenssl' 'python2-gevent'
              'python2-librabbitmq' 'python-pymongo' 'python2-pymongo' 'python-msgpack' 'python2-msgpack' 'python-pyro'
              'python2-pyro' 'python-redis' 'python2-redis' 'python-sqlalchemy' 'python2-sqlalchemy' 'python-boto' 'python2-boto'
              'python-yaml' 'python2-yaml' 'python-pyzmq' 'python2-pyzmq')
source=("https://pypi.python.org/packages/source/c/celery/celery-$pkgver.tar.gz"
        celery@.service celery2@.service python-celery.install python2-celery.install celery.tmpfiles.d celery2.tmpfiles.d)
options=('!emptydirs')
sha256sums=('0924f94070c6fc57d408b169848c5b38832668fffe060e48b4803fb23e0e3eaf'
            '2c3b6eb4ebcc02f6748141d3dd1ebb64128b7b082724ebc15141144e2e40ebff'
            'f17edc445e555ae2a6b6c4124e1ad45e89a8f5c1408f4b629d939eeb8b1ce0d9'
            'b2744f019e11d57df825d9a970099a678ec1fffc5579ab52fb2270ec4aba741a'
            '19cfffc0447ec5046b106bc33cc8422942b82f9339971405398835d5d8079f00'
            'c60185a7e2192dfd98d41cbea6ef0f8ed24cafd79aeae6f0792360fa6ddf6f46'
            'fdb55a9b2ee2b7e0b55ffb576ebcd14d846cdfdb2c5131a68c64275c391f6aa5')

prepare() {
  cp -a celery-$pkgver{,-py2}
}

build() {
  cd "$srcdir/celery-$pkgver"
  python setup.py build

  cd "$srcdir/celery-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/celery-$pkgver"
  nosetests3 || warning "Python 3 tests failed"

  cd "$srcdir/celery-$pkgver-py2"
  nosetests2 || warning "Python 2 tests failed"
}

package_python-celery() {
  depends=('python' 'python-kombu' 'python-billiard' 'python-pytz')
  optdepends=('python-pyopenssl: for celery.security'
              'python-pymongo: for celery.backends.mongodb'
              'python-msgpack: for using the msgpack serializer'
              'python-pyro: for using Pyro4 message transport'
              'python-redis: for celery.backends.redis'
              'python-sqlalchemy: for celery.backends.database'
              'python-boto: for SQS transport'
              'python-yaml: for using the yaml serializer'
              'python-pyzmq: for using ZeroMQ transport')
  install=python-celery.install

  cd "$srcdir/celery-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/celery@.service" "$pkgdir/usr/lib/systemd/system/celery@.service"
  install -Dm644 "$srcdir/celery.tmpfiles.d" "$pkgdir/usr/lib/tmpfiles.d/celery.conf"
}

package_python2-celery() {
  depends=('python2' 'python2-kombu' 'python2-billiard' 'python2-pytz')
  optdepends=('python2-pyopenssl: for celery.security'
              'python2-gevent: for celery.concurrency.gevent'
              'python2-librabbitmq: optimized client for RabbitMQ'
              'python2-pymongo: for celery.backends.mongodb'
              'python2-msgpack: for using the msgpack serializer'
              'python2-pyro: for using Pyro4 message transport'
              'python2-redis: for celery.backends.redis'
              'python2-sqlalchemy: for celery.backends.database'
              'python2-boto: for SQS transport'
              'python2-yaml: for using the yaml serializer'
              'python2-pyzmq: for using ZeroMQ transport')
  install=python2-celery.install

  cd "$srcdir/celery-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/celery2@.service" "$pkgdir/usr/lib/systemd/system/celery2@.service"
  install -Dm644 "$srcdir/celery2.tmpfiles.d" "$pkgdir/usr/lib/tmpfiles.d/celery2.conf"

  for file in celery celerybeat celeryd celeryd-multi; do
    mv "$pkgdir/usr/bin/$file"{,2}
  done
}
