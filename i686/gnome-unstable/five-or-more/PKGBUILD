# $Id: PKGBUILD 234208 2015-03-20 09:57:35Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=five-or-more
pkgver=3.15.92
pkgrel=1
pkgdesc="Remove colored balls from the board by forming lines"
arch=('i686' 'x86_64')
license=('GPL')
depends=('hicolor-icon-theme' 'gtk3' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=five-or-more.install
url="https://live.gnome.org/Five%20or%20more"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('6c7ed491a487b1bba8145a886a83e7f378eaabde99a7762fff5e18a82f47598a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
