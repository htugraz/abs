# $Id: PKGBUILD 221893 2014-09-17 21:20:39Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts
pkgver=3.13.92
pkgrel=1
pkgdesc="Contacts Manager for GNOME"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
groups=(gnome)
depends=('gtk3' 'folks' 'libnotify' 'gnome-desktop' 'dconf' 'gnome-online-accounts' 'libgee' 'telepathy-glib' 'cheese')
makedepends=('intltool' 'vala' 'gobject-introspection' 'python2')
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('e38555eb44b2d8f04264e119942b8473583a97cc849b7664b2571649ee95a73f')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/gnome-contacts
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
