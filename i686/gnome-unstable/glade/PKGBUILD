# $Id: PKGBUILD 208706 2014-03-25 09:58:38Z jgc $
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=glade
pkgver=3.18.0
pkgrel=1
pkgdesc="User interface builder for GTK+ and GNOME."
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('gtk3' 'libxml2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'python2-gobject' 'itstool' 'docbook-xsl')
optdepends=('python2: Python widgets support'
            'devhelp: help browser')
url="http://glade.gnome.org/"
install=glade.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        warnfix.patch)
sha256sums=('ab95d910c148e82a9c0bf18e44a68924b739c5a4d2d998d922cef11b3e3c6a73'
            '1669b935e9608ea6be515bd3840394b79f1046b47343f8fbc7ef359481f31f6f')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np0 -i ../warnfix.patch
}
build() {
  cd "$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
