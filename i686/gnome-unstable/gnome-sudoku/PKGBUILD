# $Id: PKGBUILD 234504 2015-03-23 07:42:41Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=3.16.0
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
arch=('x86_64' 'i686')
license=('GPL')
depends=('hicolor-icon-theme' 'dconf' 'gtk3' 'qqwing' 'libgee' 'json-glib')
makedepends=('intltool' 'itstool' 'vala')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=gnome-sudoku.install
url="https://live.gnome.org/GnomeSudoku"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('bbeb4c410ac690df967fe494da92a40ded473ed28b2565c5e2f34af91be53dad')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  sed -i 's_#! /usr/bin/python_#!/usr/bin/python2_' "$pkgdir/usr/bin/gnome-sudoku"
}
