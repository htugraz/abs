# $Id: PKGBUILD 233433 2015-03-13 13:12:25Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=3.15.4
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem"
arch=(i686 x86_64)
url="http://live.gnome.org/GnomeBluetooth"
license=(GPL LGPL)
depends=(gtk3 hicolor-icon-theme libnotify bluez)
makedepends=(intltool gobject-introspection itstool docbook-xsl)
options=(!emptydirs)
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        61-gnome-bluetooth-rfkill.rules)
sha256sums=('27d3ac2c0df7232aad7d16c55973c3731192d3ebb3416e81368448e2f50f691b'
            '8dd2d3637f380a89e72f65e6c8021e994ccbdc645f1a1b1dbffe74ac9f061f53')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -m644 -D ../61-gnome-bluetooth-rfkill.rules \
      "$pkgdir/usr/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
}
