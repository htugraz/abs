# $Id: PKGBUILD 222753 2014-09-23 07:57:58Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=3.14.0
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
arch=(i686 x86_64)
license=(GPL)
depends=(gnome-desktop libexif lcms2 desktop-file-utils exempi libpeas librsvg dconf)
makedepends=(intltool itstool)
install=eog.install
groups=(gnome)
options=('!emptydirs')
url="https://wiki.gnome.org/Apps/EyeOfGnome"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('adc4606be1074b48ba24c0bf320c597780f55a9606d2f0dc0c4fec6d6c2abcd1')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
