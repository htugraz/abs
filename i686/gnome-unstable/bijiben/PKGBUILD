# $Id: PKGBUILD 221413 2014-09-10 19:12:51Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=bijiben
pkgver=3.13.91
pkgrel=1
pkgdesc="Write out notes, every detail matters"
arch=(i686 x86_64)
url="https://live.gnome.org/Bijiben"
license=(GPL)
depends=(tracker zeitgeist webkitgtk hicolor-icon-theme desktop-file-utils gnome-online-accounts evolution-data-server)
makedepends=(intltool itstool evolution)
options=(!emptydirs)
groups=(gnome-extra)
install=bijiben.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('9225ab903d7c74e36a039f2ae4253566ef8bf6650db51fa7995167bf73b5cab6')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-update-mimedb
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
