# $Id: PKGBUILD 233092 2015-03-08 15:36:23Z jgc $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gcr
pkgver=3.15.90
pkgrel=1
pkgdesc="A library for bits of crypto UI and parsing"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
depends=('dconf' 'desktop-file-utils' 'gtk3' 'hicolor-icon-theme' 'libgcrypt'
         'p11-kit')
makedepends=('intltool' 'gobject-introspection' 'python' 'vala' 'libxslt')
options=('!makeflags')
install=gcr.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        10-gcr.conf
        skip-annotation-gck_object_cache_get_attributes.patch)
sha256sums=('b0ad0cfb9f504cda67e740f79a53b5d19c9814a07b7f257d47fbf0409376efe2'
            '5f2eda7175ae9f23ee0e09d2beceb24fd2f6daafd7bddfcc1c1f5a3734eb60fc'
            'cc4056b4cdb79ce7804ae7562d62ff108a39244bd0fe673fec8c1aa4bf193411')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../skip-annotation-gck_object_cache_get_attributes.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --libexec=/usr/lib/gcr \
    --disable-static \
    --disable-update-mime \
    --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # gcr wants to lock some memory to prevent swapping out private keys
  # https://bugs.archlinux.org/task/32616
  # https://bugzilla.gnome.org/show_bug.cgi?id=688161
  install -Dm644 ../10-gcr.conf "$pkgdir/etc/security/limits.d/10-gcr.conf"
}

# vim:set ts=2 sw=2 et:
