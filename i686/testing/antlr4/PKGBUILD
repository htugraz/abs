# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=antlr4
_shortname=${pkgname:0:-1}
pkgver=4.5
pkgrel=1
pkgdesc='Parser generator for reading, processing, executing, or translating structured text or binary files'
arch=('any')
url='http://www.antlr.org/index.html'
license=('BSD')
depends=('java-environment>=6')
provides=("antlr=${pkgver}")
source=(http://www.antlr.org/download/${_shortname}-${pkgver}-complete.jar
        https://raw.githubusercontent.com/${_shortname}/${pkgname}/master/LICENSE.txt
        bin_antlr4
        bin_grun)
noextract=("${_shortname}-${pkgver}-complete.jar")
sha256sums=('4a4ebb20c3c09bf5700af78080afadec0879e425cba4695fd21a1084fc171f2c'
            '0c76c37cb13f5bc59c40f4806274069b2404386854fcb4472ae1ee1a562b6ed8'
            '345697e77b7191e2afe70b223f205a8a7fd18a655e6865f503a764583d9a3dcc'
            '119c85b2b6fd02538ae9a7fd53d5eab874ea56d8acc244ce174e174de21c333a')

package() {
  cd "${srcdir}"
  install -D ${_shortname}-${pkgver}-complete.jar \
             "${pkgdir}"/usr/share/java/${_shortname}-${pkgver}-complete.jar
  ln -s antlr-${pkgver}-complete.jar "${pkgdir}"/usr/share/java/antlr-complete.jar
  install -D bin_antlr4 "${pkgdir}"/usr/bin/antlr4
  install -D bin_grun   "${pkgdir}"/usr/bin/grun
  install -D LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
