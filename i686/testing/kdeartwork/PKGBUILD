# $Id: PKGBUILD 254237 2015-12-08 20:35:37Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeartwork
pkgname=('kdeartwork-colorschemes'
         'kdeartwork-desktopthemes'
         'kdeartwork-emoticons'
         'kdeartwork-iconthemes'
         'kdeartwork-kscreensaver'
         'kdeartwork-styles'
         'kdeartwork-wallpapers'
         'kdeartwork-weatherwallpapers')
pkgver=15.08.3
pkgrel=3
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kdeartwork')
makedepends=('cmake' 'automoc4' 'xscreensaver' 'eigen' 'kdebase-workspace' 'libkexiv2_4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz" kdeartwork-cmake-3.4.patch)
sha1sums=('e9fbf14feba2ae1e4cad82179ad994a917c11dd4'
          '494253df5c0c170ad3df4ac83c3165313b20318b')

prepare() {
	cd $pkgbase-$pkgver
	# Fix build with cmake 3.4
	patch -p1 -i ../kdeartwork-cmake-3.4.patch
}

build() {
	cd $srcdir
	mkdir -p build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeartwork-colorschemes() {
	pkgdesc='KDE color schemes'
	replaces=('kdeaccessibility-colorschemes')
	cd $srcdir/build/ColorSchemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-desktopthemes() {
	pkgdesc='KDE desktop themes'
	cd $srcdir/build/desktopthemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-emoticons() {
	pkgdesc='KDE emoticons'
	cd $srcdir/build/emoticons
	make DESTDIR=$pkgdir install
}

package_kdeartwork-iconthemes() {
	pkgdesc='KDE icon themes'
	replaces=('kdeaccessibility-iconthemes')
	cd $srcdir/build/IconThemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-kscreensaver() {
	pkgdesc='KDE screensaver'
	depends=('kdebase-workspace' 'libkexiv2_4')
	cd $srcdir/build/kscreensaver
	make DESTDIR=$pkgdir install
}

package_kdeartwork-styles() {
	pkgdesc='KDE styles'
	depends=('kdebase-workspace')
	cd $srcdir/build/styles
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kwin-styles
	make DESTDIR=$pkgdir install
}

package_kdeartwork-wallpapers() {
	pkgdesc='KDE wallpapers'
	cd $srcdir/build/wallpapers
	make DESTDIR=$pkgdir install
	cd $srcdir/build/HighResolutionWallpapers
	make DESTDIR=$pkgdir install
}

package_kdeartwork-weatherwallpapers() {
	pkgdesc='KDE weather wallpapers'
	cd $srcdir/build/WeatherWallpapers
	make DESTDIR=$pkgdir install
}
