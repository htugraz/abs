# $Id: PKGBUILD 226477 2014-11-19 17:22:01Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-kcron
pkgver=4.14.3
pkgrel=1
pkgdesc='Configure and schedule tasks'
url='http://userbase.kde.org/KCron'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/kcron-${pkgver}.tar.xz")
sha1sums=('b39ad9bb9528a04cead1fff522f70c1d770fb9eb')

build() {
  mkdir build
  cd build
  cmake ../kcron-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
