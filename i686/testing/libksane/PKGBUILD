# $Id: PKGBUILD 214454 2014-06-09 03:05:11Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksane
pkgver=4.13.2
pkgrel=1
pkgdesc="An image scanning library"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libksane"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'sane')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('26687b8e0f408442eeb0a27339a6dd838c0c6439')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
