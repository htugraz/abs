# $Id: PKGBUILD 210069 2014-04-09 11:07:26Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem-plparser
pkgver=3.10.2
pkgrel=1
url="http://www.gnome.org"
pkgdesc="Totem playlist parser library"
license=('LGPL')
arch=(i686 x86_64)
depends=('gmime' 'libsoup' 'libarchive')
makedepends=('intltool' 'gobject-introspection' 'libquvi')
optdepends=('libquvi: video site handling (Youtube etc.)')
source=(http://ftp.gnome.org/pub/gnome/sources/totem-pl-parser/${pkgver%.*}/totem-pl-parser-$pkgver.tar.xz)
sha256sums=('38be09bddc46ddecd2b5ed7c82144ef52aafe879a5ec3d8b192b4b64ba995469')

build() {
  cd totem-pl-parser-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static
  make
}

package(){
  cd totem-pl-parser-$pkgver
  make DESTDIR="$pkgdir" install
}
