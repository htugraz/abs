# $Id: PKGBUILD 241006 2015-06-18 01:19:10Z seblu $
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=file
pkgver=5.23
pkgrel=2
pkgdesc='File type identification utility'
arch=('i686' 'x86_64')
license=('custom')
groups=('base' 'base-devel')
url='http://www.darwinsys.com/file/'
depends=('glibc' 'zlib')
source=("ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz"
        '001-fix-bug-with-long-options.patch')
md5sums=('61db35209ce71a6d576392ce6e1d2f80'
         '4138dd409ad6d67ee35a61cf3c71b0fe')

prepare() {
  patch -p1 -d $pkgname-$pkgver < '001-fix-bug-with-long-options.patch'
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --datadir=/usr/share/file
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  rmdir "$pkgdir/usr/share/man/man5"
}

# vim:set ts=2 sw=2 et:
