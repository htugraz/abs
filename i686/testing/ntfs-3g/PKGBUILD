# $Id: PKGBUILD 206519 2014-03-01 05:37:11Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Bächler <thomas.archlinux.org>

pkgname=ntfs-3g
_pkgname=ntfs-3g_ntfsprogs
pkgver=2014.2.15
pkgrel=1
pkgdesc='NTFS filesystem driver and utilities'
url='http://www.tuxera.com/community/ntfs-3g-download/'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('util-linux' 'fuse')
source=("http://tuxera.com/opensource/${_pkgname}-${pkgver}.tgz")
sha1sums=('c9836f340b508f5d7776156e5afb02434d3f0174')

conflicts=('ntfsprogs')
provides=('ntfsprogs')
replaces=('ntfsprogs')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	sed 's|$(DESTDIR)/sbin|$(DESTDIR)/usr/bin|' -i {ntfsprogs,src}/Makefile.in
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sbin=/usr/bin \
		--mandir=/usr/share/man \
		--disable-ldconfig \
		--disable-static \
		--with-fuse=external \
		--enable-posix-acls \
		--enable-extras \

	make
}

package_ntfs-3g() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" rootbindir=/usr/bin rootsbindir=/usr/bin rootlibdir=/usr/lib install
	ln -s /usr/bin/ntfs-3g "${pkgdir}/usr/bin/mount.ntfs"
}
