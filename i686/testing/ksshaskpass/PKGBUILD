# $Id: PKGBUILD 254779 2015-12-08 22:17:33Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>

pkgname=ksshaskpass
pkgver=5.5.0
pkgrel=2
pkgdesc='ssh-add helper that uses kwallet and kpassworddialog'
url='https://projects.kde.org/projects/kde/workspace/ksshaskpass'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kwallet')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
provides=('x11-ssh-askpass' 'ksshaskpass-frameworks')
conflicts=('x11-ssh-askpass' 'ksshaskpass-frameworks')
replaces=('ksshaskpass-frameworks')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "$pkgname.sh")
md5sums=('ed0f72319aa3087af8d5d4f427c030c8'
         '70396ba019ae05cc91502456f548caff')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ksshaskpass-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}
package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm755 "$srcdir"/ksshaskpass.sh \
    "${pkgdir}"/etc/profile.d/ksshaskpass.sh
}
