# $Id: PKGBUILD 226429 2014-11-19 16:57:43Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-qyoto
pkgver=4.14.3
pkgrel=1
pkgdesc=".NET/Mono bindings for the Qt libraries"
url="https://projects.kde.org/projects/kde/kdebindings/csharp/qyoto"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt' 'mono')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen')
conflicts=('kdebindings-csharp')
source=("http://download.kde.org/stable/${pkgver}/src/qyoto-${pkgver}.tar.xz"
        'fix-build.patch')
sha1sums=('adf43a0635425cbe0f81848bb7cd71dfb6cc7371'
          '8ee518bfebb5a92bb8ceb325088e6055d4e61e8a')

prepare() {
  mkdir build

  cd qyoto-${pkgver}
  patch -p1 -i "${srcdir}"/fix-build.patch
}

build() {
  cd build
  cmake ../qyoto-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
