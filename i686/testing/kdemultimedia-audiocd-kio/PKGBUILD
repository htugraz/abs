# $Id: PKGBUILD 206824 2014-03-05 10:22:43Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-audiocd-kio
pkgver=4.12.3
pkgrel=1
pkgdesc="Kioslave for accessing audio CDs"
url='https://projects.kde.org/projects/kde/kdemultimedia/audiocd-kio'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkcddb' 'libkcompactdisc' 'lame' 'cdparanoia')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdemultimedia')
replaces=('kdemultimedia-kioslave')
conflicts=('kdemultimedia-kioslave')
source=("http://download.kde.org/stable/${pkgver}/src/audiocd-kio-${pkgver}.tar.xz")
sha1sums=('ddf3c89195ed90dd261eb38b9dc1bf7c54678cc3')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../audiocd-kio-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
