# $Id: PKGBUILD 246232 2015-09-14 22:01:01Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kdf
pkgver=15.08.1
pkgrel=1
url="http://kde.org/applications/system/kdiskfree/"
arch=('i686' 'x86_64')
pkgdesc='View Disk Usage'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde-applications' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kdf-${pkgver}.tar.xz")
sha1sums=('57defcb1dedf3ff2932e96e8231c124a1f43d77b')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kdf-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
