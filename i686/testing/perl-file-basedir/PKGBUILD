# $Id: PKGBUILD 235443 2015-04-04 09:35:39Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=perl-file-basedir
_realname=File-BaseDir
pkgver=0.05
pkgrel=1
pkgdesc='Use the Freedesktop.org base directory specification'
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-ipc-system-simple' 'perl-file-which')
makedepends=('perl-module-build>0.4205')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/K/KI/KIMRYAN/${_realname}-$pkgver.tar.gz")
md5sums=('e17d5e5b81c180b2940522d390ccb523')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}
}
