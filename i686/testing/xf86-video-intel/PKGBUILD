# $Id: PKGBUILD 217445 2014-07-18 14:54:28Z lcarlier $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-intel
pkgver=2.99.912
pkgrel=3
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
install=$pkgname.install
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
depends=('intel-dri' 'libxvmc' 'pixman' 'xcb-util>=0.3.9')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=18' 'libx11' 'libxrender'
             # additional deps for intel-virtual-output
            'libxrandr' 'libxinerama' 'libxcursor' 'libxtst')
optdepends=('libxrandr: for intel-virtual-output'
            'libxinerama: for intel-virtual-output'
            'libxcursor: for intel-virtual-output'
            'libxtst: for intel-virtual-output')
replaces=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
provides=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<18' 'X-ABI-VIDEODRV_VERSION>=19'
           'xf86-video-intel-sna' 'xf86-video-intel-uxa' 'xf86-video-i810' 'xf86-video-intel-legacy')
groups=('xorg-drivers' 'xorg')
source=(${url}/archive/individual/driver/${pkgname}-${pkgver}.tar.bz2
	0001-sna-dri2-Check-that-the-window-covers-the-whole-CRTC.patch)
sha256sums=('7c8ffc492d59f34cac64093deb70717b4d9223cf416ecc6fa016ab2e8bde9501'
            '19fe39fa5cd175e14f77d67dbd731f992c8f51161d90b73c63f52604a9eed7ce')

prepare() {
  cd ${pkgname}-${pkgver}

  # fix FS#40863, merged upstream
  patch -Np1 -i ../0001-sna-dri2-Check-that-the-window-covers-the-whole-CRTC.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-glamor
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
