# $Id: PKGBUILD 223043 2014-09-26 10:53:36Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=tali
pkgver=3.14.0
pkgrel=1
pkgdesc="Beat the odds in a poker-style dice game"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'gtk3' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection' 'python2')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=tali.install
url="https://live.gnome.org/Tali"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('81f15fcab820d384d02cd39033f2931cd15926f5a39d8c02766584d3b1b2f939')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
