# $Id: PKGBUILD 254490 2015-12-08 20:43:27Z foutrelis $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=liborcus
pkgver=0.9.2
pkgrel=2
pkgdesc="File import filter library for spreadsheet documents."
arch=('i686' 'x86_64')
url="https://gitlab.com/orcus/orcus/blob/master/README.md"
license=('MPL')
depends=('libixion')
makedepends=('boost' 'mdds')
source=(http://kohei.us/files/orcus/src/${pkgname}-${pkgver}.tar.xz
        mdds-1.0.patch)
sha1sums=('4c55f1bdc65490e8e79bbf7d069a64381eb1d1bc'
          '40061fbd6276f8760ab63e136bc304f9e1a88f2a')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -Np1 -i ../mdds-1.0.patch
    autoreconf -vi
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make -k check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=$pkgdir install
}
