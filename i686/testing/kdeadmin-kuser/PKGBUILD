# $Id: PKGBUILD 229115 2015-01-16 00:05:59Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-kuser
pkgver=14.12.1
pkgrel=1
pkgdesc='User Manager'
url='http://kde.org/applications/system/kuser/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
depends=('kdebase-runtime' 'kdepimlibs')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kuser-${pkgver}.tar.xz")
sha1sums=('77628efa1b98db1e1c627a954841e54ca2ca454b')

build() {
  mkdir build
  cd build
  cmake ../kuser-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
