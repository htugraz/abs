# $Id: PKGBUILD 215526 2014-06-28 00:48:29Z anatolik $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=libcap-ng
pkgver=0.7.4
pkgrel=1
pkgdesc="A library making programming with POSIX capabilities easier than traditional libcap"
arch=('i686' 'x86_64')
url="http://people.redhat.com/sgrubb/libcap-ng/"
license=('GPL2' 'LGPL2.1')
depends=('glibc')
source=(http://people.redhat.com/sgrubb/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('55c57c0673b944ea1a755bcb2636dabd')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-static=no --with-python=no
  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
