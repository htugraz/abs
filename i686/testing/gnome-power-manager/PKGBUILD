# $Id: PKGBUILD 225172 2014-10-22 01:37:34Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-power-manager
pkgver=3.14.1
pkgrel=2
pkgdesc="System power information and statistics"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/gnome-power-manager/"
license=(GPL)
depends=(gtk3 upower dconf hicolor-icon-theme)
makedepends=(intltool)
options=(!emptydirs)
install=gnome-power-manager.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('07790a9e8274fe950a6dd653a4e2f4a0f52cb9d89eb9d5320fa6e1b3b3aa9624')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-power-manager \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
