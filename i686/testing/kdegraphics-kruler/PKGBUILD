# $Id: PKGBUILD 230622 2015-02-04 07:45:31Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kruler
pkgver=14.12.2
pkgrel=1
pkgdesc="Screen Ruler"
url="http://kde.org/applications/graphics/kruler/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kruler-${pkgver}.tar.xz")
sha1sums=('2865f6f86f2e8b8d13f02dc965a89b054944ee34')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kruler-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
