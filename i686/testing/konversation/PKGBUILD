# $Id: PKGBUILD 204390 2014-01-18 23:50:02Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Sergio Jovani Guzman <moret@paretux.org>

pkgname=konversation
pkgver=1.5
pkgrel=1
pkgdesc="A user friendly IRC client for KDE"
arch=('i686' 'x86_64')
url="http://konversation.kde.org"
depends=('kdebase-runtime' 'kdepimlibs')
makedepends=('automoc4' 'cmake' 'pkgconfig' 'docbook-xml' 'optipng')
optdepends=('python: python scripting support'
            'kdebase-konsole: open a terminal in konversation'
            'qca-ossl: Diffie-Hellman key exchange and Blowfish ECB/CBC support')
license=('GPL2' 'FDL')
install=${pkgname}.install
options=('!makeflags')
source=("http://download.kde.org/download.php?url=stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('3955a43758750b63d84299a9d9a5933b73e5492b')


build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
  
  # Remove a bunch of symlinks pointing to non-existant files
  for _lang in pt_BR uk sv et de en nl; do
     rm "${pkgdir}"/usr/share/doc/kde/html/${_lang}/konversation/common
  done
}
