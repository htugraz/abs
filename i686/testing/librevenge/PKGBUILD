# $Id: PKGBUILD 254503 2015-12-08 20:43:49Z foutrelis $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=librevenge
pkgver=0.0.2
pkgrel=2
pkgdesc="library for REVerses ENGineered formats filters"
arch=('i686' 'x86_64')
url="http://sf.net/p/libwpd/librevenge/"
license=('MPL')
depends=('boost-libs')
makedepends=('boost' 'doxygen' 'cppunit')
source=(http://sourceforge.net/projects/libwpd/files/librevenge/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz boost-1.59.patch)
sha1sums=('670c90182c4809ca8decb397c3b3970172775bc5'
          '3b2571fefd211f124f6b99a1ce9af9ffec9523bc')

prepare() {
	cd $pkgname-$pkgver
	# Fix build with boost 1.59
	patch -p1 -i ../boost-1.59.patch
	autoreconf -vi
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr
	make
}

check() {
	cd $pkgname-$pkgver
	make check
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir/" install
}
