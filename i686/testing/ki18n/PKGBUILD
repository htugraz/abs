# $Id: PKGBUILD 238621 2015-05-08 06:42:05Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ki18n
pkgver=5.10.0
pkgrel=1
pkgdesc='Advanced internationalization framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/ki18n'
license=('LGPL')
depends=('qt5-script')
makedepends=('extra-cmake-modules' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('c9d5a36b3f036f26aeccbb7854875276')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
