#$Id: PKGBUILD 227320 2014-12-06 11:59:15Z bpiotrowski $
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libcap
pkgver=2.24
pkgrel=2
pkgdesc='POSIX 1003.1e capabilities'
arch=('i686' 'x86_64')
url='http://sites.google.com/site/fullycapable/'
license=('GPL2')
depends=('glibc' 'attr')
makedepends=('linux-api-headers')
source=(https://kernel.org/pub/linux/libs/security/linux-privs/libcap2/libcap-$pkgver.tar.xz)
md5sums=('d43ab9f680435a7fff35b4ace8d45b80')

prepare() {
  cd $pkgname-$pkgver
  sed -i "/SBINDIR/s#sbin#bin#" Make.Rules
}

build() {
  make -C $pkgname-$pkgver KERNEL_HEADERS=/usr/include
}

package() {
  cd $pkgname-$pkgver
  make prefix=/usr lib=/lib DESTDIR="$pkgdir" RAISE_SETFCAP=no install

  install -Dm644 pam_cap/capability.conf \
    "$pkgdir"/usr/share/doc/$pkgname/capability.conf.example
}
