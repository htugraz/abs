# $Id: PKGBUILD 225458 2014-10-29 23:30:40Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod
pkgver=18
pkgrel=2
pkgdesc="Linux kernel module handling"
arch=('i686' 'x86_64')
url='http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary'
license=('GPL2')
depends=('glibc' 'zlib')
makedepends=('gtk-doc')
options=('strip' 'debug')
provides=('module-init-tools=3.16' 'libkmod.so')
conflicts=('module-init-tools')
replaces=('module-init-tools')
source=("ftp://ftp.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.xz"
        "depmod-search.conf"
        0001-static-nodes-indicate-that-creation-of-static-nodes-.patch)
md5sums=('82835c7f01983634e06ca72b4ee30cc6'
         'dd62cbf62bd8f212f51ef8c43bec9a77'
         'e9ffb25ec5759523875f0eb13c525bab')


prepare() {
  cd "$pkgname-$pkgver"

  patch -Np1 <../0001-static-nodes-indicate-that-creation-of-static-nodes-.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --sysconfdir=/etc \
    --enable-gtk-doc \
    --with-zlib

  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d

  for tool in {ins,ls,rm,dep}mod mod{probe,info}; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"
}

# vim: ft=sh syn=sh et
