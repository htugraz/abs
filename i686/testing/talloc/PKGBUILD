# $Id: PKGBUILD 242467 2015-07-23 23:42:50Z anatolik $
# Maintainer:
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=talloc
pkgver=2.1.3
pkgrel=1
pkgdesc="Hierarchical pool based memory allocator with destructors"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://talloc.samba.org/"
depends=('glibc')
makedepends=('python2' 'docbook-xsl')
optdepends=('python2: for python bindings')
source=(http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.{gz,asc})
sha1sums=('75f6a09a7199d0d952ebde6fc605dc0f2046b41c'
          'SKIP')
validpgpkeys=(9147A339719518EE9011BCB54793916113084025) # samba-bugs@samba.org

prepare() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   sed -i -e 's#python#python2#g' buildtools/bin/waf
}

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   ./configure --prefix=/usr \
     --sysconfdir=/etc/samba \
     --localstatedir=/var \
     --bundled-libraries=NONE \
     --builtin-libraries=replace \
     --enable-talloc-compat1
   make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install
}
