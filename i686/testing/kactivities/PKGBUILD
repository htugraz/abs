# $Id: PKGBUILD 203693 2014-01-14 07:35:15Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities
pkgver=4.12.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='API for using and interacting with the Activity Manager'
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('GPL' 'FDL')
depends=('nepomuk-core')
makedepends=('cmake' 'automoc4' 'mesa')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('ac1feed30a4f15f6b8cbe003f768f236741527c0')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
