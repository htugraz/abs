# $Id: PKGBUILD 200688 2013-12-02 16:36:16Z bpiotrowski $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=akonadi
pkgver=1.10.3
pkgrel=2
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('i686' 'x86_64')
url='http://community.kde.org/KDE_PIM/Akonadi'
license=('LGPL')
depends=('shared-mime-info' 'boost-libs' 'mariadb' 'soprano')
makedepends=('cmake' 'automoc4' 'boost' 'postgresql')
optdepends=('postgresql: PostgreSQL backend')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/src/${pkgname}-${pkgver}-1.tar.bz2")
md5sums=('3929b765baa3dc0d548a26893c64abcf')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_QSQLITE_IN_QT_PREFIX=TRUE \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DAKONADI_BUILD_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
