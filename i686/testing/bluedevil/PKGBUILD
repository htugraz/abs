# $Id: PKGBUILD 254776 2015-12-08 22:17:28Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=bluedevil
pkgver=5.5.0
pkgrel=2
epoch=1
pkgdesc='Integrate the Bluetooth technology within KDE workspace and applications'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/bluedevil'
license=('GPL2')
depends=('bluez-qt' 'kio')
makedepends=('extra-cmake-modules' 'plasma-framework' 'kded')
conflicts=('bluedevil-frameworks')
replaces=('bluedevil-frameworks')
install="$pkgname.install"
groups=('plasma')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('086f16601639c8fe0eec9655b1177c40')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
