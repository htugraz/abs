# $Id: PKGBUILD 254039 2015-12-08 20:30:00Z foutrelis $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=amarok
pkgver=2.8.0
pkgrel=7
pkgdesc="The powerful music player for KDE"
arch=("i686" "x86_64")
url="http://amarok.kde.org/"
license=('GPL2' 'LGPL2.1' 'FDL')
depends=('kdebase-runtime' 'mariadb' 'qtscriptgenerator' 'taglib-extras'
        'liblastfm' 'ffmpeg' 'libofa' 'qjson')
makedepends=('pkgconfig' 'automoc4' 'cmake' 'libgpod' 'libmtp' 'loudmouth'
             'libmygpo-qt' 'mesa' 'clamz')
optdepends=("libgpod: support for Apple iPod audio devices"
            "libmtp: support for portable media devices"
            "loudmouth: backend needed by mp3tunes for syncing"
            "ifuse: support for Apple iPod Touch and iPhone"
            "libmygpo-qt: gpodder.net Internet Service"
            "clamz: allow to download songs from Amazon.com"
            "gst-libav: MP3 codecs")
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        taglib_version_detect.patch)
sha1sums=('e76ccd53c05d57f9457d74cd08c2c41383c00937'
          '8b2752a0fe78621bdb9ccdb763919ea265b52b06')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../taglib_version_detect.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF \
    -DWITH_Soprano=OFF
  make
}

package(){
  cd build
  make DESTDIR="${pkgdir}" install
}
