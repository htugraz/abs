# $Id: PKGBUILD 248242 2015-10-01 21:02:38Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gstreamer-vaapi
pkgver=0.6.1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework VA Plugins"
arch=(i686 x86_64)
license=(LGPL)
url="http://www.freedesktop.org/software/vaapi/releases/gstreamer-vaapi/"
depends=(gst-plugins-base gst-plugins-bad libva libxrandr libvpx)
makedepends=(gtk-doc)
provides=("gst-vaapi=$pkgver")
conflicts=(gst-vaapi)
replaces=(gst-vaapi)
source=($url/$pkgname-$pkgver.tar.bz2)
sha1sums=('5b4a74137fa30da2da73a0aff97760035e24e0fd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --disable-builtin-libvpx \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
