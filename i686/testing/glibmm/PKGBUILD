# $Id: PKGBUILD 209973 2014-04-09 11:06:18Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.39.92
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('glib2' 'libsigc++2.0' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
url="http://www.gtkmm.org/"
sha256sums=('34c16517ad4e00eddf27ebac86df27b4d38473593ab2ecdf7f5500a6993e3710')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++2.0')
  cd "$pkgbase-$pkgver"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="$pkgdir" install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  cd "$pkgbase-$pkgver/docs"
  make DESTDIR="$pkgdir" install
}
