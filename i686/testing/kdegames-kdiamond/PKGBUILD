# $Id: PKGBUILD 210707 2014-04-16 14:05:32Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kdiamond
pkgver=4.13.0
pkgrel=1
pkgdesc="A single player puzzle game"
url="http://kde.org/applications/games/kdiamond/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kdiamond-${pkgver}.tar.xz")
sha1sums=('0c1e87c98c62a463d2f0e019defd9ebdffe9eab3')

build() {
  mkdir build
  cd build
  cmake ../kdiamond-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
