# $Id: PKGBUILD 206734 2014-03-05 10:11:37Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-analitza
pkgver=4.12.3
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://projects.kde.org/projects/kde/kdeedu/analitza"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'glu')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/analitza-${pkgver}.tar.xz")
sha1sums=('c241f01f7fc5e96df273331eb17f2c90f8a8ffee')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../analitza-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
