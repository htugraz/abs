# $Id: PKGBUILD 203735 2014-01-14 07:37:47Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-analitza
pkgver=4.12.1
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://projects.kde.org/projects/kde/kdeedu/analitza"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'glu')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/analitza-${pkgver}.tar.xz")
sha1sums=('784438e4d485696ee19222b968dca10cef13a562')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../analitza-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
