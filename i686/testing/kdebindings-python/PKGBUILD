# $Id: PKGBUILD 210641 2014-04-16 13:55:50Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kdebindings-python
pkgname=('kdebindings-python'
         'kdebindings-python2'
         'kdebindings-python-common')
pkgver=4.13.0
pkgrel=1
url='https://projects.kde.org/projects/kde/kdebindings/pykde4'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
makedepends=('cmake' 'automoc4' 'boost' 'kdepim-runtime' 'python2-pyqt4'
             'python-pyqt4' 'qscintilla' 'python2-sip' 'python-sip' 'mesa')
source=("http://download.kde.org/stable/${pkgver}/src/pykde4-${pkgver}.tar.xz"
        'sip-4.15.5.patch')
sha1sums=('2c47c3e642d0d38e8d8b9bb8fb22972d162d976e'
          'bffd9f642ad10fb3a49c42e4b457b2b90665d641')

prepare() {
  mkdir build

  cd pykde4-${pkgver}
  patch -p1 -i "${srcdir}"/sip-4.15.5.patch
}

build() {
  export PYTHONDONTWRITEBYTECODE="TRUE"

  cd build
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python3 \
    -DPYTHON_LIBRARY=/usr/lib/libpython3.4m.so.1.0 \
    -DPYKDEUIC4_ALTINSTALL=TRUE \
    -DWITH_Nepomuk=OFF
  make
  cd ..

  mkdir build-python2
  cd build-python2
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DPYKDEUIC4_ALTINSTALL=TRUE \
    -DWITH_Nepomuk=OFF
  make
}

package_kdebindings-python() {
  pkgdesc="A set of Python 3.x bindings for KDE"
  depends=('kdepim-runtime' 'python-pyqt4' 'kdebindings-python-common')

  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  # Provided by kdebindings-python-common
  rm -r "${pkgdir}"/usr/share/apps/pykde4/examples
  rm -r "${pkgdir}"/usr/share/sip/PyKDE4

  ln -s /usr/bin/pykdeuic4-3.4 "${pkgdir}"/usr/bin/pykdeuic4
}

package_kdebindings-python2() {
  pkgdesc="A set of Python 2.x bindings for KDE"
  depends=('kdepim-runtime' 'python2-pyqt4' 'kdebindings-python-common')
  conflicts=('kdebindings-python<4.8.1-2')

  cd "${srcdir}"/build-python2
  make DESTDIR="${pkgdir}" install

  # Provided by kdebindings-python-common
  rm -r "${pkgdir}"/usr/share/apps/pykde4/examples
  rm -r "${pkgdir}"/usr/share/sip/PyKDE4

  # pykdeuic4 should point to the python3 version
  #rm "${pkgdir}"/usr/bin/pykdeuic4

  # Use the python2 executable
  find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

package_kdebindings-python-common() {
  pkgdesc="Common files shared between python2 and python3 kdebindings"
  depends=('qscintilla')
  conflicts=('kdebindings-python<4.8.1-2')

  cd build
  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}"/usr/{bin,lib}
}
