# $Id: PKGBUILD 202431 2013-12-21 19:23:21Z bpiotrowski $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libqmi
pkgver=1.8.0
pkgrel=1
pkgdesc="QMI modem protocol helper library"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/libqmi/"
license=(GPL2)
depends=(glib2 bash)
makedepends=(gtk-doc python)
source=(http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('fadd8070dd6f95eb00d04058c616ade57966d8e54d894da8ddbf31e98310ee0d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --with-tests --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
