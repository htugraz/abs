# $Id: PKGBUILD 254330 2015-12-08 20:38:33Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfilemetadata
pkgver=5.16.0
pkgrel=2
pkgdesc="A library for extracting file metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/kfilemetadata'
license=('LGPL')
depends=('ki18n' 'karchive' 'exiv2' 'poppler-qt5' 'taglib' 'ffmpeg' 'ebook-tools')
optdepends=('catdoc: Extract text from office 98 files')
makedepends=('extra-cmake-modules' 'catdoc' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('40bab004ef3bbe21df50a57bd427cb70bde62309')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
