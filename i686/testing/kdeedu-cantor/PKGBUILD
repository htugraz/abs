# $Id: PKGBUILD 219883 2014-08-16 20:07:08Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-cantor
pkgver=4.14.0
pkgrel=1
pkgdesc="KDE Frontend to Mathematical Software"
url="http://kde.org/applications/education/cantor/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libspectre' 'kdeedu-analitza' 'libqalculate')
makedepends=('cmake' 'automoc4' 'r')
optdepends=('maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/cantor-${pkgver}.tar.xz")
sha1sums=('d199d894c441fa2c0d93bbc57c5cbfd97d3d1488')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../cantor-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
