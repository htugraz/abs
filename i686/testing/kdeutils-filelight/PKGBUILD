# $Id: PKGBUILD 203983 2014-01-14 07:52:46Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-filelight
pkgver=4.12.1
pkgrel=1
pkgdesc='View disk usage information'
url='http://kde.org/applications/utilities/filelight'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
replaces=('filelight')
conflicts=('filelight')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/filelight-${pkgver}.tar.xz")
sha1sums=('e85b1f4af7fbec72c7deb1457334315857934664')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../filelight-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
