# $Id: PKGBUILD 226411 2014-11-19 16:35:55Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-smokegen
pkgver=4.14.3
pkgrel=1
pkgdesc="A general purpose C++ parser with a plugin infrastructure"
url="https://projects.kde.org/projects/kde/kdebindings/smoke/smokegen"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
conflicts=('kdebindings-smoke')
source=("http://download.kde.org/stable/${pkgver}/src/smokegen-${pkgver}.tar.xz")
sha1sums=('1ab32f15d39ef477096e501c36358fe344ae92ea')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../smokegen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
