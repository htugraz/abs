# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netctl
pkgver=1.10
pkgrel=2
pkgdesc='Profile based systemd network management'
url='http://projects.archlinux.org/netctl.git/'
license=('GPL')
groups=('base')
depends=('coreutils' 'iproute2' 'openresolv' 'systemd')
# The source tarball includes pre-built (using asciidoc) documentation.
makedepends=('pkg-config')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support (or dhcpcd)'
            'dhcpcd: for DHCP support (or dhclient)'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ppp: for PPP connections'
            'openvswitch: for Open vSwitch connections'
           )
source=(https://sources.archlinux.org/other/packages/netctl/netctl-${pkgver}.tar.xz{,.sig}
        0001-Redirect-wpa_supplicant-output-in-wpa_start-closes-1.patch)
arch=('any')
md5sums=('15e9b126fd15efe4fc814fa18623d76e'
         'SKIP'
         '9ed06138ec001be186ef07a1725a4f34')
validpgpkeys=(CFA6AF15E5C74149FC1D8C086D1655C14CE1C13E)

prepare() {
  cd "$srcdir/netctl-${pkgver}"

  # Fix wifi-menu to work with wpa_supplicant 2.4
  # https://github.com/joukewitteveen/netctl/issues/112
  patch -Np1 -i ../0001-Redirect-wpa_supplicant-output-in-wpa_start-closes-1.patch
}

package() {
  cd "$srcdir/netctl-${pkgver}"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netctl"
}

