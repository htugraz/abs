# $Id: PKGBUILD 202623 2013-12-22 19:38:00Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio
pkgver=16
pkgrel=2
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="https://projects.archlinux.org/mkinitcpio.git/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive'
         'coreutils' 'bash' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        0001-Add-l-flag-when-using-lz4-compression.patch)
install=mkinitcpio.install
sha256sums=('10821e533eb1ca51a9d3c1d06d8999c08a6151910a9a6029a45ff1638e9228a2'
            'SKIP'
            'e8dd2d3fb29e6c41bed53be2f8659a7e5daeae3c9495d8527b6b938787c54703')

prepare() {
  patch -d "$pkgname-$pkgver" -Np1 <"$srcdir/0001-Add-l-flag-when-using-lz4-compression.patch"
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
