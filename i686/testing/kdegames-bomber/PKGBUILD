# $Id: PKGBUILD 229169 2015-01-16 03:53:42Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-bomber
pkgver=14.12.1
pkgrel=1
pkgdesc="A single player arcade game"
url="http://kde.org/applications/games/bomber/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/bomber-${pkgver}.tar.xz")
sha1sums=('9f15f9e38b34c01567e4c178a971674a9996ca4c')

build() {
  mkdir build
  cd build
  cmake ../bomber-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
