# $Id: PKGBUILD 205155 2014-02-04 13:53:56Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkmahjongg
pkgver=4.12.2
pkgrel=1
pkgdesc="Common code, backgrounds and tile sets for games using Mahjongg tiles"
url="https://projects.kde.org/projects/kde/kdegames/libmahjongg"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkdegames')
makedepends=('cmake' 'automoc4')
replaces=('kdegames-libkmahjongg')
conflicts=('kdegames-libkmahjongg')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('af2edbf816042678676044cccf66cbe8177a24e4')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
