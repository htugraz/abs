# $Id: PKGBUILD 226457 2014-11-19 17:15:00Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkmahjongg
pkgver=4.14.3
pkgrel=1
pkgdesc="Common code, backgrounds and tile sets for games using Mahjongg tiles"
url="https://projects.kde.org/projects/kde/kdegames/libmahjongg"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
replaces=('kdegames-libkmahjongg')
conflicts=('kdegames-libkmahjongg')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('312fa1ff0aaae9915a48ceeb552d09b98d1af0ff')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
