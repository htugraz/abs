# $Id: PKGBUILD 244352 2015-08-19 17:18:32Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kapptemplate
pkgver=15.08.0
pkgrel=1
pkgdesc='KDE Template Generator'
url='http://kde.org/applications/development/kapptemplate/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kio')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdesdk-kapptemplate')
conflicts=('kdesdk-kapptemplate')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kapptemplate-${pkgver}.tar.xz")
sha1sums=('3a86f69cb560f9fede96d582d6bb4c7108da4f22')

build() {
  mkdir build
  cd build
  cmake ../kapptemplate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
