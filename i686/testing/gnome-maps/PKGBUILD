# $Id: PKGBUILD 224281 2014-10-13 10:42:47Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=3.14.1
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
arch=(i686 x86_64)
license=('GPL')
url="https://live.gnome.org/Maps"
depends=(geocode-glib geoclue2 gjs libchamplain gnome-themes-standard)
makedepends=(intltool)
groups=('gnome-extra')
install=gnome-maps.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('5e9374d463e21f1f574ec94d198f56088e133894dbc2f46989c695ce2121f704')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="${pkgdir}" install
}
