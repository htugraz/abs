# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>

pkgname=('ruby' 'ruby-docs')
pkgver=2.1.0
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.ruby-lang.org/en/'
license=('BSD' 'custom')
# disable ruby tk since 1.9.3 it has caused a segfault on require 'tk'
# https://bugs.ruby-lang.org/issues/8000
# wait for upstream to start supporting tk 8.6
makedepends=('gdbm' 'openssl' 'libffi' 'doxygen' 'graphviz' 'libyaml') # 'tk'
options=('!emptydirs' '!makeflags' 'staticlibs')
source=("http://cache.ruby-lang.org/pub/ruby/ruby-${pkgver}.tar.bz2"
        'gemrc'
        '0001-Fix-installing-gem-from-file-without-dependencies.patch')
md5sums=('1546eeb763ac7754365664be763a1e8f'
         '6fb8e7a09955e0f64be3158fb4a27e7a'
         '61a1507f260866805fc6451c14733b25')

build() {
  cd ruby-${pkgver}

  # fixes https://github.com/rubygems/rubygems/issues/760
  patch -Np1 -i ${srcdir}/0001-Fix-installing-gem-from-file-without-dependencies.patch

  PKG_CONFIG=/usr/bin/pkg-config ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat

  make
}

check() {
  cd ruby-${pkgver}

  make test
}

package_ruby() {
  pkgdesc='An object-oriented language for quick and easy programming'
  depends=('gdbm' 'openssl' 'libffi' 'libyaml')
  optdepends=('ruby-docs: Ruby documentation') # 'tk: for Ruby/TK'
  provides=('rubygems' 'rake')
  conflicts=('rake')
  backup=('etc/gemrc')
  install='ruby.install'

  cd ruby-${pkgver}

  make DESTDIR="${pkgdir}" install-nodoc

  install -D -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/ruby/LICENSE"
  install -D -m644 BSDL "${pkgdir}/usr/share/licenses/ruby/BSDL"
}

package_ruby-docs() {
  pkgdesc='Documentation files for ruby'

  cd ruby-${pkgver}

  make DESTDIR="${pkgdir}" install-doc install-capi

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/ruby-docs/LICENSE"
  install -D -m644 BSDL "${pkgdir}/usr/share/licenses/ruby-docs/BSDL"
}
