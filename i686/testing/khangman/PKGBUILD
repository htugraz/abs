# $Id: PKGBUILD 254337 2015-12-08 20:38:43Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khangman
pkgver=15.08.3
pkgrel=2
pkgdesc="Hangman Game"
url="http://kde.org/applications/education/khangman/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('libkeduvocdocument' 'knewstuff' 'kdeclarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-khangman')
conflicts=('kdeedu-khangman')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/khangman-${pkgver}.tar.xz")
sha1sums=('d70a44d66b5e8f3eab8d68a40ed5782c6b48e159')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../khangman-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
