# $Id: PKGBUILD 218182 2014-07-25 23:41:26Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>

pkgname=openresolv
pkgver=3.5.7
pkgrel=1
pkgdesc="resolv.conf management framework (resolvconf)"
arch=(any)
url="http://roy.marples.name/projects/openresolv"
license=('custom:BSD')
backup=(etc/resolvconf.conf)
provides=(resolvconf)
depends=(bash)
makedepends=(systemd)
source=("http://roy.marples.name/downloads/openresolv/${pkgname}-${pkgver}.tar.bz2"
        fs33801.patch)
md5sums=('4de84738d29d54b910f5600ffd12b62c'
         '584f58b7d26419e725fe5495ed330f70')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fs33801.patch
  sed -n '2,25{s:^# \?::;p}' resolvconf.in >LICENSE 
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/resolvconf \
    --sbindir=/usr/bin
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
