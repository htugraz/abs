# $Id: PKGBUILD 226719 2014-11-19 19:20:18Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kgpg
pkgver=4.14.3
pkgrel=1
pkgdesc='A GnuPG frontend'
url='http://kde.org/applications/utilities/kgpg/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'kdepimlibs')
optdepends=('kde-agent: For starting and shutting down gpg-agent with KDE session manager')
makedepends=('cmake' 'automoc4' 'boost')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kgpg-${pkgver}.tar.xz")
sha1sums=('55f1c7a9ceb527fc8c3f21057b5701651e477af4')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kgpg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
