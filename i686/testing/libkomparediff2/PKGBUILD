# $Id: PKGBUILD 212491 2014-05-14 19:54:48Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkomparediff2
pkgver=4.13.1
pkgrel=1
pkgdesc="Library to compare files and strings"
url='https://projects.kde.org/projects/kde/kdesdk/libkomparediff2'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('a22bfd9c28cb68f3d2fd5fb65f72dd19060aaf53')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
