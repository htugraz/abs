# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=kaccounts-integration
pkgver=15.08.1
pkgrel=1
pkgdesc='Small system to administer web accounts for the sites and services across the KDE desktop, including: Google, Facebook, Owncloud, IMAP, 
Jabber and others'
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kdereview/kaccounts-integration'
license=(GPL)
groups=(kde-applications kdenetwork)
depends=(kcmutils signon-kwallet-extension signon-plugin-oauth2 signon-ui)
makedepends=(extra-cmake-modules kdoctools akonadi-client)
optdepends=('akonadi-client: Akonadi accounts support')
source=("http://download.kde.org/stable/applications/${pkgver}/src/$pkgname-${pkgver}.tar.xz")
md5sums=('e27071a5e2724f759ad7ed189ea407c9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
