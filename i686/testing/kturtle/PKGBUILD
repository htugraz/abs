# $Id: PKGBUILD 246124 2015-09-14 21:23:54Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kturtle
pkgver=15.08.1
pkgrel=1
pkgdesc="Educational Programming Environment"
url="http://kde.org/applications/education/kturtle/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('knewstuff' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdeedu-kturtle')
replaces=('kdeedu-kturtle')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kturtle-${pkgver}.tar.xz")
sha1sums=('54d505a87e5a5ea0198a296dac76669b63258b11')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kturtle-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
