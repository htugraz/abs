# $Id: PKGBUILD 215563 2014-06-29 09:18:22Z tpowa $
# Contributor: Low Kian Seong <fastmail_low@speedymail.org>
# Maintainer: dorphell <dorphell@archlinux.org>
pkgname=lzo2
pkgver=2.07
pkgrel=2
pkgdesc="Portable lossless data compression library"
arch=('i686' 'x86_64')
url="http://www.oberhumer.com/opensource/lzo"
license=('GPL')
depends=('glibc')
source=(http://www.oberhumer.com/opensource/lzo/download/lzo-${pkgver}.tar.gz
        configure-2.07.patch)
md5sums=('4011935e95171e78ad4894f7335c982a'
         'db3ead3fb5590275838d3080ea7342d1')

prepare() {
  cd "${srcdir}/lzo-${pkgver}"
  patch -Np0 -i ${srcdir}/configure-2.07.patch
}

build() {
  cd "${srcdir}/lzo-${pkgver}"
  ./configure --prefix=/usr --enable-shared
  make

  # build minilzo
  gcc $CFLAGS -fpic -Iinclude/lzo -o minilzo/minilzo.o -c minilzo/minilzo.c
  gcc $LDFLAGS -shared -o libminilzo.so.0 -Wl,-soname,libminilzo.so.0 minilzo/minilzo.o
}

check() {
  cd "${srcdir}/lzo-${pkgver}"
  make test # Larger test
  make check
}

package() {
  cd "${srcdir}/lzo-${pkgver}"
  make DESTDIR=${pkgdir} install

  # install minilzo
  install -m 755 libminilzo.so.0 ${pkgdir}/usr/lib
  install -p -m 644 minilzo/minilzo.h ${pkgdir}/usr/include/lzo
  cd ${pkgdir}/usr/lib
  ln -s libminilzo.so.0 libminilzo.so
}
