# $Id: PKGBUILD 222186 2014-09-20 16:07:29Z svenstaro $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=4.14.1
pkgrel=1
pkgdesc='Extends the functionality of kdepim'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'libkgapi' 'libkolab' 'libkfbapi' 'libaccounts-qt' 'signon')
makedepends=('cmake' 'automoc4' 'boost')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('4ea838c38244201b3f4a6ddcc479a680c84067d3')

prepare() {
  mkdir build
}

build() {
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
