# $Id: PKGBUILD 247813 2015-09-28 18:59:00Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=pyatspi
pkgname=(python-atspi python2-atspi)
pkgver=2.18.0
pkgrel=1
arch=(any)
url="http://www.linuxfoundation.org/en/AT-SPI_on_D-Bus"
license=('GPL2')
makedepends=(python-gobject python2-gobject at-spi2-core)
source=(http://download.gnome.org/sources/${pkgbase}/${pkgver:0:4}/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('f86f03ad05d0502cd60fe8b98f746f74ce3bc208e9f3798f6a2130b3c5f4ab46')

build() {
  mkdir python2 python3

  cd $pkgbase-$pkgver
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf -fi

  cd ../python2
  ../$pkgbase-$pkgver/configure --prefix=/usr --with-python=/usr/bin/python2
  make

  cd ../python3
  ../$pkgbase-$pkgver/configure --prefix=/usr --with-python=/usr/bin/python
  make
}

package_python-atspi() {
  pkgdesc="Python 3 bindings for at-spi"
  depends=(python-gobject at-spi2-core)

  cd python3
  make DESTDIR="$pkgdir" install
}

package_python2-atspi() {
  pkgdesc="Python 2 bindings for at-spi"
  depends=(python2-gobject at-spi2-core)
  provides=("pyatspi=$pkgver")
  replaces=(pyatspi)
  conflicts=(pyatspi)

  cd python2
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
