# Maintainer: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=HTTP
pkgname=haskell-http
pkgver=4000.2.12
pkgrel=2
pkgdesc="A library for client-side HTTP"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
options=('staticlibs')
depends=('ghc=7.8.2-2' 'sh' 'haskell-network=2.5.0.0-1' 'haskell-parsec=3.1.5-2' 'haskell-mtl=2.1.3.1-2')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        '0001-relax-upper-bound-on-network.patch')
install=${pkgname}.install
md5sums=('cd69429866439890353ec98100e3ba32'
         '490004d105b18535bb6c77ec2566053e')

build() {
  cd ${_hkgname}-${pkgver}

  # fixes https://github.com/haskell/HTTP/issues/55
  patch -Np1 -i ${srcdir}/0001-relax-upper-bound-on-network.patch

  runhaskell Setup configure -O -p \
    --enable-split-objs \
    --enable-shared \
    --prefix=/usr \
    --docdir=/usr/share/doc/${pkgname} \
    --libsubdir=\$compiler/site-local/\$pkgid

  runhaskell Setup build

  runhaskell Setup haddock

  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${_hkgname}-${pkgver}

  install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
  install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh

  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}

  runhaskell Setup copy --destdir=${pkgdir}

  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
