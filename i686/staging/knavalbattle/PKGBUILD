# $Id: PKGBUILD 251259 2015-11-19 20:20:39Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knavalbattle
pkgver=15.11.80
pkgrel=1
pkgdesc="A ship sinking game"
url="http://kde.org/applications/games/knavalbattle/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames)
makedepends=(extra-cmake-modules kdoctools)
install=$pkgname.install
conflicts=(kdegames-knavalbattle)
replaces=(kdegames-knavalbattle)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('f0043dc2c7c4fe086cb72ea1c5bc92ca8d787567')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
