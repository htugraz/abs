# $Id: PKGBUILD 214751 2014-06-09 14:36:42Z andyrtr $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=khtml
pkgver=4.100.0
pkgrel=2
pkgdesc='KHtml'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/khtml'
license=('LGPL')
depends=('giflib' 'kparts' 'kjs')
makedepends=('extra-cmake-modules')
groups=('kf5-aids')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=('c9b04036e1b1b3ac9f253962386c6b0e')

prepare() {
  mkdir -p build
  #fix build with giflib 5.1.0 - taken from BLFS
  sed -i "/DGifCloseFile/s:file:&, NULL:g" khtml-$pkgver/src/imload/decoders/gifloader.cpp
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
