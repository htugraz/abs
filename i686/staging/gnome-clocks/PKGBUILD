# $Id: PKGBUILD 247733 2015-09-28 18:57:25Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgver=3.18.0
pkgrel=1
pkgdesc="Clocks applications for GNOME"
arch=(i686 x86_64)
url="https://www.gnome.org/"
license=(GPL)
depends=(gtk3 libcanberra libgweather gnome-desktop
         hicolor-icon-theme gnome-themes-standard geoclue2 geocode-glib gsound)
makedepends=(intltool vala gobject-introspection itstool)
groups=(gnome-extra)
install=gnome-clocks.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('ca0818ec89e3539201da6b5388365e3d66df815198beccc90e2be44c7822baa0')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
