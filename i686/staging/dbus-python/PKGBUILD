# $Id: PKGBUILD 246711 2015-09-20 01:40:52Z fyan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=dbus-python
pkgname=('python-dbus' 'python2-dbus' 'python-dbus-common')
pkgver=1.2.0
pkgrel=5
pkgdesc="Python bindings for DBUS"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
makedepends=('docutils' 'dbus-glib' 'python' 'python2')
source=(http://dbus.freedesktop.org/releases/${pkgbase}/${pkgbase}-${pkgver}.tar.gz{,.asc})
md5sums=('b09cd2d1a057cc432ce944de3fc06bf7'
         'SKIP')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90')  # Simon McVittie

prepare() {
  mkdir build-python2 build-python3
  cd $pkgbase-$pkgver
  autoreconf -fi
}

build() {
  cd build-python2
  PYTHON=python2 ../${pkgbase}-${pkgver}/configure --prefix=/usr
  make

  cd ../build-python3
  ../${pkgbase}-${pkgver}/configure --prefix=/usr
  make
}

package_python-dbus-common() {
  pkgdesc="Common dbus-python files shared between python-dbus and python2-dbus"
  conflicts=('dbus-python<1.1.0')

  cd build-python3
  make DESTDIR="${pkgdir}" install-data
  rm -rf "${pkgdir}"/usr/lib/python*
}

package_python-dbus() {
  depends=('python-dbus-common' 'python' 'dbus')
  optdepends=('dbus-glib: glib mainloop support')
  pkgdesc="Python 3.5 bindings for DBUS"

  cd build-python3
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/share"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
}

package_python2-dbus() {
  depends=('python-dbus-common' 'python2' 'dbus')
  optdepends=('dbus-glib: glib mainloop support')
  pkgdesc="Python 2.7 bindings for DBUS"
  replaces=('dbus-python')
  conflicts=('dbus-python')
  provides=("dbus-python=$pkgver")

  cd build-python2
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/share"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
}
