# $Id: PKGBUILD 213781 2014-05-29 03:51:00Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=net-snmp
pkgver=5.7.2.1
pkgrel=3
pkgdesc="A suite of applications used to implement SNMP v1, SNMP v2c and SNMP v3 using both IPv4 and IPv6"
arch=('i686' 'x86_64')
url="http://www.net-snmp.org/"
license=('BSD')
depends=('openssl' 'libnl' 'pciutils')
makedepends=('python2-setuptools')
optdepends=('perl-term-readkey: for snmpcheck application'
            'perl-tk: for snmpcheck and tkmib applications'
            'python2: for the python modules')
options=('!emptydirs' '!makeflags')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc}
        snmpd.service snmptrapd.service libnl32.patch)
sha1sums=('815d4e5520a1ed96a27def33e7534b4190599f0f'
          'SKIP'
          '84e32c54d32e6b608747054e04a3ddfe6d6638cc'
          '0244e91c7baa0abebfb5c0560e8ce04c966c5992'
          '74a9848b95f63378eb1753fc309d2b74de5afb0f')

prepare() {
  cd ${pkgname}-${pkgver}
  #make PERL_ARCHLIB=/usr/lib/perl5/core_perl PERL_INC=/usr/lib/perl5/core_perl/CORE distclean

  # http://sourceforge.net/tracker/index.php?func=detail&aid=3250304&group_id=12694&atid=112694
  patch -Np1 -i "$srcdir/libnl32.patch"

  autoreconf -f -i
}

build() {
  cd ${pkgname}-${pkgver}
  PYTHONPROG=/usr/bin/python2 ./configure --prefix=/usr \
    --sysconfdir=/etc --sbindir=/usr/bin \
    --mandir=/usr/share/man \
    --enable-ucd-snmp-compatibility \
    --enable-ipv6 \
    --with-python-modules \
    --with-default-snmp-version="3" \
    --with-sys-contact="root@localhost" \
    --with-sys-location="Unknown" \
    --with-logfile="/var/log/snmpd.log" \
    --with-mib-modules="host misc/ipfwacc ucd-snmp/diskio tunnel ucd-snmp/dlmod" \
    --with-persistent-directory="/var/net-snmp"
  make NETSNMP_DONT_CHECK_VERSION=1
}

package() {
  cd ${pkgname}-${pkgver}
  sed -i -e "s:install --basedir=\$\$dir:install --basedir=\$\$dir --root=${pkgdir}:" Makefile
  make DESTDIR="${pkgdir}" INSTALL_PREFIX="${pkgdir}" INSTALLDIRS=vendor install
  install -D -m644 "${srcdir}/snmpd.service" "${pkgdir}/usr/lib/systemd/system/snmpd.service"
  install -D -m644 "${srcdir}/snmptrapd.service" "${pkgdir}/usr/lib/systemd/system/snmptrapd.service"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
