# $Id: PKGBUILD 174870 2013-01-08 10:06:34Z jgc $
# Maintainer:
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio-paranoia
pkgver=10.2+0.93+1
pkgrel=1
pkgdesc="CD paranoia libraries from libcdio"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://www.gnu.org/software/libcdio/"
depends=('libcdio')
source=(http://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.bz2{,.sig})
md5sums=('0255aa50e660db7f2c39658b9c565814'
         'SKIP')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-cpp-progs --disable-static --disable-example-progs
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
}
