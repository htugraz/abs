# $Id: PKGBUILD 204407 2014-01-19 11:12:56Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdcraw
pkgver=4.12.1
pkgrel=2
pkgdesc="A C++ interface used to decode RAW picture"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkdcraw"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'libraw')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('6c13e5b2fc0fccb057639d1e563e8d36fec96929')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
