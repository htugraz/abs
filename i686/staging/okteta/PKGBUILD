# $Id: PKGBUILD 236217 2015-04-10 19:43:13Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okteta
pkgver=15.04.0
pkgrel=1
pkgdesc='Hex Editor'
url='http://kde.org/applications/utilities/okteta/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kcmutils' 'kparts' 'knewstuff' 'qca-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'qt5-tools')
replaces=('kdesdk-okteta')
conflicts=('kdesdk-okteta<4.14.3-2')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/okteta-${pkgver}.tar.xz")
sha1sums=('2687cb3afee0f4bfff395c6967fa2388292df602')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../okteta-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
