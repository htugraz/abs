# $Id: PKGBUILD 211181 2014-04-18 06:26:22Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Committer: dorphell <dorphell@gmx.net>

pkgname=screen
pkgver=4.2.0
pkgrel=1
_ptygroup=5 #the UID of our PTY/TTY group
pkgdesc='Full-screen window manager that multiplexes a physical terminal'
url='http://www.gnu.org/software/screen/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('ncurses' 'pam')
backup=('etc/screenrc' 'etc/pam.d/screen')
#source=("http://ftp.gnu.org/gnu/screen/screen-${pkgver}.tar.gz"
source=("http://download.savannah.gnu.org/releases/screen/screen-${pkgver}.tar.gz"
        'long-term.patch'
        'pam.d')
sha1sums=('85a206e1e474fd608f17e88bfe5f90a55f21c51a'
          '4ca8408ec03e97777e37f45a99cf8ab503592734'
          '1ab4b512d2ac840d16db6986d7c98d7ce2f6383f')

options=('!makeflags')
install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../long-term.patch
	./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info \
		--enable-colors256 \
		--enable-pam \
		--enable-rxvt_osc \
		--enable-telnet \
		--with-pty-group=$_ptygroup \
		--with-socket-dir=/run/screens \
		--with-sys-screenrc=/etc/screenrc \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../pam.d "${pkgdir}"/etc/pam.d/screen

	install -Dm644 etc/completer.zsh "${pkgdir}"/usr/share/zsh/site-functions/screen
	install -Dm644 etc/etcscreenrc "${pkgdir}"/etc/screenrc
	install -Dm644 etc/screenrc "${pkgdir}"/etc/skel/.screenrc
}
