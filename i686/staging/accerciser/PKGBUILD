# $Id: PKGBUILD 247069 2015-09-22 10:01:53Z fyan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: Giulio Bai <giulio@hewle.com>

pkgname=accerciser
pkgver=3.14.0
pkgrel=3
pkgdesc="Interactive Python accessibility explorer for the GNOME desktop"
arch=('any')
url="http://live.gnome.org/Accerciser"
license=('BSD')
depends=('python' 'gtk3' 'python-atspi' 'ipython' 'hicolor-icon-theme' 'libwnck3' 'python-cairo')
makedepends=('itstool' 'docbook-xsl' 'intltool')
install=accerciser.install
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/accerciser/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('f5a5915d475e9241396414ccd167724c88807e77e69151cf0b40b32bd57d0574')

build () {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
                 --disable-schemas-compile
  make
}

package () {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m 755 -d "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname
}
