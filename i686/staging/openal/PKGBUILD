# $Id: PKGBUILD 252781 2015-12-06 12:11:39Z bpiotrowski $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgbase=openal
pkgname=(openal openal-examples)
pkgver=1.17.0
pkgrel=4
pkgdesc="A cross-platform 3D audio library"
arch=(i686 x86_64)
url="http://www.openal.org/"
license=(LGPL)
depends=(glibc)
makedepends=(alsa-lib pkgconfig cmake libpulse qt4 fluidsynth portaudio jack sdl2 sdl_sound ffmpeg
             git)
_commit=63567526b0a17449baf57053d11688aa277a7e40
source=("git+https://github.com/kcat/openal-soft#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  mkdir build examples
}

build() {
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release ../$pkgbase-soft
  make
}

package_openal() {
  optdepends=('qt4: alsoft-config GUI Configurator'
              'fluidsynth: MIDI rendering')

  make -C build DESTDIR="$pkgdir/" install
  install -m644 $pkgbase-soft/{env-vars,hrtf}.txt "$pkgdir/usr/share/openal/"

### Split openal-examples
  mv "$pkgdir"/usr/bin/al{ffplay,latency,loopback,reverb,stream} examples/
}

package_openal-examples() {
  pkgdesc+=" (example programs)"
  depends=("openal=$pkgver-$pkgrel" sdl2 sdl_sound ffmpeg)

  mkdir -p "$pkgdir/usr/bin"
  mv examples/* "$pkgdir/usr/bin/"
}
