# $Id: PKGBUILD 247713 2015-09-28 18:57:02Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gdk-pixbuf2
pkgver=2.32.1
pkgrel=1
pkgdesc="An image loading library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11' 'jasper')
makedepends=('gtk-doc' 'gobject-introspection')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver:0:4}/gdk-pixbuf-$pkgver.tar.xz)
sha256sums=('4432b74f25538c7d6bcb3ca51adabdd666168955f25812a2568dc9637697f3bc')

build() {
  cd gdk-pixbuf-$pkgver

  ./configure --prefix=/usr \
    --with-x11 \
    --with-libjasper \
    --with-included-loaders=png
  make
}

package() {
  cd gdk-pixbuf-$pkgver

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
