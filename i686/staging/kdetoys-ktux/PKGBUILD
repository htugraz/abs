# $Id: PKGBUILD 236226 2015-04-10 19:45:14Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdetoys-ktux
pkgver=15.04.0
pkgrel=1
pkgdesc='A Tux-in-a-spaceship screen saver'
url='https://projects.kde.org/projects/kde/kdetoys/ktux'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdetoys')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/ktux-${pkgver}.tar.xz")
sha1sums=('2cca57e163f933643749d3a738362b738bfe61ce')

build() {
  mkdir build
  cd build
  cmake ../ktux-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
